<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Rose::Object::MakeMethods::Generic - Create simple object methods.</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/librose-object-perl">librose-object-perl_0.860-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Rose::Object::MakeMethods::Generic - Create simple object methods.

</pre><h4><b>SYNOPSIS</b></h4><pre>
         package MyObject;

         use Rose::Object::MakeMethods::Generic
         (
           scalar =&gt;
           [
             'power',
             'error',
           ],

           'scalar --get_set_init' =&gt; 'name',

           'boolean --get_set_init' =&gt; 'is_tall',

           boolean =&gt;
           [
             'is_red',
             'is_happy' =&gt; { default =&gt; 1 },
           ],

           array =&gt;
           [
             jobs       =&gt; {},
             job        =&gt; { interface =&gt; 'get_set_item', hash_key =&gt; 'jobs' },
             clear_jobs =&gt; { interface =&gt; 'clear', hash_key =&gt; 'jobs' },
             reset_jobs =&gt; { interface =&gt; 'reset', hash_key =&gt; 'jobs' },
           ],

           hash =&gt;
           [
             param        =&gt; { hash_key =&gt; 'params' },
             params       =&gt; { interface =&gt; 'get_set_all' },
             param_names  =&gt; { interface =&gt; 'keys', hash_key =&gt; 'params' },
             param_values =&gt; { interface =&gt; 'values', hash_key =&gt; 'params' },
             param_exists =&gt; { interface =&gt; 'exists', hash_key =&gt; 'params' },
             delete_param =&gt; { interface =&gt; 'delete', hash_key =&gt; 'params' },

             clear_params =&gt; { interface =&gt; 'clear', hash_key =&gt; 'params' },
             reset_params =&gt; { interface =&gt; 'reset', hash_key =&gt; 'params' },
           ],
         );

         sub init_name    { 'Fred' }
         sub init_is_tall { 1 }
         ...

         $obj = MyObject-&gt;new(power =&gt; 5);

         print $obj-&gt;name; # Fred

         $obj-&gt;do_something or die $obj-&gt;error;

         $obj-&gt;is_tall;        # true
         $obj-&gt;is_tall(undef); # false (but defined)
         $obj-&gt;is_tall;        # false (but defined)

         $obj-&gt;is_red;         # undef
         $obj-&gt;<a href="../man1234/is_red.1234.html">is_red</a>(1234);   # true
         $obj-&gt;is_red('');     # false (but defined)
         $obj-&gt;is_red;         # false (but defined)

         $obj-&gt;is_happy;       # true

         $obj-&gt;params(a =&gt; 1, b =&gt; 2);   # add pairs
         $val = $obj-&gt;param('b');        # 2
         $obj-&gt;param_exists('x');        # false

         $obj-&gt;jobs('butcher', 'baker'); # add values
         $obj-&gt;job(0 =&gt; 'sailor');       # set value
         $job = $obj-&gt;<a href="../man0/job.0.html">job</a>(0);            # 'sailor'

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Rose::Object::MakeMethods::Generic is a method maker that inherits from Rose::Object::MakeMethods.  See
       the Rose::Object::MakeMethods documentation to learn about the interface.  The method types provided by
       this module are described below.  All methods work only with hash-based objects.

</pre><h4><b>METHODS</b> <b>TYPES</b></h4><pre>
       <b>scalar</b>
           Create get/set methods for scalar attributes.

           Options
               "hash_key"
                   The  key  inside  the hash-based object to use for the storage of this attribute. Defaults to
                   the name of the method.

               "init_method"
                   The name of the method to call when initializing the value of an undefined  attribute.   This
                   option  is  only  applicable when using the "get_set_init" interface.  Defaults to the method
                   name with the prefix "init_" added.

               "interface"
                   Choose one of the two possible interfaces.  Defaults to "get_set".

           Interfaces
               "get_set"
                   Creates a simple get/set accessor method for  an  object  attribute.   When  called  with  an
                   argument, the value of the attribute is set.  The current value of the attribute is returned.

               "get_set_init"
                   Behaves like the "get_set" interface unless the value of the attribute is undefined.  In that
                   case,  the method specified by the "init_method" option is called and the attribute is set to
                   the return value of that method.

           Example:

               package MyObject;

               use Rose::Object::MakeMethods::Generic
               (
                 scalar =&gt; 'power',
                 'scalar --get_set_init' =&gt; 'name',
               );

               sub init_name { 'Fred' }
               ...

               $obj-&gt;<a href="../man99/power.99.html">power</a>(99);    # returns 99
               $obj-&gt;name;         # returns "Fred"
               $obj-&gt;name('Bill'); # returns "Bill"

       <b>boolean</b>
           Create get/set methods for boolean attributes.  For each argument to these methods,  the  only  thing
           that  matters is whether it evaluates to true or false.  The return value is either, true, false (but
           defined), or undef if the value has never been set.

           Options
               "default"
                   Determines the default value of the attribute.  This option is only applicable when using the
                   "get_set" interface.

               "hash_key"
                   The key inside the hash-based object to use for the storage of this  attribute.  Defaults  to
                   the name of the method.

               "init_method"
                   The name of the method to call when initializing the value of an undefined attribute.  Again,
                   the  only  thing  that  matters about the return value of this method is whether or not it is
                   true or false.  This option is only  applicable  when  using  the  "get_set_init"  interface.
                   Defaults to the method name with the prefix "init_" added.

               "interface"
                   Choose one of the two possible interfaces.  Defaults to "get_set".

           Interfaces
               "get_set"
                   Creates  a simple get/set accessor method for a boolean object attribute. When called with an
                   argument, the value of the attribute is set to true if the argument evaluates to true,  false
                   (but defined) otherwise.  The current value of the attribute is returned.

                   If     Class::XSAccessor     version     0.14    or    later    is    installed    and    the
                   "ROSE_OBJECT_NO_CLASS_XSACCESOR" environment variable is  not  set  to  a  true  value,  then
                   Class::XSAccessor will be used to generated the method.

               "get_set_init"
                   Behaves like the "get_set" interface unless the value of the attribute is undefined.  In that
                   case,  the  method  specified  by the "init_method" option is called and the attribute is set
                   based on the boolean value of the return value of that method.

           Example:

               package MyObject;

               use Rose::Object::MakeMethods::Generic
               (
                 'boolean --get_set_init' =&gt; 'is_tall',

                 boolean =&gt;
                 [
                   'is_red',
                   'is_happy' =&gt; { default =&gt; 1 },
                 ],
               );

               sub init_is_tall { 'blah' }
               ...

               $obj-&gt;is_tall;        # returns true
               $obj-&gt;is_tall(undef); # returns false (but defined)
               $obj-&gt;is_tall;        # returns false (but defined)

               $obj-&gt;is_red;         # returns undef
               $obj-&gt;<a href="../man1234/is_red.1234.html">is_red</a>(1234);   # returns true
               $obj-&gt;is_red('');     # returns false (but defined)
               $obj-&gt;is_red;         # returns false (but defined)

               $obj-&gt;is_happy;       # returns true

       <b>hash</b>
           Create methods to manipulate hash attributes.

           Options
               "hash_key"
                   The key inside the hash-based object to use for the storage of this attribute.   Defaults  to
                   the name of the method.

               "init_method"
                   The  name  of  the method to call when initializing the value of an undefined hash attribute.
                   This method should return a reference to a hash,  and  is  only  applicable  when  using  the
                   "get_set_init" interface. Defaults to the method name with the prefix "init_" added.

               "interface"
                   Choose which interface to use.  Defaults to "get_set".

           Interfaces
               "get_set"
                   If called with no arguments, returns a list of key/value pairs in list context or a reference
                   to the actual hash stored by the object in scalar context.

                   If  called with one argument, and that argument is a reference to a hash, that hash reference
                   is used as the new value for the attribute.  Returns  a  list  of  key/value  pairs  in  list
                   context or a reference to the actual hash stored by the object in scalar context.

                   If called with one argument, and that argument is a reference to an array, then a list of the
                   hash values for each key in the array is returned.

                   If  called  with one argument, and it is not a reference to a hash or an array, then the hash
                   value for that key is returned.

                   If called with an even number of arguments, they are taken as name/value pairs and are  added
                   to the hash.  It then returns a list of key/value pairs in list context or a reference to the
                   actual hash stored by the object in scalar context.

                   Passing an odd number of arguments greater than 1 causes a fatal error.

               "get_set_init"
                   Behaves  like  the  "get_set"  interface unless the attribute is undefined. In that case, the
                   method specified by the "init_method" option is called and the attribute is set to the return
                   value of that method, which should be a reference to a hash.

               "get_set_inited"
                   Behaves like the "get_set" interface unless the attribute is undefined. In that case,  it  is
                   initialized to an empty hash before proceeding as usual.

               "get_set_all"
                   If called with no arguments, returns a list of key/value pairs in list context or a reference
                   to the actual hash stored by the object in scalar context.

                   If  called with one argument, and that argument is a reference to a hash, that hash reference
                   is used as the new value for the attribute.  Returns  a  list  of  key/value  pairs  in  list
                   context or a reference to the actual hash stored by the object in scalar context.

                   Otherwise,  the hash is emptied and the arguments are taken as name/value pairs that are then
                   added to the hash.  It then returns a list of key/value pairs in list context or a  reference
                   to the actual hash stored by the object in scalar context.

               "get_set_init_all"
                   Behaves like the "get_set_all" interface unless the attribute is undefined. In that case, the
                   method specified by the "init_method" option is called and the attribute is set to the return
                   value of that method, which should be a reference to a hash.

               "clear"
                   Sets the attribute to an empty hash.

               "reset"
                   Sets the attribute to undef.

               "delete"
                   Deletes the key(s) passed as arguments.  Failure to pass any arguments causes a fatal error.

               "exists"
                   Returns true of the argument exists in the hash, false otherwise. Failure to pass an argument
                   or passing more than one argument causes a fatal error.

               "keys"
                   Returns  the  keys of the hash in list context, or a reference to an array of the keys of the
                   hash in scalar context.  The keys are not sorted.

               "names"
                   An alias for the "keys" interface.

               "values"
                   Returns the values of the hash in list context, or a reference to an array of the  values  of
                   the hash in scalar context.  The values are not sorted.

           Example:

               package MyObject;

               use Rose::Object::MakeMethods::Generic
               (
                 hash =&gt;
                 [
                   param        =&gt; { hash_key =&gt;'params' },
                   params       =&gt; { interface=&gt;'get_set_all' },
                   param_names  =&gt; { interface=&gt;'keys',   hash_key=&gt;'params' },
                   param_values =&gt; { interface=&gt;'values', hash_key=&gt;'params' },
                   param_exists =&gt; { interface=&gt;'exists', hash_key=&gt;'params' },
                   delete_param =&gt; { interface=&gt;'delete', hash_key=&gt;'params' },

                   clear_params =&gt; { interface=&gt;'clear', hash_key=&gt;'params' },
                   reset_params =&gt; { interface=&gt;'reset', hash_key=&gt;'params' },
                 ],
               );
               ...

               $obj = MyObject-&gt;new;

               $obj-&gt;params; # undef

               $obj-&gt;params(a =&gt; 1, b =&gt; 2); # add pairs
               $val = $obj-&gt;param('b'); # 2

               %params = $obj-&gt;params; # copy hash keys and values
               $params = $obj-&gt;params; # get hash ref

               $obj-&gt;params({ c =&gt; 3, d =&gt; 4 }); # replace contents

               $obj-&gt;param_exists('a'); # false

               $keys = join(',', sort $obj-&gt;param_names);  # 'c,d'
               $vals = join(',', sort $obj-&gt;param_values); # '3,4'

               $obj-&gt;delete_param('c');
               $obj-&gt;param(f =&gt; 7, g =&gt; 8);

               $vals = join(',', sort $obj-&gt;param_values); # '4,7,8'

               $obj-&gt;clear_params;
               $params = $obj-&gt;params; # empty hash

               $obj-&gt;reset_params;
               $params = $obj-&gt;params; # undef

       <b>array</b>
           Create methods to manipulate array attributes.

           Options
               "hash_key"
                   The  key  inside the hash-based object to use for the storage of this attribute.  Defaults to
                   the name of the method.

               "init_method"
                   The name of the method to call when initializing the value of an undefined  array  attribute.
                   This method should return a reference to an array.  This option is only applicable when using
                   the  "get_set_init",  "push", and "add" interfaces.  When using the "get_set_init" interface,
                   "init_method" defaults to the method name with the prefix "init_" added.

               "interface"
                   Choose which interface to use.  Defaults to "get_set".

           Interfaces
               "get_set"
                   If called with no arguments, returns the array contents in list context or a reference to the
                   actual array stored by the object in scalar context.

                   If called with one argument, and that argument  is  a  reference  to  an  array,  that  array
                   reference  is  used  as  the  new value for the attribute. Returns the array contents in list
                   context or a reference to the actual array stored by the object in scalar context.

                   If called with one argument, and that argument is not a reference to an array, or  if  called
                   with  more than one argument, then the array contents are replaced by the arguments.  Returns
                   the array contents in list context or a reference to the actual array stored by the object in
                   scalar context.

               "get_set_init"
                   Behaves like the "get_set" interface unless the attribute is undefined.  In  that  case,  the
                   method specified by the "init_method" option is called and the attribute is set to the return
                   value of that method, which should be a reference to an array.

               "get_set_inited"
                   Behaves  like  the "get_set" interface unless the attribute is undefined. In that case, it is
                   initialized to an empty array before proceeding as usual.

               "get_set_item"
                   If called with one argument, returns the item at that array index.

                   If called with two arguments, sets the item  at  the  array  index  specified  by  the  first
                   argument to the value specified by the second argument.

                   Failure to pass any arguments causes a fatal error.

               "exists"
                   Returns true of the argument exists in the hash, false otherwise. Failure to pass an argument
                   or passing more than one argument causes a fatal error.

               "add"
                   An alias for the "push" interface.

               "push"
                   If  called  with  a  list  or a reference to an array, the contents of the list or referenced
                   array are added to the end of the array.  If called with no arguments,  a  fatal  error  will
                   occur.

               "pop"
                   Remove  an  item from the end of the array and returns it.  If an integer argument is passed,
                   then that number of items is removed  and  returned.  Otherwise,  just  one  is  removed  and
                   returned.

               "shift"
                   Remove an item from the start of the array and returns it.  If an integer argument is passed,
                   then  that  number  of  items  is  removed  and  returned. Otherwise, just one is removed and
                   returned.

               "unshift"
                   If called with a list or a reference to an array, the contents  of  the  list  or  referenced
                   array  are  added to the start of the array.  If called with no arguments, a fatal error will
                   occur.

               "clear"
                   Sets the attribute to an empty array.

               "reset"
                   Sets the attribute to undef.

           Example:

               package MyObject;

               use Rose::Object::MakeMethods::Generic
               (
                 array =&gt;
                 [
                   jobs       =&gt; {},
                   job        =&gt; { interface =&gt; 'get_set_item',
                                   hash_key  =&gt; 'jobs' },
                   clear_jobs =&gt; { interface =&gt; 'clear', hash_key =&gt; 'jobs' },
                   reset_jobs =&gt; { interface =&gt; 'reset', hash_key =&gt; 'jobs' },
                 ],
               );
               ...

               $obj = MyObject-&gt;new;

               $jobs = $obj-&gt;jobs; # undef

               $obj-&gt;clear_jobs();
               $jobs = $obj-&gt;jobs; # ref to empty array

               $obj-&gt;jobs('butcher', 'baker'); # add values
               $vals = join(',', $obj-&gt;jobs);  # 'butcher,baker'

               $obj-&gt;jobs([ 'candlestick', 'maker' ]); # replace values

               $vals = join(',', $obj-&gt;jobs); # 'candlestick,maker'

               $job = $obj-&gt;<a href="../man0/job.0.html">job</a>(0);      # 'candlestick'
               $obj-&gt;job(0 =&gt; 'sailor'); # set value
               $job = $obj-&gt;<a href="../man0/job.0.html">job</a>(0);      # 'sailor'

               $obj-&gt;reset_jobs;
               $jobs = $obj-&gt;jobs; # undef

</pre><h4><b>AUTHOR</b></h4><pre>
       John C. Siracusa (<a href="mailto:siracusa@gmail.com">siracusa@gmail.com</a>)

</pre><h4><b>LICENSE</b></h4><pre>
       Copyright (c) 2010 by John C. Siracusa.  All rights reserved.  This program is  free  software;  you  can
       redistribute it and/or modify it under the same terms as Perl itself.

perl v5.34.0                                       2022-05-28             <u>Rose::Object::M...ethods::<a href="../man3pm/Generic.3pm.html">Generic</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>