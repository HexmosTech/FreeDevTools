<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>WWW::Telegram::BotAPI - Perl implementation of the Telegram Bot API</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libwww-telegram-botapi-perl">libwww-telegram-botapi-perl_0.12-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       WWW::Telegram::BotAPI - Perl implementation of the Telegram Bot API

</pre><h4><b>SYNOPSIS</b></h4><pre>
           use WWW::Telegram::BotAPI;
           my $api = WWW::Telegram::BotAPI-&gt;new (
               token =&gt; 'my_token'
           );
           # The API methods die when an error occurs.
           say $api-&gt;getMe-&gt;{result}{username};
           # ... but error handling is available as well.
           my $result = eval { $api-&gt;getMe }
               or die 'Got error message: ', $api-&gt;parse_error-&gt;{msg};
           # Uploading files is easier than ever.
           $api-&gt;sendPhoto ({
               chat_id =&gt; 123456,
               photo   =&gt; {
                   file =&gt; '/home/me/cool_pic.png'
               },
               caption =&gt; 'Look at my cool photo!'
           });
           # Complex objects are as easy as writing a Perl object.
           $api-&gt;sendMessage ({
               chat_id      =&gt; 123456,
               # Object: ReplyKeyboardMarkup
               reply_markup =&gt; {
                   resize_keyboard =&gt; \1, # \1 = true when JSONified, \0 = false
                   keyboard =&gt; [
                       # Keyboard: row 1
                       [
                           # Keyboard: button 1
                           'Hello world!',
                           # Keyboard: button 2
                           {
                               text =&gt; 'Give me your phone number!',
                               request_contact =&gt; \1
                           }
                       ]
                   ]
               }
           });
           # Asynchronous request are supported with Mojo::UserAgent.
           $api = WWW::Telegram::BotAPI-&gt;new (
               token =&gt; 'my_token',
               async =&gt; 1 # WARNING: may fail if Mojo::UserAgent is not available!
           );
           $api-&gt;sendMessage ({
               chat_id =&gt; 123456,
               text    =&gt; 'Hello world!'
           }, sub {
               my ($ua, $tx) = @_;
               die 'Something bad happened!' if $tx-&gt;error;
               say $tx-&gt;res-&gt;json-&gt;{ok} ? 'YAY!' : ':('; # Not production ready!
           });
           Mojo::IOLoop-&gt;start;

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This module provides an easy to use interface for the Telegram Bot API
       &lt;https://core.telegram.org/bots/api&gt;. It also supports async requests out of the box using
       Mojo::UserAgent, which makes this module easy to integrate with an existing Mojolicious application.

</pre><h4><b>METHODS</b></h4><pre>
       WWW::Telegram::BotAPI implements the following methods.

   <b>new</b>
           my $api = WWW::Telegram::BotAPI-&gt;new (%options);

       Creates a new WWW::Telegram::BotAPI instance.

       <b>WARNING:</b> you should only create one instance of this module and reuse it when needed. Calling "new" each
       time you run an async request causes unexpected behavior with Mojo::UserAgent and won't work correctly.
       See also issue #13 on GitHub &lt;https://github.com/Robertof/perl-www-telegram-botapi/issues/13&gt;.

       %options may contain the following:

       •   "token =&gt; 'my_token'"

           The token that will be used to authenticate the bot.

           <b>This</b> <b>is</b> <b>required!</b> <b>The</b> <b>method</b> <b>will</b> <b>croak</b> <b>if</b> <b>this</b> <b>option</b> <b>is</b> <b>not</b> <b>specified.</b>

       •   "api_url =&gt; 'https://api.example.com/token/%s/method/%s'"

           A  format  string  that  will  be used to create the final API URL. The first parameter specifies the
           token, the second one specifies the method.

           Defaults to "https://api.telegram.org/bot%s/%s".

       •   "async =&gt; 1"

           Enables asynchronous requests.

           <b>This</b> <b>requires</b> <b>Mojo::UserAgent,</b> <b>and</b> <b>the</b> <b>method</b> <b>will</b> <b>croak</b> <b>if</b> <b>it</b> <b>isn't</b> <b>found.</b>

           Defaults to 0.

       •   "force_lwp =&gt; 1"

           Forces the usage of LWP::UserAgent instead of Mojo::UserAgent, even if the latter is available.

           By default, the module tries to load Mojo::UserAgent, and on failure it uses LWP::UserAgent.

   <b>AUTOLOAD</b>
           $api-&gt;getMe;
           $api-&gt;sendMessage ({
               chat_id =&gt; 123456,
               text    =&gt; 'Hello world!'
           });
           # with async =&gt; 1 and the IOLoop already started
           $api-&gt;setWebhook ({ url =&gt; 'https://example.com/webhook' }, sub {
               my ($ua, $tx) = @_;
               die if $tx-&gt;error;
               say 'Webhook set!'
           });

       This module makes use of "Autoloading" in perlsub. This means that <b>every</b> <b>current</b> <b>and</b> <b>future</b> <b>method</b> <b>of</b> <b>the</b>
       <b>Telegram</b> <b>Bot</b> <b>API</b> <b>can</b> <b>be</b> <b>used</b> <b>by</b> <b>calling</b> <b>its</b> <b>Perl</b> <b>equivalent</b>, without requiring an update of the module.

       If you'd like to avoid using "AUTOLOAD", then you may simply call the "api_request" method specifying the
       method name as the first argument.

           $api-&gt;api_request ('getMe');

       This is, by the way, the exact thing the "AUTOLOAD" method of this module does.

   <b>api_request</b>
           # Remember: each of these samples can be aliased with
           # $api-&gt;methodName ($params).
           $api-&gt;api_request ('getMe');
           $api-&gt;api_request ('sendMessage', {
               chat_id =&gt; 123456,
               text    =&gt; 'Oh, hai'
           });
           # file upload
           $api-&gt;api_request ('sendDocument', {
               chat_id  =&gt; 123456,
               document =&gt; {
                   filename =&gt; 'dump.txt',
                   content  =&gt; 'secret stuff'
               }
           });
           # complex objects are supported natively since v0.04
           $api-&gt;api_request ('sendMessage', {
               chat_id      =&gt; 123456,
               reply_markup =&gt; {
                   keyboard =&gt; [ [ 'Button 1', 'Button 2' ] ]
               }
           });
           # with async =&gt; 1 and the IOLoop already started
           $api-&gt;api_request ('getMe', sub {
               my ($ua, $tx) = @_;
               die if $tx-&gt;error;
               # ...
           });

       This method performs an API request.  The  first  argument  must  be  the  method  name  (here's  a  list
       &lt;https://core.telegram.org/bots/api#available-methods&gt;).

       Once  the  request  is  completed,  the  response  is  decoded  using JSON::MaybeXS and then returned. If
       Mojo::UserAgent is used as the user-agent, then the response is decoded automatically using Mojo::JSON.

       If the request is not successful or the server tells us something isn't "ok", then this method dies  with
       the  first  available error message (either the error description or the status line).  You can make this
       method non-fatal using "eval":

           my $response = eval { $api-&gt;api_request ($method, $args) }
               or warn "Request failed with error '$@', but I'm still alive!";

       Further processing of error messages can be obtained using "parse_error".

       Request parameters can be specified using  an  hash  reference.  Additionally,  complex  objects  can  be
       specified like you do in JSON. See the previous examples or the example bot provided in "SEE ALSO".

       File uploads can be specified using an hash reference containing the following mappings:

       •   "file =&gt; '/path/to/file.ext'"

           Path to the file you want to upload.

           Required only if "content" is not specified.

       •   "filename =&gt; 'file_name.ext'"

           An optional filename that will be used instead of the real name of the file.

           Particularly recommended when "content" is specified.

       •   "content =&gt; 'Being a file is cool :-)'"

           The content of the file to send. When using this, "file" must not be specified.

       •   "AnyCustom =&gt; 'Header'"

           Custom headers can be specified as hash mappings.

       Upload  of  multiple files is not supported. See "tx" in Mojo::UserAgent::Transactor for more information
       about file uploads.

       To resend files, you don't need to perform a file upload at all. Just pass the ID as a normal parameter.

           $api-&gt;sendPhoto ({
               chat_id =&gt; 123456,
               photo   =&gt; $photo_id
           });

       When asynchronous requests are enabled, a callback can be specified as an argument.  The arguments passed
       to the  callback  are,  in  order,  the  user-agent  (a  Mojo::UserAgent  object)  and  the  response  (a
       Mojo::Transaction::HTTP  object).  More  information can be found in the documentation of Mojo::UserAgent
       and Mojo::Transaction::HTTP.

       <b>NOTE:</b> ensure that the event loop Mojo::IOLoop is started when using asynchronous requests.  This  is  not
       needed when using this module inside a Mojolicious app.

       The order of the arguments, except of the first one, does not matter:

           $api-&gt;api_request ('sendMessage', $parameters, $callback);
           $api-&gt;api_request ('sendMessage', $callback, $parameters); # same thing!

   <b>parse_error</b>
           unless (eval { $api-&gt;doSomething(...) }) {
               my $error = $api-&gt;parse_error;
               die "Unknown error: $error-&gt;{msg}" if $error-&gt;{type} eq 'unknown';
               # Handle error gracefully using "type", "msg" and "code" (optional)
           }
           # Or, use it with a custom error message.
           my $error = $api-&gt;parse_error ($message);

       When sandboxing calls to WWW::Telegram::BotAPI methods using "eval", it is useful to parse error messages
       using this method.

       <b>WARNING:</b>  up  until version 0.09, this method incorrectly stopped at the first occurence of "at" in error
       messages, producing results such as "missing ch" instead of "missing chat".

       This method accepts an error message as its first argument, otherwise $@ is used.

       An hash reference containing the following elements is returned:

       •   "type =&gt; unknown|agent|api"

           The source of the error.

           "api" specifies an error originating from Telegram's BotAPI. When "type" is "api", the key "code"  is
           guaranteed to exist.

           "agent"  specifies  an  error  originating from this module's user-agent. This may indicate a network
           issue, a non-200 HTTP response code or any error not related to the API.

           "unknown" specifies an error with no known source.

       •   "msg =&gt; ..."

           The error message.

       •   "code =&gt; ..."

           The error code. <b>This</b> <b>key</b> <b>only</b> <b>exists</b> <b>when</b> <b>"type"</b> <b>is</b> <b>"api"</b>.

   <b>agent</b>
           my $user_agent = $api-&gt;agent;

       Returns the instance of the user-agent used by the module. You can  determine  if  the  module  is  using
       LWP::UserAgent or Mojo::UserAgent by using "isa":

           my $is_lwp = $user_agent-&gt;isa ('LWP::UserAgent');

       <u>USING</u> <u>A</u> <u>PROXY</u>

       Since  all  the  painful  networking  stuff  is delegated to one of the two supported user agents (either
       LWP::UserAgent or Mojo::UserAgent), you can use their built-in support for proxies by accessing the  user
       agent object. An example of how this may look like is the following:

           my $user_agent = $api-&gt;agent;
           if ($user_agent-&gt;isa ('LWP::UserAgent')) {
             # Use LWP::Protocol::connect (for https)
             $user_agent-&gt;proxy ('https', '...');
             # Or if you prefer, load proxy settings from the environment.
             # $user_agent-&gt;env_proxy;
           } else {
             # Mojo::UserAgent (builtin)
             $user_agent-&gt;proxy-&gt;https ('...');
             # Or if you prefer, load proxy settings from the environment.
             # $user_agent-&gt;detect;
           }

       <b>NOTE:</b>  Unfortunately,  Mojo::UserAgent  returns  an  opaque "Proxy connection failed" when something goes
       wrong with the "CONNECT" request made to the proxy. To alleviate this, since version  0.12,  this  module
       prints  the  real reason of failure in debug mode. See "DEBUGGING".  If you need to access the real error
       reason in your code, please  see  issue  #29  on  GitHub  &lt;https://github.com/Robertof/perl-www-telegram-
       botapi/issues/29&gt;.

</pre><h4><b>DEBUGGING</b></h4><pre>
       To  perform  some cool troubleshooting, you can set the environment variable "TELEGRAM_BOTAPI_DEBUG" to a
       true value:

           TELEGRAM_BOTAPI_DEBUG=1 perl script.pl

       This dumps the content of each request and response in a friendly, human-readable way.   It  also  prints
       the  version and the configuration of the module. As a security measure, the bot's token is automatically
       removed from the output of the dump.

       Since version 0.12, enabling this flag also gives more details when a proxy connection fails.

       <b>WARNING:</b> using this option along with an old Mojolicious version (&lt; 6.22) leads to a warning, and  forces
       LWP::UserAgent instead of Mojo::UserAgent. This is because Mojo::JSON used incompatible boolean values up
       to version 6.21, which led to an horrible death of JSON::MaybeXS when serializing the data.

</pre><h4><b>CAVEATS</b></h4><pre>
       When asynchronous mode is enabled, no error handling is performed. You have to do it by yourself as shown
       in the "SYNOPSIS".

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       LWP::UserAgent,  Mojo::UserAgent, &lt;https://core.telegram.org/bots/api&gt;, &lt;https://core.telegram.org/bots&gt;,
       example implementation of a Telegram bot  &lt;https://git.io/vlOK0&gt;,  example  implementation  of  an  async
       Telegram bot &lt;https://git.io/vDrwL&gt;

</pre><h4><b>AUTHOR</b></h4><pre>
       Roberto Frenna (robertof AT cpan DOT org)

</pre><h4><b>BUGS</b></h4><pre>
       Please report any bugs or feature requests to &lt;https://github.com/Robertof/perl-www-telegram-botapi&gt;.

</pre><h4><b>THANKS</b></h4><pre>
       Thanks to the authors of Mojolicious for inspiration about the license and the documentation.

</pre><h4><b>LICENSE</b></h4><pre>
       Copyright (C) 2015, Roberto Frenna.

       This  program  is free software, you can redistribute it and/or modify it under the terms of the Artistic
       License version 2.0.

perl v5.40.0                                       2024-09-13                         <u>WWW::Telegram::<a href="../man3pm/BotAPI.3pm.html">BotAPI</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>