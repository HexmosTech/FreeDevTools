<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Type::Tiny::Manual::UsingWithClassTiny - use of Type::Tiny with Class::Tiny</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libtype-tiny-perl">libtype-tiny-perl_2.004000-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Type::Tiny::Manual::UsingWithClassTiny - use of Type::Tiny with Class::Tiny

</pre><h4><b>MANUAL</b></h4><pre>
       Class::Tiny is an even-smaller-than-Moo class builder.

       Let's translate the classic Horse class from Moo to Class::Tiny.

       Moo:

         package Horse {
           use Moo;
           use Types::Standard qw( Str Num ArrayRef );
           use namespace::autoclean;

           has name       =&gt; ( is =&gt; 'ro', isa =&gt; Str, required =&gt; 1 );
           has gender     =&gt; ( is =&gt; 'ro', isa =&gt; Str );
           has age        =&gt; ( is =&gt; 'rw', isa =&gt; Num );
           has children   =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; ArrayRef,
             default  =&gt; sub { return [] },
           );
         }

       Class::Tiny:

         package Horse {
           use Class::Tiny qw( gender age ), {
             name     =&gt; sub { die "name is required"; },
             children =&gt; sub { return [] },
           };
           use Types::Standard qw( Str Num ArrayRef Dict Optional Slurpy Any Object );
           use Type::Params qw( signature_for );
           use namespace::autoclean;

           # type checks
           signature_for BUILD =&gt; (
             method =&gt; Object,
             named  =&gt; [
               name       =&gt; Str,
               gender     =&gt; Optional[Str],
               age        =&gt; Optional[Num],
               children   =&gt; Optional[ArrayRef],
               ()         =&gt; Slurpy[Any],
             ],
             fallback =&gt; 1,
           );
           signature_for [ 'name', 'gender', 'children' ] =&gt; (
             method     =&gt; Object,
             positional =&gt; [],
           );
           signature_for age =&gt; (
             method     =&gt; Object,
             positional =&gt; [ Optional[Num] ],
           );
         }

       What's going on here?

       Well, Class::Tiny, after it has built a new object, will do this:

         $self-&gt;BUILD($args);

       (Technically, it calls "BUILD" not just for the current class, but for all parent classes too.) We can
       hook onto this in order to check type constraints for the constructor.

       We use "signature_for" from Type::Params to wrap the original "BUILD" method (which doesn't exist, so
       "fallback =&gt; 1" will just assume an empty sub) with a type check for its arguments. The type check is
       just a <b>Dict</b> that checks the class's required and optional attributes and includes <b>Slurpy[Any]</b> at the end
       to be flexible for subclasses adding new attributes.

       Then we wrap the "name", "gender", and "children" methods with checks to make sure they're only being
       called as getters, and we wrap "age", allowing it to be called as a setter with a <b>Num</b>.

       There are also a couple of CPAN modules that can help you out.

   <b>Class::Tiny::ConstrainedAccessor</b>
       Class::Tiny::ConstrainedAccessor creates a "BUILD" and accessors that enforce Type::Tiny constraints.
       Attribute types are passed to Class::Tiny::ConstrainedAccessor; attribute defaults are passed to
       Class::Tiny.

         package Horse {
           use Types::Standard qw( Str Num ArrayRef );
           use Class::Tiny::ConstrainedAccessor {
             name     =&gt; Str,
             gender   =&gt; Str,
             age      =&gt; Num,
             children =&gt; ArrayRef,
           };
           use Class::Tiny qw( gender age ), {
             name     =&gt; sub { die "name is required"; },
             children =&gt; sub { return [] },
           };
         }

   <b>Class::Tiny::Antlers</b>
       Class::Tiny::Antlers provides Moose-like syntax for Class::Tiny, including support for "isa".  You do not
       also need to use Class::Tiny itself.

         package Horse {
           use Class::Tiny::Antlers qw(has);
           use Types::Standard qw( Str Num ArrayRef );
           use namespace::autoclean;

           has name       =&gt; (
             is        =&gt; 'ro',
             isa       =&gt; Str,
             default   =&gt; sub { die "name is required" },
           );
           has gender     =&gt; ( is =&gt; 'ro',    isa =&gt; Str );
           has age        =&gt; ( is =&gt; 'rw',    isa =&gt; Num );
           has children   =&gt; (
             is        =&gt; 'ro',
             isa       =&gt; ArrayRef,
             default   =&gt; sub { return [] },
           );
         }

</pre><h4><b>NEXT</b> <b>STEPS</b></h4><pre>
       Here's your next step:

       â€¢   Type::Tiny::Manual::UsingWithOther

           Using Type::Tiny with Class::InsideOut, Params::Check, and Object::Accessor.

</pre><h4><b>AUTHOR</b></h4><pre>
       Toby Inkster &lt;<a href="mailto:tobyink@cpan.org">tobyink@cpan.org</a>&gt;.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENCE</b></h4><pre>
       This software is copyright (c) 2013-2014, 2017-2023 by Toby Inkster.

       This  is  free  software;  you  can  redistribute  it and/or modify it under the same terms as the Perl 5
       programming language system itself.

</pre><h4><b>DISCLAIMER</b> <b>OF</b> <b>WARRANTIES</b></h4><pre>
       THIS PACKAGE IS PROVIDED "AS IS" AND WITHOUT  ANY  EXPRESS  OR  IMPLIED  WARRANTIES,  INCLUDING,  WITHOUT
       LIMITATION, THE IMPLIED WARRANTIES OF MERCHANTIBILITY AND FITNESS FOR A PARTICULAR PURPOSE.

perl v5.40.1                                       2025-05-06             <u>Type::Tiny::<a href="../man3pm/Man...ngWithClassTiny.3pm.html">Man...ngWithClassTiny</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>