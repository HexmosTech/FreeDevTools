<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Bread::Board::Manual::Example::LogDispatch - An example of composing a dynamic Log::Dispatch object.</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libbread-board-perl">libbread-board-perl_0.37-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Bread::Board::Manual::Example::LogDispatch - An example of composing a dynamic Log::Dispatch object.

</pre><h4><b>VERSION</b></h4><pre>
       version 0.37

</pre><h4><b>SYNOPSIS</b></h4><pre>
         my $c = container 'Logging' =&gt; as {
             service 'Logger' =&gt; (
                 block =&gt; sub {
                     my $s       = shift;
                     my $c       = $s-&gt;parent;
                     my $outputs = $c-&gt;get_sub_container('Outputs');
                     my $log     = Log::Dispatch-&gt;new;
                     foreach my $name ( $outputs-&gt;get_service_list ) {
                         $log-&gt;add(
                             $outputs-&gt;get_service( $name )-&gt;get
                         );
                     }
                     $log;
                 }
             );

             container 'Outputs' =&gt; as {
                 service 'File' =&gt; (
                     block =&gt; sub {
                         Log::Dispatch::File-&gt;new(
                             name      =&gt; 'file',
                             min_level =&gt; 'debug',
                             filename  =&gt; 'logfile'
                         )
                     }
                 );
                 service 'Screen' =&gt; (
                     block =&gt; sub {
                         Log::Dispatch::Screen-&gt;new(
                             name      =&gt; 'screen',
                             min_level =&gt; 'warning',
                         )
                     }
                 );
             };
         };

         my $logger = $c-&gt;resolve( service =&gt; 'Logging/Logger' );

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This example was inspired by a discussion I had with Jay Shirley.  He wanted to know an easy way to have
       a dynamic list of output types for his Log::Dispatch object.

       Often with Bread::Board you will be wiring up components that are of a fixed type and set, but this is
       not always the case.  It is in these cases when you can simply use the Bread::Board objects themselves to
       fetch your dependencies.

       The value passed into the block of a BlockInjection service is the service itself. Calling the "parent"
       method on that service will give you the container that service is in. From there you can introspect the
       other containers and services any which way you want to.

       This example can be made even more dynamic if you build the 'Logging' component as a parameterized
       container whose parameter is the 'Outputs' container. Here is what that would look like.

         my $logging = container 'Logging' =&gt; [ 'Outputs' ] =&gt; as {
             service 'Logger' =&gt; (
                 block =&gt; sub {
                     my $s       = shift;
                     my $c       = $s-&gt;parent;
                     my $outputs = $c-&gt;get_sub_container('Outputs');
                     my $log     = Log::Dispatch-&gt;new;
                     foreach my $name ( $outputs-&gt;get_service_list ) {
                         $log-&gt;add(
                             $outputs-&gt;get_service( $name )-&gt;get
                         );
                     }
                     $log;
                 }
             );
         };

         my $outputs = container 'Outputs' =&gt; as {
             service 'File' =&gt; (
                 block =&gt; sub {
                     Log::Dispatch::File-&gt;new(
                         name      =&gt; 'file',
                         min_level =&gt; 'debug',
                         filename  =&gt; 'logfile'
                     )
                 }
             );
             service 'Screen' =&gt; (
                 block =&gt; sub {
                     Log::Dispatch::Screen-&gt;new(
                         name      =&gt; 'screen',
                         min_level =&gt; 'warning',
                     )
                 }
             );
         };

         my $c  = $logging-&gt;create( Outputs =&gt; $outputs );
         my $ld = $c-&gt;resolve( service =&gt; 'Logging/Logger' );

       This example illustrates how when a parameterized container is instantiated, the parameters become sub-
       containers of the resulting container. This makes it just as easy to fetch the 'Outputs' container and
       use it inside the 'Logger' service.

</pre><h4><b>AUTHOR</b></h4><pre>
       Stevan Little &lt;<a href="mailto:stevan@iinteractive.com">stevan@iinteractive.com</a>&gt;

</pre><h4><b>BUGS</b></h4><pre>
       Please report any bugs or feature requests on the bugtracker website
       https://github.com/stevan/BreadBoard/issues

       When submitting a bug or request, please include a test-file or a patch to an existing test-file that
       illustrates the bug or desired feature.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       This software is copyright (c) 2019, 2017, 2016, 2015, 2014, 2013, 2011, 2009 by Infinity Interactive.

       This is free software; you can redistribute it and/or modify it under the same terms as the Perl 5
       programming language system itself.

perl v5.36.0                                       2022-12-12             <u>Bread::Board::M...le::<a href="../man3pm/LogDispatch.3pm.html">LogDispatch</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>