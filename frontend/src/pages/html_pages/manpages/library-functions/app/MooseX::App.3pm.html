<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>MooseX::App - Write user-friendly command line apps with even less suffering</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libmoosex-app-perl">libmoosex-app-perl_1.43-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       MooseX::App - Write user-friendly command line apps with even less suffering

</pre><h4><b>SYNOPSIS</b></h4><pre>
       In your base class:

         package MyApp;
         use MooseX::App qw(Color);

         option 'global_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Bool',
             documentation =&gt; q[Enable this to do fancy stuff],
         ); # Global option

         has 'private' =&gt; (
             is              =&gt; 'rw',
         ); # not exposed

       Write multiple command classes (If you have only a single command class you should use
       MooseX::App::Simple instead). Packackes in the namespace may be deeply nested.

         package MyApp::SomeCommand;
         use MooseX::App::Command; # important (also imports Moose)
         extends qw(MyApp); # optional, only if you want to use global options from base class

         # Positional parameter
         parameter 'some_parameter' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Str',
             required      =&gt; 1,
             documentation =&gt; q[Some parameter that you need to supply],
         );

         option 'some_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Int',
             required      =&gt; 1,
             documentation =&gt; q[Very important option!],
         ); # Option

         sub run {
             my ($self) = @_;
             # Do something
         }

       And then you need a simple wrapper script (called eg. myapp):

        #!<a href="file:///usr/lib/w3m/cgi-bin/w3mman2html.cgi?env">/usr/bin/env</a> perl
        use MyApp;
        MyApp-&gt;new_with_command-&gt;run;

       On the command line:

        bash$ myapp help
        usage:
            myapp &lt;command&gt; [long options...]
            myapp help

        global options:
            --global_option    Enable this to do fancy stuff [Flag]
            --help --usage -?  Prints this usage information. [Flag]

        available commands:
            some_command    Description of some command
            another_command Description of another command
            help            Prints this usage information

       or

        bash$ myapp some_command --help
        usage:
            myapp some_command &lt;SOME_PARAMETER&gt; [long options...]
            myapp help
            myapp some_command --help

        parameters:
            some_parameter     Some parameter that you need to supply [Required]

        options:
            --global_option    Enable this to do fancy stuff [Flag]
            --some_option      Very important option! [Int,Required]
            --help --usage -?  Prints this usage information. [Flag]

</pre><h4><b>DESCRIPTION</b></h4><pre>
       MooseX-App is a highly customisable helper to write user-friendly command line applications without
       having to worry about most of the annoying things usually involved. Just take any existing Moose class,
       add a single line ("use MooseX-App qw(PluginA PluginB ...);") and create one class for each command in an
       underlying namespace. Options and positional parameters can be defined as simple Moose accessors using
       the "option" and "parameter" keywords respectively.

       MooseX-App will then

       •   Find,  load  and  initialise  the  command  classes (see MooseX::App::Simple for single class/command
           applications)

       •   Create automated help and documentation from modules POD as well  as  attributes  metadata  and  type
           constraints

       •   Read, encode and validate the command line options and positional parameters entered by the user from
           @ARGV and %ENV (and possibly prompt the user for additional parameters see MooseX::App::Plugin::Term)

       •   Provide  helpful error messages if user input cannot be validated (either missing or wrong attributes
           or Moose type constraints not satisfied) or if the user requests help.

       Commandline options are defined using the 'option' keyword which accepts the same  attributes  as  Moose'
       'has' keyword.

         option 'some_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Str',
         );

       This is equivalent to

         has 'some_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Str',
             traits        =&gt; ['AppOption'],   # Load extra metaclass
             cmd_type      =&gt; 'option',        # Set attribute type
         );

       Single  letter  options  are  treated as flags and may be combined with each other.  However such options
       must have a Boolean type constraint.

        option 'verbose' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Bool',
             cmd_flag      =&gt; 'v',
         );

       Positional parameters are defined with the 'parameter' keyword

         parameter 'some_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Str',
         );

       This is equivalent to

         has 'some_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Str',
             traits        =&gt; ['AppOption'],
             cmd_type      =&gt; 'parameter',
         );

       All keywords are imported by Moosex::App (in the app base class) and MooseX::App::Command (in the command
       class) or MooseX::App::Simple (single class application).

       Furthermore, all options and parameters can also be supplied via %ENV

         option 'some_option' =&gt; (
             is            =&gt; 'rw',
             isa           =&gt; 'Str',
             cmd_env       =&gt; 'SOME_OPTION', # sets the env key
         );

       Moose type constraints help MooseX::App to  construct  helpful  error  messages  and  parse  @ARGV  in  a
       meaningful way. The following type constraints are supported:

       •   ArrayRef: Specify multiple values ('--opt value1 --opt value2', also see app_permute and cmd_split)

       •   HashRef: Specify multiple key value pairs ('--opt key=value --opt key2=value2', also see app_permute)

       •   Enum: Display all possibilities

       •   Bool: Flags that do not require values

       •   Int, Num: Used for proper error messages

       Read the Tutorial for getting started with a simple MooseX::App command line application.

</pre><h4><b>METHODS</b></h4><pre>
   <b>new_with_command</b>
        my $myapp_command = MyApp-&gt;new_with_command();

       This  constructor  reads  the  command line arguments and tries to create a command class instance. If it
       fails it returns a MooseX::App::Message::Envelope object holding an error message.

       You can pass a hash of default/fallback params to new_with_command

        my $obj = MyApp-&gt;new_with_command(%default);

       Optionally you can pass a custom ARGV to this constructor

        my $obj = MyApp-&gt;new_with_command( ARGV =&gt; \@myARGV );

       However, if you do so you must take care of propper @ARGV encoding yourself.

   <b>initialize_command_class</b>
        my $obj = MyApp-&gt;initialize_command_class($command_name,%default);

       Helper method to instantiate the command class for the given command.

</pre><h4><b>GLOBAL</b> <b>OPTIONS</b></h4><pre>
       These options may be used to alter the default behaviour of MooseX-App.

   <b>app_base</b>
        app_base 'my_script'; # Defaults to $0

       Usually MooseX::App will take the name of the calling wrapper script to construct  the  program  name  in
       various help messages. This name can be changed via the app_base function.

   <b>app_fuzzy</b>
        app_fuzzy 1; # default
        OR
        app_fuzzy 0;

       Enables fuzzy matching of commands and attributes. Is turned on by default.

   <b>app_strict</b>
        app_strict 0; # default
        OR
        app_strict 1;

       If  strict  is enabled the program will terminate with an error message if superfluous/unknown positional
       parameters are supplied. If disabled all extra parameters will be copied  to  the  extra_argv  attribute.
       Unknown options (with leading dashes) will always yield an error message.

       The  command_strict  config  in  the  command classes allows one to set this option individually for each
       command in the respective command class.

   <b>app_prefer_commandline</b>
        app_prefer_commandline 0; # default
        or
        app_prefer_commandline 1;

       Specifies if parameters/options supplied via @ARGV,%ENV should  take  precedence  over  arguments  passed
       directly to new_with_command.

   <b>app_namespace</b>
        app_namespace 'MyApp::Commands', 'YourApp::MoreCommands';
        OR
        app_namespace();

       Usually  MooseX::App  will  take  the  package name of the base class as the namespace for commands. This
       namespace can be changed and you can add multiple extra namespaces.

       If app_namespace is called with no arguments  then  autoloading  of  command  classes  will  be  disabled
       entirely.

   <b>app_exclude</b>
        app_exclude 'MyApp::Commands::Roles','MyApp::Commands::Utils';

       A sub namespace included via app_namespace (or the default behaviour) can be excluded using app_exclude.

   <b>app_command_name</b>
        app_command_name {
            my ($package_short,$package_full) = @_;
            # munge package name;
            return $command_name;
        };

       This  coderef  can be used to control how autoloaded package names should be translated to command names.
       If this command returns nothing the respective command class will be skipped and not loaded.

   <b>app_command_register</b>
        app_command_register
           do      =&gt; 'MyApp::Commands::DoSomething',
           undo    =&gt; 'MyApp::Commands::UndoSomething';

       This keyword can be  used  to  register  additional  commands.  Especially  useful  in  conjunction  with
       app_namespace and disabled autoloading.

   <b>app_description</b>
        app_description qq[Description text];

       Set  the  app  description  text.  If  not set this information will be taken from the Pod DESCRIPTION or
       OVERVIEW sections. (see command_description to set usage per command)

   <b>app_usage</b>
        app_usage qq[myapp --option ...];

       Set a custom usage text. If not set this will be taken from the Pod SYNOPSIS or USAGE  section.  If  both
       sections  are not available, the usage information will be autogenerated. (see command_usage to set usage
       per command)

   <b>app_permute</b>
        app_permute 0; # default
        OR
        app_permute 1;

       Allows one to specify multiple values with one  key.  So  instead  of  writing  "--list  element1  --list
       element2  --list  element3"  one  might  write "--list element1 element2 element3" for ArrayRef elements.
       HashRef elements may be expressed as "--hash key=value key2=value2".

</pre><h4><b>GLOBAL</b> <b>ATTRIBUTES</b></h4><pre>
       All MooseX::App classes will have two extra attributes

   <b>extra_argv</b>
       Carries all parameters from @ARGV that were not consumed (only if app_strict  is  turned  off,  otherwise
       superfluous parameters will raise an exception).

   <b>help_flag</b>
       Help flag that is set when help was requested.

</pre><h4><b>ATTRIBUTE</b> <b>OPTIONS</b></h4><pre>
       Options and parameters accept extra attributes for customisation:

       •   cmd_tags - Extra tags (as used by the help)

       •   cmd_flag - Override option/parameter name

       •   cmd_aliases - Additional option/parameter name aliases

       •   cmd_split - Split values into ArrayRefs on this token or RegEx

       •   cmd_position - Specify option/parameter order in help

       •   cmd_env - Read options/parameters from %ENV

       •   cmd_count - Value of option equals to number of occurrences in @ARGV

       •   cmd_negate - Adds an option to negate boolean flags

       Refer to MooseX::App::Meta::Role::Attribute::Option for detailed documentation.

</pre><h4><b>METADATA</b></h4><pre>
       MooseX::App  will  use your class metadata and POD to construct the commands and helpful error- or usage-
       messages. These bits of information are utilised and should be provided if possible:

       •   Package names

       •   required options for Moose attributes

       •   documentation options for Moose attributes

       •   Moose type constraints (Bool, ArrayRef, HashRef, Int, Num, and Enum)

       •   Documentation set via app_description, app_usage, command_short_description, command_long_description
           and command_usage

       •   POD (NAME, ABSTRACT, DESCRIPTION,  USAGE,  SYNOPSIS,  OVERVIEW,  COPYRIGHT,  LICENSE,  COPYRIGHT  AND
           LICENSE, AUTHOR and AUTHORS sections)

       •   Dist::Zilla ABSTRACT tag if no POD is available yet

</pre><h4><b>PLUGINS</b></h4><pre>
       The  behaviour  of MooseX-App can be customised with plugins. To load a plugin just pass a list of plugin
       names after the "use MooseX-App" statement.  (Attention: order sometimes matters)

        use MooseX::App qw(PluginA PluginB);

       Currently the following plugins are shipped with MooseX::App

       •   MooseX::App::Plugin::BashCompletion

           Adds a command that generates a bash  completion  script  for  your  application.   See  third  party
           MooseX::App::Plugin::ZshCompletion for Z shell completion.

       •   MooseX::App::Plugin::Color

           Colorful output for your MooseX::App applications.

       •   MooseX::App::Plugin::Config

           Config files for MooseX::App applications.

       •   MooseX::App::Plugin::ConfigHome

           Try to find config files in users home directory.

       •   MooseX::App::Plugin::Term

           Prompt  user  for  options and parameters that were not provided via options or params. Prompt offers
           basic editing capabilities and non-persistent history.

       •   MooseX::App::Plugin::Typo

           Handle typos in command names and provide suggestions.

       •   MooseX::App::Plugin::Version

           Adds a command to display the version and license of your application.

       •   MooseX::App::Plugin::Man

           Display full manpage of application and commands.

       •   MooseX::App::Plugin::MutexGroup

           Allow for mutally exclusive options.

       •   MooseX::App::Plugin::Depends

           Adds dependent options.

       Refer to Writing MooseX-App Plugins for documentation on how to create your own plugins.

</pre><h4><b>DEVELOPMENT</b></h4><pre>
       Make sure to invoke your script with APP_DEVELOPER=1 ser during development. This will come with a starup
       penalty but perform additional checks for detecting wrong attribute/type  constraint  combinations,  name
       clashes, ...

</pre><h4><b>CAVEATS</b> <b>&amp;</b> <b>KNOWN</b> <b>BUGS</b></h4><pre>
       Startup  time  may  be  an  issue  -  escpecially  if  you  load  many plugins. If you do not require the
       functionality of plugins and ability for fine grained customisation (or Moose for that matter)  then  you
       should probably use MooX::Options or MooX::Cmd.

       In  some  cases  -  especially  when  using  non-standard class inheritance - you may end up with command
       classes lacking the help attribute. In this case you need to include the  following  line  in  your  base
       class or command classes.

        with qw(MooseX::App::Role::Common);

       When  manually  registering  command classes (eg. via app_command_register) in multiple base classes with
       different sets of plugins (why would you ever want to do that?),  then  meta  attributes  may  lack  some
       attribute metaclasses. In this case you need to load the missing attribute traits explicitly:

        option 'argument' =&gt; (
           depends =&gt; 'otherargument',
           trait   =&gt; ['MooseX::App::Plugin::Depends::Meta::Attribute'], # load trait
        );

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Read the Tutorial for getting started with a simple MooseX::App command line application.

       For alternatives you can check out

       MooseX::App::Cmd, MooseX::Getopt, MooX::Options, MooX::Cmd and App::Cmd

</pre><h4><b>SUPPORT</b></h4><pre>
       Please  report  any bugs or feature requests via &lt;https://github.com/maros/MooseX-App/issues/new&gt;. I will
       be notified, and then you'll automatically be notified of progress on your report as I make changes.

</pre><h4><b>AUTHOR</b></h4><pre>
           Maroš Kollár
           CPAN ID: MAROS
           maros [at] k-1.com

           <a href="http://www.k-1.com">http://www.k-1.com</a>

</pre><h4><b>CONTRIBUTORS</b></h4><pre>
       Special thanks to all contributors.

       In no particular order: Andrew Jones, George Hartzell, Steve Nolte, Michael G,  Thomas  Klausner,  Yanick
       Champoux,  Edward Baudrez, David Golden, J.R. Mash, Thilo Fester, Gregor Herrmann, Sergey Romanov, Sawyer
       X, Roman F., Hunter McMillen, Maik Hentsche, Alexander Stoddard, Marc Logghe,  Tina  Müller,  Lisa  Hare,
       Jose Luis Martinez, Frank Schreiner, Bernhard M. Wiedemann, Petr Pisar, William Barker

       You   are   more   than   welcome   to   contribute   to   MooseX-App.   Please   have   a  look  at  the
       &lt;https://github.com/maros/MooseX-App/issues?q=is%3Aissue+is%3Aopen+label%3AWishlist&gt;   list    of    open
       wishlist issues for ideas.

</pre><h4><b>COPYRIGHT</b></h4><pre>
       MooseX::App is Copyright (c) 2012-23 Maroš Kollár.

       This  library  is free software and may be distributed under the same terms as perl itself. The full text
       of the licence can be found in the LICENCE file included with this module.

perl v5.36.0                                       2023-10-22                                   <u>MooseX::<a href="../man3pm/App.3pm.html">App</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>