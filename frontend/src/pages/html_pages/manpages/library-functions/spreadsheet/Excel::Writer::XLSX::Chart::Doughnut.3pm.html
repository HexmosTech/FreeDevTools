<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Doughnut - A class for writing Excel Doughnut charts.</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libexcel-writer-xlsx-perl">libexcel-writer-xlsx-perl_1.11-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Doughnut - A class for writing Excel Doughnut charts.

</pre><h4><b>SYNOPSIS</b></h4><pre>
       To create a simple Excel file with a Doughnut chart using Excel::Writer::XLSX:

           #!<a href="file:///usr/lib/w3m/cgi-bin/w3mman2html.cgi?perl">/usr/bin/perl</a>

           use strict;
           use warnings;
           use Excel::Writer::XLSX;

           my $workbook  = Excel::Writer::XLSX-&gt;new( 'chart.xlsx' );
           my $worksheet = $workbook-&gt;add_worksheet();

           my $chart     = $workbook-&gt;add_chart( type =&gt; 'doughnut' );

           # Configure the chart.
           $chart-&gt;add_series(
               categories =&gt; '=Sheet1!$A$2:$A$7',
               values     =&gt; '=Sheet1!$B$2:$B$7',
           );

           # Add the worksheet data the chart refers to.
           my $data = [
               [ 'Category', 2, 3, 4, 5, 6, 7 ],
               [ 'Value',    1, 4, 5, 2, 1, 5 ],
           ];

           $worksheet-&gt;write( 'A1', $data );

           __END__

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This module implements Doughnut charts for Excel::Writer::XLSX. The chart object is created via the
       Workbook "add_chart()" method:

           my $chart = $workbook-&gt;add_chart( type =&gt; 'doughnut' );

       Once the object is created it can be configured via the following methods that are common to all chart
       classes:

           $chart-&gt;add_series();
           $chart-&gt;set_title();

       These methods are explained in detail in Excel::Writer::XLSX::Chart. Class specific methods or settings,
       if any, are explained below.

</pre><h4><b>Doughnut</b> <b>Chart</b> <b>Methods</b></h4><pre>
   <b>set_rotation()</b>
       The "set_rotation()" method is used to set the rotation of the first segment of a Pie/Doughnut chart.
       This has the effect of rotating the entire chart:

           $chart-&gt;set_rotation( 90 );

       The angle of rotation must be "0 &lt;= rotation &lt;= 360".

   <b>set_hole_size()</b>
       The "set_hole_size()" method is used to set the hole size of a Doughnut chart:

           $chart-&gt;set_hole_size( 33 );

       The the hole size must be a percentage in the range  "10 &lt;= size &lt;= 90".

   <b>User</b> <b>defined</b> <b>colors</b>
       It is possible to define chart colors for most types of Excel::Writer::XLSX charts via the <b>add_series()</b>
       method. However, Pie/Doughnut charts are a special case since each segment is represented as a point so
       it is necessary to assign formatting to each point in the series:

           $chart-&gt;add_series(
               values =&gt; '=Sheet1!$A$1:$A$3',
               points =&gt; [
                   { fill =&gt; { color =&gt; '#FF0000' } },
                   { fill =&gt; { color =&gt; '#CC0000' } },
                   { fill =&gt; { color =&gt; '#990000' } },
               ],
           );

       See the main Excel::Writer::XLSX::Chart documentation for more details.

       Doughnut charts support leader lines:

           $chart-&gt;add_series(
               name        =&gt; 'Doughnut sales data',
               categories  =&gt; [ 'Sheet1', 1, 3, 0, 0 ],
               values      =&gt; [ 'Sheet1', 1, 3, 1, 1 ],
               data_labels =&gt; {
                   series_name  =&gt; 1,
                   percentage   =&gt; 1,
                   leader_lines =&gt; 1,
                   position     =&gt; 'outside_end'
               },
           );

       Note: Even when leader lines are turned on they aren't automatically visible in Excel or
       Excel::Writer::XLSX. Due to an Excel limitation (or design) leader lines only appear if the data label is
       moved manually or if the data labels are very close and need to be adjusted automatically.

   <b>Unsupported</b> <b>Methods</b>
       A Doughnut chart doesn't have an X or Y axis so the following common chart methods are ignored.

           $chart-&gt;set_x_axis();
           $chart-&gt;set_y_axis();

</pre><h4><b>EXAMPLE</b></h4><pre>
       Here is a complete example that demonstrates most of the available features when creating a chart.

           #!<a href="file:///usr/lib/w3m/cgi-bin/w3mman2html.cgi?perl">/usr/bin/perl</a>

           use strict;
           use warnings;
           use Excel::Writer::XLSX;

           my $workbook  = Excel::Writer::XLSX-&gt;new( 'chart_doughnut.xlsx' );
           my $worksheet = $workbook-&gt;add_worksheet();
           my $bold      = $workbook-&gt;add_format( bold =&gt; 1 );

           # Add the worksheet data that the charts will refer to.
           my $headings = [ 'Category', 'Values' ];
           my $data = [
               [ 'Glazed', 'Chocolate', 'Cream' ],
               [ 50,       35,          15      ],
           ];

           $worksheet-&gt;write( 'A1', $headings, $bold );
           $worksheet-&gt;write( 'A2', $data );

           # Create a new chart object. In this case an embedded chart.
           my $chart = $workbook-&gt;add_chart( type =&gt; 'doughnut', embedded =&gt; 1 );

           # Configure the series. Note the use of the array ref to define ranges:
           # [ $sheetname, $row_start, $row_end, $col_start, $col_end ].
           $chart-&gt;add_series(
               name       =&gt; 'Doughnut sales data',
               categories =&gt; [ 'Sheet1', 1, 3, 0, 0 ],
               values     =&gt; [ 'Sheet1', 1, 3, 1, 1 ],
           );

           # Add a title.
           $chart-&gt;set_title( name =&gt; 'Popular Doughnut Types' );

           # Set an Excel chart style. Colors with white outline and shadow.
           $chart-&gt;set_style( 10 );

           # Insert the chart into the worksheet (with an offset).
           $worksheet-&gt;insert_chart( 'C2', $chart, 25, 10 );

           __END__

</pre><h4><b>AUTHOR</b></h4><pre>
       John McNamara <a href="mailto:jmcnamara@cpan.org">jmcnamara@cpan.org</a>

</pre><h4><b>COPYRIGHT</b></h4><pre>
       Copyright MM-MMXXIII, John McNamara.

       All Rights Reserved. This module is free software. It may be used, redistributed and/or modified under
       the same terms as Perl itself.

perl v5.36.0                                       2023-04-12             <u>Excel::Writer::...Chart::<a href="../man3pm/Doughnut.3pm.html">Doughnut</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>