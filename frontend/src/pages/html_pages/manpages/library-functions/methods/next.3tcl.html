<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>next, nextto - invoke superclass method implementations</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/tcl9.0-doc">tcl9.0-doc_9.0.1+dfsg-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       next, nextto - invoke superclass method implementations

</pre><h4><b>SYNOPSIS</b></h4><pre>
       package require tcl::oo

       <b>next</b> ?<u>arg</u> <u>...</u>?
       <b>nextto</b> <u>class</u> ?<u>arg</u> <u>...</u>?
________________________________________________________________________________________________________________

</pre><h4><b>DESCRIPTION</b></h4><pre>
       The  <b>next</b>  command  is  used to call implementations of a method by a class, superclass or mixin that are
       overridden by the current method. It can only be used from within  a  method.  It  is  also  used  within
       filters  to  indicate  the point where a filter calls the actual implementation (the filter may decide to
       not go along the chain, and may process the results of going along the chain of methods as  it  chooses).
       The  result  of  the  <b>next</b>  command is the result of the next method in the method chain; if there are no
       further methods in the method chain, the result of <b>next</b> will be an error. The arguments, <u>arg</u>, to <b>next</b> are
       the arguments to pass to the next method in the chain.

       The <b>nextto</b> command is the same as the <b>next</b> command, except that it takes  an  additional  <u>class</u>  argument
       that identifies a class whose implementation of the current method chain (see <b>info</b> <b>object</b> <b>call</b>) should be
       used;  the  method implementation selected will be the one provided by the given class, and it must refer
       to an existing non-filter invocation that lies further along the chain than the current implementation.

</pre><h4><b>THE</b> <b>METHOD</b> <b>CHAIN</b></h4><pre>
       When a method of an object is invoked, things happen in several stages:

       [1]    The structure of the object, its class, superclasses, filters, and mixins, are examined to build a
              <u>method</u> <u>chain</u>, which contains a list of method implementations to invoke.

       [2]    The first method implementation on the chain is invoked.

       [3]    If that method implementation invokes the <b>next</b> command, the next method implementation is  invoked
              (with its arguments being those that were passed to <b>next</b>).

       [4]    The  result  from  the overall method call is the result from the outermost method implementation;
              inner method implementations return their results through <b>next</b>.

       [5]    The method chain is cached for future use.

   <b>METHOD</b> <b>SEARCH</b> <b>ORDER</b>
       When constructing the method chain, method implementations are searched for in the following order:

       [1]    In the classes mixed into the object, in class traversal order. The list of mixins is  checked  in
              natural order.

       [2]    In  the  classes mixed into the classes of the object, with sources of mixing in being searched in
              class traversal order. Within each class, the list of mixins is processed in natural order.

       [3]    In the object itself.

       [4]    In the object's class.

       [5]    In the superclasses of the class, following each  superclass  in  a  depth-first  fashion  in  the
              natural order of the superclass list.

       Any  particular  method  implementation  always comes as <u>late</u> in the resulting list of implementations as
       possible; this means that if some class, A, is both mixed into a class, B, and is also a superclass of B,
       the instances of B will always treat A as a superclass from the perspective of inheritance.  This is true
       even when the multiple inheritance is processed indirectly.

   <b>FILTERS</b>
       When an object has a list of filter names set upon it, or is an instance of a class (or has  mixed  in  a
       class) that has a list of filter names set upon it, before every invocation of any method the filters are
       processed.  Filter  implementations  are found in class traversal order, as are the lists of filter names
       (each of which is traversed in natural list order). Explicitly invoking a method used as  a  filter  will
       cause that method to be invoked twice, once as a filter and once as a normal method.

       Each  filter  should  decide  for  itself  whether  to  permit  the execution to go forward to the proper
       implementation of the method (which it does by invoking the <b>next</b> command as filters are inserted into the
       front of the method call chain) and is responsible for returning the result of <b>next</b>.

       Filters are invoked when processing an invocation of the <b>unknown</b> method because of a failure to locate  a
       method  implementation,  but <u>not</u> when invoking either constructors or destructors. (Note however that the
       <b>destroy</b> method is a conventional method, and filters are invoked as normal when it is called.)

</pre><h4><b>EXAMPLES</b></h4><pre>
       This example demonstrates how to use the <b>next</b> command to call  the  (super)class's  implementation  of  a
       method. The script:

              oo::class create theSuperclass {
                  method example {args} {
                      puts "in the superclass, args = $args"
                  }
              }

              oo::class create theSubclass {
                  superclass theSuperclass
                  method example {args} {
                      puts "before chaining from subclass, args = $args"
                      <b>next</b> a {*}$args b
                      <b>next</b> pureSynthesis
                      puts "after chaining from subclass"
                  }
              }

              theSubclass create obj
              oo::objdefine obj method example args {
                  puts "per-object method, args = $args"
                  <b>next</b> x {*}$args y
                  <b>next</b>
              }
              obj example 1 2 3

       prints the following:

              per-object method, args = 1 2 3
              before chaining from subclass, args = x 1 2 3 y
              in the superclass, args = a x 1 2 3 y b
              in the superclass, args = pureSynthesis
              after chaining from subclass
              before chaining from subclass, args =
              in the superclass, args = a b
              in the superclass, args = pureSynthesis
              after chaining from subclass

       This  example  demonstrates  how to build a simple cache class that applies memoization to all the method
       calls of the objects it is mixed into, and shows how it can make a difference to computation times:

              oo::class create cache {
                  filter Memoize
                  method Memoize args {
                      <u>#</u> <u>Do</u> <u>not</u> <u>filter</u> <u>the</u> <u>core</u> <u>method</u> <u>implementations</u>
                      if {[lindex [self target] 0] eq "::oo::object"} {
                          return [<b>next</b> {*}$args]
                      }

                      <u>#</u> <u>Check</u> <u>if</u> <u>the</u> <u>value</u> <u>is</u> <u>already</u> <u>in</u> <u>the</u> <u>cache</u>
                      my variable ValueCache
                      set key [self target],$args
                      if {[info exist ValueCache($key)]} {
                          return $ValueCache($key)
                      }

                      <u>#</u> <u>Compute</u> <u>value,</u> <u>insert</u> <u>into</u> <u>cache,</u> <u>and</u> <u>return</u> <u>it</u>
                      return [set ValueCache($key) [<b>next</b> {*}$args]]
                  }

                  method flushCache {} {
                      my variable ValueCache
                      unset ValueCache
                      <u>#</u> <u>Skip</u> <u>the</u> <u>caching</u>
                      return -level 2 ""
                  }
              }

              oo::object create demo
              oo::objdefine demo {
                  mixin cache

                  method compute {a b c} {
                      after 3000 <u>;#</u> <u>Simulate</u> <u>deep</u> <u>thought</u>
                      return [expr {$a + $b * $c}]
                  }

                  method compute2 {a b c} {
                      after 3000 <u>;#</u> <u>Simulate</u> <u>deep</u> <u>thought</u>
                      return [expr {$a * $b + $c}]
                  }
              }

              puts [demo compute  1 2 3]      <u>→</u> <u>prints</u> <u>"7"</u> <u>after</u> <u>delay</u>
              puts [demo compute2 4 5 6]      <u>→</u> <u>prints</u> <u>"26"</u> <u>after</u> <u>delay</u>
              puts [demo compute  1 2 3]      <u>→</u> <u>prints</u> <u>"7"</u> <u>instantly</u>
              puts [demo compute2 4 5 6]      <u>→</u> <u>prints</u> <u>"26"</u> <u>instantly</u>
              puts [demo compute  4 5 6]      <u>→</u> <u>prints</u> <u>"34"</u> <u>after</u> <u>delay</u>
              puts [demo compute  4 5 6]      <u>→</u> <u>prints</u> <u>"34"</u> <u>instantly</u>
              puts [demo compute  1 2 3]      <u>→</u> <u>prints</u> <u>"7"</u> <u>instantly</u>
              demo flushCache
              puts [demo compute  1 2 3]      <u>→</u> <u>prints</u> <u>"7"</u> <u>after</u> <u>delay</u>

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       oo::<a href="../man3tcl/class.3tcl.html">class</a>(3tcl), oo::<a href="../man3tcl/define.3tcl.html">define</a>(3tcl), oo::<a href="../man3tcl/object.3tcl.html">object</a>(3tcl), <a href="../man3tcl/self.3tcl.html">self</a>(3tcl)

</pre><h4><b>KEYWORDS</b></h4><pre>
       call, method, method chain

TclOO                                                  0.1                                            <u><a href="../man3tcl/next.3tcl.html">next</a></u>(3tcl)
</pre>
 </div>
</div></section>
</div>
</body>
</html>