<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Dancer2::Manual::Testing - Writing tests for Dancer2</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libdancer2-perl">libdancer2-perl_1.1.2+dfsg-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Dancer2::Manual::Testing - Writing tests for Dancer2

</pre><h4><b>VERSION</b></h4><pre>
       version 1.1.2

</pre><h4><b>Basic</b> <b>application</b> <b>testing</b></h4><pre>
       Since Dancer2 produces PSGI applications, you can easily write tests using Plack::Test and provide your
       Dancer application as the app for testing.

       A basic test (which we also scaffold with dancer2) looks like this:

           use strict;
           use warnings;

           use Test::More tests =&gt; 4;
           use Plack::Test;
           use HTTP::Request::Common;

           use_ok('MyApp');

           # create an application
           my $app = MyApp-&gt;to_app;
           isa_ok( $app, 'CODE' );

           # create a testing object
           my $test = Plack::Test-&gt;create($app);

           # now you can call requests on it and get responses
           # requests are of HTTP::Request
           # responses are of HTTP::Response

           # "GET" from HTTP::Request::Common creates an HTTP::Request object
           my $response = $test-&gt;request( GET '/' );

           # same as:
           # my $response = $test-&gt;request( HTTP::Request-&gt;new( GET =&gt; '/' ) );

           ok( $response-&gt;is_success, 'Successful request' );
           is( $response-&gt;content, 'OK', 'Correct response content' );

       Read the documentation for HTTP::Request and HTTP::Request::Common to see the different options for
       sending parameters.

</pre><h4><b>Cookies</b></h4><pre>
       If you don't want to use an entire user agent for this test, you can use HTTP::Cookies to store cookies
       and then retrieve them:

           use strict;
           use warnings;

           use Test::More tests =&gt; 3;
           use Plack::Test;
           use HTTP::Request::Common;
           use HTTP::Cookies;

           use_ok('MyApp');

           my $url  = '<a href="http://localhost">http://localhost</a>';
           my $jar  = HTTP::Cookies-&gt;new();
           my $test = Plack::Test-&gt;create( MyApp-&gt;to_app );

           subtest 'Create session' =&gt; sub {
               my $res = $test-&gt;request( GET "$url/login" );
               ok( $res-&gt;is_success, 'Successful login' );

               # extract cookies from the response and store in the jar
               $jar-&gt;extract_cookies($res);
           };

           subtest 'Check session' =&gt; sub {
               my $req = GET "$url/logout";

               # add cookies to the request
               $jar-&gt;add_cookie_header($req);

               my $res = $test-&gt;request($req);
               ok( $res-&gt;is_success, 'Successful logout' );
               like(
                   $res-&gt;content,
                   'Successfully logged out',
                   'Got correct log out content',
               );
           };

       Please note that the request URL must include scheme and host for the call to "add_cookie_header" in
       HTTP::Cookies to work.

</pre><h4><b>Plugins</b></h4><pre>
       In order to test plugins, you can create an application on the spot, as part of the test script code, and
       use the plugin there.

           use strict;
           use warnings;

           use Test::More tests =&gt; 2;
           use Plack::Test;
           use HTTP::Request::Common;

           {
               package MyTestApp;
               use Dancer2;
               use Dancer2::Plugin::MyPlugin;

               get '/' =&gt; sub { my_keyword };
           }

           my $test = Plack::Test-&gt;create( MyTestApp-&gt;to_app );
           my $res  = $test-&gt;request( GET '/' );

           ok( $res-&gt;is_success, 'Successful request' );
           is( $res-&gt;content, 'MyPlugin-MyKeyword', 'Correct content' );

</pre><h4><b>AUTHOR</b></h4><pre>
       Dancer Core Developers

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       This software is copyright (c) 2024 by Alexis Sukrieh.

       This is free software; you can redistribute it and/or modify it under the same terms as the Perl 5
       programming language system itself.

perl v5.40.0                                       2024-12-08                      <u>Dancer2::Manual::<a href="../man3pm/Testing.3pm.html">Testing</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>