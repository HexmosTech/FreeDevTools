<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Net::Daemon - Perl extension for portable daemons</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libnet-daemon-perl">libnet-daemon-perl_0.49-3_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Net::Daemon - Perl extension for portable daemons

</pre><h4><b>SYNOPSIS</b></h4><pre>
         # Create a subclass of Net::Daemon
         require Net::Daemon;
         package MyDaemon;
         @MyDaemon::ISA = qw(Net::Daemon);

         sub Run ($) {
           # This function does the real work; it is invoked whenever a
           # new connection is made.
         }

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Net::Daemon is an abstract base class for implementing portable server applications in a very simple way.
       The module is designed for Perl 5.005 and threads, but can work with <b>fork()</b> and Perl 5.004.

       The Net::Daemon class offers methods for the most common tasks a daemon needs: Starting up, logging,
       accepting clients, authorization, restricting its own environment for security and doing the true work.
       You only have to override those methods that aren't appropriate for you, but typically inheriting will
       safe you a lot of work anyways.

   <b>Constructors</b>
         $server = Net::Daemon-&gt;new($attr, $options);

         $connection = $server-&gt;Clone($socket);

       Two constructors are available: The <b>new</b> method is called upon startup and creates an object that will
       basically act as an anchor over the complete program. It supports command line parsing via Getopt::Long
       (3).

       Arguments of <b>new</b> are <u>$attr</u>, an hash ref of attributes (see below) and <u>$options</u> an array ref of options,
       typically command line arguments (for example <b>\@ARGV</b>) that will be passed to <b>Getopt::Long::GetOptions</b>.

       The second constructor is <b>Clone</b>: It is called whenever a client connects. It receives the main server
       object as input and returns a new object. This new object will be passed to the methods that finally do
       the true work of communicating with the client. Communication occurs over the socket <b>$socket</b>, <b>Clone</b>'s
       argument.

       Possible object attributes and the corresponding command line arguments are:

       <u>catchint</u> (<b>--nocatchint</b>)
           On some systems, in particular Solaris, the functions <b>accept()</b>, <b>read()</b> and so on are not safe against
           interrupts  by  signals.  For  example, if the user raises a USR1 signal (as typically used to reread
           config files), then the function returns an error EINTR.  If the <u>catchint</u> option is on (by default it
           is, use <b>--nocatchint</b> to turn this off), then the package will ignore EINTR errors whereever possible.

       <u>chroot</u> (<b>--chroot=dir</b>)
           (UNIX only)  After doing a <b>bind()</b>, change root directory to the given directory by doing a  <b>chroot()</b>.
           This  is  usefull  for  security  operations,  but  it  restricts programming a lot. For example, you
           typically have to load external Perl extensions before doing a <b>chroot()</b>, or you need to  create  hard
           links  to  Unix  sockets. This is typically done in the config file, see the --configfile option. See
           also the --group and --user options.

           If you don't know <b>chroot()</b>, think of an FTP server where you can see a certain  directory  tree  only
           after logging in.

       <u>clients</u>
           An  array  ref  with  a  list of clients. Clients are hash refs, the attributes <u>accept</u> (0 for denying
           access and 1 for permitting) and <u>mask</u>, a Perl regular expression for the clients  IP  number  or  its
           host name. See "Access control" below.

       <u>configfile</u> (<b>--configfile=file</b>)
           Net::Daemon  supports  the  use of config files. These files are assumed to contain a single hash ref
           that overrides the arguments of the  new  method.  However,  command  line  arguments  in  turn  take
           precedence over the config file. See the "Config File" section below for details on the config file.

       <u>debug</u> (<b>--debug</b>)
           Turn debugging mode on. Mainly this asserts that logging messages of level "debug" are created.

       <u>facility</u> (<b>--facility=mode</b>)
           (UNIX only) Facility to use for Sys::Syslog (3). The default is <b>daemon</b>.

       <u>group</u> (<b>--group=gid</b>)
           After  doing  a <b>bind()</b>, change the real and effective GID to the given.  This is usefull, if you want
           your server to bind to a privileged port (&lt;1024), but don't want the server to execute as  root.  See
           also the --user option.

           GID's can be passed as group names or numeric values.

       <u>localaddr</u> (<b>--localaddr=ip</b>)
           By  default  a  daemon  is  listening  to  any IP number that a machine has. This attribute allows to
           restrict the server to the given IP number.

       <u>localpath</u> (<b>--localpath=path</b>)
           If you want to restrict your server to local services only, you'll  prefer  using  Unix  sockets,  if
           available.  In  that  case  you  can  use  this  option for setting the path of the Unix socket being
           created.  This option implies <b>--proto=unix</b>.

       <u>localport</u> (<b>--localport=port</b>)
           This attribute sets the port on which the daemon is listening. It must be given somehow,  as  there's
           no default.

       <u>logfile</u> (<b>--logfile=file</b>)
           By default logging messages will be written to the syslog (Unix) or to the event log (Windows NT). On
           other  operating systems you need to specify a log file. The special value "STDERR" forces logging to
           stderr.

       <u>loop-child</u> (<b>--loop-child</b>)
           This option forces creation of a new child for loops. (See the <u>loop-timeout</u> option.) By  default  the
           loops are serialized.

       <u>loop-timeout</u> (<b>--loop-timeout=secs</b>)
           Some  servers need to take an action from time to time. For example the Net::Daemon::Spooler attempts
           to empty its spooling queue every 5 minutes. If this option is set to a positive  value  (zero  being
           the default), then the server will call its Loop method every "loop-timeout" seconds.

           Don't  trust  too  much  on  the  precision  of  the  interval: It depends on a number of factors, in
           particular the execution time of the <b>Loop()</b> method. The loop  is  implemented  by  using  the  <u>select</u>
           function.  If  you  need  an  exact interval, you should better try to use the <b>alarm()</b> function and a
           signal handler. (And don't forget to look at the <u>catchint</u> option!)

           It is recommended to use the <u>loop-child</u> option in conjunction with <u>loop-timeout</u>.

       <u>mode</u> (<b>--mode=modename</b>)
           The Net::Daemon server can run in three different modes, depending on the environment.

           If you are running Perl 5.005 and did compile it for threads, then  the  server  will  create  a  new
           thread  for  each  connection.  The thread will execute the server's <b>Run()</b> method and then terminate.
           This mode is the default, you can force it with "--mode=ithreads" or "--mode=threads".

           If threads are not available, but you have a working <b>fork()</b>, then the server will behave  similar  by
           creating  a  new process for each connection.  This mode will be used automatically in the absence of
           threads or if you use the "--mode=fork" option.

           Finally there's a single-connection mode: If the server has accepted a connection, he will enter  the
           <b>Run()</b>  method.  No other connections are accepted until the <b>Run()</b> method returns. This operation mode
           is useful if you have neither threads nor <b>fork()</b>,  for  example  on  the  Macintosh.   For  debugging
           purposes you can force this mode with "--mode=single".

           When  running  in mode single, you can still handle multiple clients at a time by preforking multiple
           child processes. The number of childs is configured with the option "--childs".

       <u>childs</u>
           Use this parameter to let Net::Daemon run in prefork mode, which means it forks the number of  childs
           processes you give with this parameter, and all child handle connections concurrently. The difference
           to  fork  mode is, that the child processes continue to run after a connection has terminated and are
           able to accept a new connection.  This  is  useful  for  caching  inside  the  childs  process  (e.g.
           DBI::ProxyServer connect_cached attribute)

       <u>options</u>
           Array ref of Command line options that have been passed to the server object via the <b>new</b> method.

       <u>parent</u>
           When  creating an object with <b>Clone</b> the original object becomes the parent of the new object. Objects
           created with <b>new</b> usually don't have a parent, thus this attribute is not set.

       <u>pidfile</u> (<b>--pidfile=file</b>)
           (UNIX only) If this option is present, a PID file will be created at the given location.

       <u>proto</u> (<b>--proto=proto</b>)
           The transport layer to use, by default <u>tcp</u> or <u>unix</u> for a Unix socket.  It  is  not  yet  possible  to
           combine both.

       <u>socket</u>
           The  socket  that  is connected to the client; passed as <b>$client</b> argument to the <b>Clone</b> method. If the
           server object was created with <b>new</b>, this attribute can be undef, as long as  the  <b>Bind</b>  method  isn't
           called.  Sockets are assumed to be IO::Socket objects.

       <u>user</u> (<b>--user=uid</b>)
           After  doing  a <b>bind()</b>, change the real and effective UID to the given.  This is usefull, if you want
           your server to bind to a privileged port (&lt;1024), but don't want the server to execute as  root.  See
           also the --group and the --chroot options.

           UID's can be passed as group names or numeric values.

       <u>version</u> (<b>--version</b>)
           Supresses  startup  of  the  server; instead the version string will be printed and the program exits
           immediately.

       Note that most  of  these  attributes  (facility,  mode,  localaddr,  localport,  pidfile,  version)  are
       meaningfull only at startup. If you set them later, they will be simply ignored. As almost all attributes
       have appropriate defaults, you will typically use the <b>localport</b> attribute only.

   <b>Command</b> <b>Line</b> <b>Parsing</b>
         my $optionsAvailable = Net::Daemon-&gt;Options();

         print Net::Daemon-&gt;Version(), "\n";

         Net::Daemon-&gt;Usage();

       The  <b>Options</b>  method returns a hash ref of possible command line options.  The keys are option names, the
       values are again hash refs with the following keys:

       template
           An option template that can be passed to <b>Getopt::Long::GetOptions</b>.

       description
           A description of this option, as used in <b>Usage</b>

       The <b>Usage</b> method prints a list of all possible options and returns.   It  uses  the  <b>Version</b>  method  for
       printing program name and version.

   <b>Config</b> <b>File</b>
       If the config file option is set in the command line options or in the in the "new" args, then the method

         $server-&gt;ReadConfigFile($file, $options, $args)

       is  invoked.  By  default  the  config file is expected to contain Perl source that returns a hash ref of
       options. These options override the "new" args and will in  turn  be  overwritten  by  the  command  line
       options, as present in the $options hash ref.

       A typical config file might look as follows, we use the DBI::ProxyServer as an example:

           # Load external modules; this is not required unless you use
           # the chroot() option.
           #require DBD::mysql;
           #require DBD::CSV;

           {
               # 'chroot' =&gt; '/var/dbiproxy',
               'facility' =&gt; 'daemon',
               'pidfile' =&gt; '/var/dbiproxy/dbiproxy.pid',
               'user' =&gt; 'nobody',
               'group' =&gt; 'nobody',
               'localport' =&gt; '1003',
               'mode' =&gt; 'fork'

               # Access control
               'clients' =&gt; [
                   # Accept the local
                   {
                       'mask' =&gt; '^192\.168\.1\.\d+$',
                       'accept' =&gt; 1
                   },
                   # Accept myhost.company.com
                   {
                       'mask' =&gt; '^myhost\.company\.com$',
                       'accept' =&gt; 1
                   }
                   # Deny everything else
                   {
                       'mask' =&gt; '.*',
                       'accept' =&gt; 0
                   }
               ]
           }

   <b>Access</b> <b>control</b>
       The  Net::Daemon  package  supports  a  host  based  access control scheme. By default access is open for
       anyone. However, if you create an attribute $self-&gt;{'clients'}, typically in the config file, then access
       control is disabled by default. For any connection the client list is processed: The clients attribute is
       an array ref to a list of hash refs. Any of the hash refs may contain arbitrary attributes, including the
       following:

       mask    A Perl regular expression that has to match the clients IP number or its host name. The  list  is
               processed  from the left to the right, whenever a 'mask' attribute matches, then the related hash
               ref is choosen as client and processing the client list stops.

       accept  This may be set to true or  false  (default  when  omitting  the  attribute),  the  former  means
               accepting the client.

   <b>Event</b> <b>logging</b>
         $server-&gt;Log($level, $format, @args);
         $server-&gt;Debug($format, @args);
         $server-&gt;Error($format, @args);
         $server-&gt;Fatal($format, @args);

       The  <b>Log</b>  method  is an interface to Sys::Syslog (3) or Win32::EventLog (3). It's arguments are <u>$level</u>, a
       syslog level like <b>debug</b>, <b>notice</b> or <b>err</b>, a format string in the style of printf  and  the  format  strings
       arguments.

       The  <b>Debug</b>  and <b>Error</b> methods are shorthands for calling <b>Log</b> with a level of debug and err, respectively.
       The <b>Fatal</b> method is like <b>Error</b>, except it additionally throws the given message as exception.

       See <b>Net::Daemon::<a href="../man3/Log.3.html">Log</a></b>(3) for details.

   <b>Flow</b> <b>of</b> <b>control</b>
         $server-&gt;Bind();
         # The following inside Bind():
         if ($connection-&gt;Accept()) {
             $connection-&gt;Run();
         } else {
             $connection-&gt;Log('err', 'Connection refused');
         }

       The <b>Bind</b> method is called by the application when the server should start. Typically  this  can  be  done
       right after creating the server object <b>$server</b>. <b>Bind</b> usually never returns, except in case of errors.

       When  a  client connects, the server uses <b>Clone</b> to derive a connection object <b>$connection</b> from the server
       object. A new thread or process is created that uses the connection object to call  your  classes  <b>Accept</b>
       method. This method is intended for host authorization and should return either FALSE (refuse the client)
       or TRUE (accept the client).

       If  the  client  is accepted, the <b>Run</b> method is called which does the true work. The connection is closed
       when <b>Run</b> returns and the corresponding thread or process exits.

   <b>Error</b> <b>Handling</b>
       All methods are supposed to throw Perl exceptions in case of errors.

</pre><h4><b>MULTITHREADING</b> <b>CONSIDERATIONS</b></h4><pre>
       All methods are working with lexically scoped data and handle data only, the exception being the  OpenLog
       method  which  is  invoked  before threading starts. Thus you are safe as long as you don't share handles
       between threads. I strongly recommend that your application behaves similar. (This doesn't apply to  mode
       'ithreads'.)

</pre><h4><b>EXAMPLE</b></h4><pre>
       As  an  example  we'll  write  a  simple  calculator server. After connecting to this server you may type
       expressions, one per line. The server evaluates the expressions and prints the result. (Note this  is  an
       example, in real life we'd never implement such a security hole. :-)

       For  the  purpose  of  example  we  add  a command line option <u>--base</u> that takes 'hex', 'oct' or 'dec' as
       values: The servers output will use the given base.

         # -*- perl -*-
         #
         # Calculator server
         #
         use strict;

         require Net::Daemon;

         package Calculator;

         our $VERSION = '0.01';
         our @ISA = qw(Net::Daemon); # to inherit from Net::Daemon

         sub Version ($) { 'Calculator Example Server, 0.01'; }

         # Add a command line option "--base"
         sub Options ($) {
             my($self) = @_;
             my($options) = $self-&gt;SUPER::Options();
             $options-&gt;{'base'} = { 'template' =&gt; 'base=s',
                                    'description' =&gt; '--base                  '
                                           . 'dec (default), hex or oct'
                                     };
             $options;
         }

         # Treat command line option in the constructor
         sub new ($$;$) {
             my($class, $attr, $args) = @_;
             my($self) = $class-&gt;SUPER::new($attr, $args);
             if ($self-&gt;{'parent'}) {
                 # Called via Clone()
                 $self-&gt;{'base'} = $self-&gt;{'parent'}-&gt;{'base'};
             } else {
                 # Initial call
                 if ($self-&gt;{'options'}  &amp;&amp;  $self-&gt;{'options'}-&gt;{'base'}) {
                     $self-&gt;{'base'} = $self-&gt;{'options'}-&gt;{'base'}
                 }
             }
             if (!$self-&gt;{'base'}) {
                 $self-&gt;{'base'} = 'dec';
             }
             $self;
         }

         sub Run ($) {
             my($self) = @_;
             my($line, $sock);
             $sock = $self-&gt;{'socket'};
             while (1) {
                 if (!defined($line = $sock-&gt;getline())) {
                     if ($sock-&gt;error()) {
                         $self-&gt;Error("Client connection error %s",
                                      $sock-&gt;error());
                     }
                     $sock-&gt;close();
                     return;
                 }
                 $line =~ s/\s+$//; # Remove CRLF
                 my($result) = eval $line;
                 my($rc);
                 if ($self-&gt;{'base'} eq 'hex') {
                     $rc = printf $sock ("%x\n", $result);
                 } elsif ($self-&gt;{'base'} eq 'oct') {
                     $rc = printf $sock ("%o\n", $result);
                 } else {
                     $rc = printf $sock ("%d\n", $result);
                 }
                 if (!$rc) {
                     $self-&gt;Error("Client connection error %s",
                                  $sock-&gt;error());
                     $sock-&gt;close();
                     return;
                 }
             }
         }

         package main;

         my $server = Calculator-&gt;new({'pidfile' =&gt; 'none',
                                       'localport' =&gt; 2000}, \@ARGV);
         $server-&gt;Bind();

</pre><h4><b>KNOWN</b> <b>PROBLEMS</b></h4><pre>
       Most, or even any, known problems are related to the Sys::Syslog module which  is  by  default  used  for
       logging events under Unix. I'll quote some examples:

       Usage: Sys::Syslog::_PATH_LOG at ...
           This problem is treated in perl bug 20000712.003. A workaround is changing line 277 of Syslog.pm to

             my $syslog = &amp;_PATH_LOG() || croak "_PATH_LOG not found in syslog.ph";

</pre><h4><b>AUTHOR</b> <b>AND</b> <b>COPYRIGHT</b></h4><pre>
         Net::Daemon is Copyright (C) 1998, Jochen Wiedmann
                                            Am Eisteich 9
                                            72555 Metzingen
                                            Germany

                                            Phone: +49 7123 14887
                                            Email: <a href="mailto:joe@ispsoft.de">joe@ispsoft.de</a>

         All rights reserved.

         You may distribute this package under the terms of either the GNU
         General Public License or the Artistic License, as specified in the
         Perl README file.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       <b>RPC::<a href="../man3/pServer.3.html">pServer</a></b>(3), <b>Netserver::<a href="../man3/Generic.3.html">Generic</a></b>(3), <b>Net::Daemon::<a href="../man3/Log.3.html">Log</a></b>(3), <b>Net::Daemon::<a href="../man3/Test.3.html">Test</a></b>(3)

perl v5.38.2                                       2024-03-07                                   <u>Net::<a href="../man3pm/Daemon.3pm.html">Daemon</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>