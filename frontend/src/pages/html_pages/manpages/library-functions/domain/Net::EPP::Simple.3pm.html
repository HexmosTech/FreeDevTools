<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Net::EPP::Simple - a simple EPP client interface for the most common jobs.</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libnet-epp-perl">libnet-epp-perl_0.28-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Net::EPP::Simple - a simple EPP client interface for the most common jobs.

</pre><h4><b>SYNOPSIS</b></h4><pre>
           #!<a href="file:///usr/lib/w3m/cgi-bin/w3mman2html.cgi?perl">/usr/bin/perl</a>
           use Net::EPP::Simple;
           use strict;

           my $epp = Net::EPP::Simple-&gt;new(
               host =&gt; 'epp.nic.tld',
               user =&gt; 'my-id',
               pass =&gt; 'my-password',
           );

           my $domain = 'example.tld';

           if ($epp-&gt;check_domain($domain) == 1) {
               print "Domain is available\n" ;

           } else {
               my $info = $epp-&gt;domain_info($domain);
               printf("Domain was registered on %s by %s\n", $info-&gt;{crDate}, $info-&gt;{crID});

           }

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This module provides a high level interface to EPP. It hides all the boilerplate of connecting, logging
       in, building request frames and parsing response frames behind a simple, Perlish interface.

       It is based on the Net::EPP::Client module and uses Net::EPP::Frame to build request frames.

</pre><h4><b>CONSTRUCTOR</b></h4><pre>
           my $epp = Net::EPP::Simple-&gt;new(%PARAMS);

       The constructor accepts the following arguments:

       •   "host" identifies the EPP server to connect to.

       •   "port" specifies the port, which defaults to 700.

       •   "user" and "pass" parameters specify authentication information.

       •   "newPW" specifies a new password that is set during login.

       •   The  "login_security" parameter can be used to force the use of the Login Security Extension (see RFC
           8807 &lt;https://www.rfc-editor.org/rfc/rfc8807.html&gt;).  "Net::EPP::Simple" will automatically use  this
           extension  if  the  server  supports  it,  but  clients  may  wish to force this behaviour to prevent
           downgrade attacks.

       •   The "appname" parameter can be used to specify the value of the "&lt;app&gt;" element in the Login Security
           extension (if used). Unless specified, the name and current version  of  "Net::EPP::Simple"  will  be
           used.

       •   The  "timeout"  parameter  controls  how  long the client waits for a response from the server before
           returning an error.

       •   if "debug" is  set,  "Net::EPP::Simple"  will  output  verbose  debugging  information  on  "STDERR",
           including all frames sent to and received from the server.

       •   "reconnect"  can be used to disable automatic reconnection (it is enabled by default). Before sending
           a frame to the server, "Net::EPP::Simple" will send a "&lt;hello&gt;" to check that the connection  is  up,
           if  not,  it  will try to reconnect, aborting after the <u>n</u>th time, where <u>n</u> is the value of "reconnect"
           (the default is 3).

       •   "login" can be used to disable automatic logins. If you set it to 0, you can manually  log  in  using
           the "$epp-&gt;_login()" method.

       •   "objects" is a reference to an array of the EPP object namespace URIs that the client requires.

       •   "stdobj"  is a flag saying the client only requires the standard EPP "contact-1.0", "domain-1.0", and
           "host-1.0" namespaces.

       •   If neither "objects" nor "stdobj" is  specified  then  the  client  will  echo  the  server's  object
           namespace list.

       •   "extensions" is a reference to an array of the EPP extension namespace URIs that the client requires.

       •   "stdext"  is  a  flag  saying the client only requires the standard EPP "secDNS-1.1" DNSSEC extension
           namespace.

       •   If neither "extensions" nor "stdext" is specified then the client will echo  the  server's  extension
           namespace list.

       •   The "lang" parameter can be used to specify the language. The default is ""en"".

       The  constructor  will establish a connection to the server and retrieve the greeting (which is available
       via "$epp-&gt;greeting") and then send a "&lt;login&gt;" request.

       If  the  login  fails,  the  constructor  will  return  "undef"  and  set  $Net::EPP::Simple::Error   and
       $Net::EPP::Simple::Code.

   <b>CLIENT</b> <b>AND</b> <b>SERVER</b> <b>SSL</b> <b>OPTIONS</b>
       RFC  5730  requires  that  all  EPP  instances  must  be  protected  using  "mutual, strong client-server
       authentication". In practice, this means that both client and server must present an SSL certificate, and
       that they must both verify the certificate of their peer.

       <u>SERVER</u> <u>CERTIFICATE</u> <u>VERIFICATION</u>

       "Net::EPP::Simple" will verify the certificate  presented  by  a  server  if  the  "verify",  and  either
       "ca_file" or "ca_path" are passed to the constructor:

           my $epp = Net::EPP::Simple-&gt;new(
               host    =&gt; 'epp.nic.tld',
               user    =&gt; 'my-id',
               pass    =&gt; 'my-password',
               verify  =&gt; 1,
               ca_file =&gt; '/etc/pki/tls/certs/ca-bundle.crt',
               ca_path =&gt; '/etc/pki/tls/certs',
           );

       "Net::EPP::Simple" will fail to connect to the server if the certificate is not valid.

       You  can disable SSL certificate verification by omitting the "verify" argument or setting it to "undef".
       This is strongly discouraged, particularly in production environments.

       You may wish to use Mozilla::CA to provide a value for the "ca_file" parameter.

       <u>SSL</u> <u>CIPHER</u> <u>SELECTION</u>

       You can restrict the ciphers that you will use to connect to the server by passing a "ciphers"  parameter
       to   the   constructor.   This   is   a   colon-   separated  list  of  cipher  names  and  aliases.  See
       &lt;<a href="http://www.openssl.org/docs/apps/ciphers.html">http://www.openssl.org/docs/apps/ciphers.html</a>#CIPHER_STRINGS&gt; for further details. As  an  example,  the
       following cipher list is suggested for clients who wish to ensure high-security connections to servers:

        HIGH:!ADH:!MEDIUM:!LOW:!SSLv2:!EXP

       <u>CLIENT</u> <u>CERTIFICATES</u>

       If  you  are  connecting  to  an  EPP  server  which  requires  a  client  certificate, you can configure
       "Net::EPP::Simple" to use one as follows:

           my $epp = Net::EPP::Simple-&gt;new(
               host        =&gt; 'epp.nic.tld',
               user        =&gt; 'my-id',
               pass        =&gt; 'my-password',
               key         =&gt; '/path/to/my.key',
               cert        =&gt; '/path/to/my.crt',
               passphrase  =&gt; 'foobar123',
           );

       "key" is the filename of the private key, "cert" is the filename of the certificate. If the  private  key
       is encrypted, the "passphrase" parameter will be used to decrypt it.

</pre><h4><b>AVAILABILITY</b> <b>CHECKS</b></h4><pre>
       You can do a simple "&lt;check&gt;" request for an object like so:

           my $result = $epp-&gt;check_domain($domain);

           my $result = $epp-&gt;check_host($host);

           my $result = $epp-&gt;check_contact($contact);

       Each of these methods has the same profile. They will return one of the following:

       •   "undef" in the case of an error (check $Net::EPP::Simple::Error and $Net::EPP::Simple::Code).

       •   0 if the object is already provisioned.

       •   1 if the object is available.

</pre><h4><b>RETRIEVING</b> <b>OBJECT</b> <b>INFORMATION</b></h4><pre>
   <b>DOMAIN</b> <b>OBJECTS</b>
           my $info = $epp-&gt;domain_info($domain, $authInfo, $follow);

       This  method  constructs  an  "&lt;info&gt;"  frame and sends it to the server, then parses the response into a
       simple hash ref. If there is an  error,  this  method  will  return  "undef",  and  you  can  then  check
       $Net::EPP::Simple::Error and $Net::EPP::Simple::Code.

       If $authInfo is defined, it will be sent to the server as per RFC 5731, Section 3.1.2.

       If  the  $follow  parameter  is  true,  then  "Net::EPP::Simple" will also retrieve the relevant host and
       contact details for a domain: instead of returning an object name or  ID  for  the  domain's  registrant,
       contact associations, DNS servers or subordinate hosts, the values will be replaced with the return value
       from  the appropriate host_info() or contact_info() command (unless there was an error, in which case the
       original object ID will be used instead).

   <b>HOST</b> <b>OBJECTS</b>
           my $info = $epp-&gt;host_info($host);

       This method constructs an "&lt;info&gt;" frame and sends it to the server, then  parses  the  response  into  a
       simple  hash  ref.  If  there  is  an  error,  this  method  will  return "undef", and you can then check
       $Net::EPP::Simple::Error and $Net::EPP::Simple::Code.

   <b>CONTACT</b> <b>OBJECTS</b>
           my $info = $epp-&gt;contact_info($contact, $authInfo, $roid);

       This method constructs an "&lt;info&gt;" frame and sends it to the server, then  parses  the  response  into  a
       simple  hash  ref.  If  there  is  an  error,  this  method  will  return "undef", and you can then check
       $Net::EPP::Simple::Error and $Net::EPP::Simple::Code.

       If $authInfo is defined, it will be sent to the server as per RFC RFC 5733, Section 3.1.2.

       If the $roid parameter to host_info() is set, then the "roid" attribute will be set on  the  "&lt;authInfo&gt;"
       element.

   <b>DOMAIN</b> <b>INFORMATION</b>
       The hash ref returned by domain_info() will usually look something like this:

           {
               'contacts' =&gt; {
                   'admin' =&gt; 'contact-id' 'tech' =&gt; 'contact-id' 'billing' =&gt; 'contact-id'
               },
               'registrant' =&gt; 'contact-id',
               'clID'       =&gt; 'registrar-id',
               'roid'       =&gt; 'tld-12345',
               'status'     =&gt; ['ok'],
               'authInfo'   =&gt; 'abc-12345',
               'name'       =&gt; 'example.tld',
               'trDate'     =&gt; '2011-01-18T11:08:03.0Z',
               'ns'         =&gt; ['ns0.example.com', 'ns1.example.com',],
               'crDate'     =&gt; '2011-02-16T12:06:31.0Z',
               'exDate'     =&gt; '2011-02-16T12:06:31.0Z',
               'crID'       =&gt; 'registrar-id',
               'upDate'     =&gt; '2011-08-29T04:02:12.0Z',
               hosts        =&gt; ['ns0.example.tld', 'ns1.example.tld',],
           }

       Members  of  the  "contacts"  hash  ref may be strings or, if there are multiple associations of the same
       type, an anonymous array of strings.  If the server uses the Host Attribute model  instead  of  the  Host
       Object model, then the "ns" member will look like this:

           [
               {
                   name  =&gt; 'ns0.example.com',
                   addrs =&gt; [
                       version =&gt; 'v4',
                       addr    =&gt; '10.0.0.1',
                   ],
               },
               {
                   name  =&gt; 'ns1.example.com',
                   addrs =&gt; [
                       version =&gt; 'v4',
                       addr    =&gt; '10.0.0.2',
                   ],
               },
           ]

       Note  that  there  may  be  multiple  members  in the "addrs" section and that the "version" attribute is
       optional.

   <b>HOST</b> <b>INFORMATION</b>
       The hash ref returned by host_info() will usually look something like this:

           {
               'crDate' =&gt; '2011-09-17T15:38:56.0Z',
               'clID'   =&gt; 'registrar-id',
               'crID'   =&gt; 'registrar-id',
               'roid'   =&gt; 'tld-12345',
               'status' =&gt; ['linked', 'serverDeleteProhibited',],
               'name'   =&gt; 'ns0.example.tld',
               'addrs'  =&gt; [
                   {
                       'version' =&gt; 'v4',
                       'addr'    =&gt; '10.0.0.1'
                   }
               ]
           }

       Note that hosts may have multiple addresses, and that "version" is optional.

   <b>CONTACT</b> <b>INFORMATION</b>
       The hash ref returned by contact_info() will usually look something like this:

           {
               'id'         =&gt; 'contact-id',
               'postalInfo' =&gt; {
                   'int' =&gt; {
                       'name' =&gt; 'John Doe',
                       'org'  =&gt; 'Example Inc.',
                       'addr' =&gt; {
                           'street' =&gt; ['123 Example Dr.', 'Suite 100'],
                           'city'   =&gt; 'Dulles',
                           'sp'     =&gt; 'VA',
                           'pc'     =&gt; '20116-6503',
                           'cc'     =&gt; 'US',
                       }}
               },
               'clID'   =&gt; 'registrar-id',
               'roid'   =&gt; 'CNIC-HA321983',
               'status' =&gt; ['linked', 'serverDeleteProhibited'],
               'voice'  =&gt; '+1.7035555555x1234',
               'fax'    =&gt; '+1.7035555556',
               'email'  =&gt; '<a href="mailto:jdoe@example.com">jdoe@example.com</a>',
               'crDate' =&gt; '2011-09-23T03:51:29.0Z',
               'upDate' =&gt; '1999-11-30T00:00:00.0Z'
           }

       There may be up to  two  members  of  the  "postalInfo"  hash,  corresponding  to  the  "int"  and  "loc"
       internationalised and localised types.

</pre><h4><b>OBJECT</b> <b>TRANSFERS</b></h4><pre>
       The  EPP  "&lt;transfer&gt;"  command  suppots  five different operations: query, request, cancel, approve, and
       reject. "Net::EPP::Simple" makes these available using the following methods:

           # For domain objects:

           $epp-&gt;domain_transfer_query($domain);
           $epp-&gt;domain_transfer_cancel($domain);
           $epp-&gt;domain_transfer_request($domain, $authInfo, $period);
           $epp-&gt;domain_transfer_approve($domain);
           $epp-&gt;domain_transfer_reject($domain);

           # For contact objects:

           $epp-&gt;contact_transfer_query($contact);
           $epp-&gt;contact_transfer_cancel($contact);
           $epp-&gt;contact_transfer_request($contact, $authInfo);
           $epp-&gt;contact_transfer_approve($contact);
           $epp-&gt;contact_transfer_reject($contact);

       Most of these methods will just set the value of $Net::EPP::Simple::Code and return either true or false.
       However,   the   domain_transfer_request(),   domain_transfer_query(),   contact_transfer_request()   and
       contact_transfer_query() methods will return a hash ref that looks like this:

           {
               'name'     =&gt; 'example.tld',
               'reID'     =&gt; 'losing-registrar',
               'acDate'   =&gt; '2011-12-04T12:24:53.0Z',
               'acID'     =&gt; 'gaining-registrar',
               'reDate'   =&gt; '2011-11-29T12:24:53.0Z',
               'trStatus' =&gt; 'pending'
           }

</pre><h4><b>CREATING</b> <b>OBJECTS</b></h4><pre>
       The following methods can be used to create a new object at the server:

           $epp-&gt;create_domain($domain);
           $epp-&gt;create_host($host);
           $epp-&gt;create_contact($contact);

       The  argument  for  these  methods  is a hash ref of the same format as that returned by the info methods
       above. As a result, cloning an existing object is as simple as the following:

           my $info = $epp-&gt;contact_info($contact);

           # set a new contact ID to avoid clashing with the existing object
           $info-&gt;{id} = $new_contact;

           # randomize authInfo:
           $info-&gt;{authInfo} = $random_string;

           $epp-&gt;create_contact($info);

       "Net::EPP::Simple" will ignore object properties that it does not recognise, and those  properties  (such
       as server-managed status codes) that clients are not permitted to set.

   <b>CREATING</b> <b>NEW</b> <b>DOMAINS</b>
       When creating a new domain object, you may also specify a "period" key, like so:

           $epp-&gt;create_domain({
               'name'       =&gt; 'example.tld',
               'period'     =&gt; 2,
               'registrant' =&gt; 'contact-id',
               'contacts'   =&gt; {
                   'tech'    =&gt; 'contact-id',
                   'admin'   =&gt; 'contact-id',
                   'billing' =&gt; 'contact-id',
               },
               'status' =&gt; ['clientTransferProhibited',],
               'ns'     =&gt; {'ns0.example.com', 'ns1.example.com',},

               # this will be ignored if the server does not support the TTL extension
               'ttl'    =&gt; {'NS' =&gt; 3600, 'DS' =&gt; 60},
           });

       The  "period"  key is assumed to be in years rather than months.  "Net::EPP::Simple" assumes the registry
       uses the host object model rather than the host attribute model.

   <b>CREATING</b> <b>HOSTS</b>
           $epp-&gt;create_host({
               name  =&gt; 'ns1.example.tld',
               addrs =&gt; [
                   {ip =&gt; '192.0.2.1', version =&gt; 'v4'},
                   {ip =&gt; '192.0.2.2',  version =&gt; 'v4'},
               ],

               # this will be ignored if the server does not support the TTL extension
               'ttl' =&gt; {
                   'A'    =&gt; 3600,
                   'AAAA' =&gt; 900,
               }
           });

</pre><h4><b>UPDATING</b> <b>OBJECTS</b></h4><pre>
       The following methods can be used to update an object at the server:

           $epp-&gt;update_domain($domain);
           $epp-&gt;update_host($host);
           $epp-&gt;update_contact($contact);

       Each of these methods has the same profile. They will return one of the following:

       •   undef in the case of an error (check $Net::EPP::Simple::Error and $Net::EPP::Simple::Code).

       •   1 if the update request was accepted.

       You may wish to check the value of $Net::EPP::Simple::Code to determine whether  the  response  code  was
       1000 (OK) or 1001 (action pending).

   <b>UPDATING</b> <b>DOMAINS</b>
       Use update_domain($info) method to update a domain name.

       The $info argument should look like this:

           {
               name =&gt; $domain,
               chg  =&gt; {
                   registrant =&gt; $new_registrant_id,
                   authInfo   =&gt; $new_domain_password,

                   # this will be ignored if the server does not support the TTL extension
                   ttl        =&gt; {'NS' =&gt; 3600, 'DS' =&gt; 60},
               },
               add =&gt; {

                   ns       =&gt; [qw/ns1.example.com ns2.example.com/],
                   contacts =&gt; {
                       tech    =&gt; 'contact-id',
                       billing =&gt; 'contact-id',
                       admin   =&gt; 'contact-id',
                   },

                   # Status info, simple form:
                   status =&gt; [qw/ clientUpdateProhibited clientHold /],

                   # Status info may be in more detailed form:
                   # status =&gt; {
                   #    clientUpdateProbhibited  =&gt; 'Avoid accidental change',
                   #    clientHold               =&gt; 'This domain is not delegated',
                   # },
               },
               rem =&gt; {
                   ns       =&gt; [...],
                   contacts =&gt; {
                       tech    =&gt; 'old_tech_id',
                       billing =&gt; 'old_billing_id',
                       admin   =&gt; 'old_admin_id',
                   },
                   status =&gt; [qw/ clientTransferProhibited ... /],
               },
           }

       All fields except "name" are optional.

   <b>UPDATING</b> <b>CONTACTS</b>
       Use update_contact(info) method to update a contact object.

       The $info argument should look like this:

           {
               id  =&gt; $contact_id,
               add =&gt; {
                   status =&gt; [qw/ clientDeleteProhibited /],

                   # OR
                   # status =&gt; {
                   #    clientDeleteProhibited  =&gt; 'Avoid accidental removal',
                   # },
               },
               rem =&gt; {
                   status =&gt; [qw/ clientUpdateProhibited /],
               },
               chg =&gt; {
                   postalInfo =&gt; {
                       int =&gt; {
                           name =&gt; 'John Doe',
                           org  =&gt; 'Example Inc.',
                           addr =&gt; {
                               street =&gt; ['123 Example Dr.', 'Suite 100'],
                               city   =&gt; 'Dulles',
                               sp     =&gt; 'VA',
                               pc     =&gt; '20116-6503',
                               cc     =&gt; 'US',
                           },
                       },
                   },
                   voice    =&gt; '+1.7035555555x1234',
                   fax      =&gt; '+1.7035555556',
                   email    =&gt; '<a href="mailto:jdoe@example.com">jdoe@example.com</a>',
                   authInfo =&gt; 'new-contact-password',
               },
           }

       All fields except "id" are optional.

   <b>UPDATING</b> <b>HOSTS</b>
       Use update_host($info) method to update a host object.

       The $info argument should look like this:

           {
               name =&gt; 'ns1.example.com',
               add  =&gt; {
                   status =&gt; [qw/ clientDeleteProhibited /],

                   # OR
                   # status =&gt; {
                   #    clientDeleteProhibited  =&gt; 'Avoid accidental removal',
                   # },

                   addrs =&gt; [{ip =&gt; '123.45.67.89', version =&gt; 'v4'}, {ip =&gt; '98.76.54.32', version =&gt; 'v4'},],
               },
               rem =&gt; {
                   status =&gt; [qw/ clientUpdateProhibited /],
                   addrs  =&gt; [{ip =&gt; '1.2.3.4', version =&gt; 'v4'}, {ip =&gt; '5.6.7.8', version =&gt; 'v4'},],
               },
               chg =&gt; {
                   name =&gt; 'ns2.example.com',
                   # this will be ignored if the server does not support the TTL extension
                   ttl    =&gt; {NS =&gt; 3600, DS =&gt; 60},
               },
           }

       All fields except "name" are optional.

</pre><h4><b>DELETING</b> <b>OBJECTS</b></h4><pre>
       The following methods can be used to delete an object at the server:

           $epp-&gt;delete_domain($domain);
           $epp-&gt;delete_host($host);
           $epp-&gt;delete_contact($contact);

       Each of these methods has the same profile. They will return one of the following:

       •   undef in the case of an error (check $Net::EPP::Simple::Error and $Net::EPP::Simple::Code).

       •   1 if the deletion request was accepted.

       You  may  wish  to  check the value of $Net::EPP::Simple::Code to determine whether the response code was
       1000 (OK) or 1001 (action pending).

</pre><h4><b>DOMAIN</b> <b>RENEWAL</b></h4><pre>
       You can extend the validity period of the domain object by issuing a <b>renew_domain()</b> command.

        my $result = $epp-&gt;renew_domain({
            name         =&gt; 'example.com',
            cur_exp_date =&gt; '2011-02-05',  # current expiration date
            period       =&gt; 2,             # prolongation period in years
        });

       Return value is 1 on success and "undef"  on  error.   In  the  case  of  error  $Net::EPP::Simple::Error
       contains the appropriate error message.

</pre><h4><b>MISCELLANEOUS</b> <b>METHODS</b></h4><pre>
           my $greeting = $epp-&gt;greeting;

       Returns the a Net::EPP::Frame::Greeting object representing the greeting returned by the server.

           $epp-&gt;ping;

       Checks  that  the  connection is up by sending a "&lt;hello&gt;" to the server. Returns false if no response is
       received.

</pre><h4><b>OVERRIDDEN</b> <b>METHODS</b> <b>FROM</b> <b>Net::EPP::Client</b></h4><pre>
       "Net::EPP::Simple" overrides some methods inherited from Net::EPP::Client. These are described below:

   <b>request()</b>
       "Net::EPP::Simple" overrides this method so it can automatically populate the "&lt;clTRID&gt;" element  with  a
       unique string. It then passes the frame back up to Net::EPP::Client.

   <b>get_frame()</b>
       "Net::EPP::Simple"  overrides  this  method so it can catch timeouts and network errors. If such an error
       occurs it will return "undef".

           $bool = $epp-&gt;server_has_object($xmlns);
           $bool = $epp-&gt;server_has_extension($xmlns);

       These methods both return a true value if the object/extension identified by $xmlns was  present  in  the
       server's greeting.

           $authenticated = $epp-&gt;authenticated;

       Returns a boolean if "Net::EPP::Simple" has successfully authenticated with the server.

</pre><h4><b>PACKAGE</b> <b>VARIABLES</b></h4><pre>
   <b>$Net::EPP::Simple::Error</b>
       This  variable contains an english text message explaining the last error to occur. This is may be due to
       invalid parameters being passed to a method, a network error, or an error response being returned by  the
       server.

   <b>$Net::EPP::Simple::Message</b>
       This variable contains the contains the text content of the "&lt;msg&gt;" element in the response frame for the
       last transaction.

   <b>$Net::EPP::Simple::Code</b>
       This  variable  contains  the  integer  result  code  returned  by the server for the last transaction. A
       successful transaction will always return an error code of 1999 or lower, for an unsuccessful transaction
       it will be 2011 or more. If there is an internal client error (due to invalid parameters being passed  to
       a   method,   or   a   network   error)   then   this   will  be  set  to  2400  ("COMMAND_FAILED").  See
       Net::EPP::ResponseCodes for more information about thes codes.

</pre><h4><b>COPYRIGHT</b></h4><pre>
       This module is (c) 2008 - 2023 CentralNic Ltd and 2024 Gavin Brown. This module is free software; you can
       redistribute it and/or modify it under the same terms as Perl itself.

perl v5.40.1                                       2025-04-19                              <u>Net::EPP::<a href="../man3pm/Simple.3pm.html">Simple</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>