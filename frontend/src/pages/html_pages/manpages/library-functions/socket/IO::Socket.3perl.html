<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>IO::Socket - Object interface to socket communications</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/perl-doc">perl-doc_5.40.1-5_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       IO::Socket - Object interface to socket communications

</pre><h4><b>SYNOPSIS</b></h4><pre>
           use strict;
           use warnings;

           use IO::Socket qw(AF_INET AF_UNIX);

           # create a new AF_INET socket
           my $sock = IO::Socket-&gt;new(Domain =&gt; AF_INET);
           # which is the same as
           $sock = IO::Socket::INET-&gt;new();

           # create a new AF_UNIX socket
           $sock = IO::Socket-&gt;new(Domain =&gt; AF_UNIX);
           # which is the same as
           $sock = IO::Socket::UNIX-&gt;new();

</pre><h4><b>DESCRIPTION</b></h4><pre>
       "IO::Socket" provides an object-oriented, IO::Handle-based interface to creating and using sockets via
       Socket, which provides a near one-to-one interface to the C socket library.

       "IO::Socket" is a base class that really only defines methods for those operations which are common to
       all types of sockets. Operations which are specific to a particular socket domain have methods defined in
       subclasses of "IO::Socket". See IO::Socket::INET, IO::Socket::UNIX, and IO::Socket::IP for examples of
       such a subclass.

       "IO::Socket" will export all functions (and constants) defined by Socket.

</pre><h4><b>CONSTRUCTOR</b> <b>ARGUMENTS</b></h4><pre>
       Given that "IO::Socket" doesn't have attributes in the traditional sense, the following arguments, rather
       than attributes, can be passed into the constructor.

       Constructor arguments should be passed in "Key =&gt; 'Value'" pairs.

       The only required argument is "Domain" in IO::Socket.

   <b>Blocking</b>
           my $sock = IO::Socket-&gt;new(..., Blocking =&gt; 1);
           $sock = IO::Socket-&gt;new(..., Blocking =&gt; 0);

       If defined but false, the socket will be set to non-blocking mode. If not specified it defaults to 1
       (blocking mode).

   <b>Domain</b>
           my $sock = IO::Socket-&gt;new(Domain =&gt; IO::Socket::AF_INET);
           $sock = IO::Socket-&gt;new(Domain =&gt; IO::Socket::AF_UNIX);

       The socket domain will define which subclass of "IO::Socket" to use. The two options available along with
       this distribution are "AF_INET" and "AF_UNIX".

       "AF_INET" is for the internet address family of sockets and is handled via IO::Socket::INET. "AF_INET"
       sockets are bound to an internet address and port.

       "AF_UNIX" is for the unix domain socket and is handled via IO::Socket::UNIX. "AF_UNIX" sockets are bound
       to the file system as their address name space.

       This argument is <b>required</b>. All other arguments are optional.

   <b>Listen</b>
           my $sock = IO::Socket-&gt;new(..., Listen =&gt; 5);

       Listen should be an integer value or left unset.

       If provided, this argument will place the socket into listening mode. New connections can then be
       accepted using the "accept" in IO::Socket method. The value given is used as the <a href="../man2/listen.2.html">listen</a>(2) queue size.

       If the "Listen" argument is given, but false, the queue size will be set to 5.

   <b>Timeout</b>
           my $sock = IO::Socket-&gt;new(..., Timeout =&gt; 5);

       The timeout value, in seconds, for this socket connection. How exactly this value is utilized is defined
       in the socket domain subclasses that make use of the value.

   <b>Type</b>
           my $sock = IO::Socket-&gt;new(..., Type =&gt; IO::Socket::SOCK_STREAM);

       The socket type that will be used. These are usually "SOCK_STREAM", "SOCK_DGRAM", or "SOCK_RAW". If this
       argument is left undefined an attempt will be made to infer the type from the service name.

       For example, you'll usually use "SOCK_STREAM" with a "tcp" connection and "SOCK_DGRAM" with a "udp"
       connection.

</pre><h4><b>CONSTRUCTORS</b></h4><pre>
       "IO::Socket" extends the IO::Handle constructor.

   <b>new</b>
           my $sock = IO::Socket-&gt;new();

           # get a new IO::Socket::INET instance
           $sock = IO::Socket-&gt;new(Domain =&gt; IO::Socket::AF_INET);
           # get a new IO::Socket::UNIX instance
           $sock = IO::Socket-&gt;new(Domain =&gt; IO::Socket::AF_UNIX);

           # Domain is the only required argument
           $sock = IO::Socket-&gt;new(
               Domain =&gt; IO::Socket::AF_INET, # AF_INET, AF_UNIX
               Type =&gt; IO::Socket::SOCK_STREAM, # SOCK_STREAM, SOCK_DGRAM, ...
               Proto =&gt; 'tcp', # 'tcp', 'udp', IPPROTO_TCP, IPPROTO_UDP
               # and so on...
           );

       Creates an "IO::Socket", which is a reference to a newly created symbol (see the Symbol package). "new"
       optionally takes arguments, these arguments are defined in "CONSTRUCTOR ARGUMENTS" in IO::Socket.

       Any of the "CONSTRUCTOR ARGUMENTS" in IO::Socket may be passed to the constructor, but if any arguments
       are provided, then one of them must be the "Domain" in IO::Socket argument. The "Domain" in IO::Socket
       argument can, by default, be either "AF_INET" or "AF_UNIX". Other domains can be used if a proper
       subclass for the domain family is registered. All other arguments will be passed to the "configuration"
       method of the package for that domain.

       If the constructor fails it will return "undef" and set the $errstr package variable to contain an error
       message.

           $sock = IO::Socket-&gt;new(...)
               or die "Cannot create socket - $IO::Socket::errstr\n";

       For legacy reasons the error message is also set into the global $@ variable, and you may still find
       older code which looks here instead.

           $sock = IO::Socket-&gt;new(...)
               or die "Cannot create socket - $@\n";

</pre><h4><b>METHODS</b></h4><pre>
       "IO::Socket" inherits all methods from IO::Handle and implements the following new ones.

   <b>accept</b>
           my $client_sock = $sock-&gt;accept();
           my $inet_sock = $sock-&gt;accept('IO::Socket::INET');

       The accept method will perform the system call "accept" on the socket and return a new object. The new
       object will be created in the same class as the listen socket, unless a specific package name is
       specified. This object can be used to communicate with the client that was trying to connect.

       This differs slightly from the "accept" function in perlfunc.

       In a scalar context the new socket is returned, or "undef" upon failure. In a list context a two-element
       array is returned containing the new socket and the peer address; the list will be empty upon failure.

   <b>atmark</b>
           my $integer = $sock-&gt;atmark();
           # read in some data on a given socket
           my $data;
           $sock-&gt;read($data, 1024) until $sock-&gt;atmark;

           # or, export the function to use:
           use IO::Socket 'sockatmark';
           $sock-&gt;read($data, 1024) until sockatmark($sock);

       True if the socket is currently positioned at the urgent data mark, false otherwise. If your system
       doesn't yet implement "sockatmark" this will throw an exception.

       If your system does not support "sockatmark", the "use" declaration will fail at compile time.

   <b>autoflush</b>
           # by default, autoflush will be turned on when referenced
           $sock-&gt;autoflush(); # turns on autoflush
           # turn off autoflush
           $sock-&gt;<a href="../man0/autoflush.0.html">autoflush</a>(0);
           # turn on autoflush
           $sock-&gt;<a href="../man1/autoflush.1.html">autoflush</a>(1);

       This attribute isn't overridden from IO::Handle's implementation. However, since we turn it on by
       default, it's worth mentioning here.

   <b>bind</b>
           use Socket qw(pack_sockaddr_in);
           my $port = 3000;
           my $ip_address = '0.0.0.0';
           my $packed_addr = pack_sockaddr_in($port, $ip_address);
           $sock-&gt;bind($packed_addr);

       Binds a network address to a socket, just as <a href="../man2/bind.2.html">bind</a>(2) does. Returns true if it succeeded, false otherwise.
       You should provide a packed address of the appropriate type for the socket.

   <b>connected</b>
           my $peer_addr = $sock-&gt;connected();
           if ($peer_addr) {
               say "We're connected to $peer_addr";
           }

       If the socket is in a connected state, the peer address is returned. If the socket is not in a connected
       state, "undef" is returned.

       Note that this method considers a half-open TCP socket to be "in a connected state".  Specifically, it
       does not distinguish between the <b>ESTABLISHED</b> and <b>CLOSE-WAIT</b> TCP states; it returns the peer address,
       rather than "undef", in either case.  Thus, in general, it cannot be used to reliably learn whether the
       peer has initiated a graceful shutdown because in most cases (see below) the local TCP state machine
       remains in <b>CLOSE-WAIT</b> until the local application calls "shutdown" in IO::Socket or "close". Only at that
       point does this function return "undef".

       The "in most cases" hedge is because local TCP state machine behavior may depend on the peer's socket
       options. In particular, if the peer socket has "SO_LINGER" enabled with a zero timeout, then the peer's
       "close" will generate a "RST" segment. Upon receipt of that segment, the local TCP transitions
       immediately to <b>CLOSED</b>, and in that state, this method <u>will</u> return "undef".

   <b>getsockopt</b>
           my $value = $sock-&gt;getsockopt(SOL_SOCKET, SO_REUSEADDR);
           my $buf = $socket-&gt;getsockopt(SOL_SOCKET, SO_RCVBUF);
           say "Receive buffer is $buf bytes";

       Get an option associated with the socket. Levels other than "SOL_SOCKET" may be specified here. As a
       convenience, this method will unpack a byte buffer of the correct size back into a number.

   <b>listen</b>
           $sock-&gt;<a href="../man5/listen.5.html">listen</a>(5);

       Does the same thing that the <a href="../man2/listen.2.html">listen</a>(2) system call does. Returns true if it succeeded, false otherwise.
       Listens to a socket with a given queue size.

   <b>peername</b>
           my $sockaddr_in = $sock-&gt;peername();

       Returns the packed "sockaddr" address of the other end of the socket connection. It calls "getpeername".

   <b>protocol</b>
           my $proto = $sock-&gt;protocol();

       Returns the number for the protocol being used on the socket, if known. If the protocol is unknown, as
       with an "AF_UNIX" socket, zero is returned.

   <b>recv</b>
           my $buffer = "";
           my $length = 1024;
           my $flags = 0; # default. optional
           $sock-&gt;recv($buffer, $length);
           $sock-&gt;recv($buffer, $length, $flags);

       Similar in functionality to "recv" in perlfunc.

       Receives a message on a socket. Attempts to receive $length characters of data into $buffer from the
       specified socket. $buffer will be grown or shrunk to the length actually read. Takes the same flags as
       the system call of the same name. Returns the address of the sender if socket's protocol supports this;
       returns an empty string otherwise. If there's an error, returns "undef". This call is actually
       implemented in terms of the <a href="../man2/recvfrom.2.html">recvfrom</a>(2) system call.

       Flags are ORed together values, such as "MSG_BCAST", "MSG_OOB", "MSG_TRUNC". The default value for the
       flags is 0.

       The cached value of "peername" in IO::Socket is updated with the result of "recv".

       <b>Note:</b> In Perl v5.30 and newer, if the socket has been marked as ":utf8", "recv" will throw an exception.
       The :encoding(...) layer implicitly introduces the ":utf8" layer. See "binmode" in perlfunc.

       <b>Note:</b> In Perl versions older than v5.30, depending on the status of the socket, either (8-bit) bytes or
       characters are received. By default all sockets operate on bytes, but for example if the socket has been
       changed using "binmode" in perlfunc to operate with the :encoding(UTF-8) I/O layer (see the "open" in
       perlfunc pragma), the I/O will operate on UTF8-encoded Unicode characters, not bytes. Similarly for the
       ":encoding" layer: in that case pretty much any characters can be read.

   <b>send</b>
           my $message = "Hello, world!";
           my $flags = 0; # defaults to zero
           my $to = '0.0.0.0'; # optional destination
           my $sent = $sock-&gt;send($message);
           $sent = $sock-&gt;send($message, $flags);
           $sent = $sock-&gt;send($message, $flags, $to);

       Similar in functionality to "send" in perlfunc.

       Sends a message on a socket. Attempts to send the scalar message to the socket. Takes the same flags as
       the system call of the same name. On unconnected sockets, you must specify a destination to send to, in
       which case it does a <a href="../man2/sendto.2.html">sendto</a>(2) syscall. Returns the number of characters sent, or "undef" on error. The
       <a href="../man2/sendmsg.2.html">sendmsg</a>(2) syscall is currently unimplemented.

       The "flags" option is optional and defaults to 0.

       After a successful send with $to, further calls to "send" on an unconnected socket without $to will send
       to the same address, and $to will be used as the result of "peername" in IO::Socket.

       <b>Note:</b> In Perl v5.30 and newer, if the socket has been marked as ":utf8", "send" will throw an exception.
       The :encoding(...) layer implicitly introduces the ":utf8" layer. See "binmode" in perlfunc.

       <b>Note:</b> In Perl versions older than v5.30, depending on the status of the socket, either (8-bit) bytes or
       characters are sent. By default all sockets operate on bytes, but for example if the socket has been
       changed using "binmode" in perlfunc to operate with the :encoding(UTF-8) I/O layer (see the "open" in
       perlfunc pragma), the I/O will operate on UTF8-encoded Unicode characters, not bytes. Similarly for the
       ":encoding" layer: in that case pretty much any characters can be sent.

   <b>setsockopt</b>
           $sock-&gt;setsockopt(SOL_SOCKET, SO_REUSEADDR, 1);
           $sock-&gt;setsockopt(SOL_SOCKET, SO_RCVBUF, 64*1024);

       Set option associated with the socket. Levels other than "SOL_SOCKET" may be specified here. As a
       convenience, this method will convert a number into a packed byte buffer.

   <b>shutdown</b>
           $sock-&gt;shutdown(SHUT_RD); # we stopped reading data
           $sock-&gt;shutdown(SHUT_WR); # we stopped writing data
           $sock-&gt;shutdown(SHUT_RDWR); # we stopped using this socket

       Shuts down a socket connection in the manner indicated by the value passed in, which has the same
       interpretation as in the syscall of the same name.

       This is useful with sockets when you want to tell the other side you're done writing but not done
       reading, or vice versa. It's also a more insistent form of "close" because it also disables the file
       descriptor in any forked copies in other processes.

       Returns 1 for success; on error, returns "undef" if the socket is not a valid filehandle, or returns 0
       and sets $! for any other failure.

   <b>sockdomain</b>
           my $domain = $sock-&gt;sockdomain();

       Returns the number for the socket domain type. For example, for an "AF_INET" socket the value of &amp;AF_INET
       will be returned.

   <b>socket</b>
           my $sock = IO::Socket-&gt;new(); # no values given
           # now let's actually get a socket with the socket method
           # domain, type, and protocol are required
           $sock = $sock-&gt;socket(AF_INET, SOCK_STREAM, 'tcp');

       Opens a socket of the specified kind and returns it. Domain, type, and protocol are specified the same as
       for the syscall of the same name.

   <b>socketpair</b>
           my ($r, $w) = $sock-&gt;socketpair(AF_UNIX, SOCK_STREAM, PF_UNSPEC);
           ($r, $w) = IO::Socket::UNIX
               -&gt;socketpair(AF_UNIX, SOCK_STREAM, PF_UNSPEC);

       Will return a list of two sockets created (read and write), or an empty list on failure.

       Differs slightly from "socketpair" in perlfunc in that the argument list is a bit simpler.

   <b>sockname</b>
           my $packed_addr = $sock-&gt;sockname();

       Returns the packed "sockaddr" address of this end of the connection. It's the same as <a href="../man2/getsockname.2.html">getsockname</a>(2).

   <b>sockopt</b>
           my $value = $sock-&gt;sockopt(SO_REUSEADDR);
           $sock-&gt;sockopt(SO_REUSEADDR, 1);

       Unified method to both set and get options in the "SOL_SOCKET" level. If called with one argument then
       "getsockopt" in IO::Socket is called, otherwise "setsockopt" in IO::Socket is called.

   <b>socktype</b>
           my $type = $sock-&gt;socktype();

       Returns the number for the socket type. For example, for a "SOCK_STREAM" socket the value of &amp;SOCK_STREAM
       will be returned.

   <b>timeout</b>
           my $seconds = $sock-&gt;timeout();
           my $old_val = $sock-&gt;<a href="../man5/timeout.5.html">timeout</a>(5); # set new and return old value

       Set or get the timeout value (in seconds) associated with this socket.  If called without any arguments
       then the current setting is returned. If called with an argument the current setting is changed and the
       previous value returned.

       This method is available to all "IO::Socket" implementations but may or may not be used by the individual
       domain subclasses.

</pre><h4><b>EXAMPLES</b></h4><pre>
       Let's create a TCP server on "localhost:3333".

           use strict;
           use warnings;
           use feature 'say';

           use IO::Socket qw(AF_INET AF_UNIX SOCK_STREAM SHUT_WR);

           my $server = IO::Socket-&gt;new(
               Domain =&gt; AF_INET,
               Type =&gt; SOCK_STREAM,
               Proto =&gt; 'tcp',
               LocalHost =&gt; '0.0.0.0',
               LocalPort =&gt; 3333,
               ReusePort =&gt; 1,
               Listen =&gt; 5,
           ) || die "Can't open socket: $IO::Socket::errstr";
           say "Waiting on 3333";

           while (1) {
               # waiting for a new client connection
               my $client = $server-&gt;accept();

               # get information about a newly connected client
               my $client_address = $client-&gt;peerhost();
               my $client_port = $client-&gt;peerport();
               say "Connection from $client_address:$client_port";

               # read up to 1024 characters from the connected client
               my $data = "";
               $client-&gt;recv($data, 1024);
               say "received data: $data";

               # write response data to the connected client
               $data = "ok";
               $client-&gt;send($data);

               # notify client that response has been sent
               $client-&gt;shutdown(SHUT_WR);
           }

           $server-&gt;close();

       A client for such a server could be

           use strict;
           use warnings;
           use feature 'say';

           use IO::Socket qw(AF_INET AF_UNIX SOCK_STREAM SHUT_WR);

           my $client = IO::Socket-&gt;new(
               Domain =&gt; AF_INET,
               Type =&gt; SOCK_STREAM,
               proto =&gt; 'tcp',
               PeerPort =&gt; 3333,
               PeerHost =&gt; '0.0.0.0',
           ) || die "Can't open socket: $IO::Socket::errstr";

           say "Sending Hello World!";
           my $size = $client-&gt;send("Hello World!");
           say "Sent data of length: $size";

           $client-&gt;shutdown(SHUT_WR);

           my $buffer;
           $client-&gt;recv($buffer, 1024);
           say "Got back $buffer";

           $client-&gt;close();

</pre><h4><b>LIMITATIONS</b></h4><pre>
       On some systems, for an IO::Socket object created with "new_from_fd", or created with "accept" in
       IO::Socket from such an object, the "protocol" in IO::Socket, "sockdomain" in IO::Socket and "socktype"
       in IO::Socket methods may return "undef".

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Socket, IO::Handle, IO::Socket::INET, IO::Socket::UNIX, IO::Socket::IP

</pre><h4><b>AUTHOR</b></h4><pre>
       Graham Barr.  <b>atmark()</b> by Lincoln Stein.  Currently maintained by the Perl 5 Porters.  Please report all
       bugs at &lt;https://github.com/Perl/perl5/issues&gt;.

</pre><h4><b>COPYRIGHT</b></h4><pre>
       Copyright (c) 1997-8 Graham Barr &lt;<a href="mailto:gbarr@pobox.com">gbarr@pobox.com</a>&gt;. All rights reserved.  This program is free software;
       you can redistribute it and/or modify it under the same terms as Perl itself.

       The <b>atmark()</b> implementation: Copyright 2001, Lincoln Stein &lt;<a href="mailto:lstein@cshl.org">lstein@cshl.org</a>&gt;.  This module is distributed
       under the same terms as Perl itself.  Feel free to use, modify and redistribute it as long as you retain
       the correct attribution.

perl v5.40.1                                       2025-07-03                                  <u>IO::<a href="../man3perl/Socket.3perl.html">Socket</a></u>(3perl)
</pre>
 </div>
</div></section>
</div>
</body>
</html>