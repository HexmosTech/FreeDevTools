<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>POE::Component::IRC::Cookbook::BasicBot - A basic IRC bot</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libpoe-component-irc-perl">libpoe-component-irc-perl_6.93+dfsg-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       POE::Component::IRC::Cookbook::BasicBot - A basic IRC bot

</pre><h4><b>SYNOPSIS</b></h4><pre>
       This a very basic bot that connects to IRC, joins a few channels, and announces its arrival.

</pre><h4><b>DESCRIPTION</b></h4><pre>
       We start off quite simply:

        #!<a href="file:///usr/lib/w3m/cgi-bin/w3mman2html.cgi?env">/usr/bin/env</a> perl

        use strict;
        use warnings;

       Then we "use" the stuff we're going to...well, use. "::State" is a subclass which keeps track of state
       information related to channels and nicknames. It is needed by the "AutoJoin" plugin which takes care of
       keeping us on our channels.

        use POE;
        use POE::Component::IRC::State;
        use POE::Component::IRC::Plugin::AutoJoin;

       Next up is our POE session. We create it and list our event handlers. We then start the POE kernel.

        POE::Session-&gt;create(
            package_states =&gt; [
                main =&gt; [ qw(_start irc_join) ]
            ]
        );

        $poe_kernel-&gt;run();

       Now all we have to do is write the handlers for "_start" and "irc_join".  In "_start", we create our IRC
       component, add an "AutoJoin" plugin, register for the "irc_join" event, and connect to the IRC server.

        sub _start {
            my $irc = POE::Component::IRC::State-&gt;spawn(
                Nick   =&gt; 'basic_bot',
                Server =&gt; 'irc.freenode.net',
            );

            $irc-&gt;plugin_add('AutoJoin', POE::Component::IRC::Plugin::AutoJoin-&gt;new(
               Channels =&gt; [ '#test_channel1', '#test_channel2' ]
            ));

            $irc-&gt;yield(register =&gt; 'join');
            $irc-&gt;yield('connect');
        }

       Now comes our "irc_join" event handler. We send a message to the channel once we've joined it.

        sub irc_join {
            my $nick = (split /!/, $_[ARG0])[0];
            my $channel = $_[ARG1];
            my $irc = $_[SENDER]-&gt;get_heap();

            # only send the message if we were the one joining
            if ($nick eq $irc-&gt;nick_name()) {
                $irc-&gt;yield(privmsg =&gt; $channel, 'Hi everybody!');
            }
        }

       That's it!

</pre><h4><b>AUTHOR</b></h4><pre>
       Hinrik Örn Sigurðsson, <a href="mailto:hinrik.sig@gmail.com">hinrik.sig@gmail.com</a>

perl v5.32.1                                       2021-09-30              <u>POE::Component...kbook::<a href="../man3pm/BasicBot.3pm.html">BasicBot</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>