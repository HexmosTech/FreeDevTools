<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Test2::API::InterceptResult - Representation of a list of events.</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/perl-doc">perl-doc_5.40.1-6_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Test2::API::InterceptResult - Representation of a list of events.

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This class represents a list of events, normally obtained using intercept() from Test2::API.

       This class is intended for people who with to verify the results of test tools they write.

       This class provides methods to normalize, summarize, or map the list of events.  The output of these
       operations makes verifying your testing tools and the events they generate significantly easier. In most
       cases this spares you from needing a deep understanding of the event/facet model.

</pre><h4><b>SYNOPSIS</b></h4><pre>
       Usually you get an instance of this class when you use intercept() from Test2::API.

           use Test2::V0;
           use Test2::API qw/intercept/;

           my $events = intercept {
               ok(1, "pass");
               ok(0, "fail");
               todo "broken" =&gt; sub { ok(0, "fixme") };
               plan 3;
           };

           # This is typically the most useful construct
           # squash_info() merges assertions and diagnostics that are associated
           #   (and returns a new instance with the modifications)
           # flatten() condenses the facet data into the key details for each event
           #   (and returns those structures in an arrayref)
           is(
               $events-&gt;squash_info-&gt;flatten(),
               [
                   {
                       causes_failure =&gt; 0,

                       name =&gt; 'pass',
                       pass =&gt; 1,

                       trace_file =&gt; 'xxx.t',
                       trace_line =&gt; 5,
                   },
                   {
                       causes_failure =&gt; 1,

                       name =&gt; 'fail',
                       pass =&gt; 0,

                       trace_file =&gt; 'xxx.t',
                       trace_line =&gt; 6,

                       # There can be more than one diagnostics message so this is
                       # always an array when present.
                       diag =&gt; ["Failed test 'fail'\nat xxx.t line 6."],
                   },
                   {
                       causes_failure =&gt; 0,

                       name =&gt; 'fixme',
                       pass =&gt; 0,

                       trace_file =&gt; 'xxx.t',
                       trace_line =&gt; 7,

                       # There can be more than one diagnostics message or todo
                       # reason, so these are always an array when present.
                       todo =&gt; ['broken'],

                       # Diag message was turned into a note since the assertion was
                       # TODO
                       note =&gt; ["Failed test 'fixme'\nat xxx.t line 7."],
                   },
                   {
                       causes_failure =&gt; 0,

                       plan =&gt; 3,

                       trace_file =&gt; 'xxx.t',
                       trace_line =&gt; 8,
                   },
               ],
               "Flattened events look like we expect"
           );

       See Test2::API::InterceptResult::Event for a full description of what flatten() provides for each event.

</pre><h4><b>METHODS</b></h4><pre>
       Please note that no methods modify the original instance unless asked to do so.

   <b>CONSTRUCTION</b>
       $events = Test2::API::InterceptResult-&gt;new(@EVENTS)
       $events = Test2::API::InterceptResult-&gt;new_from_ref(\@EVENTS)
           These create a new instance of Test2::API::InterceptResult from the given events.

           In  the  first  form a new blessed arrayref is returned. In the 'new_from_ref' form the reference you
           pass in is directly blessed.

           Both of these will throw an exception if called in void context. This is  mainly  important  for  the
           'filtering'  methods  listed  below  which normally return a new instance, they throw an exception in
           such cases as it probably means someone meant to filter the original in place.

       $clone = $events-&gt;<b>clone()</b>
           Make a clone of the original events. Note  that  this  is  a  deep  copy,  the  entire  structure  is
           duplicated. This uses "dclone" from Storable to achieve the deep clone.

   <b>NORMALIZATION</b>
       @events = $events-&gt;event_list
           This returns all the events in list-form.

       $hub = $events-&gt;hub
           This  returns  a new Test2::Hub instance that has processed all the events contained in the instance.
           This gives you a simple way to inspect the state changes your events cause.

       $state = $events-&gt;state
           This returns a summary of the state of a hub after processing all the events.

               {
                   count        =&gt; 2,      # Number of assertions made
                   failed       =&gt; 1,      # Number of test failures seen
                   is_passing   =&gt; 0,      # Boolean, true if the test would be passing
                                           # after the events are processed.

                   plan         =&gt; 2,      # Plan, either a number, undef, 'SKIP', or 'NO PLAN'
                   follows_plan =&gt; 1,      # True if there is a plan and it was followed.
                                           # False if the plan and assertions did not
                                           # match, undef if no plan was present in the
                                           # event list.

                   bailed_out   =&gt; undef,  # undef unless there was a bail-out in the
                                           # events in which case this will be a string
                                           # explaining why there was a bailout, if no
                                           # reason was given this will simply be set to
                                           # true (1).

                   skip_reason  =&gt; undef,  # If there was a skip_all this will give the
                                           # reason.
               }

       $new = $events-&gt;upgrade
       $events-&gt;upgrade(in_place =&gt; $BOOL)
           <b>Note:</b> This normally returns a new instance, leaving the original unchanged.  If you call it  in  void
           context it will throw an exception. If you want to modify the original you must pass in the "in_place
           =&gt; 1" option. You may call this in void context when you ask to modify it in place. The in-place form
           returns the instance that was modified so you can chain methods.

           This   will   create   a   clone   of   the   list   where   all  events  have  been  converted  into
           Test2::API::InterceptResult::Event     instances.     This     is      extremely      helpful      as
           Test2::API::InterceptResult::Event  provide  a  much  better  interface for working with events. This
           allows you to avoid thinking about legacy event types.

           This also means your tests against the list are not fragile if the  tool  you  are  testing  randomly
           changes  what  type  of events it generates (IE Changing from Test2::Event::Ok to Test2::Event::Pass,
           both   make   assertions   and   both   will   normalize   to    identical    (or    close    enough)
           Test2::API::InterceptResult::Event instances.

           Really  you almost always want this, the only reason it is not done automatically is to make sure the
           intercept() tool is backwards compatible.

       $new = $events-&gt;squash_info
       $events-&gt;squash_info(in_place =&gt; $BOOL)
           <b>Note:</b> This normally returns a new instance, leaving the original unchanged.  If you call it  in  void
           context it will throw an exception. If you want to modify the original you must pass in the "in_place
           =&gt; 1" option. You may call this in void context when you ask to modify it in place. The in-place form
           returns the instance that was modified so you can chain methods.

           <b>Note:</b>    All    events    in    the    new    or    modified    instance   will   be   converted   to
           Test2::API::InterceptResult::Event instances. There is no way to avoid  this,  the  squash  operation
           requires the upgraded event class.

           Test::More  and many other legacy tools would send notes, diags, and assertions as separate events. A
           subtest in Test::More would send a note with the subtest name, the subtest assertion, and  finally  a
           diagnostics  event if the subtest failed. This method will normalize things by squashing the note and
           diag into the same event as the subtest (This is different from putting them into the subtest,  which
           is not what happens).

   <b>FILTERING</b>
       <b>Note:</b>  These  normally  return  new instances, leaving the originals unchanged.  If you call them in void
       context they will throw exceptions. If you want to modify the originals you must pass in the "in_place =&gt;
       1" option. You may call these in void context when you ask to modify them in place.  The  in-place  forms
       return the instance that was modified so you can chain methods.

       <u>%PARAMS</u>

       These all accept the same 2 optional parameters:

       in_place =&gt; $BOOL
           When true the method will modify the instance in place instead of returning a new instance.

       args =&gt; \@ARGS
           If you wish to pass parameters into the event method being used for filtering, you may do so here.

       <u>METHODS</u>

       $events-&gt;grep($CALL, %PARAMS)
           This is essentially:

               Test2::API::InterceptResult-&gt;new(
                   grep { $_-&gt;$CALL( @{$PARAMS{args}} ) } $self-&gt;event_list,
               );

           <b>Note:</b>  that  $CALL  is called on an upgraded version of the event, though the events returned will be
           the original ones, not the upgraded ones.

           $CALL may be either the name of a method on Test2::API::InterceptResult::Event, or a coderef.

       $events-&gt;asserts(%PARAMS)
           This is essentially:

               $events-&gt;grep(has_assert =&gt; @{$PARAMS{args}})

           It returns a new instance containing only the events that made assertions.

       $events-&gt;subtests(%PARAMS)
           This is essentially:

               $events-&gt;grep(has_subtest =&gt; @{$PARAMS{args}})

           It returns a new instance containing only the events that have subtests.

       $events-&gt;diags(%PARAMS)
           This is essentially:

               $events-&gt;grep(has_diags =&gt; @{$PARAMS{args}})

           It returns a new instance containing only the events that have diags.

       $events-&gt;notes(%PARAMS)
           This is essentially:

               $events-&gt;grep(has_notes =&gt; @{$PARAMS{args}})

           It returns a new instance containing only the events that have notes.

       $events-&gt;errors(%PARAMS)
           <b>Note:</b> Errors are NOT failing assertions. Failing assertions are a different thing.

           This is essentially:

               $events-&gt;grep(has_errors =&gt; @{$PARAMS{args}})

           It returns a new instance containing only the events that have errors.

       $events-&gt;plans(%PARAMS)
           This is essentially:

               $events-&gt;grep(has_plan =&gt; @{$PARAMS{args}})

           It returns a new instance containing only the events that set the plan.

       $events-&gt;causes_fail(%PARAMS)
       $events-&gt;causes_failure(%PARAMS)
           These are essentially:

               $events-&gt;grep(causes_fail    =&gt; @{$PARAMS{args}})
               $events-&gt;grep(causes_failure =&gt; @{$PARAMS{args}})

           <b>Note:</b> causes_fail() and causes_failure() are both aliases for eachother in events, so  these  methods
           are effectively aliases here as well.

           It returns a new instance containing only the events that cause failure.

   <b>MAPPING</b>
       These methods <b>ALWAYS</b> return an arrayref.

       <b>Note:</b> No methods on Test2::API::InterceptResult::Event alter the event in any way.

       <b>Important</b> <b>Notes</b> <b>about</b> <b>Events</b>:

       Test2::API::InterceptResult::Event  was  tailor-made to be used in event-lists. Most methods that are not
       applicable to a given event will return an empty list, so  you  normally  do  not  need  to  worry  about
       unwanted  "undef" values or exceptions being thrown. Mapping over event methods is an entended use, so it
       works well to produce lists.

       <b>Exceptions</b> <b>to</b> <b>the</b> <b>rule:</b>

       Some methods such as "causes_fail" always return a boolean true or  false  for  all  events.  Any  method
       prefixed  with "the_" conveys the intent that the event should have exactly 1 of something, so those will
       throw an exception when that condition is not true.

       $arrayref = $events-&gt;map($CALL, %PARAMS)
           This is essentially:

               [ map { $_-&gt;$CALL(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           $CALL may be either the name of a method on Test2::API::InterceptResult::Event, or a coderef.

       $arrayref = $events-&gt;flatten(%PARAMS)
           This is essentially:

               [ map { $_-&gt;flatten(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of flattened structures.

           See Test2::API::InterceptResult::Event for details on what flatten() returns.

       $arrayref = $events-&gt;briefs(%PARAMS)
           This is essentially:

               [ map { $_-&gt;briefs(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of event briefs.

           See Test2::API::InterceptResult::Event for details on what brief() returns.

       $arrayref = $events-&gt;summaries(%PARAMS)
           This is essentially:

               [ map { $_-&gt;summaries(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of event summaries.

           See Test2::API::InterceptResult::Event for details on what summary() returns.

       $arrayref = $events-&gt;subtest_results(%PARAMS)
           This is essentially:

               [ map { $_-&gt;subtest_result(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of event summaries.

           See Test2::API::InterceptResult::Event for details on what subtest_result() returns.

       $arrayref = $events-&gt;diag_messages(%PARAMS)
           This is essentially:

               [ map { $_-&gt;diag_messages(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of diagnostic messages (strings).

           See Test2::API::InterceptResult::Event for details on what diag_messages() returns.

       $arrayref = $events-&gt;note_messages(%PARAMS)
           This is essentially:

               [ map { $_-&gt;note_messages(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of notification messages (strings).

           See Test2::API::InterceptResult::Event for details on what note_messages() returns.

       $arrayref = $events-&gt;error_messages(%PARAMS)
           This is essentially:

               [ map { $_-&gt;error_messages(@{ $PARAMS{args} }) } $events-&gt;upgrade-&gt;event_list ];

           It returns a new list of error messages (strings).

           See Test2::API::InterceptResult::Event for details on what error_messages() returns.

</pre><h4><b>SOURCE</b></h4><pre>
       The source code repository for Test2 can be found at &lt;https://github.com/Test-More/test-more/&gt;.

</pre><h4><b>MAINTAINERS</b></h4><pre>
       Chad Granum &lt;<a href="mailto:exodist@cpan.org">exodist@cpan.org</a>&gt;

</pre><h4><b>AUTHORS</b></h4><pre>
       Chad Granum &lt;<a href="mailto:exodist@cpan.org">exodist@cpan.org</a>&gt;

</pre><h4><b>COPYRIGHT</b></h4><pre>
       Copyright 2020 Chad Granum &lt;<a href="mailto:exodist@cpan.org">exodist@cpan.org</a>&gt;.

       This program is free software; you can redistribute it and/or modify it under  the  same  terms  as  Perl
       itself.

       See &lt;https://dev.perl.org/licenses/&gt;

perl v5.40.1                                       2025-07-27                 <u>Test2::API::<a href="../man3perl/InterceptResult.3perl.html">InterceptResult</a></u>(3perl)
</pre>
 </div>
</div></section>
</div>
</body>
</html>