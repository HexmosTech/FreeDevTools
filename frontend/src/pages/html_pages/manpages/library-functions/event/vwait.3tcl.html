<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>vwait - Process events until a variable is written</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/tcl9.0-doc">tcl9.0-doc_9.0.1+dfsg-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       vwait - Process events until a variable is written

</pre><h4><b>SYNOPSIS</b></h4><pre>
       <b>vwait</b> <u>varName</u>

       <b>vwait</b> ?<u>options</u>? ?<u>varName</u> <u>...</u>?
________________________________________________________________________________________________________________

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This  command  enters  the  Tcl  event  loop to process events, blocking the application if no events are
       ready.  It continues processing events until some event handler sets the value  of  the  global  variable
       <u>varName</u>.   Once  <u>varName</u>  has  been  set, the <b>vwait</b> command will return as soon as the event handler that
       modified <u>varName</u> completes.  The <u>varName</u> argument is always interpreted as a variable name  with  respect
       to the global namespace, but can refer to any namespace's variables if the fully-qualified name is given.

       In the second more complex command form <u>options</u> allow for finer control of the wait operation and to deal
       with multiple event sources.  <u>Options</u> can be made up of:

       <b>--</b>     Marks the end of options. All following arguments are handled as variable names.

       <b>-all</b>   All  conditions  for the wait operation must be met to complete the wait operation. Otherwise (the
              default) the first event completes the wait.

       <b>-extended</b>
              An extended result in list form is returned, see below for explanation.

       <b>-nofileevents</b>
              File events are not handled in the wait operation.

       <b>-noidleevents</b>
              Idle handlers are not invoked during the wait operation.

       <b>-notimerevents</b>
              Timer handlers are not serviced during the wait operation.

       <b>-nowindowevents</b>
              Events of the windowing system are not handled during the wait operation.

       <b>-readable</b> <u>channel</u>
              <u>Channel</u> must name a Tcl channel open for reading. If <u>channel</u>  is  or  becomes  readable  the  wait
              operation completes.

       <b>-timeout</b> <u>milliseconds</u>
              The wait operation is constrained to <u>milliseconds</u>.

       <b>-variable</b> <u>varName</u>
              <u>VarName</u>  must  be  the name of a global variable. Writing or unsetting this variable completes the
              wait operation.

       <b>-writable</b> <u>channel</u>
              <u>Channel</u> must name a Tcl channel open for writing. If <u>channel</u>  is  or  becomes  writable  the  wait
              operation completes.

       The result returned by <b>vwait</b> is for the simple form an empty string. If the <b>-timeout</b> option is specified,
       the  result  is  the  number of milliseconds remaining when the wait condition has been met, or -1 if the
       wait operation timed out.

       If the <b>-extended</b> option is specified, the result is made up  of  a  Tcl  list  with  an  even  number  of
       elements.  Odd elements take the values <b>readable</b>, <b>timeleft</b>, <b>variable</b>, and <b>writable</b>. Even elements are the
       corresponding variable and channel names or the remaining number of milliseconds.  The list is ordered by
       the occurrences of the event(s) with the exception of <b>timeleft</b> which always comes last.

       In some cases the <b>vwait</b> command may not return immediately after <u>varName</u> et.al. is set. This  happens  if
       the event handler that sets <u>varName</u> does not complete immediately.  For example, if an event handler sets
       <u>varName</u>  and  then itself calls <b>vwait</b> to wait for a different variable, then it may not return for a long
       time.  During this time the top-level <b>vwait</b> is blocked waiting for the event handler to complete,  so  it
       cannot return either. (See the <b>NESTED</b> <b>VWAITS</b> <b>BY</b> <b>EXAMPLE</b> below.)

       To be clear, <u>multiple</u> <b>vwait</b> <u>calls</u> <u>will</u> <u>nest</u> <u>and</u> <u>will</u> <u>not</u> <u>happen</u> <u>in</u> <u>parallel</u>.  The outermost call to <b>vwait</b>
       will  not  return until all the inner ones do.  It is recommended that code should never nest <b>vwait</b> calls
       (by avoiding putting them in event callbacks) but when that is not possible, care should be taken to  add
       interlock  variables to the code to prevent all reentrant calls to <b>vwait</b> that are not <u>strictly</u> necessary.
       Be aware that the synchronous modes of operation of some Tcl packages (e.g., <b>http</b>) use <b>vwait</b>  internally;
       if  using  the  event loop, it is best to use the asynchronous callback-based modes of operation of those
       packages where available.

</pre><h4><b>EXAMPLES</b></h4><pre>
       Run the event-loop continually until some event calls <b>exit</b>.  (You can  use  any  variable  not  mentioned
       elsewhere, but the name <u>forever</u> reminds you at a glance of the intent.)

              <b>vwait</b> forever

       Wait  five  seconds  for a connection to a server socket, otherwise close the socket and continue running
       the script:

              # Initialise the state
              after 5000 set state timeout
              set server [socket -server accept 12345]
              proc accept {args} {
                  global state connectionInfo
                  set state accepted
                  set connectionInfo $args
              }

              # Wait for something to happen
              <b>vwait</b> state

              # Clean up events that could have happened
              close $server
              after cancel set state timeout

              # Do something based on how the vwait finished...
              switch $state {
                  timeout {
                      puts "no connection on port 12345"
                  }
                  accepted {
                     puts "connection: $connectionInfo"
                     puts [lindex $connectionInfo 0] "Hello there!"
                  }
              }

       A command that will wait for some time delay by waiting for a namespace variable to be set.  Includes  an
       interlock to prevent nested waits.

              namespace eval example {
                  variable v done
                  proc wait {delay} {
                      variable v
                      if {$v ne "waiting"} {
                          set v waiting
                          after $delay [namespace code {set v done}]
                          <b>vwait</b> [namespace which -variable v]
                      }
                      return $v
                  }
              }

       When  running inside a <b>coroutine</b>, an alternative to using <b>vwait</b> is to <b>yield</b> to an outer event loop and to
       get recommenced when the variable is set, or at an idle moment after that.

              coroutine task apply {{} {
                  # simulate [after 1000]
                  after 1000 [info coroutine]
                  yield

                  # schedule the setting of a global variable, as normal
                  after 2000 {set var 1}

                  # simulate [<b>vwait</b> var]
                  proc updatedVar {task args} {
                      after idle $task
                      trace remove variable ::var write "updatedVar $task"
                  }
                  trace add variable ::var write "updatedVar [info coroutine]"
                  yield
              }}

   <b>NESTED</b> <b>VWAITS</b> <b>BY</b> <b>EXAMPLE</b>
       This example demonstrates what can happen when the <b>vwait</b> command is nested. The script will never  finish
       because  the  waiting for the <u>a</u> variable never finishes; that <b>vwait</b> command is still waiting for a script
       scheduled with <b>after</b> to complete, which just happens to be running an inner <b>vwait</b> (for <u>b</u>) even though the
       event that the outer <b>vwait</b> was waiting for (the setting of <u>a</u>) has occurred.

              after 500 {
                  puts "waiting for b"
                  <b>vwait</b> b
                  puts "b was set"
              }
              after 1000 {
                  puts "setting a"
                  set a 10
              }
              puts "waiting for a"
              <b>vwait</b> a
              puts "a was set"
              puts "setting b"
              set b 42

       If you run the above code, you get this output:

              waiting for a
              waiting for b
              setting a

       The script will never print “a was set” until after it has printed “b was set” because of the nesting  of
       <b>vwait</b>  commands,  and  yet  <u>b</u>  will  not  be  set  until after the outer <b>vwait</b> returns, so the script has
       deadlocked.  The only ways to avoid this are to either structure the  overall  program  in  continuation-
       passing style or to use <b>coroutine</b> to make the continuations implicit. The first of these options would be
       written as:

              after 500 {
                  puts "waiting for b"
                  trace add variable b write {apply {args {
                      global a b
                      trace remove variable ::b write \
                              [lrange [info level 0] 0 1]
                      puts "b was set"
                      set ::done ok
                  }}}
              }
              after 1000 {
                  puts "setting a"
                  set a 10
              }
              puts "waiting for a"
              trace add variable a write {apply {args {
                  global a b
                  trace remove variable a write [lrange [info level 0] 0 1]
                  puts "a was set"
                  puts "setting b"
                  set b 42
              }}}
              <b>vwait</b> done

       The second option, with <b>coroutine</b> and some helper procedures, is done like this:

              # A coroutine-based wait-for-variable command
              proc waitvar globalVar {
                  trace add variable ::$globalVar write \
                          [list apply {{v c args} {
                      trace remove variable $v write \
                              [lrange [info level 0] 0 3]
                      after 0 $c
                  }} ::$globalVar [info coroutine]]
                  yield
              }
              # A coroutine-based wait-for-some-time command
              proc waittime ms {
                  after $ms [info coroutine]
                  yield
              }

              coroutine task-1 eval {
                  puts "waiting for a"
                  waitvar a
                  puts "a was set"
                  puts "setting b"
                  set b 42
              }
              coroutine task-2 eval {
                  waittime 500
                  puts "waiting for b"
                  waitvar b
                  puts "b was set"
                  set done ok
              }
              coroutine task-3 eval {
                  waittime 1000
                  puts "setting a"
                  set a 10
              }
              <b>vwait</b> done

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       <a href="../man3tcl/global.3tcl.html">global</a>(3tcl), <a href="../man3tcl/update.3tcl.html">update</a>(3tcl)

</pre><h4><b>KEYWORDS</b></h4><pre>
       asynchronous I/O, event, variable, wait

Tcl                                                    8.0                                           <u><a href="../man3tcl/vwait.3tcl.html">vwait</a></u>(3tcl)
</pre>
 </div>
</div></section>
</div>
</body>
</html>