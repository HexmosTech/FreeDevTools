<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Exporter::Tiny::Manual::QuickStart - the quickest way to get up and running with Exporter::Tiny</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libexporter-tiny-perl">libexporter-tiny-perl_1.006002-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Exporter::Tiny::Manual::QuickStart - the quickest way to get up and running with Exporter::Tiny

</pre><h4><b>SYNOPSIS</b></h4><pre>
          package MyUtils;

          use Exporter::Shiny qw( frobnicate );

          sub frobnicate {
             ...;   # your code here
          }

          1;

       Now people can use your module like this:

          use MyUtils "frobnicate";

          <a href="../man42/frobnicate.42.html">frobnicate</a>(42);

       Or like this:

          use MyUtils "frobnicate" =&gt; { -as =&gt; "frob" };

          <a href="../man42/frob.42.html">frob</a>(42);

</pre><h4><b>DESCRIPTION</b></h4><pre>
       See the synopsis. Yes, it's that simple.

   <b>Next</b> <b>steps</b>
       <u>Default</u> <u>exports</u>

       Note that the module in the synopsis doesn't export anything by default.  If people load "MyUtils" like
       this:

          use MyUtils;

       Then they haven't imported any functions. You can specify a default set of functions to be exported like
       this:

          package MyUtils;

          use Exporter::Shiny qw( frobnicate );

          our @EXPORT = qw( frobnicate );

          sub frobnicate { ... }

          1;

       Or, if you want to be a superstar rock god:

          package MyUtils;

          use Exporter::Shiny our @EXPORT = qw( frobnicate );

          sub frobnicate { ... }

          1;

       <u>Tags</u>

       You can provide tags for people to use:

          package MyUtils;

          use Exporter::Shiny qw( frobnicate red green blue );

          our %EXPORT_TAGS = (
             utils   =&gt; [qw/ frobnicate /],
             colours =&gt; [qw/ red green blue /],
          );

          sub frobnicate { ... }
          sub red        { ... }
          sub green      { ... }
          sub blue       { ... }

          1;

       And people can now import your functions like this:

          use MyUtils ":colours";

       Or this:

          use MyUtils "-colours";

       Or take advantage of the fact that Perl magically quotes barewords preceded by a hyphen:

          use MyUtils -colours;

       Two tags are automatically defined for you: "-default" (which is just the same as @EXPORT) and "-all"
       (which is the union of @EXPORT and @EXPORT_OK). If you don't like them, then you can override them:

          our %EXPORT_TAGS = (
             default =&gt; \@some_other_stuff,
             all     =&gt; \@more_stuff,
          );

       <u>Generators</u>

       Exporting normally just works by copying a sub from your package into your caller's package. But
       sometimes it's useful instead to generate a <u>custom</u> sub to insert into your caller's package. This is
       pretty easy to do.

          package MyUtils;

          use Exporter::Shiny qw( frobnicate );

          sub _generate_frobnicate {
             my ( $me, $name, $args, $globals ) = @_;
             my $caller = $globals-&gt;{into};

             return sub {
                 ...;  # your code here
             };
          }

          1;

       The parameter $me here is a string containing the package name which is being imported from; $caller is
       the destination package; $name is the name of the sub (in this case "frobnicate"); and $args is a custom
       argument for this function. (By convention, $args is normally a hashref.)

          # The hashref { foo =&gt; 42 } is $args above.
          #
          use MyUtils "frobnicate" =&gt; { foo =&gt; 42 };

   <b>Avoiding</b> <b>Exporter::Shiny</b>
       Exporter::Shiny is a tiny shim around Exporter::Tiny. It should mostly do what you want, but you may
       sometimes prefer to use Exporter::Tiny directly.

       The example in the synopsis could have been written as:

          package MyUtils;

          use parent "Exporter::Tiny";
          our @EXPORT_OK = qw( frobnicate );

          sub frobnicate {
             ...;   # your code here
          }

          1;

       What Exporter::Shiny does is mostly just to set @EXPORT_OK for you and set up inheritance from the base
       class (Exporter::Tiny).

       Exporter::Shiny also sets $INC{'MyUtils.pm'} for you, which in usually makes little difference, but is
       useful in some edge cases.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       &lt;https://exportertiny.github.io/&gt;.

       Exporter::Shiny, Exporter::Tiny.

       For more advanced information, see Exporter::Tiny::Manual::Exporting.

</pre><h4><b>AUTHOR</b></h4><pre>
       Toby Inkster &lt;<a href="mailto:tobyink@cpan.org">tobyink@cpan.org</a>&gt;.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENCE</b></h4><pre>
       This software is copyright (c) 2013-2014, 2017, 2022-2023 by Toby Inkster.

       This is free software; you can redistribute it and/or modify it under the same terms as the Perl 5
       programming language system itself.

</pre><h4><b>DISCLAIMER</b> <b>OF</b> <b>WARRANTIES</b></h4><pre>
       THIS PACKAGE IS PROVIDED "AS IS" AND WITHOUT ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, WITHOUT
       LIMITATION, THE IMPLIED WARRANTIES OF MERCHANTIBILITY AND FITNESS FOR A PARTICULAR PURPOSE.

perl v5.36.0                                       2023-06-11             <u>Exporter::Tiny:...ual::<a href="../man3pm/QuickStart.3pm.html">QuickStart</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>