<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Sub::HandlesVia::HandlerLibrary::Code - library of code-related methods</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libsub-handlesvia-perl">libsub-handlesvia-perl_0.050002-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Sub::HandlesVia::HandlerLibrary::Code - library of code-related methods

</pre><h4><b>SYNOPSIS</b></h4><pre>
         package My::Class {
           use Moo;
           use Sub::HandlesVia;
           use Types::Standard 'CodeRef';
           has attr =&gt; (
             is =&gt; 'rwp',
             isa =&gt; CodeRef,
             handles_via =&gt; 'Code',
             handles =&gt; {
               'my_execute' =&gt; 'execute',
               'my_execute_list' =&gt; 'execute_list',
               'my_execute_method' =&gt; 'execute_method',
               'my_execute_method_list' =&gt; 'execute_method_list',
               'my_execute_method_scalar' =&gt; 'execute_method_scalar',
               'my_execute_method_void' =&gt; 'execute_method_void',
               'my_execute_scalar' =&gt; 'execute_scalar',
               'my_execute_void' =&gt; 'execute_void',
             },
           );
         }

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This is a library of methods for Sub::HandlesVia.

</pre><h4><b>DELEGATABLE</b> <b>METHODS</b></h4><pre>
   <b>execute(</b> <b>@args</b> <b>)</b>
       Calls the coderef, passing it any arguments.

         my $coderef = sub { 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( 1, 2, 3 )
         $object-&gt;my_execute( 1, 2, 3 );

   <b>execute_list(</b> <b>@args</b> <b>)</b>
       Calls the coderef, passing it any arguments, and forcing list context. If called in scalar context,
       returns an arrayref.

         my $context;
         my $coderef = sub { $context = wantarray(); 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( 1, 2, 3 )
         my $result = $object-&gt;my_execute_list( 1, 2, 3 );

         say Dumper( $result );  ## ==&gt; [ 'code' ]
         say $context;           ## ==&gt; true

   <b>execute_method(</b> <b>@args</b> <b>)</b>
       Calls the coderef as if it were a method, passing any arguments.

         my $coderef = sub { 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( $object, 1, 2, 3 )
         $object-&gt;my_execute_method( 1, 2, 3 );

   <b>execute_method_list(</b> <b>@args</b> <b>)</b>
       Calls the coderef as if it were a method, passing any arguments, and forcing list context. If called in
       scalar context, returns an arrayref.

         my $context;
         my $coderef = sub { $context = wantarray(); 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( $object, 1, 2, 3 )
         my $result = $object-&gt;my_execute_method_list( 1, 2, 3 );

         say Dumper( $result );  ## ==&gt; [ 'code' ]
         say $context;           ## ==&gt; true

   <b>execute_method_scalar(</b> <b>@args</b> <b>)</b>
       Calls the coderef as if it were a method, passing any arguments, and forcing scalar context.

         my $context;
         my $coderef = sub { $context = wantarray(); 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( $object, 1, 2, 3 )
         my $result = $object-&gt;my_execute_method_scalar( 1, 2, 3 );

         say $result;  ## ==&gt; 'code'
         say $context; ## ==&gt; false

   <b>execute_method_void(</b> <b>@args</b> <b>)</b>
       Calls the coderef as if it were a method, passing any arguments, and forcing void context. Returns undef.

         my $context;
         my $coderef = sub { $context = wantarray(); 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( $object, 1, 2, 3 )
         my $result = $object-&gt;my_execute_method_void( 1, 2, 3 );

         say $result;  ## ==&gt; undef
         say $context; ## ==&gt; undef

   <b>execute_scalar(</b> <b>@args</b> <b>)</b>
       Calls the coderef, passing it any arguments, and forcing scalar context.

         my $context;
         my $coderef = sub { $context = wantarray(); 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( 1, 2, 3 )
         my $result = $object-&gt;my_execute_scalar( 1, 2, 3 );

         say $result;  ## ==&gt; 'code'
         say $context; ## ==&gt; false

   <b>execute_void(</b> <b>@args</b> <b>)</b>
       Calls the coderef, passing it any arguments, and forcing void context. Returns undef.

         my $context;
         my $coderef = sub { $context = wantarray(); 'code' };
         my $object  = My::Class-&gt;new( attr =&gt; $coderef );

         # Calls: $coderef-&gt;( 1, 2, 3 )
         my $result = $object-&gt;my_execute_void( 1, 2, 3 );

         say $result;  ## ==&gt; undef
         say $context; ## ==&gt; undef

</pre><h4><b>EXTENDED</b> <b>EXAMPLES</b></h4><pre>
   <b>Using</b> <b>execute_method</b>
       The execute_method handler allows a class to effectively provide certain methods which can be overridden
       by parameters in the constructor.

         use strict;
         use warnings;
         use Data::Dumper;

         package My::Processor {
           use Moo;
           use Sub::HandlesVia;
           use Types::Standard qw( Str CodeRef );

           has name =&gt; (
             is =&gt; 'ro',
             isa =&gt; Str,
             default =&gt; 'Main Process',
           );

           my $NULL_CODEREF = sub {};

           has _debug =&gt; (
             is =&gt; 'ro',
             isa =&gt; CodeRef,
             handles_via =&gt; 'Code',
             handles =&gt; { debug =&gt; 'execute_method' },
             default =&gt; sub { $NULL_CODEREF },
             init_arg =&gt; 'debug',
           );

           sub _do_stuff {
             my $self = shift;
             $self-&gt;debug( 'continuing process' );
             return;
           }

           sub run_process {
             my $self = shift;
             $self-&gt;debug( 'starting process' );
             $self-&gt;_do_stuff;
             $self-&gt;debug( 'ending process' );
           }
         }

         my $p1 = My::Processor-&gt;new( name =&gt; 'First Process' );
         $p1-&gt;run_process; # no output

         my @got;
         my $p2 = My::Processor-&gt;new(
           name =&gt; 'Second Process',
           debug =&gt; sub {
             my ( $processor, $message ) = @_;
             push @got, sprintf( '%s: %s', $processor-&gt;name, $message );
           },
         );
         $p2-&gt;run_process; # logged output

         my @expected = (
           'Second Process: starting process',
           'Second Process: continuing process',
           'Second Process: ending process',
         );
         say Dumper( \@got ); ## ==&gt; \@expected

</pre><h4><b>BUGS</b></h4><pre>
       Please report any bugs to &lt;https://github.com/tobyink/p5-sub-handlesvia/issues&gt;.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Sub::HandlesVia.

</pre><h4><b>AUTHOR</b></h4><pre>
       Toby Inkster &lt;<a href="mailto:tobyink@cpan.org">tobyink@cpan.org</a>&gt;.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENCE</b></h4><pre>
       This software is copyright (c) 2020, 2022 by Toby Inkster.

       This is free software; you can redistribute it and/or modify it under the same terms as the Perl 5
       programming language system itself.

</pre><h4><b>DISCLAIMER</b> <b>OF</b> <b>WARRANTIES</b></h4><pre>
       THIS PACKAGE IS PROVIDED "AS IS" AND WITHOUT ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, WITHOUT
       LIMITATION, THE IMPLIED WARRANTIES OF MERCHANTIBILITY AND FITNESS FOR A PARTICULAR PURPOSE.

perl v5.40.1                                       2025-04-01              <u>Sub::HandlesVi...erLibrary::<a href="../man3pm/Code.3pm.html">Code</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>