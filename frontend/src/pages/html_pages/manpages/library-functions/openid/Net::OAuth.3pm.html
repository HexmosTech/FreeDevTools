<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Net::OAuth - OAuth 1.0 for Perl</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libnet-oauth-perl">libnet-oauth-perl_0.31-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Net::OAuth - OAuth 1.0 for Perl

</pre><h4><b>SYNOPSIS</b></h4><pre>
         # Web Server Example (Dancer)

         # This example is simplified for illustrative purposes, see the complete code in /demo

         # Note that client_id is the Consumer Key and client_secret is the Consumer Secret

         use Dancer;
         use Net::OAuth::Client;

         sub client {
               Net::OAuth::Client-&gt;new(
                       config-&gt;{client_id},
                       config-&gt;{client_secret},
                       site =&gt; 'https://www.google.com/',
                       request_token_path =&gt; '/accounts/OAuthGetRequestToken?scope=https%3A%2F%2Fwww.google.com%2Fm8%2Ffeeds%2F',
                       authorize_path =&gt; '/accounts/OAuthAuthorizeToken',
                       access_token_path =&gt; '/accounts/OAuthGetAccessToken',
                       callback =&gt; uri_for("/auth/google/callback"),
                       session =&gt; \&amp;session,
               );
         }

         # Send user to authorize with service provider
         get '/auth/google' =&gt; sub {
               redirect client-&gt;authorize_url;
         };

         # User has returned with token and verifier appended to the URL.
         get '/auth/google/callback' =&gt; sub {

               # Use the auth code to fetch the access token
               my $access_token =  client-&gt;get_access_token(params-&gt;{oauth_token}, params-&gt;{oauth_verifier});

               # Use the access token to fetch a protected resource
               my $response = $access_token-&gt;get('/m8/feeds/contacts/default/full');

               # Do something with said resource...

               if ($response-&gt;is_success) {
                 return "Yay, it worked: " . $response-&gt;decoded_content;
               }
               else {
                 return "Error: " . $response-&gt;status_line;
               }
         };

         dance;

</pre><h4><b>IMPORTANT</b></h4><pre>
       Net::OAuth provides a low-level API for reading and writing OAuth messages.

       You probably should start with Net::OAuth::Client.

</pre><h4><b>ABSTRACT</b></h4><pre>
       OAuth is

       "An open protocol to allow secure API authentication in a simple and standard method from desktop and web
       applications."

       In practical terms, OAuth is a mechanism for a Consumer to request protected resources from a Service
       Provider on behalf of a user.

       Please refer to the OAuth spec: &lt;<a href="http://oauth.net/documentation/spec">http://oauth.net/documentation/spec</a>&gt;

       Net::OAuth provides:

       •   classes that encapsulate OAuth messages (requests and responses).

       •   message signing

       •   message serialization and parsing.

       •   2-legged requests (aka. tokenless requests, aka. consumer requests), see "CONSUMER REQUESTS"

       Net::OAuth does not provide:

       •   Consumer or Service Provider encapsulation

       •   token/nonce/key storage/management

</pre><h4><b>DESCRIPTION</b></h4><pre>
   <b>OAUTH</b> <b>MESSAGES</b>
       An OAuth message is a set of key-value pairs.  The following message types are supported:

       Requests

       •   Request Token (Net::OAuth::RequestTokenRequest)

       •   Access Token (Net::OAuth::AccessTokenRequest)

       •   User Authentication (Net::OAuth::UserAuthRequest)

       •   Protected Resource (Net::OAuth::ProtectedResourceRequest)

       •   Consumer Request (Net::OAuth::ConsumerRequest) (2-legged / token-less request)

       Responses

       •   Request Token (Net::OAuth::RequestTokenResponse)

       •   Access Token (Net::OAuth:AccessTokenResponse)

       •   User Authentication (Net::OAuth::UserAuthResponse)

       Each  OAuth  message type has one or more required parameters, zero or more optional parameters, and most
       allow arbitrary parameters.

       All OAuth requests must be signed by the Consumer.  Responses from the Service Provider, however, are not
       signed.

       To  create  a  message,  the  easiest  way  is  to  use   the   factory   methods   (Net::OAuth-&gt;request,
       Net::OAuth-&gt;response, Net::OAuth-&gt;message).  The following method invocations are all equivalent:

        $request = Net::OAuth-&gt;request('user authentication')-&gt;new(%params);
        $request = Net::OAuth-&gt;request('user_auth')-&gt;new(%params);
        $request = Net::OAuth-&gt;request('UserAuth')-&gt;new(%params);
        $request = Net::OAuth-&gt;message('UserAuthRequest')-&gt;new(%params);

       The more verbose way is to use the class directly:

        use Net::OAuth::UserAuthRequest;
        $request = Net::OAuth::UserAuthRequest-&gt;new(%params);

       You  can  also create a message by deserializing it from a Authorization header, URL, query hash, or POST
       body

        $request = Net::OAuth-&gt;request('protected resource')-&gt;from_authorization_header($ENV{HTTP_AUTHORIZATION}, %api_params);
        $request = Net::OAuth-&gt;request('protected resource')-&gt;from_url($url, %api_params);
        $request = Net::OAuth-&gt;request('protected resource')-&gt;from_hash({$q-&gt;Vars}, %api_params); # CGI
        $request = Net::OAuth-&gt;request('protected resource')-&gt;from_hash($c-&gt;request-&gt;params, %api_params); # Catalyst
        $response = Net::OAuth-&gt;response('request token')-&gt;from_post_body($response_content, %api_params);

       Note that the deserialization methods (as opposed to  <b>new()</b>)  expect  OAuth  protocol  parameters  to  be
       prefixed with 'oauth_', as you would expect in a valid OAuth message.

       Before sending a request, the Consumer must first sign it:

        $request-&gt;sign;

       When receiving a request, the Service Provider should first verify the signature:

        die "Signature verification failed" unless $request-&gt;verify;

       When  sending  a  message  the  last step is to serialize it and send it to wherever it needs to go.  The
       following serialization methods are available:

        $response-&gt;to_post_body # a application/x-www-form-urlencoded POST body

        $request-&gt;to_url # the query string of a URL

        $request-&gt;to_authorization_header # the value of an HTTP Authorization header

        $request-&gt;to_hash # a hash that could be used for some other serialization

   <b>API</b> <b>PARAMETERS</b> <b>vs</b> <b>MESSAGE</b> <b>PARAMETERS</b>
       Net::OAuth defines 'message parameters' as parameters that are part of  the  transmitted  OAuth  message.
       These  include any protocol parameter (prefixed with 'oauth_' in the message), and any additional message
       parameters (the extra_params hash).

       'API parameters' are parameters required to build a message object that  are  not  transmitted  with  the
       message, e.g. consumer_secret, token_secret, request_url, request_method.

       There are various methods to inspect a message class to see what parameters are defined:

        $request-&gt;required_message_params;
        $request-&gt;optional_message_params;
        $request-&gt;all_message_params;
        $request-&gt;required_api_params;
        $request-&gt;optional_api_params;
        $request-&gt;all_api_params;
        $request-&gt;all_params;

       E.g.

        use Net::OAuth;
        use Data::Dumper;
        print Dumper(Net::OAuth-&gt;request("protected resource")-&gt;required_message_params);

        $VAR1 = [
                 'consumer_key',
                 'signature_method',
                 'timestamp',
                 'nonce',
                 'token'
               ];

   <b>ACCESSING</b> <b>PARAMETERS</b>
       All parameters can be get/set using accessor methods. E.g.

        my $consumer_key = $request-&gt;consumer_key;
        $request-&gt;request_method('POST');

   <b>THE</b> <b>REQUEST_URL</b> <b>PARAMETER</b>
       Any  query  parameters  in the request_url are removed and added to the extra_params hash when generating
       the signature.

       E.g. the following requests are pretty much equivalent:

        my $request = Net::OAuth-&gt;request('Request Token')-&gt;new(
         %params,
         request_url =&gt; 'https://photos.example.net/request_token',
         extra_params =&gt; {
          foo =&gt; 'bar'
         },
       );

        my $request = Net::OAuth-&gt;request('Request Token')-&gt;new(
         %params,
         request_url =&gt; 'https://photos.example.net/request_token?foo=bar',
        );

       Calling $request-&gt;request_url will still return whatever you set it to originally. If you want to get the
       request_url with the query parameters removed, you can do:

           my $url = $request-&gt;normalized_request_url;

   <b>SIGNATURE</b> <b>METHODS</b>
       The following signature methods are supported:

       •   PLAINTEXT

       •   HMAC-SHA1

       •   HMAC-SHA256

       •   RSA-SHA1

       The signature method is determined by the value of the signature_method parameter that is passed  to  the
       message constructor.

       If an unknown signature method is specified, the signing/verification will throw an exception.

       <u>PLAINTEXT</u> <u>SIGNATURES</u>

       This method is a trivial signature which adds no security.  Not recommended.

       <u>HMAC-SHA1</u> <u>SIGNATURES</u>

       This  method  is  available  if  you  have  Digest::SHA installed.  This is by far the most commonly used
       method.

       <u>HMAC-SHA256</u> <u>SIGNATURES</u>

       This method is available if you have Digest::SHA installed.

       <u>RSA-SHA1</u> <u>SIGNATURES</u>

       To  use  RSA-SHA1  signatures,  pass  in  a  Crypt::OpenSSL::RSA  object  (or  any  object  that  can  do
       $o-&gt;sign($str) and/or $o-&gt;verify($str, $sig))

       E.g.

       Consumer:

        use Crypt::OpenSSL::RSA;
        use File::Slurp;
        $keystring = read_file('private_key.pem');
        $private_key = Crypt::OpenSSL::RSA-&gt;new_private_key($keystring);
        $request = Net::OAuth-&gt;request('request token')-&gt;new(%params);
        $request-&gt;sign($private_key);

       Service Provider:

        use Crypt::OpenSSL::RSA;
        use File::Slurp;
        $keystring = read_file('public_key.pem');
        $public_key = Crypt::OpenSSL::RSA-&gt;new_public_key($keystring);
        $request = Net::OAuth-&gt;request('request token')-&gt;new(%params);
        if (!$request-&gt;verify($public_key)) {
               die "Signature verification failed";
        }

       Note  that  you  can  pass  the  key in as a parameter called 'signature_key' to the message constructor,
       rather than passing it to the sign/verify method, if you like.

   <b>CONSUMER</b> <b>REQUESTS</b>
       To send a request without including a token, use a Consumer Request:

           my $request = Net::OAuth-&gt;request('consumer')-&gt;new(
                   consumer_key =&gt; 'dpf43f3p2l4k3l03',
                   consumer_secret =&gt; 'kd94hf93k423kf44',
                   request_url =&gt; '<a href="http://provider.example.net/profile">http://provider.example.net/profile</a>',
                   request_method =&gt; 'GET',
                   signature_method =&gt; 'HMAC-SHA1',
                   timestamp =&gt; '1191242096',
                   nonce =&gt; 'kllo9940pd9333jh',
           );

           $request-&gt;sign;

       See Net::OAuth::ConsumerRequest

   <b>I18N</b>
       Per the OAuth spec, when making the signature Net::OAuth first encodes parameters to  UTF-8.  This  means
       that  any  parameters  you pass to Net::OAuth, if they might be outside of ASCII character set, should be
       run through <b>Encode::decode()</b> (or an equivalent PerlIO layer) first to  decode  them  to  Perl's  internal
       character structure.

   <b>OAUTH</b> <b>1.0A</b>
       Background:

       &lt;<a href="http://mojodna.net/2009/05/20/an-idiots-guide-to-oauth-10a.html">http://mojodna.net/2009/05/20/an-idiots-guide-to-oauth-10a.html</a>&gt;

       &lt;<a href="http://oauth.googlecode.com/svn/spec/core/1.0a/drafts/3/oauth-core-1_0a.html">http://oauth.googlecode.com/svn/spec/core/1.0a/drafts/3/oauth-core-1_0a.html</a>&gt;

       Net::OAuth defaults to OAuth 1.0 spec compliance, and supports OAuth 1.0 Rev A with an optional switch:

        use Net::OAuth
        $Net::OAuth::PROTOCOL_VERSION = Net::OAuth::PROTOCOL_VERSION_1_0A;

       It  is  recommended  that  any  new  projects  use this switch if possible, and existing projects move to
       supporting this switch as soon as possible.  Probably the easiest way for existing projects to do this is
       to turn on the switch and run your test suite.  The Net::OAuth constructor will throw an exception  where
       the new protocol parameters (callback, callback_confirmed, verifier) are missing.

       Internally, the Net::OAuth::Message constructor checks $Net::OAuth::PROTOCOL_VERSION and attempts to load
       the   equivalent   subclass   in   the   Net::OAuth::V1_0A::   namespace.    So   if  you  instantiate  a
       Net::OAuth::RequestTokenRequest object, you will end up with a Net::OAuth::V1_0A::RequestTokenRequest  (a
       subclass  of  Net::OAuth::RequestTokenRequest)  if  the protocol version is set to PROTOCOL_VERSION_1_0A.
       You can also select a 1.0a subclass on a per-message basis by passing

           protocol_version =&gt; Net::OAuth::PROTOCOL_VERSION_1_0A

       in the API parameters hash.

       If you are not sure whether the entity you  are  communicating  with  is  1.0A  compliant,  you  can  try
       instantiating a 1.0A message first and then fall back to 1.0 if that fails:

           use Net::OAuth
           $Net::OAuth::PROTOCOL_VERSION = Net::OAuth::PROTOCOL_VERSION_1_0A;
           my $is_oauth_1_0 = 0;
           my $response = eval{Net::OAuth-&gt;response('request token')-&gt;from_post_body($res-&gt;content)};
           if ($@) {
               if ($@ =~ /Missing required parameter 'callback_confirmed'/) {
                   # fall back to OAuth 1.0
                   $response = Net::OAuth-&gt;response('request token')-&gt;from_post_body(
                       $res-&gt;content,
                       protocol_version =&gt; Net::OAuth::PROTOCOL_VERSION_1_0
                   );
                   $is_oauth_1_0 = 1; # from now on treat the server as OAuth 1.0 compliant
               }
               else {
                   die $@;
               }
           }

       At some point in the future, Net::OAuth will default to Net::OAuth::PROTOCOL_VERSION_1_0A.

</pre><h4><b>DEMO</b></h4><pre>
       There is a demo Consumer CGI in this package, also available online at &lt;<a href="http://oauth.kg23.com/">http://oauth.kg23.com/</a>&gt;

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       &lt;<a href="http://oauth.net">http://oauth.net</a>&gt;

       Check out Net::OAuth::Simple - it has a simpler API that may be more to your liking

       Check out Net::Twitter::OAuth for a Twitter-specific OAuth API

       Check out WWW::Netflix::API for a Netflix-specific OAuth API

</pre><h4><b>TODO</b></h4><pre>
       •   Support for repeating/multivalued parameters

       •   Add convenience methods for SPs

           Something like:

               # direct from CGI.pm object
               $request = Net::OAuth-&gt;request('Request Token')-&gt;from_cgi_query($cgi, %api_params);

               # direct from Catalyst::Request object
               $request = Net::OAuth-&gt;request('Request Token')-&gt;from_catalyst_request($c-&gt;req, %api_params);

               # from Auth header and GET and POST params in one
               local $/;
               my $post_body = &lt;STDIN&gt;;
               $request = Net::OAuth-&gt;request('Request Token')-&gt;from_auth_get_and_post(
                   $ENV{HTTP_AUTHORIZATION},
                   $ENV{QUERY_STRING},
                   $post_body,
                   %api_params
               );

</pre><h4><b>AUTHOR</b></h4><pre>
       Originally by Keith Grennan &lt;<a href="mailto:kgrennan@cpan.org">kgrennan@cpan.org</a>&gt;

       Currently maintained by Robert Rothenberg &lt;<a href="mailto:rrwo@cpan.org">rrwo@cpan.org</a>&gt;

</pre><h4><b>COPYRIGHT</b> <b>&amp;</b> <b>LICENSE</b></h4><pre>
       Copyright 2007-2012, 2024-2025 Keith Grennan

       This  program  is  free  software;  you can redistribute it and/or modify it under the same terms as Perl
       itself.

perl v5.40.1                                       2025-04-12                                    <u>Net::<a href="../man3pm/OAuth.3pm.html">OAuth</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>