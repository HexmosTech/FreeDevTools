<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Thread::Queue - Thread-safe queues</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/perl-doc">perl-doc_5.40.1-6_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Thread::Queue - Thread-safe queues

</pre><h4><b>VERSION</b></h4><pre>
       This document describes Thread::Queue version 3.14

</pre><h4><b>SYNOPSIS</b></h4><pre>
           use strict;
           use warnings;

           use threads;
           use Thread::Queue;

           my $q = Thread::Queue-&gt;new();    # A new empty queue

           # Worker thread
           my $thr = threads-&gt;create(
               sub {
                   # Thread will loop until no more work
                   while (defined(my $item = $q-&gt;dequeue())) {
                       # Do work on $item
                       ...
                   }
               }
           );

           # Send work to the thread
           $q-&gt;enqueue($item1, ...);
           # Signal that there is no more work to be sent
           $q-&gt;end();
           # Join up with the thread when it finishes
           $thr-&gt;join();

           ...

           # Count of items in the queue
           my $left = $q-&gt;pending();

           # Non-blocking dequeue
           if (defined(my $item = $q-&gt;dequeue_nb())) {
               # Work on $item
           }

           # Blocking dequeue with 5-second timeout
           if (defined(my $item = $q-&gt;<a href="../man5/dequeue_timed.5.html">dequeue_timed</a>(5))) {
               # Work on $item
           }

           # Set a size for a queue
           $q-&gt;limit = 5;

           # Get the second item in the queue without dequeuing anything
           my $item = $q-&gt;<a href="../man1/peek.1.html">peek</a>(1);

           # Insert two items into the queue just behind the head
           $q-&gt;insert(1, $item1, $item2);

           # Extract the last two items on the queue
           my ($item1, $item2) = $q-&gt;extract(-2, 2);

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This module provides thread-safe FIFO queues that can be accessed safely by any number of threads.

       Any data types supported by threads::shared can be passed via queues:

       Ordinary scalars
       Array refs
       Hash refs
       Scalar refs
       Objects based on the above

       Ordinary scalars are added to queues as they are.

       If  not  already  thread-shared, the other complex data types will be cloned (recursively, if needed, and
       including any "bless"ings and read-only settings) into thread-shared structures before being placed  onto
       a queue.

       For  example,  the  following  would  cause  Thread::Queue  to create a empty, shared array reference via
       &amp;shared([]), copy the elements 'foo', 'bar' and 'baz' from @ary into  it,  and  then  place  that  shared
       reference onto the queue:

        my @ary = qw/foo bar baz/;
        $q-&gt;enqueue(\@ary);

       However,  for  the following, the items are already shared, so their references are added directly to the
       queue, and no cloning takes place:

        my @ary :shared = qw/foo bar baz/;
        $q-&gt;enqueue(\@ary);

        my $obj = &amp;shared({});
        $$obj{'foo'} = 'bar';
        $$obj{'qux'} = 99;
        bless($obj, 'My::Class');
        $q-&gt;enqueue($obj);

       See "LIMITATIONS" for caveats related to passing objects via queues.

</pre><h4><b>QUEUE</b> <b>CREATION</b></h4><pre>
       -&gt;<b>new()</b>
           Creates a new empty queue.

       -&gt;new(LIST)
           Creates a new queue pre-populated with the provided list of items.

</pre><h4><b>BASIC</b> <b>METHODS</b></h4><pre>
       The following methods deal with queues on a FIFO basis.

       -&gt;enqueue(LIST)
           Adds a list of items onto the end of the queue.

       -&gt;<b>dequeue()</b>
       -&gt;dequeue(COUNT)
           Removes the requested number of items (default is 1) from the head of the queue,  and  returns  them.
           If the queue contains fewer than the requested number of items, then the thread will be blocked until
           the requisite number of items are available (i.e., until other threads "enqueue" more items).

       -&gt;<b>dequeue_nb()</b>
       -&gt;dequeue_nb(COUNT)
           Removes  the  requested  number of items (default is 1) from the head of the queue, and returns them.
           If the queue contains fewer than the requested number of  items,  then  it  immediately  (i.e.,  non-
           blocking)  returns  whatever  items  there  are on the queue.  If the queue is empty, then "undef" is
           returned.

       -&gt;dequeue_timed(TIMEOUT)
       -&gt;dequeue_timed(TIMEOUT, COUNT)
           Removes the requested number of items (default is 1) from the head of the queue,  and  returns  them.
           If the queue contains fewer than the requested number of items, then the thread will be blocked until
           the  requisite  number  of  items  are available, or until the timeout is reached.  If the timeout is
           reached, it returns whatever items there are on the queue, or "undef" if the queue is empty.

           The timeout may be a number of seconds relative to the current time (e.g., 5 seconds  from  when  the
           call  is  made),  or  may  be  an  absolute  timeout  in <u>epoch</u> seconds the same as would be used with
           <b>cond_timedwait()</b>.  Fractional seconds (e.g., 2.5 seconds) are also supported (to the  extent  of  the
           underlying implementation).

           If  "TIMEOUT"  is  missing,  "undef",  or less than or equal to 0, then this call behaves the same as
           "dequeue_nb".

       -&gt;<b>pending()</b>
           Returns the number of items still in the queue.  Returns "undef" if the queue  has  been  ended  (see
           below), and there are no more items in the queue.

       -&gt;limit
           Sets  the size of the queue.  If set, calls to enqueue() will block until the number of pending items
           in the queue drops below the "limit".  The "limit" does  not  prevent  enqueuing  items  beyond  that
           count:

            my $q = Thread::Queue-&gt;new(1, 2);
            $q-&gt;limit = 4;
            $q-&gt;enqueue(3, 4, 5);   # Does not block
            $q-&gt;<a href="../man6/enqueue.6.html">enqueue</a>(6);         # Blocks until at least 2 items are
                                    # dequeued
            my $size = $q-&gt;limit;   # Returns the current limit (may return
                                    # 'undef')
            $q-&gt;limit = 0;          # Queue size is now unlimited

           Calling  any  of  the  dequeue  methods  with "COUNT" greater than a queue's "limit" will generate an
           error.

       -&gt;<b>end()</b>
           Declares that no more items will be added to the queue.

           All threads blocking on dequeue() calls will be unblocked with  any  remaining  items  in  the  queue
           and/or "undef" being returned.  Any subsequent calls to dequeue() will behave like dequeue_nb().

           Once ended, no more items may be placed in the queue.

</pre><h4><b>ADVANCED</b> <b>METHODS</b></h4><pre>
       The following methods can be used to manipulate items anywhere in a queue.

       To  prevent  the  contents  of  a  queue from being modified by another thread while it is being examined
       and/or changed, lock the queue inside a local block:

        {
            lock($q);   # Keep other threads from changing the queue's contents
            my $item = $q-&gt;peek();
            if ($item ...) {
                ...
            }
        }
        # Queue is now unlocked

       -&gt;<b>peek()</b>
       -&gt;peek(INDEX)
           Returns an item from the queue without dequeuing anything.  Defaults to the head of queue  (at  index
           position  0)  if no index is specified.  Negative index values are supported as with arrays (i.e., -1
           is the end of the queue, -2 is next to last, and so on).

           If no items exists at the specified index (i.e., the queue is empty,  or  the  index  is  beyond  the
           number of items on the queue), then "undef" is returned.

           Remember,  the  returned  item is not removed from the queue, so manipulating a "peek"ed at reference
           affects the item on the queue.

       -&gt;insert(INDEX, LIST)
           Adds the list of items to the queue at the specified index position (0 is the head of the list).  Any
           existing items at and beyond that position are pushed back past the newly added items:

            $q-&gt;enqueue(1, 2, 3, 4);
            $q-&gt;insert(1, qw/foo bar/);
            # Queue now contains:  1, foo, bar, 2, 3, 4

           Specifying an index position greater than the number of items in the queue just adds the list to  the
           end.

           Negative index positions are supported:

            $q-&gt;enqueue(1, 2, 3, 4);
            $q-&gt;insert(-2, qw/foo bar/);
            # Queue now contains:  1, 2, foo, bar, 3, 4

           Specifying  a  negative index position greater than the number of items in the queue adds the list to
           the head of the queue.

       -&gt;<b>extract()</b>
       -&gt;extract(INDEX)
       -&gt;extract(INDEX, COUNT)
           Removes and returns the specified number of items (defaults to 1) from the specified  index  position
           in  the  queue  (0  is the head of the queue).  When called with no arguments, "extract" operates the
           same as "dequeue_nb".

           This method is non-blocking, and will return only as many items  as  are  available  to  fulfill  the
           request:

            $q-&gt;enqueue(1, 2, 3, 4);
            my $item  = $q-&gt;<a href="../man2/extract.2.html">extract</a>(2)     # Returns 3
                                           # Queue now contains:  1, 2, 4
            my @items = $q-&gt;extract(1, 3)  # Returns (2, 4)
                                           # Queue now contains:  1

           Specifying  an  index position greater than the number of items in the queue results in "undef" or an
           empty list being returned.

            $q-&gt;enqueue('foo');
            my $nada = $q-&gt;<a href="../man3/extract.3.html">extract</a>(3)      # Returns undef
            my @nada = $q-&gt;extract(1, 3)   # Returns ()

           Negative index positions are supported.  Specifying a negative index position greater than the number
           of items in the queue may return items from the head of the queue (similar to  "dequeue_nb")  if  the
           count  overlaps the head of the queue from the specified position (i.e. if queue size + index + count
           is greater than zero):

            $q-&gt;enqueue(qw/foo bar baz/);
            my @nada = $q-&gt;extract(-6, 2);  # Returns ()      - (3+(-6)+2) &lt;= 0
            my @some = $q-&gt;extract(-6, 4);  # Returns (foo)   - (3+(-6)+4) &gt; 0
                                            # Queue now contains:  bar, baz
            my @rest = $q-&gt;extract(-3, 4);  # Returns (bar, baz) -
                                            #                   (2+(-3)+4) &gt; 0

</pre><h4><b>NOTES</b></h4><pre>
       Queues created by Thread::Queue can be used in both threaded and non-threaded applications.

</pre><h4><b>LIMITATIONS</b></h4><pre>
       Passing objects on queues may not work if the objects' classes do not support  sharing.   See  "BUGS  AND
       LIMITATIONS" in threads::shared for more.

       Passing array/hash refs that contain objects may not work for Perl prior to 5.10.0.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Thread::Queue on MetaCPAN: &lt;https://metacpan.org/release/Thread-Queue&gt;

       Code repository for CPAN distribution: &lt;https://github.com/Dual-Life/Thread-Queue&gt;

       threads, threads::shared

       Sample code in the <u>examples</u> directory of this distribution on CPAN.

</pre><h4><b>MAINTAINER</b></h4><pre>
       Jerry D. Hedden, &lt;jdhedden AT cpan DOT org&gt;

</pre><h4><b>LICENSE</b></h4><pre>
       This  program  is  free  software;  you can redistribute it and/or modify it under the same terms as Perl
       itself.

perl v5.40.1                                       2025-07-27                               <u>Thread::<a href="../man3perl/Queue.3perl.html">Queue</a></u>(3perl)
</pre>
 </div>
</div></section>
</div>
</body>
</html>