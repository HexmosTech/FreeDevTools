<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Class::Accessor - Automated accessor generation</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libclass-accessor-perl">libclass-accessor-perl_0.51-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
         Class::Accessor - Automated accessor generation

</pre><h4><b>SYNOPSIS</b></h4><pre>
         package Foo;
         use base qw(Class::Accessor);
         Foo-&gt;follow_best_practice;
         Foo-&gt;mk_accessors(qw(name role salary));

         # or if you prefer a Moose-like interface...

         package Foo;
         use Class::Accessor "antlers";
         has name =&gt; ( is =&gt; "rw", isa =&gt; "Str" );
         has role =&gt; ( is =&gt; "rw", isa =&gt; "Str" );
         has salary =&gt; ( is =&gt; "rw", isa =&gt; "Num" );

         # Meanwhile, in a nearby piece of code!
         # Class::Accessor provides new().
         my $mp = Foo-&gt;new({ name =&gt; "Marty", role =&gt; "JAPH" });

         my $job = $mp-&gt;role;  # gets $mp-&gt;{role}
         $mp-&gt;<a href="../man400000/salary.400000.html">salary</a>(400000);  # sets $mp-&gt;{salary} = 400000 # I wish

         # like my @info = @{$mp}{qw(name role)}
         my @info = $mp-&gt;get(qw(name role));

         # $mp-&gt;{salary} = 400000
         $mp-&gt;set('salary', 400000);

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This module automagically generates accessors/mutators for your class.

       Most of the time, writing accessors is an exercise in cutting and pasting.  You usually wind up with a
       series of methods like this:

           sub name {
               my $self = shift;
               if(@_) {
                   $self-&gt;{name} = $_[0];
               }
               return $self-&gt;{name};
           }

           sub salary {
               my $self = shift;
               if(@_) {
                   $self-&gt;{salary} = $_[0];
               }
               return $self-&gt;{salary};
           }

         # etc...

       One for each piece of data in your object.  While some will be unique, doing value checks and special
       storage tricks, most will simply be exercises in repetition.  Not only is it Bad Style to have a bunch of
       repetitious code, but it's also simply not lazy, which is the real tragedy.

       If you make your module a subclass of Class::Accessor and declare your accessor fields with
       <b>mk_accessors()</b> then you'll find yourself with a set of automatically generated accessors which can even
       be customized!

       The basic set up is very simple:

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;mk_accessors( qw(far bar car) );

       Done.  Foo now has simple <b>far()</b>, <b>bar()</b> and <b>car()</b> accessors defined.

       Alternatively, if you want to follow Damian's <u>best</u> <u>practice</u> guidelines you can use:

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;follow_best_practice;
           Foo-&gt;mk_accessors( qw(far bar car) );

       <b>Note:</b> you must call "follow_best_practice" before calling "mk_accessors".

   <b>Moose-like</b>
       By popular demand we now have a simple Moose-like interface.  You can now do:

           package Foo;
           use Class::Accessor "antlers";
           has far =&gt; ( is =&gt; "rw" );
           has bar =&gt; ( is =&gt; "rw" );
           has car =&gt; ( is =&gt; "rw" );

       Currently only the "is" attribute is supported.

</pre><h4><b>CONSTRUCTOR</b></h4><pre>
       Class::Accessor provides a basic constructor, "new".  It generates a hash-based object and can be called
       as either a class method or an object method.

   <b>new</b>
           my $obj = Foo-&gt;new;
           my $obj = $other_obj-&gt;new;

           my $obj = Foo-&gt;new(\%fields);
           my $obj = $other_obj-&gt;new(\%fields);

       It takes an optional %fields hash which is used to initialize the object (handy if you use read-only
       accessors).  The fields of the hash correspond to the names of your accessors, so...

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;mk_accessors('foo');

           my $obj = Foo-&gt;new({ foo =&gt; 42 });
           print $obj-&gt;foo;    # 42

       however %fields can contain anything, <b>new()</b> will shove them all into your object.

</pre><h4><b>MAKING</b> <b>ACCESSORS</b></h4><pre>
   <b>follow_best_practice</b>
       In Damian's Perl Best Practices book he recommends separate get and set methods with the prefix set_ and
       get_ to make it explicit what you intend to do.  If you want to create those accessor methods instead of
       the default ones, call:

           __PACKAGE__-&gt;follow_best_practice

       <b>before</b> you call any of the accessor-making methods.

   <b>accessor_name_for</b> <b>/</b> <b>mutator_name_for</b>
       You may have your own crazy ideas for the names of the accessors, so you can make those happen by
       overriding "accessor_name_for" and "mutator_name_for" in your subclass.  (I copied that idea from
       Class::DBI.)

   <b>mk_accessors</b>
           __PACKAGE__-&gt;mk_accessors(@fields);

       This creates accessor/mutator methods for each named field given in @fields.  Foreach field in @fields it
       will generate two accessors.  One called "<b>field()</b>" and the other called "<b>_field_accessor()</b>".  For
       example:

           # Generates foo(), _foo_accessor(), bar() and _bar_accessor().
           __PACKAGE__-&gt;mk_accessors(qw(foo bar));

       See "Overriding autogenerated accessors" in CAVEATS AND TRICKS for details.

   <b>mk_ro_accessors</b>
         __PACKAGE__-&gt;mk_ro_accessors(@read_only_fields);

       Same as <b>mk_accessors()</b> except it will generate read-only accessors (ie. true accessors).  If you attempt
       to set a value with these accessors it will throw an exception.  It only uses <b>get()</b> and not <b>set()</b>.

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;mk_ro_accessors(qw(foo bar));

           # Let's assume we have an object $foo of class Foo...
           print $foo-&gt;foo;  # ok, prints whatever the value of $foo-&gt;{foo} is
           $foo-&gt;<a href="../man42/foo.42.html">foo</a>(42);    # BOOM!  Naughty you.

   <b>mk_wo_accessors</b>
         __PACKAGE__-&gt;mk_wo_accessors(@write_only_fields);

       Same as <b>mk_accessors()</b> except it will generate write-only accessors (ie. mutators).  If you attempt to
       read a value with these accessors it will throw an exception.  It only uses <b>set()</b> and not <b>get()</b>.

       <b>NOTE</b> I'm not entirely sure why this is useful, but I'm sure someone will need it.  If you've found a use,
       let me know.  Right now it's here for orthogonality and because it's easy to implement.

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;mk_wo_accessors(qw(foo bar));

           # Let's assume we have an object $foo of class Foo...
           $foo-&gt;<a href="../man42/foo.42.html">foo</a>(42);      # OK.  Sets $self-&gt;{foo} = 42
           print $foo-&gt;foo;    # BOOM!  Can't read from this accessor.

</pre><h4><b>Moose!</b></h4><pre>
       If you prefer a Moose-like interface to create accessors, you can use "has" by importing this module like
       this:

         use Class::Accessor "antlers";

       or

         use Class::Accessor "moose-like";

       Then you can declare accessors like this:

         has alpha =&gt; ( is =&gt; "rw", isa =&gt; "Str" );
         has beta  =&gt; ( is =&gt; "ro", isa =&gt; "Str" );
         has gamma =&gt; ( is =&gt; "wo", isa =&gt; "Str" );

       Currently only the "is" attribute is supported.  And our "is" also supports the "wo" value to make a
       write-only accessor.

       If you are using the Moose-like interface then you should use the "extends" rather than tweaking your
       @ISA directly.  Basically, replace

         @ISA = qw/Foo Bar/;

       with

         extends(qw/Foo Bar/);

</pre><h4><b>DETAILS</b></h4><pre>
       An accessor generated by Class::Accessor looks something like this:

           # Your foo may vary.
           sub foo {
               my($self) = shift;
               if(@_) {    # set
                   return $self-&gt;set('foo', @_);
               }
               else {
                   return $self-&gt;get('foo');
               }
           }

       Very simple.  All it does is determine if you're wanting to set a value or get a value and calls the
       appropriate method.  Class::Accessor provides default <b>get()</b> and <b>set()</b> methods which your class can
       override.  They're detailed later.

   <b>Modifying</b> <b>the</b> <b>behavior</b> <b>of</b> <b>the</b> <b>accessor</b>
       Rather than actually modifying the accessor itself, it is much more sensible to simply override the two
       key methods which the accessor calls.  Namely <b>set()</b> and <b>get()</b>.

       If you -really- want to, you can override <b>make_accessor()</b>.

   <b>set</b>
           $obj-&gt;set($key, $value);
           $obj-&gt;set($key, @values);

       <b>set()</b> defines how generally one stores data in the object.

       override this method to change how data is stored by your accessors.

   <b>get</b>
           $value  = $obj-&gt;get($key);
           @values = $obj-&gt;get(@keys);

       <b>get()</b> defines how data is retrieved from your objects.

       override this method to change how it is retrieved.

   <b>make_accessor</b>
           $accessor = __PACKAGE__-&gt;make_accessor($field);

       Generates a subroutine reference which acts as an accessor for the given $field.  It calls <b>get()</b> and
       <b>set()</b>.

       If you wish to change the behavior of your accessors, try overriding <b>get()</b> and <b>set()</b> before you start
       mucking with <b>make_accessor()</b>.

   <b>make_ro_accessor</b>
           $read_only_accessor = __PACKAGE__-&gt;make_ro_accessor($field);

       Generates a subroutine reference which acts as a read-only accessor for the given $field.  It only calls
       <b>get()</b>.

       Override <b>get()</b> to change the behavior of your accessors.

   <b>make_wo_accessor</b>
           $write_only_accessor = __PACKAGE__-&gt;make_wo_accessor($field);

       Generates a subroutine reference which acts as a write-only accessor (mutator) for the given $field.  It
       only calls <b>set()</b>.

       Override <b>set()</b> to change the behavior of your accessors.

</pre><h4><b>EXCEPTIONS</b></h4><pre>
       If something goes wrong Class::Accessor will warn or die by calling Carp::carp or Carp::croak.  If you
       don't like this you can override <b>_carp()</b> and <b>_croak()</b> in your subclass and do whatever else you want.

</pre><h4><b>EFFICIENCY</b></h4><pre>
       Class::Accessor does not employ an autoloader, thus it is much faster than you'd think.  Its generated
       methods incur no special penalty over ones you'd write yourself.

         accessors:
                     Rate  Basic   Fast Faster Direct
         Basic   367589/s     --   -51%   -55%   -89%
         Fast    747964/s   103%     --    -9%   -77%
         Faster  819199/s   123%    10%     --   -75%
         Direct 3245887/s   783%   334%   296%     --

         mutators:
                     Rate    Acc   Fast Faster Direct
         Acc     265564/s     --   -54%   -63%   -91%
         Fast    573439/s   116%     --   -21%   -80%
         Faster  724710/s   173%    26%     --   -75%
         Direct 2860979/s   977%   399%   295%     --

       Class::Accessor::Fast is faster than methods written by an average programmer (where "average" is based
       on Schwern's example code).

       Class::Accessor is slower than average, but more flexible.

       Class::Accessor::Faster is even faster than Class::Accessor::Fast.  It uses an array internally, not a
       hash.  This could be a good or bad feature depending on your point of view.

       Direct hash access is, of course, much faster than all of these, but it provides no encapsulation.

       Of course, it's not as simple as saying "Class::Accessor is slower than average".  These are benchmarks
       for a simple accessor.  If your accessors do any sort of complicated work (such as talking to a database
       or writing to a file) the time spent doing that work will quickly swamp the time spend just calling the
       accessor.  In that case, Class::Accessor and the ones you write will be roughly the same speed.

</pre><h4><b>EXAMPLES</b></h4><pre>
       Here's an example of generating an accessor for every public field of your class.

           package Altoids;

           use base qw(Class::Accessor Class::Fields);
           use fields qw(curiously strong mints);
           Altoids-&gt;mk_accessors( Altoids-&gt;show_fields('Public') );

           sub new {
               my $proto = shift;
               my $class = ref $proto || $proto;
               return fields::new($class);
           }

           my Altoids $tin = Altoids-&gt;new;

           $tin-&gt;curiously('Curiouser and curiouser');
           print $tin-&gt;{curiously};    # prints 'Curiouser and curiouser'

           # Subclassing works, too.
           package Mint::Snuff;
           use base qw(Altoids);

           my Mint::Snuff $pouch = Mint::Snuff-&gt;new;
           $pouch-&gt;strong('Blow your head off!');
           print $pouch-&gt;{strong};     # prints 'Blow your head off!'

       Here's a simple example of altering the behavior of your accessors.

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;mk_accessors(qw(this that up down));

           sub get {
               my $self = shift;

               # Note every time someone gets some data.
               print STDERR "Getting @_\n";

               $self-&gt;SUPER::get(@_);
           }

           sub set {
               my ($self, $key) = splice(@_, 0, 2);

               # Note every time someone sets some data.
               print STDERR "Setting $key to @_\n";

               $self-&gt;SUPER::set($key, @_);
           }

</pre><h4><b>CAVEATS</b> <b>AND</b> <b>TRICKS</b></h4><pre>
       Class::Accessor has to do some internal wackiness to get its job done quickly and efficiently.  Because
       of this, there's a few tricks and traps one must know about.

       Hey, nothing's perfect.

   <b>Don't</b> <b>make</b> <b>a</b> <b>field</b> <b>called</b> <b>DESTROY</b>
       This is bad.  Since DESTROY is a magical method it would be bad for us to define an accessor using that
       name.  Class::Accessor will carp if you try to use it with a field named "DESTROY".

   <b>Overriding</b> <b>autogenerated</b> <b>accessors</b>
       You may want to override the autogenerated accessor with your own, yet have your custom accessor call the
       default one.  For instance, maybe you want to have an accessor which checks its input.  Normally, one
       would expect this to work:

           package Foo;
           use base qw(Class::Accessor);
           Foo-&gt;mk_accessors(qw(email this that whatever));

           # Only accept addresses which look valid.
           sub email {
               my($self) = shift;
               my($email) = @_;

               if( @_ ) {  # Setting
                   require Email::Valid;
                   unless( Email::Valid-&gt;address($email) ) {
                       carp("$email doesn't look like a valid address.");
                       return;
                   }
               }

               return $self-&gt;SUPER::email(@_);
           }

       There's a subtle problem in the last example, and it's in this line:

           return $self-&gt;SUPER::email(@_);

       If we look at how Foo was defined, it called <b>mk_accessors()</b> which stuck <b>email()</b> right into Foo's
       namespace.  There *is* no <b>SUPER::email()</b> to delegate to!  Two ways around this... first is to make a
       "pure" base class for Foo.  This pure class will generate the accessors and provide the necessary super
       class for Foo to use:

           package Pure::Organic::Foo;
           use base qw(Class::Accessor);
           Pure::Organic::Foo-&gt;mk_accessors(qw(email this that whatever));

           package Foo;
           use base qw(Pure::Organic::Foo);

       And now <b>Foo::email()</b> can override the generated <b>Pure::Organic::Foo::email()</b> and use it as <b>SUPER::email()</b>.

       This is probably the most obvious solution to everyone but me.  Instead, what first made sense to me was
       for <b>mk_accessors()</b> to define an alias of <b>email()</b>, <b>_email_accessor()</b>.  Using this solution, <b>Foo::email()</b>
       would be written with:

           return $self-&gt;_email_accessor(@_);

       instead of the expected <b>SUPER::email()</b>.

</pre><h4><b>AUTHORS</b></h4><pre>
       Copyright 2017 Marty Pauley &lt;marty+<a href="mailto:perl@martian.org">perl@martian.org</a>&gt;

       This program is free software; you can redistribute it and/or modify it under the same terms as Perl
       itself.  That means either (a) the GNU General Public License or (b) the Artistic License.

   <b>ORIGINAL</b> <b>AUTHOR</b>
       Michael G Schwern &lt;<a href="mailto:schwern@pobox.com">schwern@pobox.com</a>&gt;

   <b>THANKS</b>
       Liz and RUZ for performance tweaks.

       Tels, for his big feature request/bug report.

       Various presenters at YAPC::Asia 2009 for criticising the non-Moose interface.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       See Class::Accessor::Fast and Class::Accessor::Faster if speed is more important than flexibility.

       These are some modules which do similar things in different ways Class::Struct, Class::Methodmaker,
       Class::Generate, Class::Class, Class::Contract, Moose, Mouse

       See Class::DBI for an example of this module in use.

perl v5.36.0                                       2023-01-22                               <u>Class::<a href="../man3pm/Accessor.3pm.html">Accessor</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>