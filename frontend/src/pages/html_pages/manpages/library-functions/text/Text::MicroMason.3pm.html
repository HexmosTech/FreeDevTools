<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Text::MicroMason - Simple and Extensible Templating</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libtext-micromason-perl">libtext-micromason-perl_2.23-5_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Text::MicroMason - Simple and Extensible Templating

</pre><h4><b>SYNOPSIS</b></h4><pre>
       Mason syntax provides several ways to mix Perl into a text template:

           &lt;%args&gt;
             $name
           &lt;/%args&gt;
           % if ( $name eq 'Dave' ) {
             I'm sorry &lt;% $name %&gt;, I'm afraid I can't do that right now.
           % } else {
             &lt;%perl&gt;
               my $hour = (localtime)[2];
               my $daypart = ( $hour &gt; 11 ) ? 'afternoon' : 'morning';
             &lt;/%perl&gt;
             Good &lt;% $daypart %&gt;, &lt;% $name %&gt;!
           % }

       Create a MicroMason object to interpret the templates:

           use Text::MicroMason;
           $mason = Text::MicroMason-&gt;new();

       Use the compile method to convert templates into a subroutines:

           $coderef = $mason-&gt;compile( text=&gt;$template );
           print $coderef-&gt;('name'=&gt;'Alice');

       Or use the execute method to parse and evaluate in one call:

           print $mason-&gt;execute( text=&gt;$template, 'name'=&gt;'Bob' );

       Templates stored in files can be run directly or included in others:

           print $mason-&gt;execute( file=&gt;"./greeting.msn", 'name'=&gt;'Charles');

       For additional features, select mixin classes to add to your MicroMason object:

           $mason = Text::MicroMason-&gt;new( qw( -CatchErrors -Safe -Filters ) );

       You can import various functions if you prefer to avoid method calls:

           use Text::MicroMason::Functions qw( compile execute );

           print execute($template, 'name'=&gt;'Dave');

           $coderef = compile($template);
           print $coderef-&gt;('name'=&gt;'Bob');

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Text::MicroMason interpolates blocks of Perl code embedded into text strings.

       Each MicroMason object acts as a "template compiler," which converts templates from text-with-embedded-
       code formats into ready-to-execute Perl subroutines.

   <b>MicroMason</b> <b>Initialization</b>
       Use the <b>new()</b> method to create a Text::MicroMason object with the appropriate mixins and attributes.

         $mason = Text::MicroMason-&gt;new( %attribs );

       You may pass attributes as key-value pairs to the <b>new()</b> method to save various options for later use by
       the <b>compile()</b> method.

   <b>Template</b> <b>Compilation</b>
       To compile a text template, pass it to the <b>compile()</b> method to produce a new Perl subroutine to be
       returned as a code reference:

         $code_ref = $mason-&gt;compile( $type =&gt; $source, %attribs );

       Any attributes provided to <b>compile()</b> will temporarily override the persistent options defined by <b>new()</b>,
       for that template only.

       You can provide the template as a text string, a file name, or an open file handle:

         $code_ref = $mason-&gt;compile( text =&gt; $template );
         $code_ref = $mason-&gt;compile( text =&gt; \$template );
         $code_ref = $mason-&gt;compile( file =&gt; $filename );
         $code_ref = $mason-&gt;compile( handle =&gt; $fh );
         $code_ref = $mason-&gt;compile( handle =&gt; \*FILE );

       Template files are just plain text files that contains the string to be parsed. The files may have any
       name and extension you wish. The filename specified can either be absolute or relative to the program's
       current directory.

   <b>Template</b> <b>Execution</b>
       To execute the template and obtain the output, call a compiled function:

         $result = $code_ref-&gt;( @arguments );

       (Note that the $code_ref-&gt;() syntax is unavailable in older versions of Perl; use the equivalent
       &amp;$<b>code_ref()</b> syntax instead.)

       As a shortcut, the execute method compiles and runs the template one time:

         $result = $mason-&gt;execute( $type =&gt; $source, @arguments );
         $result = $mason-&gt;execute( $type =&gt; $source, \%attribs, @arguments );

   <b>Argument</b> <b>Passing</b>
       You can pass arguments to a template subroutine using positional or named arguments.

       For positional arguments, pass the argument list and read from @_ as usual:

         $mason-&gt;compile( text=&gt;'Hello &lt;% shift(@_) %&gt;.' )-&gt;( 'Dave' );

       For named arguments, pass in a hash of key-value pairs to be made accessible in an %ARGS hash within the
       template subroutine:

         $mason-&gt;compile( text=&gt;'Hello &lt;% $ARGS{name} %&gt;.' )-&gt;( name=&gt;'Dave' );

       Additionally, you can use named arguments with the %args block syntax:

         $mason-&gt;compile( text=&gt;'&lt;%args&gt;$name&lt;/%args&gt;Hello &lt;% $name %&gt;.' )-&gt;( name=&gt;'Dave' );

   <b>Mixin</b> <b>Selection</b>
       Arguments passed to <b>new()</b> that begin with a dash will be added as mixin classes.

         $mason = Text::MicroMason-&gt;new( -Mixin1, %attribs, -Mixin2 );

       Every MicroMason object inherits from an abstract Base class and some set of mixin classes. By combining
       mixins you can create subclasses with the desired combination of features. See Text::MicroMason::Base for
       documentation of the base class, including private methods and extension mechanisms.

       If you call the new method on Text::MicroMason, it automatically includes the HTMLMason mixin, which
       provides the standard template syntax. If you want to create an object without the default HTMLMason
       functionality, call Text::MicroMason::Base-&gt;<b>new()</b> instead.

       Some mixins define the syntax for a particular template format. You will generally need to select one,
       and only one, of the mixins listed in "TEMPLATE SYNTAXES".

       Other mixins provide optional functionality. Those mixins may define additional public methods, and may
       support or require values for various additional attributes. For a list of such mixin classes, see "MIXIN
       FEATURES".

</pre><h4><b>TEMPLATE</b> <b>SYNTAXES</b></h4><pre>
       Templates contain a mix of literal text to be output with some type of markup syntax which specifies more
       complex behaviors.

       The Text::MicroMason::HTMLMason mixin is selected by default. To enable an alternative, pass its name to
       Text::MicroMason::Base-&gt;new( - MixinName ).

   <b>HTMLMason</b>
       The HTMLMason mixin provides lexer and assembler methods that handle most elements of HTML::Mason's
       template syntax.

         my $mason = Text::MicroMason::Base-&gt;new( -HTMLMason );
         my $output = $mason-&gt;execute( text =&gt; $template, name =&gt; 'Bob' );

           &lt;%args&gt;
             $name =&gt; 'Guest'
           &lt;/%args&gt;

           % if ( $name eq 'Dave' ) {
             I'm sorry &lt;% $name %&gt;, I'm afraid I can't do that right now.
           % } else {
             &lt;%perl&gt;
               my $hour = (localtime)[2];
               my $daypart = ( $hour &gt; 11 ) ? 'afternoon' : 'morning';
             &lt;/%perl&gt;
             Good &lt;% $daypart %&gt;, &lt;% $name %&gt;!
           % }

           &lt;&amp; "includes/standard_footer.msn" &amp;&gt;

           &lt;%doc&gt;
             Here's a private developr comment describing this template.
           &lt;/%doc&gt;

       For a definition of the template syntax, see Text::MicroMason::HTMLMason.

   <b>DoubleQuote</b>
       The DoubleQuote mixin uses Perl's double-quoting interpolation as a minimalist syntax for templating.

         my $mason = Text::MicroMason::Base-&gt;new( -DoubleQuote );
         my $output = $mason-&gt;execute( text =&gt; $template, name =&gt; 'Bob' );

           ${ $::hour = (localtime)[2];
             $::daypart = ( $::hour &gt; 11 ) ? 'afternoon' : 'morning';
           \'' }
           Good $::daypart, $ARGS{name}!

       For more information see Text::MicroMason::DoubleQuote.

   <b>Embperl</b>
       The Embperl mixin support a template syntax similar to that used by the HTML::Embperl module.

         my $mason = Text::MicroMason::Base-&gt;new( -Embperl );
         my $output = $mason-&gt;execute( text =&gt; $template, name =&gt; 'Bob' );

           [- my $name = $ARGS{name}; -]
           [$ if $name eq 'Dave' $]
             I'm sorry [+ $name +], I'm afraid I can't do that right now.
           [$ else $]
             [-
               my $hour = (localtime)[2];
               my $daypart = ( $hour &gt; 11 ) ? 'afternoon' : 'morning';
             -]
             Good [+ $daypart +], [+ $name +]!
           [$ endif $]

       For more information see Text::MicroMason::Embperl.

   <b>HTMLTemplate</b>
       The HTMLTemplate mixin supports a syntax similar to that used by the HTML::Template module.

         my $mason = Text::MicroMason::Base-&gt;new( -HTMLTemplate );
         my $output = $mason-&gt;execute( text =&gt; $template, name =&gt; 'Bob' );

           &lt;TMPL_IF NAME="user_is_dave"&gt;
             I'm sorry &lt;TMPLVAR NAME="name"&gt;, I'm afraid I can't do that right now.
           &lt;TMPL_ELSE&gt;
             &lt;TMPL_IF NAME="daytime_is_morning"&gt;
               Good morning, &lt;TMPLVAR NAME="name"&gt;!
             &lt;TMPL_ELSE&gt;
               Good afternoon, &lt;TMPLVAR NAME="name"&gt;!
             &lt;/TMPL_IF&gt;
           &lt;/TMPL_IF&gt;

       For more information see Text::MicroMason::HTMLTemplate.

   <b>ServerPages</b>
       The ServerPages mixin supports a syntax similar to that used by the Apache::ASP module.

         my $mason = Text::MicroMason::Base-&gt;new( -ServerPages );
         my $output = $mason-&gt;execute( text =&gt; $template, name =&gt; 'Bob' );

           &lt;% my $name = $ARGS{name};
             if ( $name eq 'Dave' ) {  %&gt;
             I'm sorry &lt;%= $name %&gt;, I'm afraid I can't do that right now.
           &lt;% } else {
               my $hour = (localtime)[2];
               my $daypart = ( $hour &gt; 11 ) ? 'afternoon' : 'morning';
             %&gt;
             Good &lt;%= $daypart %&gt;, &lt;%= $name %&gt;!
           &lt;% } %&gt;

       For more information see Text::MicroMason::ServerPages.

   <b>Sprintf</b>
       The Sprintf mixin uses Perl's sprintf formatting syntax for templating.

         my $mason = Text::MicroMason::Base-&gt;new( -Sprintf );
         my $output = $mason-&gt;execute( text =&gt; $template, 'morning', 'Bob' );

           Good %s, %s!

       For more information see Text::MicroMason::Sprintf.

   <b>TextTemplate</b>
       The TextTemplate mixin supports a syntax similar to that used by the Text::Template module.

         my $mason = Text::MicroMason::Base-&gt;new( -TextTemplate );
         my $output = $mason-&gt;execute( text =&gt; $template, name =&gt; 'Bob' );

           { $hour = (localtime)[2];
             $daypart = ( $hour &gt; 11 ) ? 'afternoon' : 'morning';
           '' }
           Good { $daypart }, { $name }!

       For more information see Text::MicroMason::TextTemplate.

</pre><h4><b>MIXIN</b> <b>FEATURES</b></h4><pre>
       The following mixin classes can be layered on to your MicroMason object to provide additional
       functionality.

       To add a mixin's functionality, pass it's name with a dash to the <b>new()</b> method:

         $mason = Text::MicroMason-&gt;new( -CatchErrors, -PostProcess );

   <b>AllowGlobals</b>
       Enables access to a set of package variables to be shared with templates.

       For details see Text::MicroMason::AllowGlobals.

   <b>CatchErrors</b>
       Both compilation and run-time errors in your template are handled as fatal exceptions. To prevent a
       template error from ending your program, enclose it in an eval block:

         my $result = eval { $mason-&gt;execute( text =&gt; $template ) };
         if ( $@ ) {
           print "Unable to execute template: $@";
         } else {
           print $result;
         }

       To transparently add this functionality to your MicroMason object, see Text::MicroMason::CatchErrors.

   <b>CompileCache</b>
       Calling execute repeatedly will be slower than compiling once and calling the template function
       repeatedly, unless you enable compilation caching.

       For details see Text::MicroMason::CompileCache.

   <b>Debug</b>
       When trying to debug a template problem, it can be helpful to watch the internal processes of template
       compilation. This mixin adds controllable warning messages that show the intermediate parse information.

       For details see Text::MicroMason::Debug.

   <b>LineNumbers</b>
       Provide better line numbers when compilation fails, at the cost of potentially slower compilation and
       execution.

       For details see Text::MicroMason::LineNumbers.

   <b>ExecuteCache</b>
       Each time you execute the template all of the logic will be re- evaluated, unless you enable execution
       caching, which stores the output of each template for each given set of arguments.

       For details see Text::MicroMason::ExecuteCache.

   <b>Filters</b>
       HTML::Mason provides an expression filtering mechanism which is typically used for applying HTML and URL
       escaping functions to output.

         Text::MicroMason-&gt;new(-Filters)-&gt;compile( text =&gt; $template );

         &lt;p&gt; Hello &lt;% $name |h %&gt;!

       The Filters mixin provides this capability for Text::MicroMason templates. To select it, add its name to
       your Mason initialization call:

         my $mason = Text::MicroMason-&gt;new( -Filters );

       Output expressions may then be followed by "|h" or "|u" escapes; for example this line would convert any
       ampersands in the output to the equivalent HTML entity:

         Welcome to &lt;% $company_name |h %&gt;

       For more information see Text::MicroMason::Filters.

   <b>PassVariables</b>
       Allows you to pass arguments to templates as variables instead of the basic argument list.

       For details see Text::MicroMason::PostProcess.

   <b>PostProcess</b>
       Allows you to specify one or more functions through which all template output should be passed before it
       is returned.

       For details see Text::MicroMason::PostProcess.

   <b>Safe</b>
       By default, the code embedded in a template has accss to all of the capabilities of your Perl process,
       and could potentially perform dangerous activities such as accessing or modifying files and starting
       other programs.

       If you need to execute untrusted templates, use the Safe module, which can restrict the operations and
       data structures that template code can access.

       To add this functionality to your MicroMason object, see Text::MicroMason::Safe.

   <b>TemplateDir</b>
       The filenames passed to the <b>compile()</b> or <b>execute()</b> methods can be looked up relative to a base directory
       path or the current template file.

       To add this functionality to your MicroMason object, see Text::MicroMason::TemplateDir.

   <b>TemplatePath</b>
       The filenames passed to the <b>compile()</b> or <b>execute()</b> methods are looked up relative to a list of multiple
       base directory paths, in order. It tries as hard as possible to maintain compatibility with caching and
       &lt;&amp; &amp;&gt; template includes.

       To add this functionality to your MicroMason object, see Text::MicroMason::TemplatePath.

</pre><h4><b>OTHER</b> <b>INTERFACES</b></h4><pre>
   <b>Function</b> <b>Exporter</b>
       Importable functions are provided for users who prefer a procedural interface.

       The supported functions are listed in Text::MicroMason::Functions.  (For backwards compatibility, those
       functions can also be imported from the main Text::MicroMason package.)

   <b>Template</b> <b>Frameworks</b>
       Adaptor modules are available to use MicroMason from within other frameworks.  For more information, see
       Any::Template::Backend::Text::MicroMason and Catalyst::View::MicroMason.

   <b>Inline</b>
       MicroMason templates can be embbeded within your source code using Inline.  For more information, see
       Inline::Mason.

</pre><h4><b>EXCEPTIONS</b></h4><pre>
       Text::MicroMason croaks on error, with an appropriate error string. Some commonly occurring error
       messages are described below (where %s indicates variable message text). See also the pod for each mixin
       class, for additional exception strings that may be thrown.

       •   MicroMason parsing halted at %s

           Indicates  that the parser was unable to finish tokenising the source text. Generally this means that
           there is a bug somewhere in the regular expressions used by <b>lex()</b>.

           (If you encounter this error, please feel free to file a bug report or send an example of  the  error
           to the author using the addresses below, and I'll attempt to correct it in a future release.)

       •   MicroMason compilation failed: %s

           The  template was parsed successfully, but the Perl subroutine declaration it was converted to failed
           to compile. This is generally a result of a syntax error in one of the Perl expressions  used  within
           the template.

       •   Error in template subroutine: %s

           Additional  diagnostic  for  compilation  errors,  showing the text of the subroutine which failed to
           compile.

       •   Error in template file %s, interpreted as: %s

           Additional diagnostic for compilation errors in external files, showing the filename and the text  of
           the subroutine which failed to compile.

       •   MicroMason execution failed: %s

           After  parsing  and  compiling  the  template successfully, the subroutine was run and caused a fatal
           exception, generally because that some Perl code used within the template caused <b>die()</b> to  be  called
           (or an equivalent function like croak or confess).

       •   MicroMason: filename is missing or empty

           One  of  the compile or execute methods was called with an empty or undefined filename, or one of the
           compile_file or execute_file methods was called with no arguments.

       •   MicroMason can't read from %s: %s

           One of the compile_file or execute_file functions was called but we were unable to read the requested
           file, because the file path is incorrect or we have insufficient privileges to read that file.

       •   [Reference / Object]

           If the code inside the template throws a reference or object while calling <b>die()</b>, MicroMason will re-
           throw the reference to the calling code.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       For    distribution,    installation,    support,    copyright    and    license     information,     see
       Text::MicroMason::Docs::ReadMe.

perl v5.36.0                                       2023-08-10                              <u>Text::<a href="../man3pm/MicroMason.3pm.html">MicroMason</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>