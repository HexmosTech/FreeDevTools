<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Moose::Meta::Attribute::Native - Delegate to native Perl types</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libmoose-perl">libmoose-perl_2.2207-1build3_amd64</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Moose::Meta::Attribute::Native - Delegate to native Perl types

</pre><h4><b>VERSION</b></h4><pre>
       version 2.2207

</pre><h4><b>SYNOPSIS</b></h4><pre>
         package MyClass;
         use Moose;

         has 'mapping' =&gt; (
             traits  =&gt; ['Hash'],
             is      =&gt; 'rw',
             isa     =&gt; 'HashRef[Str]',
             default =&gt; sub { {} },
             handles =&gt; {
                 exists_in_mapping =&gt; 'exists',
                 ids_in_mapping    =&gt; 'keys',
                 get_mapping       =&gt; 'get',
                 set_mapping       =&gt; 'set',
                 set_quantity      =&gt; [ set =&gt; 'quantity' ],
             },
         );

         my $obj = MyClass-&gt;new;
         $obj-&gt;<a href="../man10/set_quantity.10.html">set_quantity</a>(10);      # quantity =&gt; 10
         $obj-&gt;set_mapping('foo', 4); # foo =&gt; 4
         $obj-&gt;set_mapping('bar', 5); # bar =&gt; 5
         $obj-&gt;set_mapping('baz', 6); # baz =&gt; 6

         # prints 5
         print $obj-&gt;get_mapping('bar') if $obj-&gt;exists_in_mapping('bar');

         # prints 'quantity, foo, bar, baz'
         print join ', ', $obj-&gt;ids_in_mapping;

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Native delegations allow you to delegate to native Perl data structures as if they were objects. For
       example, in the "SYNOPSIS" you can see a hash reference being treated as if it has methods named
       exists(), keys(), get(), and set().

       The delegation methods (mostly) map to Perl builtins and operators. The return values of these
       delegations should be the same as the corresponding Perl operation. Any deviations will be explicitly
       documented.

</pre><h4><b>API</b></h4><pre>
       Native delegations are enabled by passing certain options to "has" when creating an attribute.

   <b>traits</b>
       To enable this feature, pass the appropriate name in the "traits" array reference for the attribute. For
       example, to enable this feature for hash reference, we include 'Hash' in the list of traits.

   <b>isa</b>
       You will need to make sure that the attribute has an appropriate type. For example, to use this with a
       Hash you must specify that your attribute is some sort of "HashRef".

   <b>handles</b>
       This is just like any other delegation, but only a hash reference is allowed when defining native
       delegations. The keys are the methods to be created in the class which contains the attribute. The values
       are the methods provided by the associated trait. Currying works the same way as it does with any other
       delegation.

       See the docs for each native trait for details on what methods are available.

</pre><h4><b>TRAITS</b> <b>FOR</b> <b>NATIVE</b> <b>DELEGATIONS</b></h4><pre>
       Below are some simple examples of each native trait. More features are available than what is shown here;
       this is just a quick synopsis.

       Array (Moose::Meta::Attribute::Native::Trait::Array)
               has 'queue' =&gt; (
                   traits  =&gt; ['Array'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'ArrayRef[Str]',
                   default =&gt; sub { [] },
                   handles =&gt; {
                       add_item  =&gt; 'push',
                       next_item =&gt; 'shift',
                       # ...
                   }
               );

       Bool (Moose::Meta::Attribute::Native::Trait::Bool)
               has 'is_lit' =&gt; (
                   traits  =&gt; ['Bool'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'Bool',
                   default =&gt; 0,
                   handles =&gt; {
                       illuminate  =&gt; 'set',
                       darken      =&gt; 'unset',
                       flip_switch =&gt; 'toggle',
                       is_dark     =&gt; 'not',
                       # ...
                   }
               );

       Code (Moose::Meta::Attribute::Native::Trait::Code)
               has 'callback' =&gt; (
                   traits  =&gt; ['Code'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'CodeRef',
                   default =&gt; sub {
                       sub {'called'}
                   },
                   handles =&gt; {
                       call =&gt; 'execute',
                       # ...
                   }
               );

       Counter (Moose::Meta::Attribute::Native::Trait::Counter)
               has 'counter' =&gt; (
                   traits  =&gt; ['Counter'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'Num',
                   default =&gt; 0,
                   handles =&gt; {
                       inc_counter   =&gt; 'inc',
                       dec_counter   =&gt; 'dec',
                       reset_counter =&gt; 'reset',
                       # ...
                   }
               );

       Hash (Moose::Meta::Attribute::Native::Trait::Hash)
               has 'options' =&gt; (
                   traits  =&gt; ['Hash'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'HashRef[Str]',
                   default =&gt; sub { {} },
                   handles =&gt; {
                       set_option =&gt; 'set',
                       get_option =&gt; 'get',
                       has_option =&gt; 'exists',
                       # ...
                   }
               );

       Number (Moose::Meta::Attribute::Native::Trait::Number)
               has 'integer' =&gt; (
                   traits  =&gt; ['Number'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'Int',
                   default =&gt; 5,
                   handles =&gt; {
                       set =&gt; 'set',
                       add =&gt; 'add',
                       sub =&gt; 'sub',
                       mul =&gt; 'mul',
                       div =&gt; 'div',
                       mod =&gt; 'mod',
                       abs =&gt; 'abs',
                       # ...
                   }
               );

       String (Moose::Meta::Attribute::Native::Trait::String)
               has 'text' =&gt; (
                   traits  =&gt; ['String'],
                   is      =&gt; 'ro',
                   isa     =&gt; 'Str',
                   default =&gt; q{},
                   handles =&gt; {
                       add_text     =&gt; 'append',
                       replace_text =&gt; 'replace',
                       # ...
                   }
               );

</pre><h4><b>COMPATIBILITY</b> <b>WITH</b> <b>MooseX::AttributeHelpers</b></h4><pre>
       This feature used to be a separated CPAN distribution called MooseX::AttributeHelpers.

       When  the  feature  was  incorporated  into  the  Moose  core,  some of the API details were changed. The
       underlying capabilities are the same, but some details of the API were changed.

</pre><h4><b>BUGS</b></h4><pre>
       See "BUGS" in Moose for details on reporting bugs.

</pre><h4><b>AUTHORS</b></h4><pre>
       •   Stevan Little &lt;<a href="mailto:stevan@cpan.org">stevan@cpan.org</a>&gt;

       •   Dave Rolsky &lt;<a href="mailto:autarch@urth.org">autarch@urth.org</a>&gt;

       •   Jesse Luehrs &lt;<a href="mailto:doy@cpan.org">doy@cpan.org</a>&gt;

       •   Shawn M Moore &lt;<a href="mailto:sartak@cpan.org">sartak@cpan.org</a>&gt;

       •   יובל קוג'מן (Yuval Kogman) &lt;<a href="mailto:nothingmuch@woobling.org">nothingmuch@woobling.org</a>&gt;

       •   Karen Etheridge &lt;<a href="mailto:ether@cpan.org">ether@cpan.org</a>&gt;

       •   Florian Ragwitz &lt;<a href="mailto:rafl@debian.org">rafl@debian.org</a>&gt;

       •   Hans Dieter Pearcey &lt;<a href="mailto:hdp@cpan.org">hdp@cpan.org</a>&gt;

       •   Chris Prather &lt;<a href="mailto:chris@prather.org">chris@prather.org</a>&gt;

       •   Matt S Trout &lt;<a href="mailto:mstrout@cpan.org">mstrout@cpan.org</a>&gt;

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       This software is copyright (c) 2006 by Infinity Interactive, Inc.

       This is free software; you can redistribute it and/or modify it under  the  same  terms  as  the  Perl  5
       programming language system itself.

perl v5.40.0                                       2024-10-20                <u>Moose::Meta::Attribute::<a href="../man3pm/Native.3pm.html">Native</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>