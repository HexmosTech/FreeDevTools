<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>DBIx::Class - Extensible and flexible object <-> relational mapper.</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libdbix-class-perl">libdbix-class-perl_0.082844-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       DBIx::Class - Extensible and flexible object &lt;-&gt; relational mapper.

</pre><h4><b>WHERE</b> <b>TO</b> <b>START</b> <b>READING</b></h4><pre>
       See DBIx::Class::Manual::DocMap for an overview of the exhaustive documentation.  To get the most out of
       DBIx::Class with the least confusion it is strongly recommended to read (at the very least) the Manuals
       in the order presented there.

</pre><h4><b>GETTING</b> <b>HELP/SUPPORT</b></h4><pre>
       Due to the sheer size of its problem domain, DBIx::Class is a relatively complex framework. After you
       start using DBIx::Class questions will inevitably arise. If you are stuck with a problem or have doubts
       about a particular approach do not hesitate to contact us via any of the following options (the list is
       sorted by "fastest response time"):

       •   RT Bug Tracker: &lt;https://rt.cpan.org/Public/Dist/Display.html?Name=DBIx-Class&gt;

       •   Email: &lt;<a href="mailto:bug-DBIx-Class@rt.cpan.org">mailto:bug-DBIx-Class@rt.cpan.org</a>&gt;

       •   Twitter: &lt;https://twitter.com/intent/tweet?text=%40ribasushi%20%23DBIC&gt;

</pre><h4><b>SYNOPSIS</b></h4><pre>
       For the very impatient: DBIx::Class::Manual::QuickStart

       This  code  in  the next step can be generated automatically from an existing database, see dbicdump from
       the distribution "DBIx-Class-Schema-Loader".

   <b>Schema</b> <b>classes</b> <b>preparation</b>
       Create a schema class called <u>MyApp/Schema.pm</u>:

         package MyApp::Schema;
         use base qw/DBIx::Class::Schema/;

         __PACKAGE__-&gt;load_namespaces();

         1;

       Create a result class to represent artists, who have many CDs, in <u>MyApp/Schema/Result/Artist.pm</u>:

       See DBIx::Class::ResultSource for docs on defining result classes.

         package MyApp::Schema::Result::Artist;
         use base qw/DBIx::Class::Core/;

         __PACKAGE__-&gt;table('artist');
         __PACKAGE__-&gt;add_columns(qw/ artistid name /);
         __PACKAGE__-&gt;set_primary_key('artistid');
         __PACKAGE__-&gt;has_many(cds =&gt; 'MyApp::Schema::Result::CD', 'artistid');

         1;

       A result class to represent a CD, which belongs to an artist, in <u>MyApp/Schema/Result/CD.pm</u>:

         package MyApp::Schema::Result::CD;
         use base qw/DBIx::Class::Core/;

         __PACKAGE__-&gt;load_components(qw/InflateColumn::DateTime/);
         __PACKAGE__-&gt;table('cd');
         __PACKAGE__-&gt;add_columns(qw/ cdid artistid title year /);
         __PACKAGE__-&gt;set_primary_key('cdid');
         __PACKAGE__-&gt;belongs_to(artist =&gt; 'MyApp::Schema::Result::Artist', 'artistid');

         1;

   <b>API</b> <b>usage</b>
       Then you can use these classes in your application's code:

         # Connect to your database.
         use MyApp::Schema;
         my $schema = MyApp::Schema-&gt;connect($dbi_dsn, $user, $pass, \%dbi_params);

         # Query for all artists and put them in an array,
         # or retrieve them as a result set object.
         # $schema-&gt;resultset returns a DBIx::Class::ResultSet
         my @all_artists = $schema-&gt;resultset('Artist')-&gt;all;
         my $all_artists_rs = $schema-&gt;resultset('Artist');

         # Output all artists names
         # $artist here is a DBIx::Class::Row, which has accessors
         # for all its columns. Rows are also subclasses of your Result class.
         foreach $artist (@all_artists) {
           print $artist-&gt;name, "\n";
         }

         # Create a result set to search for artists.
         # This does not query the DB.
         my $johns_rs = $schema-&gt;resultset('Artist')-&gt;search(
           # Build your WHERE using an SQL::Abstract::Classic-compatible structure:
           { name =&gt; { like =&gt; 'John%' } }
         );

         # Execute a joined query to get the cds.
         my @all_john_cds = $johns_rs-&gt;search_related('cds')-&gt;all;

         # Fetch the next available row.
         my $first_john = $johns_rs-&gt;next;

         # Specify ORDER BY on the query.
         my $first_john_cds_by_title_rs = $first_john-&gt;cds(
           undef,
           { order_by =&gt; 'title' }
         );

         # Create a result set that will fetch the artist data
         # at the same time as it fetches CDs, using only one query.
         my $millennium_cds_rs = $schema-&gt;resultset('CD')-&gt;search(
           { year =&gt; 2000 },
           { prefetch =&gt; 'artist' }
         );

         my $cd = $millennium_cds_rs-&gt;next; # SELECT ... FROM cds JOIN artists ...
         my $cd_artist_name = $cd-&gt;artist-&gt;name; # Already has the data so no 2nd query

         # new() makes a Result object but doesn't insert it into the DB.
         # create() is the same as new() then insert().
         my $new_cd = $schema-&gt;resultset('CD')-&gt;new({ title =&gt; 'Spoon' });
         $new_cd-&gt;artist($cd-&gt;artist);
         $new_cd-&gt;insert; # Auto-increment primary key filled in after INSERT
         $new_cd-&gt;title('Fork');

         $schema-&gt;txn_do(sub { $new_cd-&gt;update }); # Runs the update in a transaction

         # change the year of all the millennium CDs at once
         $millennium_cds_rs-&gt;update({ year =&gt; 2002 });

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This is an SQL to OO mapper with an object API inspired by Class::DBI (with a compatibility  layer  as  a
       springboard  for  porting) and a resultset API that allows abstract encapsulation of database operations.
       It aims to make representing queries in your code as perl-ish as possible while still providing access to
       as many of the capabilities of the database  as  possible,  including  retrieving  related  records  from
       multiple  tables  in a single query, "JOIN", "LEFT JOIN", "COUNT", "DISTINCT", "GROUP BY", "ORDER BY" and
       "HAVING" support.

       DBIx::Class can handle multi-column primary and foreign keys, complex queries and database-level  paging,
       and does its best to only query the database in order to return something you've directly asked for. If a
       resultset  is  used as an iterator it only fetches rows off the statement handle as requested in order to
       minimise memory usage. It has auto-increment support for SQLite, MySQL, PostgreSQL,  Oracle,  SQL  Server
       and  DB2  and  is known to be used in production on at least the first four, and is fork- and thread-safe
       out of the box (although your DBD may not be).

       This project is still under rapid development, so large new features may be marked  <b>experimental</b>  -  such
       APIs  are  still usable but may have edge bugs.  Failing test cases are <u>always</u> welcome and point releases
       are put out rapidly as bugs are found and fixed.

       We do our best to maintain full backwards compatibility for published APIs, since DBIx::Class is used  in
       production  in  many organisations, and even backwards incompatible changes to non-published APIs will be
       fixed if they're reported and doing so doesn't cost the codebase anything.

       The test suite is quite substantial, and several developer releases are generally made to CPAN before the
       branch for the next release is merged back to trunk for a major release.

</pre><h4><b>HOW</b> <b>TO</b> <b>CONTRIBUTE</b></h4><pre>
       Contributions are always welcome, in all usable forms (we especially welcome documentation improvements).
       The delivery methods include git- or unified-diff formatted patches, GitHub pull requests, or  plain  bug
       reports either via RT or the Mailing list. Do not hesitate to get in touch with any further questions you
       may have.

       This  project  is  maintained  in  a  git  repository.  The  code and related tools are accessible at the
       following locations:

       •   Current git repository: &lt;https://github.com/Perl5/DBIx-Class&gt;

       •   Travis-CI log: &lt;https://travis-ci.com/github/Perl5/DBIx-Class/branches&gt;

</pre><h4><b>AUTHORS</b></h4><pre>
       Even though a large portion of the source <u>appears</u> to be written by just a handful of people, this library
       continues to remain a collaborative effort - perhaps one of the most successful  such  projects  on  CPAN
       &lt;<a href="http://cpan.org">http://cpan.org</a>&gt;.   It  is  important  to  remember  that  ideas  do  not always result in a direct code
       contribution, but deserve acknowledgement  just  the  same.  Time  and  time  again  the  seemingly  most
       insignificant  questions  and  suggestions  have  been  shown  to  catalyze  monumental  improvements  in
       consistency, accuracy and performance.

       The canonical source of authors and their details is the <u>AUTHORS</u> file at the root  of  this  distribution
       (or repository). The canonical source of per-line authorship is the git repository history itself.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       Copyright  (c)  2005  by mst, castaway, ribasushi, and other DBIx::Class "AUTHORS" as listed above and in
       <u>AUTHORS</u>.

       This library is free software and may be distributed under the same terms as perl5  itself.  See  <u>LICENSE</u>
       for the complete licensing terms.

perl v5.40.0                                       2025-02-01                                   <u>DBIx::<a href="../man3pm/Class.3pm.html">Class</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>