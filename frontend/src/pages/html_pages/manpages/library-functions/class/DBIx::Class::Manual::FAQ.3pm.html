<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>DBIx::Class::Manual::FAQ - Frequently Asked Questions (in theory)</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libdbix-class-perl">libdbix-class-perl_0.082844-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       DBIx::Class::Manual::FAQ - Frequently Asked Questions (in theory)

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This document is intended as an anti-map of the documentation. If you know what you want to do, but not
       how to do it in DBIx::Class, then look here. It does <b>not</b> contain much code or examples, it just gives
       explanations and pointers to the correct pieces of documentation to read.

</pre><h4><b>FAQs</b></h4><pre>
       How Do I:

   <b>Getting</b> <b>started</b>
       .. create a database to use?
           First,  choose  a  database.  For  testing/experimenting,  we recommend DBD::SQLite, which is a self-
           contained small database (i.e. all you need to do is to install DBD::SQLite from CPAN, and it works).

           Next, spend some time defining which data you need to store, and how it relates to the other data you
           have. For some help on normalisation, go to &lt;<a href="http://b62.tripod.com/doc/dbbase.htm">http://b62.tripod.com/doc/dbbase.htm</a>&gt;.

           Now, decide whether you want to have the database itself be  the  definitive  source  of  information
           about  the data layout, or your DBIx::Class schema. If it's the former, look up the documentation for
           your database, eg. &lt;<a href="http://sqlite.org/lang_createtable.html">http://sqlite.org/lang_createtable.html</a>&gt;, on how  to  create  tables,  and  start
           creating  them.  For  a  nice  universal  interface  to your database, you can try DBI::Shell. If you
           decided on the latter choice, read the FAQ on setting up  your  classes  manually,  and  the  one  on
           creating tables from your schema.

       .. use DBIx::Class with Catalyst?
           Install  Catalyst::Model::DBIC::Schema  from  CPAN.  See  its  documentation,  or  below, for further
           details.

       .. set up my DBIx::Class classes automatically from my database?
           Install DBIx::Class::Schema::Loader from CPAN, and read its documentation.

       .. set up my DBIx::Class classes manually?
           Look at the DBIx::Class::Manual::Example and come back here if you get lost.

       .. create my database tables from my DBIx::Class schema?
           Create your classes manually, as above. Write a script that calls  "deploy"  in  DBIx::Class::Schema.
           See there for details, or the DBIx::Class::Manual::Cookbook.

       .. store/retrieve Unicode data in my database?
           Make  sure  you  database  supports Unicode and set the connect attributes appropriately - see "Using
           Unicode" in DBIx::Class::Manual::Cookbook

       .. connect to my database?
           Once you have created all the appropriate table/source classes, and an overall Schema class, you  can
           start  using them in an application. To do this, you need to create a central Schema object, which is
           used to access all the data in the various tables. See "connect" in DBIx::Class::Schema for  details.
           The  actual  connection  does  not happen until you actually request data, so don't be alarmed if the
           error from incorrect connection details happens a lot later.

       .. use DBIx::Class across multiple databases?
           If your database server allows you to run queries across multiple databases  at  once,  then  so  can
           DBIx::Class.  All  you need to do is make sure you write the database name as part of the table call.
           Eg:

             __PACKAGE__-&gt;table('mydb.mytablename');

           And load  all  the  Result  classes  for  both  /  all  databases  by  calling  "load_namespaces"  in
           DBIx::Class::Schema.

       .. use DBIx::Class across PostgreSQL/DB2/Oracle schemas?
           Add  the  name  of  the schema to the table name, when invoking table, and make sure the user you are
           about to connect as has permissions to read/write all the schemas/tables as necessary.

   <b>Relationships</b>
       .. tell DBIx::Class about relationships between my tables?
           There are a variety of relationship types that come pre-defined for you to use.  These are all listed
           in DBIx::Class::Relationship. If  you  need  a  non-standard  type,  or  more  information,  look  in
           DBIx::Class::Relationship::Base.

       .. define a one-to-many relationship?
           This is called a "has_many" relationship on the one side, and a "belongs_to" relationship on the many
           side.  Currently these need to be set up individually on each side. See DBIx::Class::Relationship for
           details.

       .. define a relationship where this table contains another table's primary key? (foreign key)
           Create a "belongs_to" relationship for the field containing the foreign  key.   See  "belongs_to"  in
           DBIx::Class::Relationship.

       .. define a foreign key relationship where the key field may contain NULL?
           Just  create  a  "belongs_to" relationship, as above. If the column is NULL then the inflation to the
           foreign object will not happen. This has a side effect of not always fetching all the relevant  data,
           if  you  use  a  nullable  foreign-key  relationship  in  a  JOIN,  then you probably want to set the
           "join_type" to "left".

       .. define a relationship where the key consists of more than one column?
           Instead of supplying a single column name, all relationship types also allow you to supply a  hashref
           containing  the condition across which the tables are to be joined. The condition may contain as many
           fields as you like. See DBIx::Class::Relationship::Base.

       .. define a relationship bridge across an intermediate table? (many-to-many)
           The term 'relationship' is used loosely with many_to_many as it is not considered a  relationship  in
           the    fullest   sense.    For   more   info,   read   the   documentation   on   "many_to_many"   in
           DBIx::Class::Relationship.

       .. stop DBIx::Class from attempting to cascade deletes on my has_many and might_have relationships?
           By  default,  DBIx::Class  cascades  deletes  and  updates   across   "has_many"   and   "might_have"
           relationships.   You   can   disable   this  behaviour  on  a  per-relationship  basis  by  supplying
           "cascade_delete =&gt; 0" in the relationship attributes.

           The cascaded operations are performed after the requested delete or update, so if your database has a
           constraint on the relationship, it will  have  deleted/updated  the  related  records  or  raised  an
           exception before DBIx::Class gets to perform the cascaded operation.

           See DBIx::Class::Relationship.

       .. use a relationship?
           Use  its  name.  An  accessor  is  created  using  the name. See examples in "USING RELATIONSHIPS" in
           DBIx::Class::Manual::Cookbook.

   <b>Searching</b>
       .. search for data?
           Create a $schema object, as mentioned above in ".. connect to my database?". Find the ResultSet  that
           you  want  to  search  in,  by  calling "$schema-&gt;resultset('MySource')" and call "search" on it. See
           "search" in DBIx::Class::ResultSet.

       .. search using database functions?
           Supplying something like:

            -&gt;search({'mydatefield' =&gt; 'now()'})

           to search, will probably not do what you expect. It will quote the text "<b>now()</b>", instead of trying to
           call the function. To provide literal, unquoted text you need to pass in a scalar reference, like so:

            -&gt;search({'mydatefield' =&gt; \'now()'})

       .. sort the results of my search?
           Supply a list of columns you want  to  sort  by  to  the  "order_by"  attribute.  See  "order_by"  in
           DBIx::Class::ResultSet.

       .. sort my results based on fields I've aliased using "as"?
           You  didn't  alias  anything,  since  as  <b>has</b>  <b>nothing</b>  <b>to</b>  <b>do</b> with the produced SQL. See "select" in
           DBIx::Class::ResultSet for details.

       .. group the results of my search?
           Supply a list of columns you want to group  on,  to  the  "group_by"  attribute,  see  "group_by"  in
           DBIx::Class::ResultSet.

       .. group my results based on fields I've aliased using "as"?
           You don't. See the explanation on ordering by an alias above.

       .. filter the results of my search?
           The  first  argument  to  "search"  is  a hashref of accessor names and values to filter them by, for
           example:

            -&gt;search({'created_time' =&gt; { '&gt;=', '2006-06-01 00:00:00' } })

           Note that to use a function here you need to make it a scalar reference:

            -&gt;search({'created_time' =&gt; { '&gt;=', \'yesterday()' } })

       .. search in several tables simultaneously?
           To search in two related tables, you first need to set up  appropriate  relationships  between  their
           respective  classes.  When  searching  you  then  supply  the  name of the relationship to the "join"
           attribute in your search, for example when searching in the Books table for  all  the  books  by  the
           author "Fred Bloggs":

            -&gt;search({'authors.name' =&gt; 'Fred Bloggs'}, { join =&gt; 'authors' })

           The  type of join created in your SQL depends on the type of relationship between the two tables, see
           DBIx::Class::Relationship for the join used by each relationship.

       .. create joins with conditions other than column equality?
           Currently, DBIx::Class can only create join conditions using equality, so you're probably better  off
           creating  a  "view"  in your database, and using that as your source. A "view" is a stored SQL query,
           which can be accessed similarly to a table, see your database documentation for details.

       .. search with an SQL function on the left hand side?
           To use an SQL function on the left hand side of a comparison you currently need to resort to  literal
           SQL:

            -&gt;search( \[ 'YEAR(date_of_birth) = ?', 1979 ] );

       .. find more help on constructing searches?
           Behind  the scenes, DBIx::Class uses SQL::Abstract::Classic to help construct its SQL searches. So if
           you fail to find help in the DBIx::Class::Manual::Cookbook, try looking in the SQL::Abstract::Classic
           documentation.

       .. make searches in Oracle (10gR2 and newer) case-insensitive?
           To make Oracle behave like most RDBMS use on_connect_do to issue alter session statements on database
           connection establishment:

            -&gt;on_connect_do("ALTER SESSION SET NLS_COMP = 'LINGUISTIC'");
            -&gt;on_connect_do("ALTER SESSION SET NLS_SORT = '&lt;NLS&gt;_CI'");
            e.g.
            -&gt;on_connect_do("ALTER SESSION SET NLS_SORT = 'BINARY_CI'");
            -&gt;on_connect_do("ALTER SESSION SET NLS_SORT = 'GERMAN_CI'");

       .. format a DateTime object for searching?
           search and find do not take DBIx::Class::InflateColumn into account, and so your DateTime object will
           not be correctly deflated into a format your RDBMS expects.

           The datetime_parser method on your storage object can  be  used  to  return  the  object  that  would
           normally do this, so it's easy to do it manually:

             my $dtf = $schema-&gt;storage-&gt;datetime_parser;
             my $rs = $schema-&gt;resultset('users')-&gt;search(
               {
                 signup_date =&gt; {
                   -between =&gt; [
                     $dtf-&gt;format_datetime($dt_start),
                     $dtf-&gt;format_datetime($dt_end),
                   ],
                 }
               },
             );

           With in a Result Class method, you can get this from the "result_source".

             my $dtf = $self-&gt;result_source-&gt;storage-&gt;datetime_parser;

           This  kludge  is necessary only for conditions passed to search and "find" in DBIx::Class::ResultSet,
           whereas create and "update" in DBIx::Class::Row (but  not  "update"  in  DBIx::Class::ResultSet)  are
           DBIx::Class::InflateColumn-aware  and  will  do  the  right  thing when supplied an inflated DateTime
           object.

   <b>Fetching</b> <b>data</b>
       .. fetch as much data as possible in as few select calls as possible?
           See the prefetch examples in the Cookbook.

       .. fetch a whole column of data instead of a row?
           Call "get_column" on a DBIx::Class::ResultSet. This returns a DBIx::Class::ResultSetColumn.  See  its
           documentation and the Cookbook for details.

       .. fetch a formatted column?
           In your table schema class, create a "private" column accessor with:

             __PACKAGE__-&gt;add_columns(my_column =&gt; { accessor =&gt; '_hidden_my_column' });

           Then,  in  the  same class, implement a subroutine called "my_column" that fetches the real value and
           does the formatting you want.

           See the Cookbook for more details.

       .. fetch a single (or topmost) row?
           Use the "rows" in DBIx::Class::ResultSet and "order_by" in DBIx::Class::ResultSet attributes to order
           your data and pick off a single row.

           See also "Retrieve one and only one row from a resultset" in DBIx::Class::Manual::Cookbook.

           A  less  readable  way  is  to  ask  a  regular  search  to  return   1   row,   using   "slice"   in
           DBIx::Class::ResultSet:

             -&gt;search-&gt;(undef, { order_by =&gt; "id DESC" })-&gt;<a href="../man0/slice.0.html">slice</a>(0)

           which  (if  supported  by  the database) will use LIMIT/OFFSET to hint to the database that we really
           only need one row. This can result in a significant speed improvement. The method using  "single"  in
           DBIx::Class::ResultSet  mentioned  in  the cookbook can do the same if you pass a "rows" attribute to
           the search.

       .. refresh a row from storage?
           Use "discard_changes" in DBIx::Class::Row.

             $result-&gt;discard_changes

           Discarding changes and refreshing from storage are two sides of the same  coin.   When  you  want  to
           discard  your  local  changes, just re-fetch the row from storage.  When you want to get a new, fresh
           copy of the row, just re-fetch the row from storage.  "discard_changes" in DBIx::Class::Row does just
           that by re-fetching the row from storage using the row's primary key.

       .. fetch my data a "page" at a time?
           Pass the "rows" and "page" attributes to your search, eg:

             -&gt;search({}, { rows =&gt; 10, page =&gt; 1});

       .. get a count of all rows even when paging?
           Call "pager" on the paged resultset, it will return a pager object
            with an API/behavior identical to that of Data::Page from late 2009 through late 2019
            &lt;https://metacpan.org/pod/release/LBROCARD/Data-Page-2.02/lib/Data/Page.pm&gt;.                 Calling
           "total_entries" on the pager will return the correct total.

           "count" on the resultset will only return the total number in the page.

   <b>Inserting</b> <b>and</b> <b>updating</b> <b>data</b>
       .. insert a row with an auto incrementing primary key?
           This  happens  automatically.  After  creating a result object, the primary key value created by your
           database can be fetched by calling "id" (or the access of your primary key column) on the object.

       .. insert a row with a primary key that uses a sequence?
           You need to create a trigger in your database that updates your primary key field from the  sequence.
           To  help  PK::Auto  find  the  next  key  value,  you  can  tell  it  the name of the sequence in the
           "column_info" supplied with "add_columns".

            -&gt;add_columns({ id =&gt; { sequence =&gt; 'mysequence', auto_nextval =&gt; 1 } });

       .. insert many rows of data efficiently?
           The "populate" method in DBIx::Class::ResultSet provides efficient bulk inserts.

           DBIx::Class::Fixtures provides an alternative way to do this.

       .. update a collection of rows at the same time?
           Create a resultset using a "search", to filter the rows of data you would like to update,  then  call
           "update" on the resultset to change all the rows at once.

       .. use database functions when updating rows?
       .. update a column using data from another column?
           To  stop  the  column name from being quoted, you'll need to tell DBIC that the right hand side is an
           SQL identifier (it will be quoted properly if you have quoting enabled):

            -&gt;update({ somecolumn =&gt; { -ident =&gt; 'othercolumn' } })

           This method will not retrieve the new value and put it in your Row object. To fetch  the  new  value,
           use the "discard_changes" method on the Row.

             # will return the scalar reference:
             $result-&gt;somecolumn()

             # issue a select using the PK to re-fetch the row data:
             $result-&gt;discard_changes();

             # Now returns the correct new value:
             $result-&gt;somecolumn()

           To update and refresh at once, chain your calls:

             $result-&gt;update({ 'somecolumn' =&gt; { -ident =&gt; 'othercolumn' } })-&gt;discard_changes;

       .. store JSON/YAML in a column and have it deflate/inflate automatically?
           You can use DBIx::Class::InflateColumn to accomplish YAML/JSON storage transparently.

           If you want to use JSON, then in your table schema class, do the following:

            use JSON;

            __PACKAGE__-&gt;add_columns(qw/ ... my_column ../)
            __PACKAGE__-&gt;inflate_column('my_column', {
                inflate =&gt; sub { jsonToObj(shift) },
                deflate =&gt; sub { objToJson(shift) },
            });

           For YAML, in your table schema class, do the following:

            use YAML;

            __PACKAGE__-&gt;add_columns(qw/ ... my_column ../)
            __PACKAGE__-&gt;inflate_column('my_column', {
                inflate =&gt; sub { YAML::Load(shift) },
                deflate =&gt; sub { YAML::Dump(shift) },
            });

           This  technique  is an easy way to store supplemental unstructured data in a table. Be careful not to
           overuse this capability, however. If you find yourself depending more and more on  some  data  within
           the inflated column, then it may be time to factor that data out.

   <b>Custom</b> <b>methods</b> <b>in</b> <b>Result</b> <b>classes</b>
       You can add custom methods that do arbitrary things, even to unrelated tables.  For example, to provide a
       "$book-&gt;foo()" method which searches the cd table, you'd could add this to Book.pm:

         sub foo {
           my ($self, $col_data) = @_;
           return $self-&gt;result_source-&gt;schema-&gt;resultset('cd')-&gt;search($col_data);
         }

       And invoke that on any Book Result object like so:

         my $rs = $book-&gt;foo({ title =&gt; 'Down to Earth' });

       When two tables ARE related, DBIx::Class::Relationship::Base provides many methods to find or create data
       in related tables for you. But if you want to write your own methods, you can.

       For  example,  to  provide  a  "$book-&gt;foo()"  method  to  manually  implement what <b>create_related()</b> from
       DBIx::Class::Relationship::Base does, you could add this to Book.pm:

         sub foo {
           my ($self, $rel_name, $col_data) = @_;
           return $self-&gt;related_resultset($rel_name)-&gt;create($col_data);
         }

       Invoked like this:

         my $author = $book-&gt;foo('author', { name =&gt; 'Fred' });

   <b>Misc</b>
       How do I store my own (non-db) data in my DBIx::Class objects?
           You can add your own data accessors to your Result classes.

           One method is to use the built in mk_group_accessors (via Class::Accessor::Grouped)

                   package App::Schema::Result::MyTable;

                   use parent 'DBIx::Class::Core';

                   __PACKAGE__-&gt;table('foo'); #etc
                   __PACKAGE__-&gt;mk_group_accessors('simple' =&gt; qw/non_column_data/); # must use simple group

           And another method is to use Moose with your DBIx::Class package.

                   package App::Schema::Result::MyTable;

                   use Moose; # import Moose
                   use Moose::Util::TypeConstraint; # import Moose accessor type constraints

                   extends 'DBIx::Class::Core'; # Moose changes the way we define our parent (base) package

                   has 'non_column_data' =&gt; ( is =&gt; 'rw', isa =&gt; 'Str' ); # define a simple attribute

                   __PACKAGE__-&gt;table('foo'); # etc

           With either of these methods the resulting use of the accessor would be

                   my $result;

                   # assume that somewhere in here $result will get assigned to a MyTable row

                   $result-&gt;non_column_data('some string'); # would set the non_column_data accessor

                   # some other stuff happens here

                   $result-&gt;update(); # would not inline the non_column_data accessor into the update

       How do I use DBIx::Class objects in my TT templates?
           Like normal objects, mostly. However you need to watch out for TT calling methods  in  list  context.
           When  calling  relationship  accessors  you  will  not  get resultsets, but a list of all the related
           objects.

           Use the "search_rs" in DBIx::Class::ResultSet method, or the  relationship  accessor  methods  ending
           with "_rs" to work around this issue.

           See also "has_many" in DBIx::Class::Relationship.

       See the SQL statements my code is producing?
           Set the shell environment variable "DBIC_TRACE" to a true value.

           For  more  info  see DBIx::Class::Storage for details of how to turn on debugging in the environment,
           pass your own filehandle to save debug to, or create your own callback.

       Why didn't my search run any SQL?
           DBIx::Class runs the actual SQL statement as late as possible, thus if you create a  resultset  using
           "search"  in  scalar  context,  no query is executed. You can create further resultset refinements by
           calling search again or relationship accessors. The SQL query is only run when you ask the  resultset
           for an actual result object.

       How do I deal with tables that lack a primary key?
           If  your  table  lacks  a primary key, DBIx::Class can't work out which row it should operate on, for
           example to delete or update.  However, a UNIQUE constraint on one or more columns allows  DBIx::Class
           to  uniquely  identify  the  row,  so  you  can tell DBIx::Class::ResultSource these columns act as a
           primary key, even if they don't from the database's point of view:

            $resultset-&gt;set_primary_key(@column);

       How do I make my program start faster?
           Look at the tips in "STARTUP SPEED" in DBIx::Class::Manual::Cookbook

       How do I reduce the overhead of database queries?
           You can reduce the overhead of object creation within DBIx::Class using  the  tips  in  "Skip  result
           object creation for faster results" in DBIx::Class::Manual::Cookbook and "Get raw data for blindingly
           fast results" in DBIx::Class::Manual::Cookbook

       How do I override a run time method (e.g. a relationship accessor)?
           If  you  need  access to the original accessor, then you must "wrap around" the original method.  You
           can do that either with Moose::Manual::MethodModifiers or Class::Method::Modifiers.  The code example
           works for both modules:

               package Your::Schema::Group;
               use Class::Method::Modifiers;

               # ... declare columns ...

               __PACKAGE__-&gt;has_many('group_servers', 'Your::Schema::GroupServer', 'group_id');
               __PACKAGE__-&gt;many_to_many('servers', 'group_servers', 'server');

               # if the server group is a "super group", then return all servers
               # otherwise return only servers that belongs to the given group
               around 'servers' =&gt; sub {
                   my $orig = shift;
                   my $self = shift;

                   return $self-&gt;$orig(@_) unless $self-&gt;is_super_group;
                   return $self-&gt;result_source-&gt;schema-&gt;resultset('Server')-&gt;all;
               };

           If you just want to override the original method, and don't care about the  data  from  the  original
           accessor, then you have two options. Either use Method::Signatures::Simple that does most of the work
           for you, or do it the "dirty way".

           Method::Signatures::Simple way:

               package Your::Schema::Group;
               use Method::Signatures::Simple;

               # ... declare columns ...

               __PACKAGE__-&gt;has_many('group_servers', 'Your::Schema::GroupServer', 'group_id');
               __PACKAGE__-&gt;many_to_many('servers', 'group_servers', 'server');

               # The method keyword automatically injects the annoying my $self = shift; for you.
               method servers {
                   return $self-&gt;result_source-&gt;schema-&gt;resultset('Server')-&gt;search({ ... });
               }

           The dirty way:

               package Your::Schema::Group;
               use Sub::Name;

               # ... declare columns ...

               __PACKAGE__-&gt;has_many('group_servers', 'Your::Schema::GroupServer', 'group_id');
               __PACKAGE__-&gt;many_to_many('servers', 'group_servers', 'server');

               *servers = subname servers =&gt; sub {
                   my $self = shift;
                   return $self-&gt;result_source-&gt;schema-&gt;resultset('Server')-&gt;search({ ... });
               };

   <b>Notes</b> <b>for</b> <b>CDBI</b> <b>users</b>
       Is there a way to make an object auto-stringify itself as a particular column or group of columns (a-la
       cdbi Stringfy column group, or stringify_self method) ?
           See "Stringification" in DBIx::Class::Manual::Cookbook

   <b>Troubleshooting</b>
       Help, I can't connect to postgresql!
           If you get an error such as:

             DBI connect('dbname=dbic','user',...) failed: could not connect to server:
             No such file or directory Is the server running locally and accepting
             connections on Unix domain socket "/var/run/postgresql/.s.PGSQL.5432"?

           Likely  you  have/had  two  copies  of postgresql installed simultaneously, the second one will use a
           default port of 5433, while DBD::Pg is compiled with a default port of 5432.

           You can change the port setting in "postgresql.conf".

       I've lost or forgotten my mysql password
           Stop mysqld and restart it with the --skip-grant-tables option.

           Issue the following statements in the mysql client.

             UPDATE mysql.user SET Password=PASSWORD('MyNewPass') WHERE User='root';
             FLUSH PRIVILEGES;

           Restart mysql.

           Taken from:

           &lt;<a href="http://dev.mysql.com/doc/refman/5.1/en/resetting-permissions.html">http://dev.mysql.com/doc/refman/5.1/en/resetting-permissions.html</a>&gt;.

</pre><h4><b>FURTHER</b> <b>QUESTIONS?</b></h4><pre>
       Check the list of additional DBIC resources.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       This module is free software copyright by the DBIx::Class (DBIC) authors. You can redistribute it  and/or
       modify it under the same terms as the DBIx::Class library.

perl v5.40.0                                       2025-02-01                      <u>DBIx::Class::Manual::<a href="../man3pm/FAQ.3pm.html">FAQ</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>