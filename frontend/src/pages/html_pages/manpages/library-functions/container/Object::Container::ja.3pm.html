<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Object::Container::ja - シンプルなオブジェクトコンテナインタフェース</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libobject-container-perl">libobject-container-perl_0.16-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Object::Container::ja - シンプルなオブジェクトコンテナインタフェース

</pre><h4><b>SYNOPSIS</b></h4><pre>
           use Object::Container;

           ## OO インタフェース
           # 初期化
           my $container = Object::Container-&gt;new;

           # クラスを登録
           $container-&gt;register('HTML::TreeBuilder');

           # クラスをイニシャライザ指定して登録
           $container-&gt;register('WWW::Mechanize', sub {
               my $mech = WWW::Mechanize-&gt;new( stack_depth =&gt; 1 );
               $mech-&gt;agent_alias('Windows IE 6');
               return $mech;
           });

           # 登録したオブジェクトを得る
           my $mech = $container-&gt;get('WWW::Mechanize');

           ## Singletonインタフェース
           my $container = Object::Container-&gt;instance;

           # Singletonインタフェースの場合はregister/getはクラスメソッドとして動作する
           Object::Container-&gt;register('WWW::Mechanize');
           my $mech = Object::Container-&gt;get('WWW::Mechanize');

           # Singletonインタフェースはget関数を任意の名前でエクスポートできる
           use Object::Container 'container';
           container-&gt;register('WWW::Mechanize');
           my $mech = container-&gt;get('WWW::Mechanize');
           my $mech = container('WWW::Mechanize'); # save as above

           # Singletonインタフェースのサブクラス化
           package MyObj;
           use Object::Container '-base';

           register 'ua' =&gt; sub { LWP::UserAgent-&gt;new };

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Object::Container は Singleton インタフェース、OO
       インタフェースどちらでもつかえるシンプルなオブジェクトコンテナーを提供するモジュールです。

       アプリケーション中で同一のオブジェクトをいろいろな場所で使用したい場合があるかもしれません。
       そのような場合に、Class::Singleton
       などを使用してどこからでもそのオブジェクトを取り出せるように設計することがありますが、この方法だと使用したいクラスをサブクラス化して使用する必要があります。

       このモジュールではオブジェクトを複数格納できるコンテナーを提供し、コンテナー自身を Singleton
       にすることで複数のオブジェクトを簡単にどこからでもアクセスできるようにすることができます。

       設計思想は Object::Registrar
       というモジュールに似ていますが、OOインターフェースを持つ点、登録されたオブジェクトの初期化を実際に必要になるまで行わない
       (遅延実行）点が異なっています。

   <b>OOインターフェースとSingletonインターフェース</b>
       このモジュールは OO インターフェースと Singleton インタフェースの二種類のインターフェースを持ちます。

       OOインターフェスは

           my $container = Object::Container-&gt;new;

       などのようにコンストラクタを呼び、その返り値のオブジェクトを介してオブジェクトの登録や取得を行います。この場合登録したオブジェクトはコンテナーオブジェクトごとに独立しています。

       例えば

           my $container1 = Object::Container-&gt;new;
           my $container2 = Object::Container-&gt;new;

       などのように複数のコンテナーを使い分けるような使い方ができます。

       Singletonインタフェースは

           my $container = Object::Container-&gt;instance;

       というように明示的にコンストラクタをよばす、クラスに割り当てられた唯一のオブジェクトを使用するインターフェースです。

       Singletonインタフェースを使用する場合は、register や get 関数などは

           Object::Container-&gt;register('WWW::Mechanize', sub { WWW::Mechanize-&gt;new( stack_depth =&gt; 1 ) });

       というようにすべてクラスメソッドとして使用することができます。Singletonインターフェースで複数のコンテナーを使いたい場合はサブクラス化をして

           MyContainer1-&gt;get('WWW::Mechanize');
           MyContainer2-&gt;get('WWW::Mechanize');

       のようにします。

   <b>SingletonインタフェースとEXPORT関数</b>
       Singletonインタフェースで、いちいち

           MyContainer-&gt;get('WWW::Mechanize');

       と書くのがだるい、と言う人のために好きな名前でコンテナをEXPORTできる機能を用意してあります。

           use MyContainer 'obj';

       と、use 時にエクスポートしたい関数名を指定します。すると

           obj-&gt;register( mech =&gt; sub { WWW::Mechanize-&gt;new });

           obj-&gt;get('mech');
           obj('mech');      # shortcut to obj-&gt;get('mech')

       などと短い書き方でコンテナーにアクセスできるようになります。

   <b>Singletonインタフェースとサブクラス化</b>
       Singletonインタフェースのサブクラス内でオブジェクトを登録したいときに

           __PACKAGE__-&gt;register( mech =&gt; sub { WWW::Mechanize-&gt;new } );

       と書くのがだるい、と言う人のためにサブクラス化時のインタフェースも用意してあります。

       サブクラス化するときに、

           use base 'Object::Container';

       とするかわりに

           use Object::Container '-base';

       とすると register と言う関数がエクスポートされます。こうすると上記の "__PACKAGE__-&gt;register" のかわりに

           register mech =&gt; sub { WWW::Mechanize-&gt;new };

       と書くことができるようになります。

   <b>遅延ロードと依存性解決</b>
       registerメソッドで登録されたオブジェクトは、初回の get メソッドを実行したときに初めて初期化されます。

           Object::Container-&gt;register('WWW::Mechanize', sub { WWW::Mechanize-&gt;new }); # ここで WWW::Mechanize-&gt;new は実行されない
           my $mech = Object::Container-&gt;get('WWW::Mechanize'); # ここで実行される

       この機能により大量にクラスが登録されていても、必要な物のみ初期化されるためリソースを大量に消費することがないため永続プロセス以外でも手軽に導入できるでしょう。

       また Singleton
       インタフェースは初期化関数と組み合わせることにより、オブジェクト同士の依存性の解決も行うことができます。

       たとえば、HTTP::Cookies オブジェクトに依存した LWP::UserAgent を考えます。このような場合、

           Object::Container-&gt;register('HTTP::Cookies', sub { HTTP::Cookies-&gt;new( file =&gt; '/path/to/cookie.dat' ) });
           Object::Container-&gt;register('LWP::UserAgent', sub {
               my $cookies = Object::Container-&gt;get('HTTP::Cookies');
               LWP::UserAgent-&gt;new( cookie_jar =&gt; $cookies );
           });

       というように初期化関数のなかで get メソッドをしようすることで依存性の解決が行えます。

       上記の場合、

           my $ua = Object::Container-&gt;get('LWP::UserAgent');

       した場合に LWP::UserAgent と HTTP::Cookies の両方が初期化されます。

       もし、登録と同時に初期化したい場合、以下のようにできます。

           Object::Container-&gt;register({ class =&gt; 'LWP::UserAgent', preload =&gt; 1 });

       <u>initializer</u> オプションを指定することができます。

           Object::Container-&gt;register({ class =&gt; 'WWW::Mechanize', initializer =&gt; sub {
               my $mech = WWW::Mechanize-&gt;new( stack_depth );
               $mech-&gt;agent_alias('Windows IE 6');
               return $mech;
           }, preload =&gt; 1 });

       これは、以下のように書くのと同じです。

           Object::Container-&gt;register('WWW::Mechanize', sub {
               my $mech = WWW::Mechanize-&gt;new( stack_depth );
               $mech-&gt;agent_alias('Windows IE 6');
               return $mech;
           });
           Object::Container-&gt;get('WWW::Mechanize');

       <u>args</u> オプションを指定した場合は:

           Object::Container-&gt;register({ class =&gt; 'LWP::UserAgent', args =&gt; \@args, preload =&gt; 1 });

       これは、もうおわかりのように、以下と同じです。

           Object::Container-&gt;register('LWP::UserAgent', @args);
           Object::Container-&gt;get('LWP::UserAgent');

</pre><h4><b>METHODS</b></h4><pre>
   <b>register(</b> <b>$class,</b> <b>@args</b> <b>)</b>
   <b>register(</b> <b>$class_or_name,</b> <b>$initialize_code</b> <b>)</b>
       Object::Container にオブジェクトを登録します。

       いちばんシンプルな使い方は

           Object::Container-&gt;register('WWW::Mechanize');

       などのようにクラス名のみを登録する方法です。この場合 get した場合に WWW::Mechanize-&gt;new
       が引数なしで呼ばれます。

       new の引数を指定したい場合は

           Object::Container-&gt;register('WWW::Mechanize', @constructor_args);

       などのように第二引数以降に配列をわたせばそれがそのまま new にわたされます。

       new
       以外のコンストラクタが必要な場合、他に初期化処理が必要な場合、依存しているモジュールがある場合などは、第二引数にコードリファレンスを渡すことで任意の初期化処理が行えます。

           Object::Container-&gt;register('WWW::Mechanize', sub {
               my $mech = WWW::Mechanize-&gt;new( stack_depth );
               $mech-&gt;agent_alias('Windows IE 6');
               return $mech;
           });

       このコードリファレンスではコンテナに格納するオブジェクトを返す必要があります。

       またこのように初期化関数を渡す場合、第一引数ではクラス名を与える必要はなく任意の名前を与えることができます。

           Object::Container-&gt;register('ua1', sub { LWP::UserAgent-&gt;new });
           Object::Container-&gt;register('ua2', sub { LWP::UserAgent-&gt;new });

       などと言った使い方も可能です。

   <b>get($class_or_name)</b>
       registerメソッドで登録したオブジェクトを取得します。

       与える引数はregisterメソッドに与えた第一引数と同じ物を渡します。

   <b>ensure_class_loaded($class)</b>
       $class がロードされているか確認し、ロードされていなかった場合そのクラスを use
       してくれるユーティリティ関数です。

       初期化関数に依存性を含ませるような場合でその依存モジュールを遅延ロードしたい場合などに使用すると便利です。

   <b>load_all</b>
   <b>load_all_except(@classes_or_names)</b>
       基本的にこのモジュールは必要になるまで（getメソッドが呼ばれるまで）オブジェクトを初期化しませんが、
       "Copy-On-Write"
       や、実行時の速度を重視する場合など、あらかじめオブジェクトを初期化しておきたい場合があるかもしれません。そのような場合には

           Object::Container-&gt;load_all;

       とすることで全てのオブジェクトを初期化済みにすることができます。

       また、特定のオブジェクトだけは初期化したくないという場合、

           Object::Container-&gt;load_all_except(qw/Foo Bar/);

       などとすると初期化したくないオブジェクト以外の全てのオブジェクトを初期化することも出来ます。 上記の場合は
       Foo と Bar と言うオブジェクト以外の全てのオブジェクトを初期化します。

</pre><h4><b>EXPORT</b> <b>FUNCTIONS</b> <b>ON</b> <b>SUBCLASS</b> <b>INTERFACE</b></h4><pre>
           package MyContainer;
           use strict;
           use warnings;
           use Object::Container '-base';

       とすることで Object::Container を継承し独自のコンテナークラスを定義することが出来ます。

       このサブクラス中では以下の関数をしようしてオブジェクト定義をすることができます。

   <b>register(</b> <b>$class,</b> <b>@args</b> <b>)</b>
   <b>register(</b> <b>$class_or_name,</b> <b>$initialize_code</b> <b>)</b>
           register Foo =&gt; sub {
               my ($self) = @_;
               $self-&gt;ensure_class_loaded('Foo');
               Foo-&gt;new;
           };

       オブジェクトを登録します。上述したクラス（オブジェクト）メソッドの "register"
       メソッドとおなじ役割をします。

   <b>preload(@classes_or_names)</b>
   <b>preload_all</b>
   <b>preload_all_except</b>
       これらはクラス（オブジェクト）メソッドの "load_all"、"load_all_except"
       と同じようにつかえる関数で、その名前の通り "preload_all" が "load_all" と、"preload_all_except" が
       "load_all_except" とそれぞれ対応しています。

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Class::Singleton, Object::Registrar.

</pre><h4><b>AUTHOR</b></h4><pre>
       Daisuke Murase &lt;<a href="mailto:typester@cpan.org">typester@cpan.org</a>&gt;

</pre><h4><b>COPYRIGHT</b> <b>&amp;</b> <b>LICENSE</b></h4><pre>
       Copyright (c) 2009 by KAYAC Inc.

       This program is free software; you can redistribute it and/or modify it under the same terms as Perl
       itself.

       The full text of the license can be found in the LICENSE file included with this module.

perl v5.34.0                                       2022-07-22                         <u>Object::Container::<a href="../man3pm/ja.3pm.html">ja</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>