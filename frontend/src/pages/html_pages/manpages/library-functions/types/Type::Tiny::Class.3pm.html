<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Type::Tiny::Class - type constraints based on the "isa" method</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libtype-tiny-perl">libtype-tiny-perl_2.004000-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Type::Tiny::Class - type constraints based on the "isa" method

</pre><h4><b>SYNOPSIS</b></h4><pre>
       Using via Types::Standard:

         package Local::Horse {
           use Moo;
           use Types::Standard qw( Str InstanceOf );

           has name =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; Str,
           );

           has owner =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; InstanceOf[ 'Local::Person' ],
             default  =&gt; sub { Local::Person-&gt;new },
           );
         }

       Using Type::Tiny::Class's export feature:

         package Local::Horse {
           use Moo;
           use Types::Standard qw( Str );
           use Type::Tiny::Class 'Local::Person';

           has name =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; Str,
           );

           has owner =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; LocalPerson,
             default  =&gt; sub { LocalPerson-&gt;new },
           );
         }

       Using Type::Tiny::Class's object-oriented interface:

         package Local::Horse {
           use Moo;
           use Types::Standard qw( Str );
           use Type::Tiny::Class;

           my $Person = Type::Tiny::Class-&gt;new( class =&gt; 'Local::Person' );

           has name =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; Str,
           );

           has owner =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; $Person,
             default  =&gt; sub { $Person-&gt;new },
           );
         }

       Using Type::Utils's functional interface:

         package Local::Horse {
           use Moo;
           use Types::Standard qw( Str );
           use Type::Utils;

           my $Person = class_type 'Local::Person';

           has name =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; Str,
           );

           has owner =&gt; (
             is       =&gt; 'ro',
             isa      =&gt; $Person,
             default  =&gt; sub { $Person-&gt;new },
           );
         }

</pre><h4><b>STATUS</b></h4><pre>
       This module is covered by the Type-Tiny stability policy.

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Type constraints of the general form "{ $_-&gt;isa("Some::Class") }".

       This package inherits from Type::Tiny; see that for most documentation.  Major differences are listed
       below:

   <b>Constructor</b>
       "new"
           When the constructor is called on an <u>instance</u> of Type::Tiny::Class, it passes the call through to the
           constructor of the class for the constraint.  So for example:

              my $type = Type::Tiny::Class-&gt;new(class =&gt; "Foo::Bar");
              my $obj  = $type-&gt;new(hello =&gt; "World");
              say ref($obj);   # prints "Foo::Bar"

           This  little  bit of DWIM was borrowed from MooseX::Types::TypeDecorator, but Type::Tiny doesn't take
           the idea quite as far.

   <b>Attributes</b>
       "class"
           The class for the constraint.

       "constraint"
           Unlike Type::Tiny, you <u>cannot</u> pass a constraint coderef to the  constructor.   Instead  rely  on  the
           default.

       "inlined"
           Unlike  Type::Tiny,  you  <u>cannot</u>  pass  an  inlining coderef to the constructor.  Instead rely on the
           default.

       "parent"
           Parent is automatically calculated, and cannot be passed to the constructor.

   <b>Methods</b>
       "plus_constructors($source, $method_name)"
           Much like "plus_coercions" but adds  coercions  that  go  via  a  constructor.   (In  fact,  this  is
           implemented as a wrapper for "plus_coercions".)

           Example:

              package MyApp::Minion;

              use Moose; extends "MyApp::Person";

              use Types::Standard qw( HashRef Str );
              use Type::Utils qw( class_type );

              my $Person = class_type({ class =&gt; "MyApp::Person" });

              has boss =&gt; (
                 is     =&gt; "ro",
                 isa    =&gt; $Person-&gt;plus_constructors(
                    HashRef,     "new",
                    Str,         "_new_from_name",
                 ),
                 coerce =&gt; 1,
              );

              package main;

              MyApp::Minion-&gt;new(
                 ...,
                 boss =&gt; "Bob",  ## via MyApp::Person-&gt;_new_from_name
              );

              MyApp::Minion-&gt;new(
                 ...,
                 boss =&gt; { name =&gt; "Bob" },  ## via MyApp::Person-&gt;new
              );

           Because  coercing  "HashRef" via constructor is a common desire, if you call "plus_constructors" with
           no arguments at all, this is the default.

              $classtype-&gt;plus_constructors(HashRef, "new")
              $classtype-&gt;plus_constructors()  ## identical to above

           This is handy for Moose/Mouse/Moo-based classes.

       stringifies_to($constraint)
           See Type::Tiny::ConstrainedObject.

       numifies_to($constraint)
           See Type::Tiny::ConstrainedObject.

       "with_attribute_values($attr1 =&gt; $constraint1, ...)"
           See Type::Tiny::ConstrainedObject.

   <b>Exports</b>
       Type::Tiny::Class can be used as an exporter.

         use Type::Tiny::Class 'HTTP::Tiny';

       This will export the following functions into your namespace:

       "HTTPTiny"
       is_HTTPTiny( $value )
       assert_HTTPTiny( $value )
       to_HTTPTiny( $value )

       You will also be able to use "HTTPTiny-&gt;new(...)" as a shortcut for "HTTP::Tiny-&gt;new(...)".

       Multiple types can be exported at once:

         use Type::Tiny::Class qw( HTTP::Tiny LWP::UserAgent );

</pre><h4><b>BUGS</b></h4><pre>
       Please report any bugs to &lt;https://github.com/tobyink/p5-type-tiny/issues&gt;.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Type::Tiny::Manual.

       Type::Tiny.

       Moose::Meta::TypeConstraint::Class.

</pre><h4><b>AUTHOR</b></h4><pre>
       Toby Inkster &lt;<a href="mailto:tobyink@cpan.org">tobyink@cpan.org</a>&gt;.

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENCE</b></h4><pre>
       This software is copyright (c) 2013-2014, 2017-2023 by Toby Inkster.

       This is free software; you can redistribute it and/or modify it under  the  same  terms  as  the  Perl  5
       programming language system itself.

</pre><h4><b>DISCLAIMER</b> <b>OF</b> <b>WARRANTIES</b></h4><pre>
       THIS  PACKAGE  IS  PROVIDED  "AS  IS"  AND  WITHOUT ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, WITHOUT
       LIMITATION, THE IMPLIED WARRANTIES OF MERCHANTIBILITY AND FITNESS FOR A PARTICULAR PURPOSE.

perl v5.40.1                                       2025-05-06                             <u>Type::Tiny::<a href="../man3pm/Class.3pm.html">Class</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>