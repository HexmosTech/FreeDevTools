<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Date::Manip::Examples - examples of how to use Date::Manip</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libdate-manip-perl">libdate-manip-perl_6.98-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Date::Manip::Examples - examples of how to use Date::Manip

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This document includes a number of examples on how to do common Date::Manip operations.  I will be happy
       to add new examples over time, and welcome suggestions and examples to include.

       In most cases, an example will include two different ways of getting the answer.  The first way will be
       using the new (as of 6.00) OO modules. The second will be using the old-style functional interface.

       It should be noted that any time you want to work with alternate time zones, the OO interface is STRONGLY
       recommended since the functional interface does not preserve time zone information with the date, and may
       therefore give incorrect results in some cases. However, working in the time zone of the system should
       give correct results.

       It should be noted that, in the examples below, it appears that the OO method often requires more lines
       of code than the functional interface.  There are a number of ways to shorten the OO method, but for the
       examples, I wanted to include all the steps explicitly.

</pre><h4><b>PARSING</b> <b>A</b> <b>DATE</b></h4><pre>
       Dates can be parsed in practically any form in common usage:

       <b>OO</b> <b>method</b>
              $date = new Date::Manip::Date;
              $err = $date-&gt;parse("today");
              $err = $date-&gt;parse("1st Thursday in June 1992");
              $err = $date-&gt;parse("05/10/93");
              $err = $date-&gt;parse("12:30 Dec 12th 1880");
              $err = $date-&gt;parse("8:00pm December tenth");

       <b>Functional</b>
              $date = ParseDate("today");
              $date = ParseDate("1st Thursday in June 1992");
              $date = ParseDate("05/10/93");
              $date = ParseDate("12:30 Dec 12th 1880");
              $date = ParseDate("8:00pm December tenth");

       The Date::Manip::Date manual has a list of all valid formats.

</pre><h4><b>PARSING</b> <b>AN</b> <b>AMOUNT</b> <b>OF</b> <b>TIME</b></h4><pre>
       Amounts of time (referred to as deltas) can also be parsed:

       <b>OO</b> <b>method</b>
              $delta = new Date::Manip::Delta;
              $err = $delta-&gt;parse("in 12 hours");
              $err = $delta-&gt;parse("-1:30:0");
              $err = $delta-&gt;parse("4 business days later");

       <b>Functional</b>
              $delta = ParseDateDelta("in 12 hours");
              $delta = ParseDateDelta("-1:30:0");
              $delta = ParseDateDelta("4 business days later");

</pre><h4><b>TO</b> <b>CALCULATE</b> <b>THE</b> <b>AMOUNT</b> <b>OF</b> <b>TIME</b> <b>BETWEEN</b> <b>TWO</b> <b>DATES</b></h4><pre>
          $datestr1 = "Jan 30 1999 13:00 EST";
          $datestr2 = "2/Mar/1999 15:30:00 +0500";

       <b>OO</b> <b>method</b>
              $date1 = new Date::Manip::Date;
              $date2 = $date1-&gt;new_date();
              $err = $date1-&gt;parse($datestr1);
              $err = $date2-&gt;parse($datestr2);

           To  get  an  exact  amount  of time between the two dates (expressed only in terms of hours, minutes,
           seconds), use:

              $delta = $date1-&gt;calc($date2);

           To get an approximate amount of time (expressed in terms of years, months, weeks, etc. in terms  that
           a human would typically think of), use:

              $delta = $date1-&gt;calc($date2,"approx");

       <b>Functional</b>
              $date1 = ParseDate($string1);
              $date2 = ParseDate($string2);

           To get an exact amount:

              $delta = DateCalc($date1,$date2);

           and the approximate amount:

              $delta = DateCalc($date1,$date2,1);

       The Date::Manip::Calc manual has information about these, and other types of calculations.

</pre><h4><b>TO</b> <b>ADD</b> <b>AN</b> <b>AMOUNT</b> <b>OF</b> <b>TIME</b> <b>TO</b> <b>A</b> <b>DATE</b></h4><pre>
       To find a second date a given amount of time before or after a first date, use the following:

          $datestr  = "Jan 30 1999 13:00 EST";
          $deltastr = "12 hours ago";
          $deltastr = "in 3 business days";

       <b>OO</b> <b>method</b>
              $date = new Date::Manip::Date;
              $delta = $date-&gt;new_delta();
              $date-&gt;parse($datestr);
              $delta-&gt;parse($deltastr);

              $d = $date-&gt;calc($delta);

       <b>Functional</b>
              $date = DateCalc($datestr,$deltastr);

       If the delta is a business delta, it will do a business mode calculation.

       The Date::Manip::Calc manual has information about these, and other types of calculations.

</pre><h4><b>COMPARE</b> <b>TWO</b> <b>DATES</b></h4><pre>
       To take two different dates and see which is earlier, do the following:

          $datestr1 = "Jan 30 1999 13:00 EST";
          $datestr2 = "2/Mar/1999 15:30:00 +0500";

       <b>OO</b> <b>method</b>
              $date1 = new Date::Manip::Date;
              $date2 = $date1-&gt;new_date;
              $date1-&gt;parse($datestr1);
              $date2-&gt;parse($datestr2);

              $date1-&gt;cmp($date2);
                 =&gt; -1, 0, 1

       <b>Functional</b>
              $date1 = ParseDate($datestr1);
              $date2 = ParseDate($datestr2);

              Date_Cmp($date1,$date2);
                 =&gt; -1, 0, 1

</pre><h4><b>TO</b> <b>EXTRACT</b> <b>INFORMATION</b> <b>ABOUT</b> <b>A</b> <b>DATE</b> <b>OR</b> <b>DELTA</b></h4><pre>
       If you have a date or a delta, you can extract information about them as follows:

          $datestr  = "1:24:08 PM EST Feb 3, 1996";
          $deltastr = "12 hours ago";

       <b>OO</b> <b>method</b>
              $date = new Date::Manip::Date;
              $delta = $date-&gt;new_delta();
              $date-&gt;parse($datestr);
              $delta-&gt;parse($deltastr);

              $str = $date-&gt;printf("It is now %T on %b %e, %Y.");
                =&gt;  "It is now 13:24:08 on Feb  3, 1996."

              $str = $delta-&gt;printf("In %hv hours, %mv minutes, %sv seconds");
                =&gt; "In -12 hours, 0 minutes, 0 seconds";

       <b>Functional</b>
              $str = UnixDate($datestr,"It is now %T on %b %e, %Y.");
                =&gt;  "It is now 13:24:08 on Feb  3, 1996."

              $str = Delta_Format($deltastr,"In %hv hours, %mv minutes, %sv seconds");
                =&gt; "In -12 hours, 0 minutes, 0 seconds";

       The  Date::Manip::Date  manual  contains  all of the format codes that can be used to extract information
       from a date. The Date::Manip::Delta manual contains the codes for a delta.

</pre><h4><b>WORKING</b> <b>WITH</b> <b>EPOCH</b></h4><pre>
       Date::Manip can easily be used to work with the number of seconds since the epoch (Jan 1,  1970  00:00:00
       UTC).

       If you have a date, and you want to find out how many seconds it is after the epoch, you can do it in the
       following ways:

          $datestr  = "1999-04-30-15:30:00 EDT";
          $secs  = 1234567;

       <b>OO</b> <b>method</b>
           To find out how many seconds have elapsed on a certain date, you can do the following:

              $date     = new Date::Manip::Date;
              $err      = $date-&gt;parse($datestr);

              $str      = $date-&gt;printf('%s');
                 =&gt; number of seconds

           To find out the date that is a certain number of seconds since the epoch, you can use the following:

              $date  = new Date::Manip::Date;
              $err   = $date-&gt;parse("epoch $secs");

           $date now contains the date wanted (in the local time zone)

       <b>Functional</b>
           To find out how many seconds have elapsed:

              $str = UnixDate($datestr,'%s');
                =&gt; number of seconds

           To find the date that is a number of seconds since the epoch:

              $date = ParseDateString("epoch $secs");

       Note that Date::Manip will work with both positive seconds (for dates that have come since the epoch) and
       negative seconds (for dates that occurred before the epoch).

</pre><h4><b>RECURRING</b> <b>EVENTS</b></h4><pre>
       To  find  a  list  of  dates  where  a  recurring  event  happens (even very complex recurrences), do the
       following:

       <b>OO</b> <b>method</b>
             # To find the 2nd Tuesday of every month from Jan 1 1999 to Apr 30 1999

             $recur = new Date::Manip::Recur;
             $start = $recur-&gt;new_date();
             $end   = $recur-&gt;new_date();
             $start-&gt;parse("Jan 1 1999");
             $end-&gt;parse("Apr 30 1999");

             $recur-&gt;parse("0:1*2:2:0:0:0",$start,$end);
             @date = $recur-&gt;dates();

             # To find the Monday after Easter in 1997-1999

             $recur = new Date::Manip::Recur;
             $recur-&gt;parse("*1997-1999:0:0:0:0:0:0*EASTER,ND1");
             @date = $recur-&gt;dates();

       <b>Functional</b>
             # To find the 2nd Tuesday of every month from Jan 1 1999 to Apr 30 1999
             @date = ParseRecur("0:1*2:2:0:0:0","","Jan 1 1999","Apr 30 1999");

             # To find the Monday after Easter in 1997-1999.
             @date = ParseRecur("*1997-1999:0:0:0:0:0:0*EASTER,ND1");

       The Date::Manip::Recur manual contains information about recurring events.

</pre><h4><b>WORKING</b> <b>WITH</b> <b>DATES</b> <b>IN</b> <b>ANOTHER</b> <b>LANGUAGE</b></h4><pre>
       If you want to work with dates in a language other than English (but you are only working with  a  single
       language), do the following:

       <b>OO</b> <b>method</b>
              $date = new Date::Manip::Date;
              $date-&gt;config("Language","French","DateFormat","non-US");
              $date-&gt;parse("1er decembre 1990");

       <b>Functional</b>
              Date_Init("Language=French","DateFormat=non-US");
              $date = ParseDate("1er decembre 1990");

       The  Date::Manip::Config  manual  has  a  list of all supported languages (in the section on the Language
       config variable). The meaning of the DateFormat config variable is also included.

</pre><h4><b>WORKING</b> <b>WITH</b> <b>TWO</b> <b>DIFFERENT</b> <b>LANGUAGES</b></h4><pre>
       If you want to work with dates in two (or more) languages, it is STRONGLY recommended that you use the OO
       interface. The functional interface will be much slower since it has to re-initialize a lot of  language-
       specific stuff every time you switch back and forth between languages.

       <b>OO</b> <b>method</b>
              $date_eng = new Date::Manip::Date;
              $date_eng-&gt;config("Language","English","DateFormat","US");

              $date_fre = new Date::Manip::Date;
              $date_fre-&gt;config("Language","French","DateFormat","non-US");

           Use the $date_eng object to do English operations, the $date_fre object to do French operations.

       <b>Functional</b>
           If  you  are working with both French and English dates, you can call the following to switch between
           them:

              Date_Init("Language=French","DateFormat=non-US");
              Date_Init("Language=English","DateFormat=US");

           This is NOT recommended. Use the OO method instead.

</pre><h4><b>BUGS</b> <b>AND</b> <b>QUESTIONS</b></h4><pre>
       Please refer to the Date::Manip::Problems documentation for information  on  submitting  bug  reports  or
       questions to the author.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Date::Manip        - main module documentation

</pre><h4><b>LICENSE</b></h4><pre>
       This  script  is  free  software;  you  can redistribute it and/or modify it under the same terms as Perl
       itself.

</pre><h4><b>AUTHOR</b></h4><pre>
       Sullivan Beck (<a href="mailto:sbeck@cpan.org">sbeck@cpan.org</a>)

perl v5.40.1                                       2025-06-08                         <u>Date::Manip::<a href="../man3pm/Examples.3pm.html">Examples</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>