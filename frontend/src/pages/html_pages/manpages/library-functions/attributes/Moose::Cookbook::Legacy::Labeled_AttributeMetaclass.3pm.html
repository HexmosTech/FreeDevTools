<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Moose::Cookbook::Legacy::Labeled_AttributeMetaclass - A meta-attribute, attributes with labels</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libmoose-perl">libmoose-perl_2.2207-1build3_amd64</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Moose::Cookbook::Legacy::Labeled_AttributeMetaclass - A meta-attribute, attributes with labels

</pre><h4><b>VERSION</b></h4><pre>
       version 2.2207

</pre><h4><b>SYNOPSIS</b></h4><pre>
         package MyApp::Meta::Attribute::Labeled;
         use Moose;
         extends 'Moose::Meta::Attribute';

         has label =&gt; (
             is        =&gt; 'rw',
             isa       =&gt; 'Str',
             predicate =&gt; 'has_label',
         );

         package Moose::Meta::Attribute::Custom::Labeled;
         sub register_implementation {'MyApp::Meta::Attribute::Labeled'}

         package MyApp::Website;
         use Moose;

         has url =&gt; (
             metaclass =&gt; 'Labeled',
             is        =&gt; 'rw',
             isa       =&gt; 'Str',
             label     =&gt; "The site's URL",
         );

         has name =&gt; (
             is  =&gt; 'rw',
             isa =&gt; 'Str',
         );

         sub dump {
             my $self = shift;

             my $meta = $self-&gt;meta;

             my $dump = '';

             for my $attribute ( map { $meta-&gt;get_attribute($_) }
                 sort $meta-&gt;get_attribute_list ) {

                 if (   $attribute-&gt;isa('MyApp::Meta::Attribute::Labeled')
                     &amp;&amp; $attribute-&gt;has_label ) {
                     $dump .= $attribute-&gt;label;
                 }
                 else {
                     $dump .= $attribute-&gt;name;
                 }

                 my $reader = $attribute-&gt;get_read_method;
                 $dump .= ": " . $self-&gt;$reader . "\n";
             }

             return $dump;
         }

         package main;

         my $app = MyApp::Website-&gt;new( url =&gt; "<a href="http://google.com">http://google.com</a>", name =&gt; "Google" );

</pre><h4><b>SUMMARY</b></h4><pre>
       <b>WARNING:</b> <b>Subclassing</b> <b>metaclasses</b> <b>(as</b> <b>opposed</b> <b>to</b> <b>providing</b> <b>metaclass</b> <b>traits)</b> <b>is</b> <b>strongly</b> <b>discouraged.</b> <b>This</b>
       <b>recipe</b> <b>is</b> <b>provided</b> <b>solely</b> <b>for</b> <b>reference</b> <b>when</b> <b>encountering</b> <b>older</b> <b>code</b> <b>that</b> <b>does</b> <b>this.</b>

       In this recipe, we begin to delve into the wonder of meta-programming.  Some readers may scoff and claim
       that this is the arena of only the most twisted Moose developers. Absolutely not! Any sufficiently
       twisted developer can benefit greatly from going more meta.

       Our goal is to allow each attribute to have a human-readable "label" attached to it. Such labels would be
       used when showing data to an end user. In this recipe we label the "url" attribute with "The site's URL"
       and create a simple method showing how to use that label.

       The proper, modern way to extend attributes (using a role instead of a subclass) is described in
       Moose::Cookbook::Meta::Recipe3, but that recipe assumes you've read and at least tried to understand this
       one.

</pre><h4><b>META-ATTRIBUTE</b> <b>OBJECTS</b></h4><pre>
       All the attributes of a Moose-based object are actually objects themselves.  These objects have methods
       and attributes. Let's look at a concrete example.

         has 'x' =&gt; ( isa =&gt; 'Int', is =&gt; 'ro' );
         has 'y' =&gt; ( isa =&gt; 'Int', is =&gt; 'rw' );

       Internally, the metaclass for "Point" has two Moose::Meta::Attribute. There are several methods for
       getting meta-attributes out of a metaclass, one of which is "get_attribute_list". This method is called
       on the metaclass object.

       The "get_attribute_list" method returns a list of attribute names. You can then use "get_attribute" to
       get the Moose::Meta::Attribute object itself.

       Once you have this meta-attribute object, you can call methods on it like this:

         print $point-&gt;meta-&gt;get_attribute('x')-&gt;type_constraint;
            =&gt; Int

       To add a label to our attributes there are two steps. First, we need a new attribute metaclass that can
       store a label for an attribute. Second, we need to create attributes that use that attribute metaclass.

</pre><h4><b>RECIPE</b> <b>REVIEW</b></h4><pre>
       We start by creating a new attribute metaclass.

         package MyApp::Meta::Attribute::Labeled;
         use Moose;
         extends 'Moose::Meta::Attribute';

       We can subclass a Moose metaclass in the same way that we subclass anything else.

         has label =&gt; (
             is        =&gt; 'rw',
             isa       =&gt; 'Str',
             predicate =&gt; 'has_label',
         );

       Again, this is standard Moose code.

       Then we need to register our metaclass with Moose:

         package Moose::Meta::Attribute::Custom::Labeled;
         sub register_implementation { 'MyApp::Meta::Attribute::Labeled' }

       This is a bit of magic that lets us use a short name, "Labeled", when referring to our new metaclass.

       That was the whole attribute metaclass.

       Now we start using it.

         package MyApp::Website;
         use Moose;
         use MyApp::Meta::Attribute::Labeled;

       We have to load the metaclass to use it, just like any Perl class.

       Finally, we use it for an attribute:

         has url =&gt; (
             metaclass =&gt; 'Labeled',
             is        =&gt; 'rw',
             isa       =&gt; 'Str',
             label     =&gt; "The site's URL",
         );

       This looks like a normal attribute declaration, except for two things, the "metaclass" and "label"
       parameters. The "metaclass" parameter tells Moose we want to use a custom metaclass for this (one)
       attribute. The "label" parameter will be stored in the meta-attribute object.

       The reason that we can pass the name "Labeled", instead of "MyApp::Meta::Attribute::Labeled", is because
       of the "register_implementation" code we touched on previously.

       When you pass a metaclass to "has", it will take the name you provide and prefix it with
       "Moose::Meta::Attribute::Custom::". Then it calls "register_implementation" in the package. In this case,
       that means Moose ends up calling "Moose::Meta::Attribute::Custom::Labeled::register_implementation".

       If this function exists, it should return the <u>real</u> metaclass package name. This is exactly what our code
       does, returning "MyApp::Meta::Attribute::Labeled". This is a little convoluted, and if you don't like it,
       you can always use the fully-qualified name.

       We can access this meta-attribute and its label like this:

         $website-&gt;meta-&gt;get_attribute('url')-&gt;label()

         MyApp::Website-&gt;meta-&gt;get_attribute('url')-&gt;label()

       We also have a regular attribute, "name":

         has name =&gt; (
             is  =&gt; 'rw',
             isa =&gt; 'Str',
         );

       This is a regular Moose attribute, because we have not specified a new metaclass.

       Finally, we have a "dump" method, which creates a human-readable representation of a "MyApp::Website"
       object. It will use an attribute's label if it has one.

         sub dump {
             my $self = shift;

             my $meta = $self-&gt;meta;

             my $dump = '';

             for my $attribute ( map { $meta-&gt;get_attribute($_) }
                 sort $meta-&gt;get_attribute_list ) {

                 if (   $attribute-&gt;isa('MyApp::Meta::Attribute::Labeled')
                     &amp;&amp; $attribute-&gt;has_label ) {
                     $dump .= $attribute-&gt;label;
                 }

       This is a bit of defensive code. We cannot depend on every meta-attribute having a label. Even if we
       define one for every attribute in our class, a subclass may neglect to do so. Or a superclass could add
       an attribute without a label.

       We also check that the attribute has a label using the predicate we defined. We could instead make the
       label "required". If we have a label, we use it, otherwise we use the attribute name:

                 else {
                     $dump .= $attribute-&gt;name;
                 }

                 my $reader = $attribute-&gt;get_read_method;
                 $dump .= ": " . $self-&gt;$reader . "\n";
             }

             return $dump;
         }

       The "get_read_method" is part of the Moose::Meta::Attribute API. It returns the name of a method that can
       read the attribute's value, <u>when</u> <u>called</u> <u>on</u> <u>the</u> <u>real</u> <u>object</u> (don't call this on the meta-attribute).

</pre><h4><b>CONCLUSION</b></h4><pre>
       You might wonder why you'd bother with all this. You could just hardcode "The Site's URL" in the "dump"
       method. But we want to avoid repetition. If you need the label once, you may need it elsewhere, maybe in
       the "as_form" method you write next.

       Associating a label with an attribute just makes sense! The label is a piece of information <u>about</u> the
       attribute.

       It's also important to realize that this was a trivial example. You can make much more powerful
       metaclasses that <u>do</u> things, as opposed to just storing some more information. For example, you could
       implement a metaclass that expires attributes after a certain amount of time:

          has site_cache =&gt; (
              metaclass     =&gt; 'TimedExpiry',
              expires_after =&gt; { hours =&gt; 1 },
              refresh_with  =&gt; sub { get( $_[0]-&gt;url ) },
              isa           =&gt; 'Str',
              is            =&gt; 'ro',
          );

       The sky's the limit!

</pre><h4><b>AUTHORS</b></h4><pre>
       •   Stevan Little &lt;<a href="mailto:stevan@cpan.org">stevan@cpan.org</a>&gt;

       •   Dave Rolsky &lt;<a href="mailto:autarch@urth.org">autarch@urth.org</a>&gt;

       •   Jesse Luehrs &lt;<a href="mailto:doy@cpan.org">doy@cpan.org</a>&gt;

       •   Shawn M Moore &lt;<a href="mailto:sartak@cpan.org">sartak@cpan.org</a>&gt;

       •   יובל קוג'מן (Yuval Kogman) &lt;<a href="mailto:nothingmuch@woobling.org">nothingmuch@woobling.org</a>&gt;

       •   Karen Etheridge &lt;<a href="mailto:ether@cpan.org">ether@cpan.org</a>&gt;

       •   Florian Ragwitz &lt;<a href="mailto:rafl@debian.org">rafl@debian.org</a>&gt;

       •   Hans Dieter Pearcey &lt;<a href="mailto:hdp@cpan.org">hdp@cpan.org</a>&gt;

       •   Chris Prather &lt;<a href="mailto:chris@prather.org">chris@prather.org</a>&gt;

       •   Matt S Trout &lt;<a href="mailto:mstrout@cpan.org">mstrout@cpan.org</a>&gt;

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       This software is copyright (c) 2006 by Infinity Interactive, Inc.

       This  is  free  software;  you  can  redistribute  it and/or modify it under the same terms as the Perl 5
       programming language system itself.

perl v5.40.0                                       2024-10-20              <u>Moose::<a href="../man3pm/Cookboo...ributeMetaclass.3pm.html">Cookboo...ributeMetaclass</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>