<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Moose::Cookbook::Meta::Labeled_AttributeTrait - Labels implemented via attribute traits</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libmoose-perl">libmoose-perl_2.2207-1build3_amd64</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Moose::Cookbook::Meta::Labeled_AttributeTrait - Labels implemented via attribute traits

</pre><h4><b>VERSION</b></h4><pre>
       version 2.2207

</pre><h4><b>SYNOPSIS</b></h4><pre>
         package MyApp::Meta::Attribute::Trait::Labeled;
         use Moose::Role;
         Moose::Util::meta_attribute_alias('Labeled');

         has label =&gt; (
             is        =&gt; 'rw',
             isa       =&gt; 'Str',
             predicate =&gt; 'has_label',
         );

         package MyApp::Website;
         use Moose;

         has url =&gt; (
             traits =&gt; [qw/Labeled/],
             is     =&gt; 'rw',
             isa    =&gt; 'Str',
             label  =&gt; "The site's URL",
         );

         has name =&gt; (
             is  =&gt; 'rw',
             isa =&gt; 'Str',
         );

         sub dump {
             my $self = shift;

             my $meta = $self-&gt;meta;

             my $dump = '';

             for my $attribute ( map { $meta-&gt;get_attribute($_) }
                 sort $meta-&gt;get_attribute_list ) {

                 if (   $attribute-&gt;does('MyApp::Meta::Attribute::Trait::Labeled')
                     &amp;&amp; $attribute-&gt;has_label ) {
                     $dump .= $attribute-&gt;label;
                 }
                 else {
                     $dump .= $attribute-&gt;name;
                 }

                 my $reader = $attribute-&gt;get_read_method;
                 $dump .= ": " . $self-&gt;$reader . "\n";
             }

             return $dump;
         }

         package main;

         my $app = MyApp::Website-&gt;new( url =&gt; "<a href="http://google.com">http://google.com</a>", name =&gt; "Google" );

</pre><h4><b>SUMMARY</b></h4><pre>
       In this recipe, we begin to delve into the wonder of meta-programming.  Some readers may scoff and claim
       that this is the arena of only the most twisted Moose developers. Absolutely not! Any sufficiently
       twisted developer can benefit greatly from going more meta.

       Our goal is to allow each attribute to have a human-readable "label" attached to it. Such labels would be
       used when showing data to an end user. In this recipe we label the "url" attribute with "The site's URL"
       and create a simple method showing how to use that label.

</pre><h4><b>META-ATTRIBUTE</b> <b>OBJECTS</b></h4><pre>
       All the attributes of a Moose-based object are actually objects themselves.  These objects have methods
       and attributes. Let's look at a concrete example.

         has 'x' =&gt; ( isa =&gt; 'Int', is =&gt; 'ro' );
         has 'y' =&gt; ( isa =&gt; 'Int', is =&gt; 'rw' );

       Internally, the metaclass for "Point" has two Moose::Meta::Attribute objects. There are several methods
       for getting meta-attributes out of a metaclass, one of which is "get_attribute_list". This method is
       called on the metaclass object.

       The "get_attribute_list" method returns a list of attribute names. You can then use "get_attribute" to
       get the Moose::Meta::Attribute object itself.

       Once you have this meta-attribute object, you can call methods on it like this:

         print $point-&gt;meta-&gt;get_attribute('x')-&gt;type_constraint;
            =&gt; Int

       To add a label to our attributes there are two steps. First, we need a new attribute metaclass trait that
       can store a label for an attribute. Second, we need to apply that trait to our attributes.

</pre><h4><b>TRAITS</b></h4><pre>
       Roles that apply to metaclasses have a special name: traits. Don't let the change in nomenclature fool
       you, <b>traits</b> <b>are</b> <b>just</b> <b>roles</b>.

       "has" in Moose allows you to pass a "traits" parameter for an attribute. This parameter takes a list of
       trait names which are composed into an anonymous metaclass, and that anonymous metaclass is used for the
       attribute.

       Yes, we still have lots of metaclasses in the background, but they're managed by Moose for you.

       Traits can do anything roles can do. They can add or refine attributes, wrap methods, provide more
       methods, define an interface, etc. The only difference is that you're now changing the attribute
       metaclass instead of a user-level class.

</pre><h4><b>DISSECTION</b></h4><pre>
       We start by creating a package for our trait.

         package MyApp::Meta::Attribute::Trait::Labeled;
         use Moose::Role;

         has label =&gt; (
             is        =&gt; 'rw',
             isa       =&gt; 'Str',
             predicate =&gt; 'has_label',
         );

       You can see that a trait is just a Moose::Role. In this case, our role contains a single attribute,
       "label". Any attribute which does this trait will now have a label.

       We also register our trait with Moose:

         Moose::Util::meta_attribute_alias('Labeled');

       This allows Moose to find our trait by the short name "Labeled" when passed to the "traits" attribute
       option, rather than requiring the full package name to be specified.

       Finally, we pass our trait when defining an attribute:

         has url =&gt; (
             traits =&gt; [qw/Labeled/],
             is     =&gt; 'rw',
             isa    =&gt; 'Str',
             label  =&gt; "The site's URL",
         );

       The "traits" parameter contains a list of trait names. Moose will build an anonymous attribute metaclass
       from these traits and use it for this attribute.

       The reason that we can pass the name "Labeled", instead of "MyApp::Meta::Attribute::Trait::Labeled", is
       because of the "register_implementation" code we touched on previously.

       When you pass a metaclass to "has", it will take the name you provide and prefix it with
       "Moose::Meta::Attribute::Custom::Trait::". Then it calls "register_implementation" in the package. In
       this case, that means Moose ends up calling "register_implementation" from
       "Moose::Meta::Attribute::Custom::Trait::Labeled".

       If this function exists, it should return the <u>real</u> trait's package name. This is exactly what our code
       does, returning "MyApp::Meta::Attribute::Trait::Labeled". This is a little convoluted, and if you don't
       like it, you can always use the fully-qualified name.

       We can access this meta-attribute and its label like this:

         $website-&gt;meta-&gt;get_attribute('url')-&gt;label()

         MyApp::Website-&gt;meta-&gt;get_attribute('url')-&gt;label()

       We also have a regular attribute, "name":

         has name =&gt; (
             is  =&gt; 'rw',
             isa =&gt; 'Str',
         );

       Finally, we have a "dump" method, which creates a human-readable representation of a "MyApp::Website"
       object. It will use an attribute's label if it has one.

         sub dump {
             my $self = shift;

             my $meta = $self-&gt;meta;

             my $dump = '';

             for my $attribute ( map { $meta-&gt;get_attribute($_) }
                 sort $meta-&gt;get_attribute_list ) {

                 if (   $attribute-&gt;does('MyApp::Meta::Attribute::Trait::Labeled')
                     &amp;&amp; $attribute-&gt;has_label ) {
                     $dump .= $attribute-&gt;label;
                 }

       This is a bit of defensive code. We cannot depend on every meta-attribute having a label. Even if we
       define one for every attribute in our class, a subclass may neglect to do so. Or a superclass could add
       an attribute without a label.

       We also check that the attribute has a label using the predicate we defined. We could instead make the
       label "required". If we have a label, we use it, otherwise we use the attribute name:

                 else {
                     $dump .= $attribute-&gt;name;
                 }

                 my $reader = $attribute-&gt;get_read_method;
                 $dump .= ": " . $self-&gt;$reader . "\n";
             }

             return $dump;
         }

       The "get_read_method" is part of the Moose::Meta::Attribute API. It returns the name of a method that can
       read the attribute's value, <u>when</u> <u>called</u> <u>on</u> <u>the</u> <u>real</u> <u>object</u> (don't call this on the meta-attribute).

</pre><h4><b>CONCLUSION</b></h4><pre>
       You might wonder why you'd bother with all this. You could just hardcode "The Site's URL" in the "dump"
       method. But we want to avoid repetition. If you need the label once, you may need it elsewhere, maybe in
       the "as_form" method you write next.

       Associating a label with an attribute just makes sense! The label is a piece of information <u>about</u> the
       attribute.

       It's also important to realize that this was a trivial example. You can make much more powerful
       metaclasses that <u>do</u> things, as opposed to just storing some more information. For example, you could
       implement a metaclass that expires attributes after a certain amount of time:

          has site_cache =&gt; (
              traits        =&gt; ['TimedExpiry'],
              expires_after =&gt; { hours =&gt; 1 },
              refresh_with  =&gt; sub { get( $_[0]-&gt;url ) },
              isa           =&gt; 'Str',
              is            =&gt; 'ro',
          );

       The sky's the limit!

</pre><h4><b>AUTHORS</b></h4><pre>
       •   Stevan Little &lt;<a href="mailto:stevan@cpan.org">stevan@cpan.org</a>&gt;

       •   Dave Rolsky &lt;<a href="mailto:autarch@urth.org">autarch@urth.org</a>&gt;

       •   Jesse Luehrs &lt;<a href="mailto:doy@cpan.org">doy@cpan.org</a>&gt;

       •   Shawn M Moore &lt;<a href="mailto:sartak@cpan.org">sartak@cpan.org</a>&gt;

       •   יובל קוג'מן (Yuval Kogman) &lt;<a href="mailto:nothingmuch@woobling.org">nothingmuch@woobling.org</a>&gt;

       •   Karen Etheridge &lt;<a href="mailto:ether@cpan.org">ether@cpan.org</a>&gt;

       •   Florian Ragwitz &lt;<a href="mailto:rafl@debian.org">rafl@debian.org</a>&gt;

       •   Hans Dieter Pearcey &lt;<a href="mailto:hdp@cpan.org">hdp@cpan.org</a>&gt;

       •   Chris Prather &lt;<a href="mailto:chris@prather.org">chris@prather.org</a>&gt;

       •   Matt S Trout &lt;<a href="mailto:mstrout@cpan.org">mstrout@cpan.org</a>&gt;

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       This software is copyright (c) 2006 by Infinity Interactive, Inc.

       This  is  free  software;  you  can  redistribute  it and/or modify it under the same terms as the Perl 5
       programming language system itself.

perl v5.40.0                                       2024-10-20              <u>Moose::<a href="../man3pm/Cookboo..._AttributeTrait.3pm.html">Cookboo..._AttributeTrait</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>