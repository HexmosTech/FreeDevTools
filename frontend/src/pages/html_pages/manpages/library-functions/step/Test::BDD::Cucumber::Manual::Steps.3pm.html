<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Test::BDD::Cucumber::Manual::Steps - How to write Step Definitions</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libtest-bdd-cucumber-perl">libtest-bdd-cucumber-perl_0.87-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Test::BDD::Cucumber::Manual::Steps - How to write Step Definitions

</pre><h4><b>VERSION</b></h4><pre>
       version 0.87

</pre><h4><b>INTRODUCTION</b></h4><pre>
       The 'code' part of a Cucumber test-suite are the Step Definition files which match steps, and execute
       code based on them. This document aims to give you a quick overview of those.

</pre><h4><b>STARTING</b> <b>OFF</b></h4><pre>
       Most of your step files will want to start something like:

        #!perl

        package my_step_functions_for_feature_X;

        use strict;
        use warnings;

        use Test::More; # 'use Test2::V0;' is also supported
        use Test::BDD::Cucumber::StepFile;

       The fake shebang line gives some hints to syntax highlighters, and "use strict;" and "use warnings;" are
       hopefully fairly standard at this point.

       Most of <u>my</u> Step Definition files make use of Test::More, but you can use any Test2 or Test::Builder based
       testing module. E.g. "Test2::V0" or "Test::Exception".

       Test::BDD::Cucumber::StepFile gives us the functions Given(), When(), Then() and Step().

</pre><h4><b>STEP</b> <b>DEFINITIONS</b></h4><pre>
        Given qr/I have (\d+)/, sub {
           S-&gt;{'count'} += $1;
        };

        When "The count is an integer", sub {
           S-&gt;{'count'} =
               int( S-&gt;{'count'} );
        };

        Then qr/The count should be (\d+)/, sub {
           is( S-&gt;{'count'}, C-&gt;matches-&gt;[0], "Count matches" );
        };

       Each of the exported verb functions accept a regular expression (or a string that's used as one), and a
       coderef. The coderef is passed a single argument, the Test::BDD::Cucumber::StepContext object. Before the
       subref is executed, localized definitions of "S" and "C" are set, such that the lines below are
       equivalent:

         # Access the first match
         sub { my $context = shift; print $context-&gt;matches-&gt;[0] }
         sub { C-&gt;matches-&gt;[0] }

         # Set a value in the scenario-level stash
         sub { my $context = shift; my $stash = $context-&gt;stash-&gt;{'scenario'}; $stash-&gt;{'count'} = 1 }
         sub { S-&gt;{'count'} = 1 }

       We will evaluate the regex immediately before we execute the coderef, so you can use $1, $2, $etc.
       Similarly you can access named matches using $+{match_name}.

   <b>Accessing</b> <b>step,</b> <b>scenario</b> <b>and</b> <b>feature</b> <b>properties</b>
       Step functions have access to the various properties of the step, scenario and feature in which they're
       being used. This includes tags, line numbers, etc. E.g.:

         # Examples of step properties
         C-&gt;step-&gt;line-&gt;number
         C-&gt;step-&gt;verb
         C-&gt;step-&gt;original_verb

         # Examples of scenario properties
         C-&gt;scenario-&gt;name
         C-&gt;scenario-&gt;tags

         # Examples of feature properties
         C-&gt;feature-&gt;name
         C-&gt;feature-&gt;tags
         C-&gt;feature-&gt;language

       For a full review of available properties, see Test::BDD::Cucumber::Model::Step,
       Test::BDD::Cucumber::Model::Scenario and Test::BDD::Cucumber::Model::Feature respectively.

   <b>Re-using</b> <b>step</b> <b>definitions</b>
       Sometimes you want to call one step from another step. You can do this via the <u>StepContext</u>, using the
       dispatch() method. For example:

         Given qr/I have entered (\d+)/, sub {
               C-&gt;dispatch( 'Given', "I have pressed $1");
               C-&gt;dispatch( 'Given', "I have pressed enter", { some =&gt; 'data' } );
         };

       For more on this topic, check the Redispatching section in the documentation for
       "Test::BDD::Cucumber::StepContext".

</pre><h4><b>LOCALIZATION</b></h4><pre>
       Both feature files and step files can be written using non-english Gherkin keywords. A german feature
       file could look like the example below.

         # language: de
         Funktionalität: Grundlegende Taschenrechnerfunktionen
           Um sicherzustellen, dass ich die Calculator-Klasse korrekt programmiert habe,
           möchte ich als Entwickler einige grundlegende Funktionen prüfen,
           damit ich beruhigt meine Calculator-Klasse verwenden kann.

           Szenario: Anzeige des ersten Tastendrucks
             Gegeben sei ein neues Objekt der Klasse Calculator
             Wenn ich 1 gedrückt habe
             Dann ist auf der Anzeige 1 zu sehen

       To see which keywords (and sub names) to use, ask pherkin about a specific language:

        &gt; pherkin --i18n de
        | feature          | "Funktionalität"                             |
        | background       | "Grundlage"                                  |
        ...
        | given (code)     | "Angenommen", "Gegebensei", "Gegebenseien"   |
        | when (code)      | "Wenn"                                       |
        | then (code)      | "Dann"                                       |

       The last three lines of this list show you which sub names to use in your step file as indicated by the
       '(code)' suffix. A corresponding step file specifying a step function for "Wenn ich 1 gedrückt habe",
       could be:

         #!perl

         use strict;
         use warnings;
         use utf8;    # Interpret accented German chars in regexes and identifiers properly

         use Test::More;
         use Test::BDD::Cucumber::StepFile;

         Wenn qr/^ich (.+) gedrückt habe/, sub {
             S-&gt;{'Calculator'}-&gt;press($_) for split( /(,| und) /, C-&gt;matches-&gt;[0] );
         };

       For more extensive examples see <u>examples/i18n_de/</u> and <u>examples/i18n_es</u>.

</pre><h4><b>ADDITIONAL</b> <b>STEPS</b></h4><pre>
       Next to the steps that will be matched directly against feature file input, a number of additional step
       functions are supported:

       •   "Before" and "After"

           These steps create hooks into the evaluation process of feature files. E.g.

              Before sub {  # Run before every scenario
                 # ... scenario set up code
              };

              After sub {  # Run after every scenario
                # ... scenario tear down code
              };

           For more extensive hook functionality, see Test::BDD::Cucumber::Extension.

       •   "Transform"

           The  "Transform"  step serves to map matched values or table rows from feature file (string) input to
           step input values. The step takes two arguments, same as the "Given",  "When"  and  "Then"  steps:  a
           regular expression and a code reference. E.g.

              Transform qr/^(\d+)$/, sub {
                 # transform matches of digit-only strings

                 my $rv = $1;
                 # ... do something with $rv
                 return $rv;
              };

              Transform qr/^table:col1,col2$/, sub {
                 # transform tables with 2 columns, named col1 and col2 respectively

                 my ($step_context, $data) = @_;
                 # ... transform data in $data
                 return $data;
              };

</pre><h4><b>BEST</b> <b>PRACTICES</b></h4><pre>
       When writing step files, it's a good idea to take a few things into account.

       •   Declare a "package" at the top of your step file

           By  declaring a specific package (your own), you make sure not to step on internals of other modules.
           At the time of writing, the default package is "Test::BDD::Cucumber::StepFile",  which  may  lead  to
           errors being reported in that package, even though they occur in your step file (which is confusing).

           The default package may change in the future and it will likely not be seeded with the content of the
           "T::B::C::StepFile" package.

       •   Declare a different "package" per step file

           By  using  different  packages per step file (or group of step files), name spaces are isolated which
           reduces the risk of importing functions with the same name from different packages.

           An example where this will be the case is when some of your step files are written using "Test::More"
           and some others are written using "Test2::Bundle::More" -- both export  a  function  "ok",  but  with
           conflicting function prototypes.

       •   Don't define functions in your step file

           Especially  step  files provided by extensions. Step files may be loaded more than once, depending on
           the exact scenario in which "App::pherkin" is run. When the step  files  are  being  loaded  multiple
           times,  there  won't  be  any  impact  on  step  definition,  but any function definitions will cause
           'function redefined' warnings.

</pre><h4><b>NEXT</b> <b>STEPS</b></h4><pre>
       How step files are loaded is discussed in Test::BDD::Cucumber::Manual::Architecture, but  isn't  of  much
       interest.    Of    far    more    interest    should    be    seeing   what   you   have   available   in
       Test::BDD::Cucumber::StepContext...

</pre><h4><b>AUTHOR</b></h4><pre>
       Peter Sergeant "<a href="mailto:pete@clueball.com">pete@clueball.com</a>"

</pre><h4><b>LICENSE</b></h4><pre>
         Copyright 2019-2023, Erik Huelsmann
         Copyright 2011-2019, Peter Sergeant; Licensed under the same terms as Perl

perl v5.40.1                                       2025-05-29             <u>Test::BDD::Cucu...::Manual::<a href="../man3pm/Steps.3pm.html">Steps</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>