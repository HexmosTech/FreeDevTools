<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Net::netent - by-name interface to Perl's built-in getnet*() functions</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/perl-doc">perl-doc_5.40.1-5_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Net::netent - by-name interface to Perl's built-in getnet*() functions

</pre><h4><b>SYNOPSIS</b></h4><pre>
        use Net::netent qw(:FIELDS);
        getnetbyname("loopback")               or die "bad net";
        printf "%s is %08X\n", $n_name, $n_net;

        use Net::netent;

        $n = getnetbyname("loopback")          or die "bad net";
        { # there's gotta be a better way, eh?
            @bytes = unpack("C4", pack("N", $n-&gt;net));
            shift @bytes while @bytes &amp;&amp; $bytes[0] == 0;
        }
        printf "%s is %08X [%d.%d.%d.%d]\n", $n-&gt;name, $n-&gt;net, @bytes;

</pre><h4><b>DESCRIPTION</b></h4><pre>
       This module's default exports override the core <b>getnetbyname()</b> and <b>getnetbyaddr()</b> functions, replacing
       them with versions that return "Net::netent" objects.  This object has methods that return the similarly
       named structure field name from the C's netent structure from <u>netdb.h</u>; namely name, aliases, addrtype,
       and net.  The aliases method returns an array reference, the rest scalars.

       You may also import all the structure fields directly into your namespace as regular variables using the
       :FIELDS import tag.  (Note that this still overrides your core functions.)  Access these fields as
       variables named with a preceding "n_".  Thus, "$net_obj-&gt;name()" corresponds to $n_name if you import the
       fields.  Array references are available as regular array variables, so for example "@{
       $net_obj-&gt;aliases() }" would be simply @n_aliases.

       The <b>getnet()</b> function is a simple front-end that forwards a numeric argument to <b>getnetbyaddr()</b>, and the
       rest to <b>getnetbyname()</b>.

       To access this functionality without the core overrides, pass the "use" an empty import list, and then
       access function functions with their full qualified names.  On the other hand, the built-ins are still
       available via the "CORE::" pseudo-package.

</pre><h4><b>EXAMPLES</b></h4><pre>
       The <b>getnet()</b> functions do this in the Perl core:

           sv_setiv(sv, (I32)nent-&gt;n_net);

       The <b>gethost()</b> functions do this in the Perl core:

           sv_setpvn(sv, hent-&gt;h_addr, len);

       That means that the address comes back in binary for the host functions, and as a regular perl integer
       for the net ones.  This seems a bug, but here's how to deal with it:

        use strict;
        use Socket;
        use Net::netent;

        @ARGV = ('loopback') unless @ARGV;

        my($n, $net);

        for $net ( @ARGV ) {

            unless ($n = getnetbyname($net)) {
               warn "$0: no such net: $net\n";
               next;
            }

            printf "\n%s is %s%s\n",
                   $net,
                   lc($n-&gt;name) eq lc($net) ? "" : "*really* ",
                   $n-&gt;name;

            print "\taliases are ", join(", ", @{$n-&gt;aliases}), "\n"
                       if @{$n-&gt;aliases};

            # this is stupid; first, why is this not in binary?
            # second, why am i going through these convolutions
            # to make it looks right
            {
               my @a = unpack("C4", pack("N", $n-&gt;net));
               shift @a while @a &amp;&amp; $a[0] == 0;
               printf "\taddr is %s [%d.%d.%d.%d]\n", $n-&gt;net, @a;
            }

            if ($n = getnetbyaddr($n-&gt;net)) {
               if (lc($n-&gt;name) ne lc($net)) {
                   printf "\tThat addr reverses to net %s!\n", $n-&gt;name;
                   $net = $n-&gt;name;
                   redo;
               }
            }
        }

</pre><h4><b>NOTE</b></h4><pre>
       While this class is currently implemented using the Class::Struct module to build a struct-like class,
       you shouldn't rely upon this.

</pre><h4><b>AUTHOR</b></h4><pre>
       Tom Christiansen

perl v5.40.1                                       2025-07-03                                 <u>Net::<a href="../man3perl/netent.3perl.html">netent</a></u>(3perl)
</pre>
 </div>
</div></section>
</div>
</body>
</html>