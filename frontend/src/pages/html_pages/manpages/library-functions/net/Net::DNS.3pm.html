<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Net::DNS - Perl Interface to the Domain Name System</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libnet-dns-perl">libnet-dns-perl_1.50-1ubuntu1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Net::DNS - Perl Interface to the Domain Name System

</pre><h4><b>SYNOPSIS</b></h4><pre>
               use Net::DNS;
               my $resolver = Net::DNS::Resolver-&gt;new(...);
               my $response = $resolver-&gt;send(...);

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Net::DNS is a collection of Perl modules that act as a Domain Name System (DNS) resolver. It allows the
       programmer to perform DNS queries that are beyond the capabilities of "gethostbyname" and
       "gethostbyaddr".

       The programmer should be familiar with the structure of a DNS packet and the zone file presentation
       format described in RFC1035.

   <b>Resolver</b> <b>Objects</b>
       A resolver object is an instance of the Net::DNS::Resolver class.  A program may have multiple resolver
       objects, each maintaining its own state information such as the nameservers to be queried, whether
       recursion is desired, etc.

   <b>Packet</b> <b>Objects</b>
       Net::DNS::Resolver queries return Net::DNS::Packet objects.  A packet object has five sections:

       •  header, represented by a Net::DNS::Header object

       •  question, a list of no more than one Net::DNS::Question object

       •  answer, a list of Net::DNS::RR objects

       •  authority, a list of Net::DNS::RR objects

       •  additional, a list of Net::DNS::RR objects

   <b>Update</b> <b>Objects</b>
       Net::DNS::Update is a subclass of Net::DNS::Packet useful for creating dynamic update requests.

   <b>Header</b> <b>Object</b>
       The  Net::DNS::Header  object  mediates  access to the header data which resides within the corresponding
       Net::DNS::Packet.

   <b>Question</b> <b>Object</b>
       The Net::DNS::Question object represents the content of the question section of the DNS packet.

   <b>RR</b> <b>Objects</b>
       Net::DNS::RR is the base class for DNS resource  record  (RR)  objects  in  the  answer,  authority,  and
       additional sections of a DNS packet.

       Do  not  assume  that RR objects will be of the type requested.  The type of an RR object must be checked
       before calling any methods.

</pre><h4><b>METHODS</b></h4><pre>
       Net::DNS exports methods and auxiliary functions to support DNS updates, zone serial  number  management,
       and simple DNS queries.

   <b>version</b>
               use Net::DNS;
               print Net::DNS-&gt;version, "\n";

       Returns the version of Net::DNS.

   <b>rr</b>
               # Use a default resolver -- can not get an error string this way.
               use Net::DNS;
               my @rr = rr("example.com");
               my @rr = rr("example.com", "AAAA");
               my @rr = rr("example.com", "AAAA", "IN");

               # Use your own resolver object.
               my $res = Net::DNS::Resolver-&gt;new;
               my @rr  = rr($res, "example.com" ... );

               my ($ptr) = rr("2001:DB8::dead:beef");

       The  rr()  method  provides  simple RR lookup for scenarios where the full flexibility of Net::DNS is not
       required.

       Returns a list of Net::DNS::RR objects for the specified name or an empty list if the query failed or  no
       record was found.

       See "EXAMPLES" for more complete examples.

   <b>mx</b>
               # Use a default resolver -- can not get an error string this way.
               use Net::DNS;
               my @mx = mx("example.com");

               # Use your own resolver object.
               my $res = Net::DNS::Resolver-&gt;new;
               my @mx  = mx($res, "example.com");

       Returns  a list of Net::DNS::RR::MX objects representing the MX records for the specified name.  The list
       will be sorted by preference.  Returns an empty list if the query failed or no MX record was found.

       This method does not look up address records; it resolves MX only.

</pre><h4><b>Dynamic</b> <b>DNS</b> <b>Update</b> <b>Support</b></h4><pre>
       The Net::DNS module provides auxiliary functions which support dynamic DNS update requests.

               $update = Net::DNS::Update-&gt;new( 'example.com' );

               $update-&gt;push( prereq =&gt; nxrrset('example.com. AAAA') );
               $update-&gt;push( update =&gt; rr_add('example.com. 86400 AAAA 2001::DB8::F00') );

   <b>yxrrset</b>
       Use this method to add an "RRset exists" prerequisite to a dynamic update packet. There  are  two  forms,
       value-independent and value-dependent:

               # RRset exists (value-independent)
               $update-&gt;push( pre =&gt; yxrrset("host.example.com AAAA") );

       Meaning:  At least one RR with the specified name and type must exist.

               # RRset exists (value-dependent)
               $update-&gt;push( pre =&gt; yxrrset("host.example.com AAAA 2001:DB8::1") );

       Meaning:  At least one RR with the specified name and type must exist and must have matching data.

       Returns a Net::DNS::RR object or "undef" if the object could not be created.

   <b>nxrrset</b>
       Use this method to add an "RRset does not exist" prerequisite to a dynamic update packet.

               $update-&gt;push( pre =&gt; nxrrset("host.example.com AAAA") );

       Meaning:  No RRs with the specified name and type can exist.

       Returns a Net::DNS::RR object or "undef" if the object could not be created.

   <b>yxdomain</b>
       Use this method to add a "name is in use" prerequisite to a dynamic update packet.

               $update-&gt;push( pre =&gt; yxdomain("host.example.com") );

       Meaning:  At least one RR with the specified name must exist.

       Returns a Net::DNS::RR object or "undef" if the object could not be created.

   <b>nxdomain</b>
       Use this method to add a "name is not in use" prerequisite to a dynamic update packet.

               $update-&gt;push( pre =&gt; nxdomain("host.example.com") );

       Meaning:  No RR with the specified name can exist.

       Returns a Net::DNS::RR object or "undef" if the object could not be created.

   <b>rr_add</b>
       Use this method to add RRs to a zone.

               $update-&gt;push( update =&gt; rr_add("host.example.com AAAA 2001:DB8::c001:a1e") );

       Meaning:  Add this RR to the zone.

       RR  objects  created  by  this method should be added to the "update" section of a dynamic update packet.
       The TTL defaults to 86400 seconds (24 hours) if not specified.

       Returns a Net::DNS::RR object or "undef" if the object could not be created.

   <b>rr_del</b>
       Use this method to delete RRs from a zone.  There are three forms: delete all RRsets,  delete  an  RRset,
       and delete a specific RR.

               # Delete all RRsets.
               $update-&gt;push( update =&gt; rr_del("host.example.com") );

       Meaning:  Delete all RRs having the specified name.

               # Delete an RRset.
               $update-&gt;push( update =&gt; rr_del("host.example.com AAAA") );

       Meaning:  Delete all RRs having the specified name and type.

               # Delete a specific RR.
               $update-&gt;push( update =&gt; rr_del("host.example.com AAAA 2001:DB8::dead:beef") );

       Meaning:  Delete the RR which matches the specified argument.

       RR objects created by this method should be added to the "update" section of a dynamic update packet.

       Returns a Net::DNS::RR object or "undef" if the object could not be created.

</pre><h4><b>Zone</b> <b>Serial</b> <b>Number</b> <b>Management</b></h4><pre>
       The  Net::DNS  module  provides  auxiliary  functions  which  support policy-driven zone serial numbering
       regimes.

               $soa-&gt;serial(SEQUENTIAL);
               $soa-&gt;serial(YYYMMDDxx);

   <b>SEQUENTIAL</b>
               $successor = $soa-&gt;serial( SEQUENTIAL );

       The existing serial number is incremented modulo 2**32.

   <b>UNIXTIME</b>
               $successor = $soa-&gt;serial( UNIXTIME );

       The Unix time scale will be used as the basis for zone  serial  numbering.  The  serial  number  will  be
       incremented if the time elapsed since the previous update is less than one second.

   <b>YYYYMMDDxx</b>
               $successor = $soa-&gt;serial( YYYYMMDDxx );

       The  32  bit value returned by the auxiliary YYYYMMDDxx() function will be used as the base for the date-
       coded zone serial number.  Serial number increments  must  be  limited  to  100  per  day  for  the  date
       information to remain useful.

</pre><h4><b>Sorting</b> <b>of</b> <b>RR</b> <b>arrays</b></h4><pre>
       rrsort()  provides  functionality to help you sort RR arrays. In most cases this will give you the result
       that   you   expect,   but   you   can   specify   your    own    sorting    method    by    using    the
       "Net::DNS::RR::FOO-&gt;set_rrsort_func()" class method. See Net::DNS::RR for details.

   <b>rrsort</b>
               use Net::DNS;
               my @sorted = rrsort( $rrtype, $attribute, @rr_array );

       rrsort()  selects  all RRs from the input array that are of the type defined by the first argument. Those
       RRs are sorted based on the attribute that is specified as second argument.

       There are a number of RRs for which the sorting function is defined in the code.

       For instance:

               my @prioritysorted = rrsort( "SRV", "priority", @rr_array );

       returns the SRV records sorted from lowest to highest priority and for equal priorities from  highest  to
       lowest weight.

       If the function does not exist then a numerical sort on the attribute value is performed.

               my @portsorted = rrsort( "SRV", "port", @rr_array );

       If  the  attribute  is  not  defined  then  either the default_sort() function or "canonical sorting" (as
       defined by DNSSEC) will be used.

       rrsort() returns a sorted array containing only elements of the specified RR type.  Any  other  RR  types
       are silently discarded.

       rrsort() returns an empty list when arguments are incorrect.

</pre><h4><b>EXAMPLES</b></h4><pre>
       The  following  brief  examples  illustrate  some  of  the  features  of Net::DNS.  The documentation for
       individual modules and the demo scripts included with the distribution provide more extensive examples.

       See Net::DNS::Update for an example of performing dynamic updates.

   <b>Look</b> <b>up</b> <b>host</b> <b>addresses.</b>
               use Net::DNS;
               my $res   = Net::DNS::Resolver-&gt;new;
               my $reply = $res-&gt;search( "www.example.com", "AAAA" );
               die "query failed: ", $res-&gt;errorstring unless $reply;

               foreach my $rr ( $reply-&gt;answer ) {
                       print $rr-&gt;address, "\n" if $rr-&gt;can("address");
               }

   <b>Find</b> <b>the</b> <b>nameservers</b> <b>for</b> <b>a</b> <b>domain.</b>
               use Net::DNS;
               my $res   = Net::DNS::Resolver-&gt;new;
               my $reply = $res-&gt;query( "example.com", "NS");
               die "query failed: ", $res-&gt;errorstring unless $reply;

               foreach $rr ( grep {$_-&gt;type eq "NS"} $reply-&gt;answer ) {
                       print $rr-&gt;nsdname, "\n";
               }

   <b>Find</b> <b>the</b> <b>MX</b> <b>records</b> <b>for</b> <b>a</b> <b>domain.</b>
               use Net::DNS;
               my $name = "example.com";
               my $res  = Net::DNS::Resolver-&gt;new;
               my @mx   = mx( $res, $name );

               foreach $rr (@mx) {
                       print $rr-&gt;preference, "\t", $rr-&gt;exchange, "\n";
               }

   <b>Print</b> <b>domain</b> <b>SOA</b> <b>record</b> <b>in</b> <b>zone</b> <b>file</b> <b>format.</b>
               use Net::DNS;
               my $res   = Net::DNS::Resolver-&gt;new;
               my $reply = $res-&gt;query( "example.com", "SOA" );
               die "query failed: ", $res-&gt;errorstring unless $reply;

               foreach my $rr ( $reply-&gt;answer ) {
                       $rr-&gt;print;
               }

   <b>Perform</b> <b>a</b> <b>zone</b> <b>transfer</b> <b>and</b> <b>print</b> <b>all</b> <b>the</b> <b>records.</b>
               use Net::DNS;
               my $res  = Net::DNS::Resolver-&gt;new(
                       nameservers =&gt; ["a.iana-servers.net", "b.iana-servers.net"],
                       tcp_timeout =&gt; 20
                       );

               my @zone = $res-&gt;axfr("example.com");
               warn $res-&gt;errorstring if $res-&gt;errorstring;

               foreach $rr (@zone) {
                       $rr-&gt;print;
               }

   <b>Perform</b> <b>a</b> <b>background</b> <b>query</b> <b>and</b> <b>print</b> <b>the</b> <b>reply.</b>
               use Net::DNS;
               my $res    = Net::DNS::Resolver-&gt;new;
               $res-&gt;<a href="../man10/udp_timeout.10.html">udp_timeout</a>(10);
               $res-&gt;<a href="../man20/tcp_timeout.20.html">tcp_timeout</a>(20);
               my $socket = $res-&gt;bgsend( "www.example.com", "AAAA" );

               while ( $res-&gt;bgbusy($socket) ) {
                       # do some work here whilst awaiting the response
                       # ...and some more here
               }

               my $packet = $res-&gt;bgread($socket);
               die "query failed: ", $res-&gt;errorstring unless $packet;

               $packet-&gt;print;

</pre><h4><b>BUGS</b></h4><pre>
       Net::DNS is slow.

       For other items to be fixed, or if you discover a bug in  this  distribution  please  use  the  CPAN  bug
       reporting system.

</pre><h4><b>COPYRIGHT</b></h4><pre>
       Copyright (c)1997-2000 Michael Fuhr.

       Portions Copyright (c)2002,2003 Chris Reinhardt.

       Portions Copyright (c)2005 Olaf Kolkman (RIPE NCC)

       Portions Copyright (c)2006 Olaf Kolkman (NLnet Labs)

       Portions Copyright (c)2014 Dick Franks

       All rights reserved.

</pre><h4><b>LICENSE</b></h4><pre>
       Permission  to  use, copy, modify, and distribute this software and its documentation for any purpose and
       without fee is hereby granted, provided that the original copyright notices appear in all copies and that
       both copyright notice and this permission notice appear in supporting documentation, and that the name of
       the author not be used in advertising or publicity pertaining to distribution  of  the  software  without
       specific prior written permission.

       THE  SOFTWARE  IS  PROVIDED  "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
       LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO
       EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
       IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE  SOFTWARE  OR
       THE USE OR OTHER DEALINGS IN THE SOFTWARE.

</pre><h4><b>AUTHOR</b> <b>INFORMATION</b></h4><pre>
       Net::DNS is maintained at NLnet Labs (www.nlnetlabs.nl) by Willem Toorop and Dick Franks.

       Between 2005 and 2012 Net::DNS was maintained by Olaf Kolkman.

       Between 2002 and 2004 Net::DNS was maintained by Chris Reinhardt.

       Net::DNS was created in 1997 by Michael Fuhr.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       perl   Net::DNS::Resolver   Net::DNS::Question  Net::DNS::RR  Net::DNS::Packet  Net::DNS::Update  RFC1035
       &lt;https://iana.org/go/rfc1035&gt;

perl v5.40.1                                       2025-03-05                                      <u>Net::<a href="../man3pm/DNS.3pm.html">DNS</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>