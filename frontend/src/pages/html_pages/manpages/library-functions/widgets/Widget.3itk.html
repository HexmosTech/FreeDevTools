<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Widget - base class for mega-widgets within a frame</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/tk-itk4-doc">tk-itk4-doc_4.2.5~20240521-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Widget - base class for mega-widgets within a frame

</pre><h4><b>INHERITANCE</b></h4><pre>
       itk::Archetype &lt;- itk::Widget

</pre><h4><b>STANDARD</b> <b>OPTIONS</b></h4><pre>
       <b>background</b>      <b>cursor</b>

       See the "options" manual entry for details on the standard options.
_________________________________________________________________

</pre><h4><b>DESCRIPTION</b></h4><pre>
       The  <b>Widget</b>  class  inherits  everything  from the <b>Archetype</b> class, and adds a Tk frame called the "hull"
       component to represent the body of the mega-widget.  The window class name for the hull  is  set  to  the
       most-specific  class  name  for the mega-widget.  The protected variable <b>itk_interior</b> contains the window
       path name for the "hull" component.  Derived classes specialize  this  widget  by  packing  other  widget
       components into the hull.

       Since  the hull for the <b>Widget</b> class is implemented with a Tk frame, mega-widgets in the <b>Widget</b> class can
       be packed into other frames and toplevels.

</pre><h4><b>COMPONENTS</b></h4><pre>
       Name:           <b>hull</b>
       Class:          <b>Frame</b>

              The "hull" component acts as the body for the entire mega-widget.   Other  components  are  packed
              into the hull to further specialize the widget.

</pre><h4><b>EXAMPLE</b></h4><pre>
       The  following  example  implements  a simple <b>TextDisplay</b> mega-widget.  It creates a read-only display of
       text with a text widget and a scrollbar.
              option add *TextDisplay.wrap none widgetDefault
              option add *TextDisplay.textBackground ivory widgetDefault
              option add *TextDisplay.width 40 widgetDefault
              option add *TextDisplay.height 10 widgetDefault

              itcl::class TextDisplay {
                  inherit itk::Widget

                  constructor {args} {
                      itk_component add text {
                          text $itk_interior.info -state disabled                 -yscrollcommand [code $itk_interior.sbar set]
                      } {
                          usual
                          keep -tabs -wrap -width -height
                          rename -background -textbackground textBackground Background
                      }
                      pack $itk_component(text) -side left -expand yes -fill both

                      itk_component add scrollbar {
                          scrollbar $itk_interior.sbar                 -command [code $itk_interior.info yview]
                      }
                      pack $itk_component(scrollbar) -side right -fill y

                      eval itk_initialize $args
                  }

                  public method display {info}
                  public method append {info}
              }

              itcl::body TextDisplay::display {info} {
                  $itk_component(text) configure -state normal
                  $itk_component(text) delete 1.0 end
                  $itk_component(text) insert 1.0 $info
                  $itk_component(text) configure -state disabled
              }

              itcl::body TextDisplay::append {info} {
                  $itk_component(text) configure -state normal
                  $itk_component(text) insert end $info
                  $itk_component(text) configure -state disabled
              }

              itk::usual TextDisplay {
                  keep -background -cursor -foreground -font
                  keep -activebackground -activerelief
                  keep -highlightcolor -highlightthickness
                  keep -insertbackground -insertborderwidth -insertwidth
                  keep -insertontime -insertofftime
                  keep -selectbackground -selectborderwidth -selectforeground
                  keep -textbackground -troughcolor
              }

              #
              # EXAMPLE:  Display the <a href="file:/etc/passwd">/etc/passwd</a> file
              #
              TextDisplay .file -background red
              pack .file

              .file display [exec cat <a href="file:/etc/passwd">/etc/passwd</a>]

</pre><h4><b>KEYWORDS</b></h4><pre>
       itk, Archetype, Widget, mega-widget

itk                                                    3.0                                          <u><a href="../man3itk/Widget.3itk.html">Widget</a></u>(3itk)
</pre>
 </div>
</div></section>
</div>
</body>
</html>