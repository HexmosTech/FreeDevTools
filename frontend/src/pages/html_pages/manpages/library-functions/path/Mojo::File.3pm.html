<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Mojo::File - File system paths</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libmojolicious-perl">libmojolicious-perl_9.39+dfsg-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Mojo::File - File system paths

</pre><h4><b>SYNOPSIS</b></h4><pre>
         use Mojo::File;

         # Portably deal with file system paths
         my $path = Mojo::File-&gt;new('/home/sri/.vimrc');
         say $path-&gt;slurp;
         say $path-&gt;dirname;
         say $path-&gt;basename;
         say $path-&gt;extname;
         say $path-&gt;sibling('.bashrc');

         # Use the alternative constructor
         use Mojo::File qw(path);
         my $path = path('/tmp/foo/bar')-&gt;make_path;
         $path-&gt;child('test.txt')-&gt;spew('Hello Mojo!');

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Mojo::File is a scalar-based container for file system paths that provides a friendly API for dealing
       with different operating systems.

         # Access scalar directly to manipulate path
         my $path = Mojo::File-&gt;new('/home/sri/test');
         $$path .= '.txt';

</pre><h4><b>FUNCTIONS</b></h4><pre>
       Mojo::File implements the following functions, which can be imported individually.

   <b>curfile</b>
         my $path = curfile;

       Construct a new scalar-based Mojo::File object for the absolute path to the current source file.

   <b>path</b>
         my $path = path;
         my $path = path('/home/sri/.vimrc');
         my $path = path('<a href="file:/home">/home</a>', 'sri', '.vimrc');
         my $path = path(File::Temp-&gt;newdir);

       Construct a new scalar-based Mojo::File object, defaults to using the current working directory.

         # "foo/bar/baz.txt" (on UNIX)
         path('foo', 'bar', 'baz.txt');

   <b>tempdir</b>
         my $path = tempdir;
         my $path = tempdir('tempXXXXX');

       Construct a new scalar-based Mojo::File object for a temporary directory with File::Temp.

         # Longer version
         my $path = path(File::Temp-&gt;newdir('tempXXXXX'));

   <b>tempfile</b>
         my $path = tempfile;
         my $path = tempfile(DIR =&gt; '<a href="file:/tmp">/tmp</a>');

       Construct a new scalar-based Mojo::File object for a temporary file with File::Temp.

         # Longer version
         my $path = path(File::Temp-&gt;new(DIR =&gt; '<a href="file:/tmp">/tmp</a>'));

</pre><h4><b>METHODS</b></h4><pre>
       Mojo::File implements the following methods.

   <b>basename</b>
         my $name = $path-&gt;basename;
         my $name = $path-&gt;basename('.txt');

       Return the last level of the path with File::Basename.

         # ".vimrc" (on UNIX)
         path('/home/sri/.vimrc')-&gt;basename;

         # "test" (on UNIX)
         path('/home/sri/test.txt')-&gt;basename('.txt');

   <b>child</b>
         my $child = $path-&gt;child('.vimrc');

       Return a new Mojo::File object relative to the path.

         # "/home/sri/.vimrc" (on UNIX)
         path('<a href="file:/home">/home</a>')-&gt;child('sri', '.vimrc');

   <b>chmod</b>
         $path = $path-&gt;<a href="../man0644/chmod.0644.html">chmod</a>(0644);

       Change file permissions.

   <b>copy_to</b>
         my $destination = $path-&gt;copy_to('/home/sri');
         my $destination = $path-&gt;copy_to('/home/sri/.vimrc.backup');

       Copy file with File::Copy and return the destination as a Mojo::File object.

   <b>dirname</b>
         my $name = $path-&gt;dirname;

       Return all but the last level of the path with File::Basename as a Mojo::File object.

         # "/home/sri" (on UNIX)
         path('/home/sri/.vimrc')-&gt;dirname;

   <b>extname</b>
         my $ext = $path-&gt;extname;

       Return file extension of the path.

         # "js"
         path('/home/sri/test.js')-&gt;extname;

   <b>is_abs</b>
         my $bool = $path-&gt;is_abs;

       Check if the path is absolute.

         # True (on UNIX)
         path('/home/sri/.vimrc')-&gt;is_abs;

         # False (on UNIX)
         path('.vimrc')-&gt;is_abs;

   <b>list</b>
         my $collection = $path-&gt;list;
         my $collection = $path-&gt;list({hidden =&gt; 1});

       List all files in the directory and return a Mojo::Collection object containing the results as Mojo::File
       objects. The list does not include "." and "..".

         # List files
         say for path('/home/sri/myapp')-&gt;list-&gt;each;

       These options are currently available:

       dir
           dir =&gt; 1

         Include directories.

       hidden
           hidden =&gt; 1

         Include hidden files.

   <b>list_tree</b>
         my $collection = $path-&gt;list_tree;
         my $collection = $path-&gt;list_tree({hidden =&gt; 1});

       List  all  files recursively in the directory and return a Mojo::Collection object containing the results
       as Mojo::File objects. The list does not include "." and "..".

         # List all templates
         say for path('/home/sri/myapp/templates')-&gt;list_tree-&gt;each;

       These options are currently available:

       dir
           dir =&gt; 1

         Include directories.

       dont_use_nlink
           dont_use_nlink =&gt; 1

         Force File::Find to always stat directories.

       hidden
           hidden =&gt; 1

         Include hidden files and directories.

       max_depth
           max_depth =&gt; 3

         Maximum number of levels to descend when searching for files.

   <b>lstat</b>
         my $stat = $path-&gt;lstat;

       Return a File::stat object for the symlink.

         # Get symlink size
         say path('/usr/sbin/sendmail')-&gt;lstat-&gt;size;

         # Get symlink modification time
         say path('/usr/sbin/sendmail')-&gt;lstat-&gt;mtime;

   <b>make_path</b>
         $path = $path-&gt;make_path;
         $path = $path-&gt;make_path({mode =&gt; 0711});

       Create the directories if they don't already exist,  any  additional  arguments  are  passed  through  to
       File::Path.

   <b>move_to</b>
         my $destination = $path-&gt;move_to('/home/sri');
         my $destination = $path-&gt;move_to('/home/sri/.vimrc.backup');

       Move file with File::Copy and return the destination as a Mojo::File object.

   <b>new</b>
         my $path = Mojo::File-&gt;new;
         my $path = Mojo::File-&gt;new('/home/sri/.vimrc');
         my $path = Mojo::File-&gt;new('<a href="file:/home">/home</a>', 'sri', '.vimrc');
         my $path = Mojo::File-&gt;new(File::Temp-&gt;new);
         my $path = Mojo::File-&gt;new(File::Temp-&gt;newdir);

       Construct a new Mojo::File object, defaults to using the current working directory.

         # "foo/bar/baz.txt" (on UNIX)
         Mojo::File-&gt;new('foo', 'bar', 'baz.txt');

   <b>open</b>
         my $handle = $path-&gt;open('+&lt;');
         my $handle = $path-&gt;open('r+');
         my $handle = $path-&gt;open(O_RDWR);
         my $handle = $path-&gt;open('&lt;:encoding(UTF-8)');

       Open file with IO::File.

         # Combine "fcntl.h" constants
         use Fcntl qw(O_CREAT O_EXCL O_RDWR);
         my $handle = path('/home/sri/test.pl')-&gt;open(O_RDWR | O_CREAT | O_EXCL);

   <b>realpath</b>
         my $realpath = $path-&gt;realpath;

       Resolve the path with Cwd and return the result as a Mojo::File object.

   <b>remove</b>
         $path = $path-&gt;remove;

       Delete file.

   <b>remove_tree</b>
         $path = $path-&gt;remove_tree;
         $path = $path-&gt;remove_tree({keep_root =&gt; 1});

       Delete  this  directory  and  any  files  and subdirectories it may contain, any additional arguments are
       passed through to File::Path.

   <b>sibling</b>
         my $sibling = $path-&gt;sibling('.vimrc');

       Return a new Mojo::File object relative to the directory part of the path.

         # "/home/sri/.vimrc" (on UNIX)
         path('/home/sri/.bashrc')-&gt;sibling('.vimrc');

         # "/home/sri/.ssh/known_hosts" (on UNIX)
         path('/home/sri/.bashrc')-&gt;sibling('.ssh', 'known_hosts');

   <b>slurp</b>
         my $bytes = $path-&gt;slurp;
         my $chars = $path-&gt;slurp('UTF-8');

       Read all data at once from the file. If an encoding is provided, an attempt will be made  to  decode  the
       content.

   <b>spew</b>
         $path = $path-&gt;spew($bytes);
         $path = $path-&gt;spew($chars, 'UTF-8');

       Write  all data at once to the file. If an encoding is provided, an attempt to encode the content will be
       made prior to writing.

   <b>stat</b>
         my $stat = $path-&gt;stat;

       Return a File::stat object for the path.

         # Get file size
         say path('/home/sri/.bashrc')-&gt;stat-&gt;size;

         # Get file modification time
         say path('/home/sri/.bashrc')-&gt;stat-&gt;mtime;

   <b>tap</b>
         $path = $path-&gt;tap(sub {...});

       Alias for "tap" in Mojo::Base.

   <b>to_abs</b>
         my $absolute = $path-&gt;to_abs;

       Return absolute path as a Mojo::File object, the path does not need to exist on the file system.

   <b>to_array</b>
         my $parts = $path-&gt;to_array;

       Split the path on directory separators.

         # "home:sri:.vimrc" (on UNIX)
         join ':', @{path('/home/sri/.vimrc')-&gt;to_array};

   <b>to_rel</b>
         my $relative = $path-&gt;to_rel('/some/base/path');

       Return a relative path from the original path to the destination path as a Mojo::File object.

         # "sri/.vimrc" (on UNIX)
         path('/home/sri/.vimrc')-&gt;to_rel('<a href="file:/home">/home</a>');

   <b>to_string</b>
         my $str = $path-&gt;to_string;

       Stringify the path.

   <b>touch</b>
         $path = $path-&gt;touch;

       Create file if it does not exist or change the modification and access time to the current time.

         # Safely read file
         say path('.bashrc')-&gt;touch-&gt;slurp;

   <b>with_roles</b>
         my $new_class = Mojo::File-&gt;with_roles('Mojo::File::Role::One');
         my $new_class = Mojo::File-&gt;with_roles('+One', '+Two');
         $path         = $path-&gt;with_roles('+One', '+Two');

       Alias for "with_roles" in Mojo::Base.

</pre><h4><b>OPERATORS</b></h4><pre>
       Mojo::File overloads the following operators.

   <b>array</b>
         my @parts = @$path;

       Alias for "to_array".

   <b>bool</b>
         my $bool = !!$path;

       Always true.

   <b>stringify</b>
         my $str = "$path";

       Alias for "to_string".

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Mojolicious, Mojolicious::Guides, &lt;https://mojolicious.org&gt;.

perl v5.40.0                                       2024-12-07                                    <u>Mojo::<a href="../man3pm/File.3pm.html">File</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>