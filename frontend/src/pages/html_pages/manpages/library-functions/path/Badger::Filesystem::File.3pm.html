<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Badger::Filesystem::File - file object</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libbadger-perl">libbadger-perl_0.16-3_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Badger::Filesystem::File - file object

</pre><h4><b>SYNOPSIS</b></h4><pre>
           # using Badger::Filesytem constructor subroutine
           use Badger::Filesystem 'File';

           # use native OS-specific paths:
           $file = File('/path/to/file');

           # or generic OS-independent paths
           $file = File('path', 'to', 'file');

           # manual object construction
           use Badger::Filesystem::File;

           # positional arguments
           $file = Badger::Filesystem::File-&gt;new('/path/to/file');
           $file = Badger::Filesystem::File-&gt;new(['path', 'to', 'file']);

           # named parameters
           $file = Badger::Filesystem::File-&gt;new(
               path =&gt; '/path/to/file'             # native
           );
           $file = Badger::Filesystem::File-&gt;new(
               path =&gt; ['path', 'to', 'file']      # portable
           );

           # path inspection methods
           $file-&gt;path;                    # full path
           $file-&gt;name;                    # file name
           $file-&gt;directory;               # parent directory
           $file-&gt;dir;                     # alias to directory()
           $file-&gt;base;                    # same thing as directory()
           $file-&gt;volume;                  # path volume (e.g. C:)
           $file-&gt;is_absolute;             # path is absolute
           $file-&gt;is_relative;             # path is relative
           $file-&gt;exists;                  # returns true/false
           $file-&gt;must_exist;              # throws error if not
           @stats = $file-&gt;stat;           # returns list
           $stats = $file-&gt;stat;           # returns list ref

           # path translation methods
           $file-&gt;relative;                # relative to cwd
           $file-&gt;relative($base);         # relative to $base
           $file-&gt;absolute;                # relative to filesystem root
           $file-&gt;definitive;              # physical file location
           $file-&gt;collapse;                # resolve '.' and '..' in $file path

           # path comparison methods
           $file-&gt;above($another_path);    # $file is ancestor of $another_path
           $file-&gt;below($another_path);    # $file is descendant of $another_path

           # file manipulation methods
           $file-&gt;create;                  # create file
           $file-&gt;touch;                   # create file or update timestamp
           $file-&gt;delete;                  # delete file
           $fh = $file-&gt;open($mode);       # open file (for read by default)
           $fh = $file-&gt;write;             # open for write
           $fh = $file-&gt;append;            # open for append;

           # all-in-one read/write methods
           @data = $file-&gt;read;            # return list of lines
           $data = $file-&gt;read;            # slurp whole content
           $text = $file-&gt;text;            # same as read();
           $file-&gt;write(@content);         # write @content to file
           $file-&gt;append(@content);        # append @content to file

</pre><h4><b>DESCRIPTION</b></h4><pre>
       The "Badger::Filesystem::File" module is a subclass of Badger::Filesystem::Path for representing files in
       a file system.

       You can create a file object "File" constructor function in Badger::Filesystem.

           use Badger::Filesystem 'File';

       File paths can be specified as a single string using your native filesystem format or as a list or
       reference to a list of items in the path for platform-independent paths.

           my $file = File('/path/to/file');

       If you're concerned about portability to other operating systems and/or file systems, then you can
       specify the file path as a list or reference to a list of component names.

           my $file = File('path', 'to', 'file');
           my $file = File(['path', 'to', 'file']);

       You can specify a reference to a hash array of additional configuration items as the final argument.  At
       present, there is are two configuration options.  The first, "encoding", can be use to specify the
       encoding of the file.  This is applied through Perl's IO layer encodings.  See "perldoc binmode" for
       further information.

           my $file = File('path' , 'to', 'file', { encoding =&gt; 'utf8' });

       The other option is "codec".  This allows you to define the name of a Badger::Codec which will be used to
       serialise data to and from the file via the <b>data()</b> method.

           my $file = File('path' , 'to', 'file', { codec =&gt; 'storable' });

           # save some data, automatically serialised via Storable (freeze)
           $file-&gt;data({
               message =&gt; 'Hello World',
               numbers =&gt; [1.618, 2.718, 3.142],
           });

           # later... load data again and automatically de-serialise (thaw)
           $data = $file-&gt;data;
           print $data-&gt;{ message }, "\n";             # Hello World
           print join(', ', @{ $data-&gt;{ numbers } });  # 1.618, 2.718, 3.142

</pre><h4><b>METHODS</b></h4><pre>
       In addition to the methods inherited from Badger::Filesystem::Path, the following methods are defined or
       re-defined.

   <b>init(\%config)</b>
       Customised initialisation method specific to files.

   <b>volume()</b> <b>/</b> <b>vol()</b>
       Returns any volume defined as part of the path.  This is most commonly used on MS Windows platforms to
       indicate drive letters, e.g. "C:".

   <b>directory()</b> <b>/</b> <b>dir()</b> <b>/</b> <b>base()</b>
       Returns the directory portion of the file path.  This can also be used with an argument to locate another
       directory relative to this file.

           my $file = File('/path/to/file');
           print $file-&gt;dir;                   # /path/to
           print $file-&gt;dir('subdir');         # /path/to/subdir

   <b>name()</b>
       Returns the file name portion of the path.

   <b>file()</b>
       When called without arguments, this method simply returns the file object itself.  It can also be called
       with an argument to locate another file relative to the directory in which the current file is located.

           my $file = File('/path/to/file1');
           print $file-&gt;file;                   # /path/to/file1
           print $file-&gt;file('file2');          # /path/to/file2

   <b>exists</b>
       Returns true if the file exists in the filesystem.  Returns false if the file does not exists or if it is
       not a file (e.g. a directory).

   <b>is_file()</b>
       This method returns true for all "Badger::Filesystem::File" instances.

   <b>create()</b>
       This method can be used to create the file if it doesn't already exist.

   <b>touch()</b>
       This method can be used to create the file if it doesn't already exist or update the timestamp if it
       does.

   <b>open($mode,$perms)</b>
       This method opens the file and returns an IO::File handle to it. The default is to open the file for
       read-only access. The optional arguments can be used to specify a different mode and default permissions
       for the file (these arguments are forwarded to IO::File).

           my $fh = $file-&gt;open;       # read
           my $fh = $file-&gt;open('w');  # write

   <b>read()</b>
       This method read the content of the file.  It is returned as a single string in scalar context and a list
       of each line in list context.

           my $text  = $file-&gt;read;
           my @lines = $file-&gt;read;

   <b>write(@content)</b>
       When called without arguments this method opens the file for writing and returns an IO::File handle.

           my $fh = $file-&gt;write;
           $fh-&gt;print("Hello World!\n");
           $fh-&gt;close;

       When called with arguments, the method opens the file, writes the argument to it, and then closes the
       file again.

           $file-&gt;write("Hello World!\n");

   <b>copy($to,</b> <b>%params)</b> <b>/</b> <b>copy_to($to,</b> <b>%params)</b>
       This method copies the file to the new location specified by the first argument. It delegates to the
       <b>copy_file()</b> method in Badger::Filesystem.

           $file-&gt;copy('/some/where/else');

       The destination can be specified as a file name, file object or file handle.  An optional list of
       reference to a hash array of named parameters can follow.

           $file-&gt;copy(
               '/some/where/else' =&gt; {
                   mkdir     =&gt; 1,         # create intermediate directories
                   dir_mode  =&gt; 0775,      # permissions for created directories
                   file_mode =&gt; 0664,      # permissions for created file
               }
           );

   <b>copy_from($from,</b> <b>%params)</b>
       Like <b>copy_to()</b> but working in reverse.

           $target_file-&gt;copy_from( $source_file );

   <b>move($to,</b> <b>%params)</b> <b>/</b> <b>move_to($to,</b> <b>%params)</b>
       This method moves the file to the new location specified by the first argument.  It delegates to the
       <b>copy_file()</b> method in Badger::Filesystem.

           $file-&gt;move('/some/where/else');

       Arguments are as per <b>copy()</b>.

   <b>move_from($from,</b> <b>%params)</b>
       Like <b>move_to()</b> but working in reverse.

           $target_file-&gt;move_from( $source_file );

   <b>print(@content)</b>
       This method concatenates all arguments into a single string which it then forwards to the <b>write()</b> method.
       This effectively forces the <b>write()</b> method to always write something to the file, even if it's an empty
       string.

           $file-&gt;print("hello");
           $file-&gt;print(@stuff);       # works OK if @stuff is empty

   <b>append(@content)</b>
       This method is similar to <b>write()</b>, but opens the file for appending (when called with no arguments), or
       appends any arguments to the end of the file.

           # manual open, append, close
           my $fh = $file-&gt;append;
           $fh-&gt;print("Hello World!\n");
           $fh-&gt;close;

           # all-in-one
           $file-&gt;append("Hello World\n");

   <b>delete()</b>
       This method deletes the file permanently.  Use it wisely.

   <b>text()</b>
       This method is a wrapper around the <b>read()</b> method which forces scalar context.  The content of the file
       is always returned as a single string.  NOTE: future versions will probably return this as a text object.

   <b>accept($visitor)</b>
       This method is called to dispatch a visitor to the correct method for a filesystem object.  In the
       Badger::Filesystem::File class, it calls the visitor <b>visit_file()</b> method, passing the $self object
       reference as an argument.

   <b>encoding($enc)</b>
       This method can be used to get or set the encoding for the file.

           $file-&gt;encoding(':utf8');

       The encoding will affect all operations that read data from, or write data to the file.

       TODO: note that you can call <b>encoding()</b> on a directory or filesystem to make that the default for all
       files contained therein

   <b>utf()</b>
       A method of convenience to set the file's encoding to UTF-8.

           $file-&gt;utf8;

       It has the same affect as calling the <b>encoding()</b> method with an argument of ":utf8".  See "perldoc -f
       binmode" for further information.

           # same
           $file-&gt;encoding(':utf8');

       The method returns the file object itself, so can be used in a chain.

           $file-&gt;utf8-&gt;must_exist.

   <b>bytes()</b>
       Like "utf8()", this is a method of convenience to set the file encoding to ":bytes".

   <b>crlf()</b>
       Like "utf8()", this is a method of convenience to set the file encoding to ":crlf".

   <b>raw()</b>
       Like "utf8()", this is a method of convenience to set the file encoding to ":raw".

   <b>codec()</b>
       This method can be used to get or set the codec used to serialise data to and from the file via the
       <b>data()</b> method.  The codec should be specified by name, using any of the names that Badger::Codecs
       recognises or can load.

           $file-&gt;codec('storable');

           # first save the data to file
           $file-&gt;data($some_data_to_save);

           # later... load the data back out
           my $data = $file-&gt;data;

       You can use chained codec specifications if you want to pass the data through more than one codec.

           $file-&gt;code('storable+base64');

       See Badger::Codecs for further information on codecs.

       TODO: note that you can call <b>codec()</b> on a directory or filesystem to make that the default for all files
       contained therein

   <b>data(@data)</b>
       This method is used to read or write serialised data to and from the file.  When called with arguments,
       the method serialises the data through any <b>codec()</b> defined and writes it to the file.  A single argument
       should be a reference to an array or hash array.

           $file-&gt;data({               # either: HASH reference
               e   =&gt; 2.718,
               pi  =&gt; 3.142,
               phi =&gt; 1.618,
           });

           $file-&gt;data([               # or: ARRAY reference
               2.718,
               3.142,
               1.618
           ]);

       If multiple arguments are specified then they are implicitly converted to a list reference.

           $file-&gt;data(                # same as ARRAY reference above
               2.718,
               3.142,
               1.618
           );

       When called without any arguments the method will read the data from the file and de-serialise it using
       any <b>codec()</b> that is defined.

           my $data = $file-&gt;data;     # HASH or ARRAY reference

       If no <b>codec()</b> is defined then the method behaves the same as a direct call to either <b>read()</b> (called
       without arguments) or <b>write()</b> (called with arguments).

</pre><h4><b>AUTHOR</b></h4><pre>
       Andy Wardley &lt;<a href="http://wardley.org/">http://wardley.org/</a>&gt;

</pre><h4><b>COPYRIGHT</b></h4><pre>
       Copyright (C) 2005-2009 Andy Wardley. All rights reserved.

</pre><h4><b>ACKNOWLEDGEMENTS</b></h4><pre>
       The "Badger::Filesystem" modules are built around a number of existing Perl modules, including
       File::Spec, File::Path, Cwd, IO::File, IO::Dir and draw heavily on ideas in Path::Class.

       Please see the ACKNOWLEDGEMENTS in Badger::Filesystem for further information.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Badger::Filesystem, Badger::Filesystem::Path, Badger::Filesystem::Directory, Badger::Filesystem::Visitor.

perl v5.36.0                                       2023-08-28                      <u>Badger::Filesystem::<a href="../man3pm/File.3pm.html">File</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>