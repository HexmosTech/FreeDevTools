<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Gtk3::SimpleList - A simple interface to Gtk3's complex MVC list widget</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libgtk3-simplelist-perl">libgtk3-simplelist-perl_0.21-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Gtk3::SimpleList - A simple interface to Gtk3's complex MVC list widget

</pre><h4><b>SYNOPSIS</b></h4><pre>
         use Glib qw(TRUE FALSE);
         use Gtk3 '-init';
         use Gtk3::SimpleList;

         my $slist = Gtk3::SimpleList-&gt;new (
                       'Text Field'    =&gt; 'text',
                       'Markup Field'  =&gt; 'markup',
                       'Int Field'     =&gt; 'int',
                       'Double Field'  =&gt; 'double',
                       'Bool Field'    =&gt; 'bool',
                       'Scalar Field'  =&gt; 'scalar',
                       'Pixbuf Field'  =&gt; 'pixbuf',
                     );

         @{$slist-&gt;{data}} = (
                 [ 'text', 1, 1.1,  TRUE, $var, $pixbuf ],
                 [ 'text', 2, 2.2, FALSE, $var, $pixbuf ],
         );

         # (almost) anything you can do to an array you can do to
         # $slist-&gt;{data} which is an array reference tied to the list model
         push @{$slist-&gt;{data}}, [ 'text', 3, 3.3, TRUE, $var, $pixbuf ];

         # mess with selections
         $slist-&gt;get_selection-&gt;set_mode ('multiple');
         $slist-&gt;get_selection-&gt;unselect_all;
         $slist-&gt;select (1, 3, 5..9); # select rows by index
         $slist-&gt;unselect (3, 8); # unselect rows by index
         @sel = $slist-&gt;get_selected_indices;

         # simple way to make text columns editable
         $slist-&gt;set_column_editable ($col_num, TRUE);

         # Gtk3::SimpleList derives from Gtk3::TreeView, so all methods
         # on a treeview are available.
         $slist-&gt;set_rules_hint (TRUE);
         $slist-&gt;signal_connect (row_activated =&gt; sub {
                 my ($sl, $path, $column) = @_;
                 my $row_ref = $sl-&gt;get_row_data_from_path ($path);
                 # $row_ref is now an array ref to the double-clicked row's data.
             });

         # turn an existing TreeView into a SimpleList; useful for
         # Glade-generated interfaces.
         $simplelist = Gtk3::SimpleList-&gt;new_from_treeview (
                           $glade-&gt;get_widget ('treeview'),
                           'Text Field'    =&gt; 'text',
                           'Int Field'     =&gt; 'int',
                           'Double Field'  =&gt; 'double',
                        );

</pre><h4><b>ABSTRACT</b></h4><pre>
       SimpleList is a simple interface to the powerful but complex Gtk3::TreeView and Gtk3::ListStore
       combination, implementing using tied arrays to make thing simple and easy.

</pre><h4><b>DESCRIPTION</b></h4><pre>
       Gtk3 has a powerful, but complex MVC (Model, View, Controller) system used to implement list and tree
       widgets.  Gtk3::SimpleList automates the complex setup work and allows you to treat the list model as a
       more natural list of lists structure.

       After creating a new Gtk3::SimpleList object with the desired columns you may set the list data with a
       simple Perl array assignment. Rows may be added or deleted with all of the normal array operations. You
       can treat the "data" member of the list simplelist object as an array reference, and manipulate the list
       data with perl's normal array operators.

       A mechanism has also been put into place allowing columns to be Perl scalars.  The scalar is converted to
       text through Perl's normal mechanisms and then displayed in the list. This same mechanism can be expanded
       by defining arbitrary new column types before calling the new function.

</pre><h4><b>OBJECT</b> <b>HIERARCHY</b></h4><pre>
        Glib::Object
        +--- Gtk3::Object
             +--- Gtk3::Widget
                  +--- Gtk3::TreeView
                       +--- Gtk3::SimpleList

</pre><h4><b>METHODS</b></h4><pre>
       $slist = Gtk3::SimpleList-&gt;new ($cname, $ctype, ...)
               •   $cname (string)

               •   $ctype (string)

           Creates  a  new Gtk3::SimpleList object with the specified columns. The parameter "cname" is the name
           of the column, what will be displayed in the list headers if they are turned on. The parameter  ctype
           is the type of the column, one of:

            text    normal text strings
            markup  pango markup strings
            int     integer values
            double  double-precision floating point values
            bool    boolean values, displayed as toggle-able checkboxes
            scalar  a perl scalar, displayed as a text string by default
            pixbuf  a Gtk3::Gdk::Pixbuf

           or  the  name  of  a  custom  type you add with "add_column_type".  These should be provided in pairs
           according to the desired columns for your list.

       $slist = Gtk3::SimpleList-&gt;new_from_treeview ($treeview, $cname, $ctype, ...)
               •   $treeview (Gtk3::TreeView)

               •   $cname (string)

               •   $ctype (string)

           Like "Gtk3::SimpleList-&gt;new()", but turns an existing Gtk3::TreeView into a  Gtk3::SimpleList.   This
           is  intended  mostly  for  use with stuff like Glade, where the widget is created for you.  This will
           create and attach a new model and remove any existing columns from <u>treeview</u>.  Returns  <u>treeview</u>,  re-
           blessed as a Gtk3::SimpleList.

       $slist-&gt;set_data_array ($arrayref)
               •   $arrayref (array reference)

           Set  the  data  in  the  list  to  the  array  reference  $arrayref. This is completely equivalent to
           @{$list-&gt;{data}} = @{$arrayref} and is only here for convenience and for those programmers who  don't
           like to type-cast and have static, set once data.

       @indices = $slist-&gt;get_selected_indices
           Return the indices of the selected rows in the ListStore.

       $slist-&gt;get_row_data_from_path ($path)
               •   $path (Gtk3::TreePath) the path of the desired row

           Returns an array ref with the data of the row indicated by $path.

       $slist-&gt;select ($index, ...);
       $slist-&gt;unselect ($index, ...);
               •   $index (integer)

           Select or unselect rows in the list by index.  If the list is set for multiple selection, all indices
           in  the  list  will  be  set/unset;  otherwise,  just  the  first is used.  If the list is set for no
           selection, then nothing happens.

           To  set  the  selection  mode,  or  to  select  all  or  none   of   the   rows,   use   the   normal
           TreeView/TreeSelection  stuff,  e.g.  $slist-&gt;get_selection and the TreeSelection methods "get_mode",
           "set_mode", "select_all", and "unselect_all".

       $slist-&gt;set_column_editable ($index, $editable)
               •   $index (integer)

               •   $editable (boolean)

       boolean = $slist-&gt;get_column_editable ($index)
               •   $index (integer)

           This is a very simple interface to Gtk3::TreeView's editable text column cells.   All  columns  which
           use  the attr "text" (basically, any text or number column, see "add_column_type") automatically have
           callbacks installed to update data when cells are edited.  With "set_column_editable", you can enable
           the in-place editing.

           "get_column_editable" tells you if column <u>index</u> is currently editable.

       Gtk3::SimpleList-&gt;add_column_type ($type_name, ...)
               $type_name (string)

           Add a new column type to the list of possible types. Initially six column types  are  defined,  text,
           int,  double,  bool, scalar, and pixbuf. The bool column type uses a toggle cell renderer, the pixbuf
           uses a pixbuf cell renderer, and the rest use text cell renderers. In the process  of  adding  a  new
           column type you may use any cell renderer you wish.

           The first parameter is the column type name, the list of six are examples.  There are no restrictions
           on  the  names and you may even overwrite the existing ones should you choose to do so. The remaining
           parameters are the type definition consisting of key value pairs. There  are  three  required:  type,
           renderer,  and  attr.  The  type key determines what actual datatype will be stored in the underlying
           model representation; this is a package name, e.g.  Glib::String, Glib::Int,  Glib::Boolean,  but  in
           general  if  you  want  an  arbitrary  Perl  data  structure you will want to use 'Glib::Scalar'. The
           renderer key should hold the class name of the cell renderer to create for this column type; this may
           be any of Gtk3::CellRendererText, Gtk3::CellRendererToggle, Gtk3::CellRendererPixbuf, or some  other,
           possibly  custom,  cell renderer class.  The attr key is magical; it may be either a string, in which
           case it specifies the attribute which will be set from the specified column (e.g. 'text' for  a  text
           renderer,  'active'  for a toggle renderer, etc), or it may be a reference to a subroutine which will
           be called each time the renderer needs to draw the data.

           This function, described as a GtkTreeCellDataFunc in the API reference, will  receive  5  parameters:
           $treecol,  $cell,  $model, $iter, $col_num (when SimpleList hooks up the function, it sets the column
           number to be passed as the user data).  The data  value  for  the  particular  cell  in  question  is
           available  via $model-&gt;get ($iter, $col_num); you can then do whatever it is you have to do to render
           the cell the way you want.  Here are some examples:

             # just displays the value in a scalar as
             # Perl would convert it to a string
             Gtk3::SimpleList-&gt;add_column_type( 'a_scalar',
                     type     =&gt; 'Glib::Scalar',
                     renderer =&gt; 'Gtk3::CellRendererText',
                     attr     =&gt; sub {
                          my ($treecol, $cell, $model, $iter, $col_num) = @_;
                          my $info = $model-&gt;get ($iter, $col_num);
                          $cell-&gt;set (text =&gt; $info);
                     }
                );

             # sums up the values in an array ref and displays
             # that in a text renderer
             Gtk3::SimpleList-&gt;add_column_type( 'sum_of_array',
                     type     =&gt; 'Glib::Scalar',
                     renderer =&gt; 'Gtk3::CellRendererText',
                     attr     =&gt; sub {
                          my ($treecol, $cell, $model, $iter, $col_num) = @_;
                          my $sum = 0;
                          my $info = $model-&gt;get ($iter, $col_num);
                          foreach (@$info)
                          {
                              $sum += $_;
                          }
                          $cell-&gt;set (text =&gt; $sum);
                     }
                );

</pre><h4><b>MODIFYING</b> <b>LIST</b> <b>DATA</b></h4><pre>
       After creating a new Gtk3::SimpleList object there will be a member called "data" which is a tied  array.
       That  means  data may be treated as an array, but in reality the data resides in something else. There is
       no need to understand the details of this it just means that you put data into, take  data  out  of,  and
       modify  it  just  like any other array. This includes using array operations like push, pop, unshift, and
       shift. For those of you very familiar with perl this section will prove redundant, but just in case:

         Adding and removing rows:

           # push a row onto the end of the list
           push @{$slist-&gt;{data}}, [col1_data, col2_data, ..., coln_data];
           # pop a row off of the end of the list
           $rowref = pop @{$slist-&gt;{data}};
           # unshift a row onto the beginning of the list
           unshift @{$slist-&gt;{data}}, [col1_data, col2_data, ..., coln_data];
           # shift a row off of the beginning of the list
           $rowref = shift @{$slist-&gt;{data}};
           # delete the row at index $n, 0 indexed
           splice @{ $slist-&gt;{data} }, $n, 1;
           # set the entire list to be the data in a array
           @{$slist-&gt;{data}} = ( [row1, ...], [row2, ...], [row3, ...] );

         Getting at the data in the list:

           # get an array reference to the entire nth row
           $rowref = $slist-&gt;{data}[n];
           # get the scalar in the mth column of the nth row, 0 indexed
           $val = $slist-&gt;{data}[n][m];
           # set an array reference to the entire nth row
           $slist-&gt;{data}[n] = [col1_data, col2_data, ..., coln_data];
           # get the scalar in the mth column of the nth row, 0 indexed
           $slist-&gt;{data}[n][m] = $rowm_coln_value;

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       <b><a href="../man1/Perl.1.html">Perl</a></b>(1), <b><a href="../man3pm/Glib.3pm.html">Glib</a></b>(3pm), <b><a href="../man3pm/Gtk3.3pm.html">Gtk3</a></b>(3pm), <b>Gtk3::<a href="../man3pm/TreeView.3pm.html">TreeView</a></b>(3pm), <b>Gtk3::<a href="../man3pm/TreeModel.3pm.html">TreeModel</a></b>(3pm), <b>Gtk3::<a href="../man3pm/ListStore.3pm.html">ListStore</a></b>(3pm).

       Note: Gtk2::SimpleList was deprecated in  favor  of  Gtk2::Ex::Simple::List,  part  of  the  Gtk2-Perl-Ex
       project  at <a href="http://gtk2-perl-ex.sf.net">http://gtk2-perl-ex.sf.net</a> .  Gtk3::SimpleList is a simple port of Gtk2::SimpleList on top of
       Gtk3 for its users that wanted to switch to Gtk3.

</pre><h4><b>AUTHORS</b></h4><pre>
        muppet &lt;scott at asofyet dot org&gt;
        Ross McFarland &lt;rwmcfa1 at neces dot com&gt;
        Gavin Brown &lt;gavin dot brown at uk dot com&gt;
        Thierry Vignaud

</pre><h4><b>COPYRIGHT</b> <b>AND</b> <b>LICENSE</b></h4><pre>
       Copyright 2003-2004 by the Gtk2-Perl team.  Copyright 2013 by Thierry Vignaud

       This library is free software; you can redistribute it and/or modify  it  under  the  terms  of  the  GNU
       Library  General  Public  License as published by the Free Software Foundation; either version 2.1 of the
       License, or (at your option) any later version.

       This library is distributed in the hope that it will be useful, but WITHOUT ANY  WARRANTY;  without  even
       the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Library General
       Public License for more details.

       You  should  have  received  a copy of the GNU Library General Public License along with this library; if
       not, write to the Free Software Foundation, Inc., 59 Temple Place - Suite  330,  Boston,  MA   02111-1307
       USA.

perl v5.30.0                                       2020-05-09                              <u>Gtk3::<a href="../man3pm/SimpleList.3pm.html">SimpleList</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>