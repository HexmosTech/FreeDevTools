<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>POE::Filter::Map - transform input and/or output within a filter stack</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libpoe-perl">libpoe-perl_1.3700-1_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       POE::Filter::Map - transform input and/or output within a filter stack

</pre><h4><b>SYNOPSIS</b></h4><pre>
         #!perl

         use POE qw(
           Wheel::FollowTail
           Filter::Line Filter::Map Filter::Stackable
         );

         POE::Session-&gt;create(
           inline_states =&gt; {
             _start =&gt; sub {
               my $parse_input_as_lines = POE::Filter::Line-&gt;new();

               my $redact_some_lines = POE::Filter::Map-&gt;new(
                 Code =&gt; sub {
                   my $input = shift;
                   $input = "[REDACTED]" unless $input =~ /sudo\[\d+\]/i;
                   return $input;
                 },
               );

               my $filter_stack = POE::Filter::Stackable-&gt;new(
                 Filters =&gt; [
                   $parse_input_as_lines, # first on get, last on put
                   $redact_some_lines, # first on put, last on get
                 ]
               );

               $_[HEAP]{tailor} = POE::Wheel::FollowTail-&gt;new(
                 Filename =&gt; "/var/log/system.log",
                 InputEvent =&gt; "got_log_line",
                 Filter =&gt; $filter_stack,
               );
             },
             got_log_line =&gt; sub {
               print "Log: $_[ARG0]\n";
             }
           }
         );

         POE::Kernel-&gt;run();
         exit;

</pre><h4><b>DESCRIPTION</b></h4><pre>
       POE::Filter::Map transforms data inside the filter stack.  It may be used to transform input, output, or
       both depending on how it is constructed.  This filter is named and modeled after Perl's built-in <b>map()</b>
       function.

       POE::Filter::Map is designed to be combined with other filters through POE::Filter::Stackable.  In the
       "SYNOPSIS" example, a filter stack is created to parse logs as lines and redact all entries that don't
       pertain to a sudo process.

</pre><h4><b>PUBLIC</b> <b>FILTER</b> <b>METHODS</b></h4><pre>
       In addition to the usual POE::Filter methods, POE::Filter::Map also supports the following.

   <b>new</b>
       <b>new()</b> constructs a new POE::Filter::Map object.  It must either be called with a single Code parameter,
       or both a Put and a Get parameter.  The values for Code, Put and Get are code references that, when
       invoked, return transformed versions of their sole parameters.  A Code function will be used for both
       input and output, while Get and Put functions allow input and output to be filtered in different ways.

         # Decrypt rot13.
         sub decrypt_rot13 {
           my $encrypted = shift;
           $encrypted =~ tr[a-zA-Z][n-za-mN-ZA-M];
           return $encrypted;
         }

         # Encrypt rot13.
         sub encrypt_rot13 {
           my $plaintext = shift;
           $plaintext =~ tr[a-zA-Z][n-za-mN-ZA-M];
           return $plaintext;
         }

         # Decrypt rot13 on input, and encrypt it on output.
         my $rot13_transcrypter = POE::Filter::Map-&gt;new(
           Get =&gt; \&amp;decrypt_rot13,
           Put =&gt; \&amp;encrypt_rot13,
         );

       Rot13 is symmetric, so the above example can be simplified to use a single Code function.

         my $rot13_transcrypter = POE::Filter::Map-&gt;new(
           Code =&gt; sub {
             local $_ = shift;
             tr[a-zA-Z][n-za-mN-ZA-M];
             return $_;
           }
         );

   <b>modify</b>
       <b>modify()</b> changes a POE::Filter::Map object's behavior at run-time.  It accepts the same parameters as
       <b>new()</b>, and it replaces the existing transforms with new ones.

         # Switch to "reverse" encryption for testing.
         $rot13_transcrypter-&gt;modify(
           Code =&gt; sub { return scalar reverse shift }
         );

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       POE::Filter for more information about filters in general.

       POE::Filter::Stackable for more details on stacking filters.

</pre><h4><b>BUGS</b></h4><pre>
       None known.

</pre><h4><b>AUTHORS</b> <b>&amp;</b> <b>COPYRIGHTS</b></h4><pre>
       The Map filter was contributed by Dieter Pearcey.  Documentation is provided by Rocco Caputo.

       Please see the POE manpage for more information about authors and contributors.

perl v5.34.0                                       2022-03-25                              <u>POE::Filter::<a href="../man3pm/Map.3pm.html">Map</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>