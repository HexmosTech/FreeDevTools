<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Getopt::EX::Hashed - Hash store object automation for Getopt::Long</title>
    <style>
        body { font-family: monospace; margin: 20px; line-height: 1.4; }
        a { color: #0066cc; }
        pre { white-space: pre-wrap; }
    </style>
</head>
<body>
    <div id="main-content">
<section class="p-strip is-bordered">
<div class="row">
<div class="col-3 u-hide--small u-hide" id="toc">
</div>
<div id="tableWrapper">
<p id="distroAndSection"></p>

Provided by: <a href="https://launchpad.net/ubuntu/questing/+package/libgetopt-ex-hashed-perl">libgetopt-ex-hashed-perl_1.0601-2_all</a> <br><br><pre>
</pre><h4><b>NAME</b></h4><pre>
       Getopt::EX::Hashed - Hash store object automation for Getopt::Long

</pre><h4><b>VERSION</b></h4><pre>
       Version 1.0601

</pre><h4><b>SYNOPSIS</b></h4><pre>
         # script/foo
         use App::foo;
         App::foo-&gt;new-&gt;run();

         # lib/App/foo.pm
         package App::foo;

         use Getopt::EX::Hashed; {
             Getopt::EX::Hashed-&gt;configure( DEFAULT =&gt; [ is =&gt; 'rw' ] );
             has start    =&gt; ' =i  s begin ' , default =&gt; 1;
             has end      =&gt; ' =i  e       ' ;
             has file     =&gt; ' =s@ f       ' , any =&gt; qr/^(?!\.)/;
             has score    =&gt; ' =i          ' , min =&gt; 0, max =&gt; 100;
             has answer   =&gt; ' =i          ' , must =&gt; sub { $_[1] == 42 };
             has mouse    =&gt; ' =s          ' , any =&gt; [ 'Frankie', 'Benjy' ];
             has question =&gt; ' =s          ' , any =&gt; qr/^(life|universe|everything)$/i;
         } no Getopt::EX::Hashed;

         sub run {
             my $app = shift;
             use Getopt::Long;
             $app-&gt;getopt or pod2usage();
             if ($app-&gt;answer == 42) {
                 $app-&gt;question //= 'life';
                 ...

</pre><h4><b>DESCRIPTION</b></h4><pre>
       <b>Getopt::EX::Hashed</b> is a module to automate a hash object to store command line option values for
       <b>Getopt::Long</b> and compatible modules including <b>Getopt::EX::Long</b>.  Module name shares <b>Getopt::EX</b> prefix,
       but it works independently from other modules in <b>Getopt::EX</b>, so far.

       Major objective of this module is integrating initialization and specification into single place.  It
       also provides simple validation interface.

       Accessor methods are automatically generated when "is" parameter is given.  If the same function is
       already defined, the program causes fatal error.  Accessors are removed when the object is destroyed.
       Problems may occur when multiple objects are present at the same time.

</pre><h4><b>FUNCTION</b></h4><pre>
   <b>has</b>
       Declare option parameters in a following form.  The parentheses are for clarity only and may be omitted.

           has option_name =&gt; ( param =&gt; value, ... );

       For example, to define the option "--number", which takes an integer value as a parameter, and also can
       be used as "-n", do the following

           has number =&gt; spec =&gt; "=i n";

       The accessor is created with the first name. In this example, the accessor will be defined as
       "$app-&gt;number".

       If array reference is given, multiple names can be declared at once.

           has [ 'left', 'right' ] =&gt; ( spec =&gt; "=i" );

       If the name start with plus ("+"), given parameter updates existing setting.

           has '+left' =&gt; ( default =&gt; 1 );

       As for "spec" parameter, label can be omitted if it is the first parameter.

           has left =&gt; "=i", default =&gt; 1;

       If the number of parameter is not even, default label is assumed to be exist at the head: "action" if the
       first parameter is code reference, "spec" otherwise.

       Following parameters are available.

       [ <b>spec</b> =&gt; ] <u>string</u>
              Give  option  specification.   "spec  =&gt;"  label  can  be  omitted  if and only if it is the first
              parameter.

              In <u>string</u>, option spec and alias names are separated by white space, and can show up in any order.

              To have an option called "--start" that takes an integer as its value and can also  be  used  with
              the names "-s" and "--begin", declare as follows.

                  has start =&gt; "=i s begin";

              Above declaration will be compiled into the next string.

                  start|s|begin=i

              which conform to "Getopt::Long" definition.  Of course, you can write as this:

                  has start =&gt; "s|begin=i";

              If the name and aliases contain underscore ("_"), another alias name is defined with dash ("-") in
              place of underscores.

                  has a_to_z =&gt; "=s";

              Above declaration will be compiled into the next string.

                  a_to_z|a-to-z=s

              If  nothing  special is necessary, give empty (or white space only) string as a value.  Otherwise,
              it is not considered as an option.

       <b>alias</b> =&gt; <u>string</u>
              Additional alias names can be specified by <b>alias</b> parameter too.  There is no difference with  ones
              in "spec" parameter.

                  has start =&gt; "=i", alias =&gt; "s begin";

       <b>is</b> =&gt; "ro" | "rw"
              To  produce  accessor method, "is" parameter is necessary.  Set the value "ro" for read-only, "rw"
              for read-write.

              Read-write accessor has lvalue attribute, so it can be assigned to.  You can use like this:

                  $app-&gt;foo //= 1;

              This is much simpler than writing as in the following.

                  $app-&gt;<a href="../man1/foo.1.html">foo</a>(1) unless defined $app-&gt;foo;

              If you want to make  accessor  for  all  following  members,  use  "configure"  to  set  "DEFAULT"
              parameter.

                  Getopt::EX::Hashed-&gt;configure( DEFAULT =&gt; [ is =&gt; 'rw' ] );

              If  you  don't  like  assignable  accessor,  configure  "ACCESSOR_LVALUE" parameter to 0.  Because
              accessor is generated at the time of "new", this value is effective for all members.

       <b>default</b> =&gt; <u>value</u> | <u>coderef</u>
              Set default value.  If no default is given, the member is initialized as "undef".

              If the value is a reference for ARRAY or HASH, new reference with same member is  assigned.   This
              means that member data is shared across multiple "new" calls.  Please be careful if you call "new"
              multiple times and alter the member data.

              If  a  code  reference  is  given,  it is called at the time of <b>new</b> to get default value.  This is
              effective when you want to evaluate the value at the time of execution, rather  than  declaration.
              If you want to define a default action, use the <b>action</b> parameter.

              If  a  reference  to  SCALAR  is  given,  the  option value is stored in the data indicated by the
              reference, not in the hash object member.  In this case, the expected value cannot be obtained  by
              accessing the hash member.

       [ <b>action</b> =&gt; ] <u>coderef</u>
              Parameter  "action" takes code reference which is called to process the option.  "action =&gt;" label
              can be omitted if and only if it is the first parameter.

              When called, hash object is passed as $_.

                  has [ qw(left right both) ] =&gt; '=i';
                  has "+both" =&gt; sub {
                      $_-&gt;{left} = $_-&gt;{right} = $_[1];
                  };

              You can use this for "&lt;&gt;" to catch everything.  In that case, spec parameter does not  matter  and
              not required.

                  has ARGV =&gt; default =&gt; [];
                  has "&lt;&gt;" =&gt; sub {
                      push @{$_-&gt;{ARGV}}, $_[0];
                  };

       Following  parameters are all for data validation.  First "must" is a generic validator and can implement
       anything.  Others are shortcut for common rules.

       <b>must</b> =&gt; <u>coderef</u> | [ <u>coderef</u> ... ]
              Parameter "must" takes a code reference to validate option values.  It  takes  same  arguments  as
              "action" and returns boolean.  With next example, option <b>--answer</b> takes only 42 as a valid value.

                  has answer =&gt; '=i',
                      must =&gt; sub { $_[1] == 42 };

              If multiple code reference is given, all code have to return true.

                  has answer =&gt; '=i',
                      must =&gt; [ sub { $_[1] &gt;= 42 }, sub { $_[1] &lt;= 42 } ];

       <b>min</b> =&gt; <u>number</u>
       <b>max</b> =&gt; <u>number</u>
              Set the minimum and maximum limit for the argument.

       <b>any</b> =&gt; <u>arrayref</u> | qr/<u>regex</u>/
              Set the valid string parameter list.  Each item is a string or a regex reference.  The argument is
              valid  when  it  is  same  as,  or  match  to  any item of the given list.  If the value is not an
              arrayref, it is taken as a single item list (regexpref usually).

              Following declarations are almost equivalent, except second one is case insensitive.

                  has question =&gt; '=s',
                      any =&gt; [ 'life', 'universe', 'everything' ];

                  has question =&gt; '=s',
                      any =&gt; qr/^(life|universe|everything)$/i;

              If you are using optional argument, don't forget to include default value in the list.   Otherwise
              it causes validation error.

                  has question =&gt; ':s',
                      any =&gt; [ 'life', 'universe', 'everything', '' ];

</pre><h4><b>METHOD</b></h4><pre>
   <b>new</b>
       Class method to get initialized hash object.

   <b>optspec</b>
       Return option specification list which can be given to "GetOptions" function.

           GetOptions($obj-&gt;optspec)

       "GetOptions"  has  a  capability  of  storing  values  in a hash, by giving the hash reference as a first
       argument, but it is not necessary.

   <b>getopt</b> <b>[</b> <u><b>arrayref</b></u> <b>]</b>
       Call appropriate function defined in caller's context to process options.

           $obj-&gt;getopt

           $obj-&gt;getopt(\@argv);

       Above examples are shortcut for following code.

           GetOptions($obj-&gt;optspec)

           GetOptionsFromArray(\@argv, $obj-&gt;optspec)

   <b>use_keys</b> <u><b>keys</b></u>
       Because hash keys are protected by  "Hash::Util::lock_keys",  accessing  non-existent  member  causes  an
       error.  Use this function to declare new member key before use.

           $obj-&gt;use_keys( qw(foo bar) );

       If you want to access arbitrary keys, unlock the object.

           use Hash::Util 'unlock_keys';
           unlock_keys %{$obj};

       You can change this behavior by "configure" with "LOCK_KEYS" parameter.

   <b>configure</b> <b>label</b> <b>=&gt;</b> <u><b>value</b></u><b>,</b> <b>...</b>
       Use  class method "Getopt::EX::Hashed-&gt;configure()" before creating an object; this information is stored
       in the area unique for calling package.  After calling new(), package unique configuration is  copied  in
       the  object,  and  it  is  used  for  further operation.  Use "$obj-&gt;configure()" to update object unique
       configuration.

       There are following configuration parameters.

       <b>LOCK_KEYS</b> (default: 1)
              Lock hash keys.  This avoids accidental access to non-existent hash entry.

       <b>REPLACE_UNDERSCORE</b> (default: 1)
              Produce alias with underscores replaced by dash.

       <b>REMOVE_UNDERSCORE</b> (default: 0)
              Produce alias with underscores removed.

       <b>GETOPT</b> (default: 'GetOptions')
       <b>GETOPT_FROM_ARRAY</b> (default: 'GetOptionsFromArray')
              Set function name called from "getopt" method.

       <b>ACCESSOR_PREFIX</b> (default: '')
              When specified, it is prepended to the member name to make accessor method.  If  "ACCESSOR_PREFIX"
              is defined as "opt_", accessor for member "file" will be "opt_file".

       <b>ACCESSOR_LVALUE</b> (default: 1)
              If true, read-write accessors have lvalue attribute.  Set zero if you don't like that behavior.

       <b>DEFAULT</b>
              Set  default  parameters.   At the call for "has", DEFAULT parameters are inserted before argument
              parameters.  So if both include same  parameter,  later  one  in  argument  list  has  precedence.
              Incremental call with "+" is not affected.

              Typical use of DEFAULT is "is" to prepare accessor method for all following hash entries.  Declare
              "DEFAULT =&gt; []" to reset.

                  Getopt::EX::Hashed-&gt;configure(DEFAULT =&gt; [ is =&gt; 'ro' ]);

   <b>reset</b>
       Reset the class to the original state.

</pre><h4><b>SEE</b> <b>ALSO</b></h4><pre>
       Getopt::Long

       Getopt::EX, Getopt::EX::Long

</pre><h4><b>AUTHOR</b></h4><pre>
       Kazumasa Utashiro

</pre><h4><b>COPYRIGHT</b></h4><pre>
       The  following  copyright notice applies to all the files provided in this distribution, including binary
       files, unless explicitly noted otherwise.

       Copyright 2021-2024 Kazumasa Utashiro

</pre><h4><b>LICENSE</b></h4><pre>
       This library is free software; you can redistribute it and/or modify it under  the  same  terms  as  Perl
       itself.

perl v5.38.2                                       2024-05-21                            <u>Getopt::EX::<a href="../man3pm/Hashed.3pm.html">Hashed</a></u>(3pm)
</pre>
 </div>
</div></section>
</div>
</body>
</html>